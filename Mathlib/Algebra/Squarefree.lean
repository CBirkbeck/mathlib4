/-
Copyright (c) 2020 Aaron Anderson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Anderson
-/
import Mathlib.RingTheory.UniqueFactorizationDomain

#align_import algebra.squarefree from "leanprover-community/mathlib"@"00d163e35035c3577c1c79fa53b68de17781ffc1"

/-!
# Squarefree elements of monoids
An element of a monoid is squarefree when it is not divisible by any squares
except the squares of units.

Results about squarefree natural numbers are proved in `Data.Nat.Squarefree`.

## Main Definitions
 - `Squarefree r` indicates that `r` is only divisible by `x * x` if `x` is a unit.

## Main Results
 - `multiplicity.squarefree_iff_multiplicity_le_one`: `x` is `Squarefree` iff for every `y`, either
  `multiplicity y x ‚â§ 1` or `IsUnit y`.
 - `UniqueFactorizationMonoid.squarefree_iff_nodup_factors`: A nonzero element `x` of a unique
 factorization monoid is squarefree iff `factors x` has no duplicate factors.

## Tags
squarefree, multiplicity

-/


variable {R : Type*}

/-- An element of a monoid is squarefree if the only squares that
  divide it are the squares of units. -/
def Squarefree [Monoid R] (r : R) : Prop :=
  ‚àÄ x : R, x * x ‚à£ r ‚Üí IsUnit x
#align squarefree Squarefree

@[simp]
theorem IsUnit.squarefree [CommMonoid R] {x : R} (h : IsUnit x) : Squarefree x := fun _ hdvd =>
  isUnit_of_mul_isUnit_left (isUnit_of_dvd_unit hdvd h)
#align is_unit.squarefree IsUnit.squarefree

-- @[simp] -- Porting note: simp can prove this
theorem squarefree_one [CommMonoid R] : Squarefree (1 : R) :=
  isUnit_one.squarefree
#align squarefree_one squarefree_one

@[simp]
theorem not_squarefree_zero [MonoidWithZero R] [Nontrivial R] : ¬¨Squarefree (0 : R) := by
  erw [not_forall]
  -- ‚ä¢ ‚àÉ x, ¬¨(x * x ‚à£ 0 ‚Üí IsUnit x)
  exact ‚ü®0, by simp‚ü©
  -- üéâ no goals
#align not_squarefree_zero not_squarefree_zero

theorem Squarefree.ne_zero [MonoidWithZero R] [Nontrivial R] {m : R} (hm : Squarefree (m : R)) :
    m ‚â† 0 := by
  rintro rfl
  -- ‚ä¢ False
  exact not_squarefree_zero hm
  -- üéâ no goals
#align squarefree.ne_zero Squarefree.ne_zero

@[simp]
theorem Irreducible.squarefree [CommMonoid R] {x : R} (h : Irreducible x) : Squarefree x := by
  rintro y ‚ü®z, hz‚ü©
  -- ‚ä¢ IsUnit y
  rw [mul_assoc] at hz
  -- ‚ä¢ IsUnit y
  rcases h.isUnit_or_isUnit hz with (hu | hu)
  -- ‚ä¢ IsUnit y
  ¬∑ exact hu
    -- üéâ no goals
  ¬∑ apply isUnit_of_mul_isUnit_left hu
    -- üéâ no goals
#align irreducible.squarefree Irreducible.squarefree

@[simp]
theorem Prime.squarefree [CancelCommMonoidWithZero R] {x : R} (h : Prime x) : Squarefree x :=
  h.irreducible.squarefree
#align prime.squarefree Prime.squarefree

theorem Squarefree.of_mul_left [CommMonoid R] {m n : R} (hmn : Squarefree (m * n)) : Squarefree m :=
  fun p hp => hmn p (dvd_mul_of_dvd_left hp n)
#align squarefree.of_mul_left Squarefree.of_mul_left

theorem Squarefree.of_mul_right [CommMonoid R] {m n : R} (hmn : Squarefree (m * n)) :
    Squarefree n := fun p hp => hmn p (dvd_mul_of_dvd_right hp m)
#align squarefree.of_mul_right Squarefree.of_mul_right

theorem Squarefree.squarefree_of_dvd [CommMonoid R] {x y : R} (hdvd : x ‚à£ y) (hsq : Squarefree y) :
    Squarefree x := fun _ h => hsq _ (h.trans hdvd)
#align squarefree.squarefree_of_dvd Squarefree.squarefree_of_dvd

section SquarefreeGcdOfSquarefree

variable {Œ± : Type*} [CancelCommMonoidWithZero Œ±] [GCDMonoid Œ±]

theorem Squarefree.gcd_right (a : Œ±) {b : Œ±} (hb : Squarefree b) : Squarefree (gcd a b) :=
  hb.squarefree_of_dvd (gcd_dvd_right _ _)
#align squarefree.gcd_right Squarefree.gcd_right

theorem Squarefree.gcd_left {a : Œ±} (b : Œ±) (ha : Squarefree a) : Squarefree (gcd a b) :=
  ha.squarefree_of_dvd (gcd_dvd_left _ _)
#align squarefree.gcd_left Squarefree.gcd_left

end SquarefreeGcdOfSquarefree

namespace multiplicity

section CommMonoid

variable [CommMonoid R] [DecidableRel (Dvd.dvd : R ‚Üí R ‚Üí Prop)]

theorem squarefree_iff_multiplicity_le_one (r : R) :
    Squarefree r ‚Üî ‚àÄ x : R, multiplicity x r ‚â§ 1 ‚à® IsUnit x := by
  refine' forall_congr' fun a => _
  -- ‚ä¢ a * a ‚à£ r ‚Üí IsUnit a ‚Üî multiplicity a r ‚â§ 1 ‚à® IsUnit a
  rw [‚Üê sq, pow_dvd_iff_le_multiplicity, or_iff_not_imp_left, not_le, imp_congr _ Iff.rfl]
  -- ‚ä¢ ‚Üë2 ‚â§ multiplicity a r ‚Üî 1 < multiplicity a r
  rw [‚Üêone_add_one_eq_two]
  -- ‚ä¢ ‚Üë(1 + 1) ‚â§ multiplicity a r ‚Üî 1 < multiplicity a r
  simpa using PartENat.add_one_le_iff_lt (PartENat.natCast_ne_top 1)
  -- üéâ no goals
#align multiplicity.squarefree_iff_multiplicity_le_one multiplicity.squarefree_iff_multiplicity_le_one

end CommMonoid

section CancelCommMonoidWithZero

variable [CancelCommMonoidWithZero R] [WfDvdMonoid R]

theorem finite_prime_left {a b : R} (ha : Prime a) (hb : b ‚â† 0) : multiplicity.Finite a b := by
  classical
    revert hb
    refine'
      WfDvdMonoid.induction_on_irreducible b (fun c => c.irrefl.elim) (fun u hu _ => _)
        fun b p hb hp ih _ => _
    ¬∑ rw [multiplicity.finite_iff_dom, multiplicity.isUnit_right ha.not_unit hu]
      exact PartENat.dom_natCast 0
    ¬∑ refine'
        multiplicity.finite_mul ha
          (multiplicity.finite_iff_dom.mpr
            (PartENat.dom_of_le_natCast (show multiplicity a p ‚â§ ‚Üë1 from _)))
          (ih hb)
      norm_cast
      exact
        ((multiplicity.squarefree_iff_multiplicity_le_one p).mp hp.squarefree a).resolve_right
          ha.not_unit
#align multiplicity.finite_prime_left multiplicity.finite_prime_left

end CancelCommMonoidWithZero

end multiplicity

section Irreducible

variable [CommMonoidWithZero R] [WfDvdMonoid R]

theorem irreducible_sq_not_dvd_iff_eq_zero_and_no_irreducibles_or_squarefree (r : R) :
    (‚àÄ x : R, Irreducible x ‚Üí ¬¨x * x ‚à£ r) ‚Üî (r = 0 ‚àß ‚àÄ x : R, ¬¨Irreducible x) ‚à® Squarefree r := by
  symm
  -- ‚ä¢ (r = 0 ‚àß ‚àÄ (x : R), ¬¨Irreducible x) ‚à® Squarefree r ‚Üî ‚àÄ (x : R), Irreducible  ‚Ä¶
  constructor
  -- ‚ä¢ (r = 0 ‚àß ‚àÄ (x : R), ¬¨Irreducible x) ‚à® Squarefree r ‚Üí ‚àÄ (x : R), Irreducible  ‚Ä¶
  ¬∑ rintro (‚ü®rfl, h‚ü© | h)
    -- ‚ä¢ ‚àÄ (x : R), Irreducible x ‚Üí ¬¨x * x ‚à£ 0
    ¬∑ simpa using h
      -- üéâ no goals
    intro x hx t
    -- ‚ä¢ False
    exact hx.not_unit (h x t)
    -- üéâ no goals
  intro h
  -- ‚ä¢ (r = 0 ‚àß ‚àÄ (x : R), ¬¨Irreducible x) ‚à® Squarefree r
  rcases eq_or_ne r 0 with (rfl | hr)
  -- ‚ä¢ (0 = 0 ‚àß ‚àÄ (x : R), ¬¨Irreducible x) ‚à® Squarefree 0
  ¬∑ exact Or.inl (by simpa using h)
    -- üéâ no goals
  right
  -- ‚ä¢ Squarefree r
  intro x hx
  -- ‚ä¢ IsUnit x
  by_contra i
  -- ‚ä¢ False
  have : x ‚â† 0 := by
    rintro rfl
    apply hr
    simpa only [zero_dvd_iff, mul_zero] using hx
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := WfDvdMonoid.exists_irreducible_factor i this
  -- ‚ä¢ False
  exact h _ hj‚ÇÅ ((mul_dvd_mul hj‚ÇÇ hj‚ÇÇ).trans hx)
  -- üéâ no goals
#align irreducible_sq_not_dvd_iff_eq_zero_and_no_irreducibles_or_squarefree irreducible_sq_not_dvd_iff_eq_zero_and_no_irreducibles_or_squarefree

theorem squarefree_iff_irreducible_sq_not_dvd_of_ne_zero {r : R} (hr : r ‚â† 0) :
    Squarefree r ‚Üî ‚àÄ x : R, Irreducible x ‚Üí ¬¨x * x ‚à£ r := by
  simpa [hr] using (irreducible_sq_not_dvd_iff_eq_zero_and_no_irreducibles_or_squarefree r).symm
  -- üéâ no goals
#align squarefree_iff_irreducible_sq_not_dvd_of_ne_zero squarefree_iff_irreducible_sq_not_dvd_of_ne_zero

theorem squarefree_iff_irreducible_sq_not_dvd_of_exists_irreducible {r : R}
    (hr : ‚àÉ x : R, Irreducible x) : Squarefree r ‚Üî ‚àÄ x : R, Irreducible x ‚Üí ¬¨x * x ‚à£ r := by
  rw [irreducible_sq_not_dvd_iff_eq_zero_and_no_irreducibles_or_squarefree, ‚Üê not_exists]
  -- ‚ä¢ Squarefree r ‚Üî (r = 0 ‚àß ¬¨‚àÉ x, Irreducible x) ‚à® Squarefree r
  simp only [hr, not_true, false_or_iff, and_false_iff]
  -- üéâ no goals
#align squarefree_iff_irreducible_sq_not_dvd_of_exists_irreducible squarefree_iff_irreducible_sq_not_dvd_of_exists_irreducible

end Irreducible

section IsRadical

variable [CancelCommMonoidWithZero R]

theorem IsRadical.squarefree {x : R} (h0 : x ‚â† 0) (h : IsRadical x) : Squarefree x := by
  rintro z ‚ü®w, rfl‚ü©
  -- ‚ä¢ IsUnit z
  specialize h 2 (z * w) ‚ü®w, by simp_rw [pow_two, mul_left_comm, ‚Üê mul_assoc]‚ü©
  -- ‚ä¢ IsUnit z
  rwa [‚Üê one_mul (z * w), mul_assoc, mul_dvd_mul_iff_right, ‚Üê isUnit_iff_dvd_one] at h
  -- ‚ä¢ z * w ‚â† 0
  rw [mul_assoc, mul_ne_zero_iff] at h0; exact h0.2
  -- ‚ä¢ z * w ‚â† 0
                                         -- üéâ no goals
#align is_radical.squarefree IsRadical.squarefree

variable [GCDMonoid R]

theorem Squarefree.isRadical {x : R} (hx : Squarefree x) : IsRadical x :=
  (isRadical_iff_pow_one_lt 2 one_lt_two).2 fun y hy =>
    And.right <|
      (dvd_gcd_iff x x y).1
        (by
          by_cases gcd x y = 0
          -- ‚ä¢ x ‚à£ gcd x y
          -- ‚ä¢ x ‚à£ gcd x y
          ¬∑ rw [h]
            -- ‚ä¢ x ‚à£ 0
            apply dvd_zero
            -- üéâ no goals
          replace hy := ((dvd_gcd_iff x x _).2 ‚ü®dvd_rfl, hy‚ü©).trans gcd_pow_right_dvd_pow_gcd
          -- ‚ä¢ x ‚à£ gcd x y
          obtain ‚ü®z, hz‚ü© := gcd_dvd_left x y
          -- ‚ä¢ x ‚à£ gcd x y
          nth_rw 1 [hz] at hy ‚ä¢
          -- ‚ä¢ gcd x y * z ‚à£ gcd x y
          rw [pow_two, mul_dvd_mul_iff_left h] at hy
          -- ‚ä¢ gcd x y * z ‚à£ gcd x y
          obtain ‚ü®w, hw‚ü© := hy
          -- ‚ä¢ gcd x y * z ‚à£ gcd x y
          exact (hx z ‚ü®w, by rwa [mul_right_comm, ‚Üê hw]‚ü©).mul_right_dvd.2 dvd_rfl)
          -- üéâ no goals
#align squarefree.is_radical Squarefree.isRadical

theorem isRadical_iff_squarefree_or_zero {x : R} : IsRadical x ‚Üî Squarefree x ‚à® x = 0 :=
  ‚ü®fun hx => (em <| x = 0).elim Or.inr fun h => Or.inl <| hx.squarefree h,
    Or.rec Squarefree.isRadical <| by
      rintro rfl
      -- ‚ä¢ IsRadical 0
      rw [zero_isRadical_iff]
      -- ‚ä¢ IsReduced R
      infer_instance‚ü©
      -- üéâ no goals
#align is_radical_iff_squarefree_or_zero isRadical_iff_squarefree_or_zero

theorem isRadical_iff_squarefree_of_ne_zero {x : R} (h : x ‚â† 0) : IsRadical x ‚Üî Squarefree x :=
  ‚ü®IsRadical.squarefree h, Squarefree.isRadical‚ü©
#align is_radical_iff_squarefree_of_ne_zero isRadical_iff_squarefree_of_ne_zero

end IsRadical

namespace UniqueFactorizationMonoid

variable [CancelCommMonoidWithZero R] [UniqueFactorizationMonoid R]

theorem squarefree_iff_nodup_normalizedFactors [NormalizationMonoid R] [DecidableEq R] {x : R}
    (x0 : x ‚â† 0) : Squarefree x ‚Üî Multiset.Nodup (normalizedFactors x) := by
  have drel : DecidableRel (Dvd.dvd : R ‚Üí R ‚Üí Prop) := by classical infer_instance
  -- ‚ä¢ Squarefree x ‚Üî Multiset.Nodup (normalizedFactors x)
  rw [multiplicity.squarefree_iff_multiplicity_le_one, Multiset.nodup_iff_count_le_one]
  -- ‚ä¢ (‚àÄ (x_1 : R), multiplicity x_1 x ‚â§ 1 ‚à® IsUnit x_1) ‚Üî ‚àÄ (a : R), Multiset.cou ‚Ä¶
  haveI := nontrivial_of_ne x 0 x0
  -- ‚ä¢ (‚àÄ (x_1 : R), multiplicity x_1 x ‚â§ 1 ‚à® IsUnit x_1) ‚Üî ‚àÄ (a : R), Multiset.cou ‚Ä¶
  constructor <;> intro h a
  -- ‚ä¢ (‚àÄ (x_1 : R), multiplicity x_1 x ‚â§ 1 ‚à® IsUnit x_1) ‚Üí ‚àÄ (a : R), Multiset.cou ‚Ä¶
                  -- ‚ä¢ Multiset.count a (normalizedFactors x) ‚â§ 1
                  -- ‚ä¢ multiplicity a x ‚â§ 1 ‚à® IsUnit a
  ¬∑ by_cases hmem : a ‚àà normalizedFactors x
    -- ‚ä¢ Multiset.count a (normalizedFactors x) ‚â§ 1
    ¬∑ have ha := irreducible_of_normalized_factor _ hmem
      -- ‚ä¢ Multiset.count a (normalizedFactors x) ‚â§ 1
      rcases h a with (h | h)
      -- ‚ä¢ Multiset.count a (normalizedFactors x) ‚â§ 1
      ¬∑ rw [‚Üê normalize_normalized_factor _ hmem]
        -- ‚ä¢ Multiset.count (‚Üënormalize a) (normalizedFactors x) ‚â§ 1
        rw [multiplicity_eq_count_normalizedFactors ha x0] at h
        -- ‚ä¢ Multiset.count (‚Üënormalize a) (normalizedFactors x) ‚â§ 1
        assumption_mod_cast
        -- üéâ no goals
      ¬∑ have := ha.1
        -- ‚ä¢ Multiset.count a (normalizedFactors x) ‚â§ 1
        contradiction
        -- üéâ no goals
    ¬∑ simp [Multiset.count_eq_zero_of_not_mem hmem]
      -- üéâ no goals
  ¬∑ rw [or_iff_not_imp_right]
    -- ‚ä¢ ¬¨IsUnit a ‚Üí multiplicity a x ‚â§ 1
    intro hu
    -- ‚ä¢ multiplicity a x ‚â§ 1
    by_cases h0 : a = 0
    -- ‚ä¢ multiplicity a x ‚â§ 1
    ¬∑ simp [h0, x0]
      -- üéâ no goals
    rcases WfDvdMonoid.exists_irreducible_factor hu h0 with ‚ü®b, hib, hdvd‚ü©
    -- ‚ä¢ multiplicity a x ‚â§ 1
    apply le_trans (multiplicity.multiplicity_le_multiplicity_of_dvd_left hdvd)
    -- ‚ä¢ multiplicity b x ‚â§ 1
    rw [multiplicity_eq_count_normalizedFactors hib x0]
    -- ‚ä¢ ‚Üë(Multiset.count (‚Üënormalize b) (normalizedFactors x)) ‚â§ 1
    specialize h (normalize b)
    -- ‚ä¢ ‚Üë(Multiset.count (‚Üënormalize b) (normalizedFactors x)) ‚â§ 1
    assumption_mod_cast
    -- üéâ no goals
#align unique_factorization_monoid.squarefree_iff_nodup_normalized_factors UniqueFactorizationMonoid.squarefree_iff_nodup_normalizedFactors

theorem dvd_pow_iff_dvd_of_squarefree {x y : R} {n : ‚Ñï} (hsq : Squarefree x) (h0 : n ‚â† 0) :
    x ‚à£ y ^ n ‚Üî x ‚à£ y := by
  classical
    haveI := UniqueFactorizationMonoid.toGCDMonoid R
    exact ‚ü®hsq.isRadical n y, fun h => h.pow h0‚ü©
#align unique_factorization_monoid.dvd_pow_iff_dvd_of_squarefree UniqueFactorizationMonoid.dvd_pow_iff_dvd_of_squarefree

end UniqueFactorizationMonoid

namespace Int

@[simp]
theorem squarefree_natAbs {n : ‚Ñ§} : Squarefree n.natAbs ‚Üî Squarefree n := by
  simp_rw [Squarefree, natAbs_surjective.forall, ‚Üê natAbs_mul, natAbs_dvd_natAbs,
    isUnit_iff_natAbs_eq, Nat.isUnit_iff]
#align int.squarefree_nat_abs Int.squarefree_natAbs

@[simp]
theorem squarefree_coe_nat {n : ‚Ñï} : Squarefree (n : ‚Ñ§) ‚Üî Squarefree n := by
  rw [‚Üê squarefree_natAbs, natAbs_ofNat]
  -- üéâ no goals
#align int.squarefree_coe_nat Int.squarefree_coe_nat

end Int
