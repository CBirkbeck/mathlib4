/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Algebra.Category.GroupCat.FilteredColimits

#align_import algebra.category.Ring.filtered_colimits from "leanprover-community/mathlib"@"c43486ecf2a5a17479a32ce09e4818924145e90e"

/-!
# The forgetful functor from (commutative) (semi-) rings preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J ‚•§ SemiRing`.
We show that the colimit of `F ‚ãô forget‚ÇÇ SemiRing Mon` (in `Mon`) carries the structure of a
semiring, thereby showing that the forgetful functor `forget‚ÇÇ SemiRing Mon` preserves filtered
colimits. In particular, this implies that `forget SemiRing` preserves filtered colimits.
Similarly for `CommSemiRing`, `Ring` and `CommRing`.

-/


universe v u w

noncomputable section

open Classical

open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.IsFiltered renaming max ‚Üí max' -- avoid name collision with `_root_.max`.

open AddMonCat.FilteredColimits (colimit_zero_eq colimit_add_mk_eq)

open MonCat.FilteredColimits (colimit_one_eq colimit_mul_mk_eq)

namespace SemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviations `R` and `R.mk` below, without
-- passing around `F` all the time.
variable {J : Type v} [Category.{w} J] (F : J ‚•§ SemiRingCat.{u}) [UnivLE.{v, u}]

-- This instance is needed below in `colimit_semiring`, during the verification of the
-- semiring axioms.
instance semiringObj (j : J) :
    Semiring (((F ‚ãô forget‚ÇÇ SemiRingCat MonCat) ‚ãô forget MonCat).obj j) :=
  show Semiring (F.obj j) by infer_instance
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.semiring_obj SemiRingCat.FilteredColimits.semiringObj

variable [IsFiltered J]

/-- The colimit of `F ‚ãô forget‚ÇÇ SemiRing Mon` in the category `Mon`.
In the following, we will show that this has the structure of a semiring.
-/
abbrev R : MonCat.{u} :=
  MonCat.FilteredColimits.colimit (F ‚ãô forget‚ÇÇ SemiRingCat MonCat.{u})
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.R SemiRingCat.FilteredColimits.R

instance colimitSemiring : Semiring.{u} <| (R F) :=
  { (R F).str,
    AddCommMonCat.FilteredColimits.colimitAddCommMonoid
      (F ‚ãô forget‚ÇÇ SemiRingCat AddCommMonCat.{u}) with
    mul_zero := fun x => by
      obtain ‚ü®‚ü®j, x : F.obj j‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [mul_zero x]
      rfl
    zero_mul := fun x => by
      obtain ‚ü®‚ü®j, x : F.obj j‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ x
      erw [colimit_zero_eq _ j, colimit_mul_mk_eq _ ‚ü®j, _‚ü© ‚ü®j, _‚ü© j (ùüô j) (ùüô j)]
      rw [CategoryTheory.Functor.map_id]
      dsimp
      rw [zero_mul x]
      rfl
    left_distrib := fun x y z => by
      obtain ‚ü®‚ü®j‚ÇÅ, x : F.obj j‚ÇÅ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ x
      obtain ‚ü®‚ü®j‚ÇÇ, y : F.obj j‚ÇÇ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ y
      obtain ‚ü®‚ü®j‚ÇÉ, z : F.obj j‚ÇÉ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ z
      let k := IsFiltered.max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let f := IsFiltered.firstToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let g := IsFiltered.secondToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let h := IsFiltered.thirdToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      erw [colimit_add_mk_eq _ ‚ü®j‚ÇÇ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k g h, colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®k, _‚ü© k f (ùüô k),
        colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÇ, _‚ü© k f g, colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k f h,
        colimit_add_mk_eq _ ‚ü®k, _‚ü© ‚ü®k, _‚ü© k (ùüô k) (ùüô k)]
      simp only [CategoryTheory.Functor.map_id, id_apply]
      erw [left_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl
    right_distrib := fun x y z => by
      obtain ‚ü®‚ü®j‚ÇÅ, x : F.obj j‚ÇÅ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ x
      obtain ‚ü®‚ü®j‚ÇÇ, y : F.obj j‚ÇÇ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ y
      obtain ‚ü®‚ü®j‚ÇÉ, z : F.obj j‚ÇÉ‚ü©, rfl‚ü© := MonCat.FilteredColimits.M.surjective_mk _ z
      let k := IsFiltered.max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let f := IsFiltered.firstToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let g := IsFiltered.secondToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      let h := IsFiltered.thirdToMax‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ
      erw [colimit_add_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÇ, _‚ü© k f g, colimit_mul_mk_eq _ ‚ü®k, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k (ùüô k) h,
        colimit_mul_mk_eq _ ‚ü®j‚ÇÅ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k f h, colimit_mul_mk_eq _ ‚ü®j‚ÇÇ, _‚ü© ‚ü®j‚ÇÉ, _‚ü© k g h,
        colimit_add_mk_eq _ ‚ü®k, _‚ü© ‚ü®k, _‚ü© k (ùüô k) (ùüô k)]
      simp only [CategoryTheory.Functor.map_id, id_apply]
      erw [right_distrib (F.map f x) (F.map g y) (F.map h z)]
      rfl }
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.colimit_semiring SemiRingCat.FilteredColimits.colimitSemiring

/-- The bundled semiring giving the filtered colimit of a diagram. -/
def colimit : SemiRingCat.{u} :=
  SemiRingCat.of <| R F
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.colimit SemiRingCat.FilteredColimits.colimit

/-- The cocone over the proposed colimit semiring. -/
def colimitCocone : Cocone F where
  pt := colimit F
  Œπ :=
    { app := fun j =>
        { (MonCat.FilteredColimits.colimitCocone
            (F ‚ãô forget‚ÇÇ SemiRingCat.{u} MonCat)).Œπ.app j,
            (AddCommMonCat.FilteredColimits.colimitCocone
              (F ‚ãô forget‚ÇÇ SemiRingCat.{u} AddCommMonCat)).Œπ.app j with }
      naturality := fun {_ _} f =>
        RingHom.coe_inj ((Types.colimitCocone (F ‚ãô forget SemiRingCat)).Œπ.naturality f) }
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.colimit_cocone SemiRingCat.FilteredColimits.colimitCocone

/-- The proposed colimit cocone is a colimit in `SemiRing`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone F where
  desc t :=
    { (MonCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ SemiRingCat MonCat)).desc
        ((forget‚ÇÇ SemiRingCat MonCat).mapCocone t),
      (AddCommMonCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ SemiRingCat AddCommMonCat)).desc
        ((forget‚ÇÇ SemiRingCat AddCommMonCat).mapCocone t) with }
  fac t j :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget SemiRingCat)).fac
        ((forget SemiRingCat).mapCocone t) j
  uniq t _ h :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget SemiRingCat)).uniq
        ((forget SemiRingCat).mapCocone t) _ fun j => funext fun x => RingHom.congr_fun (h j) x
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.colimit_cocone_is_colimit SemiRingCat.FilteredColimits.colimitCoconeIsColimit

instance forget‚ÇÇMonPreservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ SemiRingCat MonCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
          (MonCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ SemiRingCat MonCat.{u})) }
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.forget‚ÇÇ_Mon_preserves_filtered_colimits SemiRingCat.FilteredColimits.forget‚ÇÇMonPreservesFilteredColimits

instance forgetPreservesFilteredColimits : PreservesFilteredColimits (forget SemiRingCat.{u}) :=
  Limits.compPreservesFilteredColimits (forget‚ÇÇ SemiRingCat MonCat) (forget MonCat.{u})
set_option linter.uppercaseLean3 false in
#align SemiRing.filtered_colimits.forget_preserves_filtered_colimits SemiRingCat.FilteredColimits.forgetPreservesFilteredColimits

end

end SemiRingCat.FilteredColimits

namespace CommSemiRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [Category.{w} J] [IsFiltered J] (F : J ‚•§ CommSemiRingCat.{u}) [UnivLE.{v, u}]

/-- The colimit of `F ‚ãô forget‚ÇÇ CommSemiRing SemiRing` in the category `SemiRing`.
In the following, we will show that this has the structure of a _commutative_ semiring.
-/
abbrev R : SemiRingCat.{u} :=
  SemiRingCat.FilteredColimits.colimit (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat)
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.R CommSemiRingCat.FilteredColimits.R

instance colimitCommSemiring : CommSemiring.{u} <| R F :=
  { (R F).str,
    CommMonCat.FilteredColimits.colimitCommMonoid
      (F ‚ãô forget‚ÇÇ CommSemiRingCat CommMonCat) with }
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.colimit_comm_semiring CommSemiRingCat.FilteredColimits.colimitCommSemiring

/-- The bundled commutative semiring giving the filtered colimit of a diagram. -/
def colimit : CommSemiRingCat.{u} :=
  CommSemiRingCat.of <| R F
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.colimit CommSemiRingCat.FilteredColimits.colimit

/-- The cocone over the proposed colimit commutative semiring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { (SemiRingCat.FilteredColimits.colimitCocone
          (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat.{u})).Œπ with }
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.colimit_cocone CommSemiRingCat.FilteredColimits.colimitCocone

/-- The proposed colimit cocone is a colimit in `CommSemiRing`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone F where
  desc t :=
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat)).desc
      ((forget‚ÇÇ CommSemiRingCat SemiRingCat).mapCocone t)
  fac t j :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit.{v, u} (F ‚ãô forget CommSemiRingCat)).fac
        ((forget CommSemiRingCat).mapCocone t) j
  uniq t _ h :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget CommSemiRingCat)).uniq
        ((forget CommSemiRingCat).mapCocone t) _ fun j => funext fun x => RingHom.congr_fun (h j) x
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.colimit_cocone_is_colimit CommSemiRingCat.FilteredColimits.colimitCoconeIsColimit

instance forget‚ÇÇSemiRingPreservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommSemiRingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ CommSemiRingCat SemiRingCat.{u})) }
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.forget‚ÇÇ_SemiRing_preserves_filtered_colimits CommSemiRingCat.FilteredColimits.forget‚ÇÇSemiRingPreservesFilteredColimits

instance forgetPreservesFilteredColimits : PreservesFilteredColimits (forget CommSemiRingCat.{u}) :=
  Limits.compPreservesFilteredColimits (forget‚ÇÇ CommSemiRingCat SemiRingCat)
    (forget SemiRingCat.{u})
set_option linter.uppercaseLean3 false in
#align CommSemiRing.filtered_colimits.forget_preserves_filtered_colimits CommSemiRingCat.FilteredColimits.forgetPreservesFilteredColimits

end

end CommSemiRingCat.FilteredColimits

namespace RingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [Category.{w} J] [IsFiltered J] (F : J ‚•§ RingCat.{u}) [UnivLE.{v, u}]

/-- The colimit of `F ‚ãô forget‚ÇÇ Ring SemiRing` in the category `SemiRing`.
In the following, we will show that this has the structure of a ring.
-/
abbrev R : SemiRingCat.{u} :=
  SemiRingCat.FilteredColimits.colimit (F ‚ãô forget‚ÇÇ RingCat SemiRingCat.{u})
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.R RingCat.FilteredColimits.R

instance colimitRing : Ring.{u} <| R F :=
  { (R F).str,
    AddCommGroupCat.FilteredColimits.colimitAddCommGroup.{v, u}
      (F ‚ãô forget‚ÇÇ RingCat AddCommGroupCat.{u}) with }
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.colimit_ring RingCat.FilteredColimits.colimitRing

/-- The bundled ring giving the filtered colimit of a diagram. -/
def colimit : RingCat.{u} :=
  RingCat.of <| R F
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.colimit RingCat.FilteredColimits.colimit

/-- The cocone over the proposed colimit ring. -/
def colimitCocone : Cocone F where
  pt := colimit F
  Œπ :=
    { (SemiRingCat.FilteredColimits.colimitCocone
          (F ‚ãô forget‚ÇÇ RingCat SemiRingCat)).Œπ with }
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.colimit_cocone RingCat.FilteredColimits.colimitCocone

/-- The proposed colimit cocone is a colimit in `Ring`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone F where
  desc t :=
    (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ RingCat SemiRingCat)).desc
      ((forget‚ÇÇ RingCat SemiRingCat).mapCocone t)
  fac t j :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit.{v, u} (F ‚ãô forget RingCat)).fac
        ((forget RingCat).mapCocone t) j
  uniq t _ h :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget RingCat)).uniq ((forget RingCat).mapCocone t) _
        fun j => funext fun x => RingHom.congr_fun (h j) x
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.colimit_cocone_is_colimit RingCat.FilteredColimits.colimitCoconeIsColimit

instance forget‚ÇÇSemiRingPreservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ RingCat SemiRingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
          (SemiRingCat.FilteredColimits.colimitCoconeIsColimit
            (F ‚ãô forget‚ÇÇ RingCat SemiRingCat.{u})) }
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.forget‚ÇÇ_SemiRing_preserves_filtered_colimits RingCat.FilteredColimits.forget‚ÇÇSemiRingPreservesFilteredColimits

instance forgetPreservesFilteredColimits : PreservesFilteredColimits (forget RingCat.{u}) :=
  Limits.compPreservesFilteredColimits (forget‚ÇÇ RingCat SemiRingCat) (forget SemiRingCat.{u})
set_option linter.uppercaseLean3 false in
#align Ring.filtered_colimits.forget_preserves_filtered_colimits RingCat.FilteredColimits.forgetPreservesFilteredColimits

end

end RingCat.FilteredColimits

namespace CommRingCat.FilteredColimits

section

-- We use parameters here, mainly so we can have the abbreviation `R` below, without
-- passing around `F` all the time.
variable {J : Type v} [Category.{w} J] [IsFiltered J] (F : J ‚•§ CommRingCat.{u}) [UnivLE.{v, u}]

/-- The colimit of `F ‚ãô forget‚ÇÇ CommRing Ring` in the category `Ring`.
In the following, we will show that this has the structure of a _commutative_ ring.
-/
abbrev R : RingCat.{u} :=
  RingCat.FilteredColimits.colimit.{v, u} (F ‚ãô forget‚ÇÇ CommRingCat RingCat.{u})
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.R CommRingCat.FilteredColimits.R

instance colimitCommRing : CommRing.{u} <| R F :=
  { (R F).str,
    CommSemiRingCat.FilteredColimits.colimitCommSemiring
      (F ‚ãô forget‚ÇÇ CommRingCat CommSemiRingCat.{u}) with }
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.colimit_comm_ring CommRingCat.FilteredColimits.colimitCommRing

/-- The bundled commutative ring giving the filtered colimit of a diagram. -/
def colimit : CommRingCat.{u} :=
  CommRingCat.of <| R F
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.colimit CommRingCat.FilteredColimits.colimit

/-- The cocone over the proposed colimit commutative ring. -/
def colimitCocone : Cocone F where
  pt := colimit.{v, u} F
  Œπ :=
    { (RingCat.FilteredColimits.colimitCocone (F ‚ãô forget‚ÇÇ CommRingCat RingCat)).Œπ with }
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.colimit_cocone CommRingCat.FilteredColimits.colimitCocone

/-- The proposed colimit cocone is a colimit in `CommRing`. -/
def colimitCoconeIsColimit : IsColimit <| colimitCocone.{v, u} F where
  desc t :=
    (RingCat.FilteredColimits.colimitCoconeIsColimit
          (F ‚ãô forget‚ÇÇ CommRingCat RingCat)).desc
      ((forget‚ÇÇ CommRingCat RingCat).mapCocone t)
  fac t j :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit.{v, u} (F ‚ãô forget CommRingCat)).fac
        ((forget CommRingCat).mapCocone t) j
  uniq t _ h :=
    RingHom.coe_inj <|
      (Types.colimitCoconeIsColimit (F ‚ãô forget CommRingCat)).uniq
        ((forget CommRingCat).mapCocone t) _ fun j => funext fun x => RingHom.congr_fun (h j) x
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.colimit_cocone_is_colimit CommRingCat.FilteredColimits.colimitCoconeIsColimit

instance forget‚ÇÇRingPreservesFilteredColimits :
    PreservesFilteredColimits (forget‚ÇÇ CommRingCat RingCat.{u}) where
  preserves_filtered_colimits {J hJ1 _} :=
    letI : Category J := hJ1
    { preservesColimit := fun {F} =>
        preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u, u} F)
          (RingCat.FilteredColimits.colimitCoconeIsColimit (F ‚ãô forget‚ÇÇ CommRingCat RingCat.{u})) }
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.forget‚ÇÇ_Ring_preserves_filtered_colimits CommRingCat.FilteredColimits.forget‚ÇÇRingPreservesFilteredColimits

instance forgetPreservesFilteredColimits : PreservesFilteredColimits (forget CommRingCat.{u}) :=
  Limits.compPreservesFilteredColimits (forget‚ÇÇ CommRingCat RingCat) (forget RingCat.{u})
set_option linter.uppercaseLean3 false in
#align CommRing.filtered_colimits.forget_preserves_filtered_colimits CommRingCat.FilteredColimits.forgetPreservesFilteredColimits

end

end CommRingCat.FilteredColimits
