/-
Copyright (c) 2025 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.Algebra.Category.ModuleCat.Limits

/-!
# A categorical description of the center of a ring

In this file we prove that the center of a ring `R` is isomorphic to `End (ùü≠ R-Mod)` the
endomorphism ring of the identity functor on the category of `R`-modules. Consequently, the ring
structure of a commutative ring is complete determined by its module category.

## Main results

- `Subring.centerEquivEndIdFunctor`: the center of a ring `R` is isomorphic to `End (ùü≠ R-Mod)`.
- `RingEquiv.ofModuleCatEquiv`: if two commutative rings have equivalent module categories, they are
  isomorphic as rings.

-/

universe u u' v v'

variable (R : Type u) [Ring R]

open CategoryTheory

/--
For any ring `R`, the center of `R` is isomorphic to `End (ùü≠ (ModuleCat R))`, the endomorphism ring
of the identity functor on the category of `R`-modules.
-/
@[simps]
def Subring.centerEquivEndIdFunctor : Subring.center R ‚âÉ+* End (ùü≠ (ModuleCat.{max v u} R)) where
  toFun x :=
    { app M := ModuleCat.ofHom
        { toFun := (x.1 ‚Ä¢ ¬∑)
          map_add' := by aesop
          map_smul' r m := by simp [‚Üê mul_smul, Subring.mem_center_iff.1 x.2 r] } }
  map_add' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact add_smul x.1 y.1 m
  map_mul' x y := by
    apply NatTrans.ext
    ext M (m : M)
    exact mul_smul x.1 y.1 m
  invFun f := ‚ü®f.app (.of R <| ULift R) |>.hom (1 : ULift R) |>.down, by
    rw [Subring.mem_center_iff]
    intro r
    have := congr(($(f.naturality (X := .of R <| ULift R) (Y := .of R <| ULift R) (ModuleCat.ofHom
      { toFun x := x * .up r
        map_add' := by simp [add_mul]
        map_smul' := by intros; ext; simp [mul_assoc] })).hom (1 : ULift R)).down)
    simp only [Functor.id_obj, Functor.id_map, ModuleCat.hom_comp, LinearMap.coe_comp,
      LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply, one_mul, ULift.mul_down] at this
    erw [‚Üê this]
    have := congr($((f.app (ModuleCat.of R <| ULift R)).hom.map_smul r (1 : ULift R)).down)
    rw [show r ‚Ä¢ (1 : ULift R) = .up r by ext; simp] at this
    simp only [Functor.id_obj, ULift.smul_down, smul_eq_mul] at this
    exact this.symm‚ü©
  left_inv x := by simp
  right_inv f := by
    apply NatTrans.ext
    ext M (m : M)
    simp only [Functor.id_obj, LinearMap.coe_mk, AddHom.coe_mk]
    have := congr($(f.naturality (X := .of R <| ULift R) (Y := .of R M)
      (ModuleCat.ofHom
        { toFun x := x ‚Ä¢ m
          map_add' := by simp [add_smul]
          map_smul' x y := by simp [mul_smul] })).hom (1 : ULift R))
    simp only [ModuleCat.of_coe, Functor.id_obj, Functor.id_map, ModuleCat.hom_comp,
      LinearMap.coe_comp, LinearMap.coe_mk, AddHom.coe_mk, Function.comp_apply,
      one_smul] at this
    exact this.symm

/--
For any two commutative rings `R` and `S`, if the categories of `R`-modules and `S`-modules are
equivalent, then `R` and `S` are isomorphic as rings.
-/
def RingEquiv.ofModuleCatEquiv {R : Type u} {S : Type u'} [CommRing R] [CommRing S]
    (e : ModuleCat.{max u v} R ‚âå ModuleCat.{max u' v'} S) : R ‚âÉ+* S :=
  letI : e.functor.Additive := Functor.additive_of_preserves_binary_products e.functor
  let i‚ÇÅ : R ‚âÉ+* (‚ä§ : Subring R) := Subring.topEquiv.symm
  let i‚ÇÇ : (‚ä§ : Subring R) ‚âÉ+* Subring.center R := Subring.center_eq_top R ‚ñ∏ .refl _
  let i‚ÇÑ : Subring.center S ‚âÉ+* (‚ä§ : Subring S) := Subring.center_eq_top S ‚ñ∏ .refl _
  let i‚ÇÖ : (‚ä§ : Subring S) ‚âÉ+* S := Subring.topEquiv
  let i : End (ùü≠ (ModuleCat.{max u v} R)) ‚âÉ+* End (ùü≠ (ModuleCat.{max u' v'} S)) :=
  { toFun f := .of
      { app N :=
          e.counitInv.app N ‚â´ e.functor.map (f.app (e.inverse.obj N)) ‚â´
          e.counit.app N
        naturality M N g := by
          -- Is `aesop_cat` expected to solve this?
          have eq :=
            e.counitInv.app M ‚â´=
            congr(e.functor.map $(f.naturality (e.inverse.map g))) =‚â´
            e.counit.app N
          simpa using eq}
    invFun f := .of
      { app N := e.unit.app N ‚â´ e.inverse.map (f.app (e.functor.obj N)) ‚â´ e.unitInv.app N
        naturality M N g := by
          -- Is `aesop_cat` expected to solve this?
          have eq :=
            e.unit.app M ‚â´=
            congr(e.inverse.map $(f.naturality (e.functor.map g))) =‚â´
            e.unitInv.app N
          simp only [Functor.id_obj, Functor.comp_obj, Functor.id_map, Functor.map_comp,
            Equivalence.inv_fun_map, Category.assoc, Iso.hom_inv_id_app_assoc,
            Iso.hom_inv_id_app, Category.comp_id] at eq
          simpa using eq }
    left_inv f := by
      apply NatTrans.ext
      ext N : 1
      have eq := f.naturality (e.unit.app N) =‚â´ e.unitInv.app N
      simpa [End.of] using eq
    right_inv f := by
      apply NatTrans.ext
      ext N : 1
      have eq := e.counitInv.app N ‚â´= f.naturality (e.counit.app N) |>.symm
      simpa [End.of] using eq
    map_mul' f g := by
      apply NatTrans.ext
      ext N : 1
      simp only [Functor.id_obj, Functor.comp_obj, End.mul_def, NatTrans.comp_app, Functor.map_comp,
        Category.assoc]
      simp [End.of]
    map_add' f g := by
      apply NatTrans.ext
      ext N : 1
      change _‚â´ e.functor.map (_ + _) ‚â´ _  = _ ‚â´ _ + _ ‚â´ _
      simp [Functor.map_add] }
  i‚ÇÅ.trans <| i‚ÇÇ.trans <| (Subring.centerEquivEndIdFunctor R).trans <|
    i.trans <| (Subring.centerEquivEndIdFunctor S).symm.trans <| i‚ÇÑ.trans i‚ÇÖ
