/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
import Mathlib.Algebra.Category.ModuleCat.Presheaf

/-!
# Change of presheaf of rings

In this file, we define the restriction of scalars functor
`restrictScalars Œ± : PresheafOfModules.{v} R' ‚•§ PresheafOfModules.{v} R`
attached to a morphism of presheaves of rings `Œ± : R ‚ü∂ R'`.

-/

universe v v' u u'

open CategoryTheory

namespace PresheafOfModules

variable {C : Type u'} [Category.{v'} C] {R R' : C·µí·µñ ‚•§ RingCat.{u}}

open ModuleCat.restrictScalars

/-- The restriction of scalars of presheaves of modules, on objects. -/
@[simps]
noncomputable def restrictScalarsObj (M' : PresheafOfModules.{v} R') (Œ± : R ‚ü∂ R') :
    PresheafOfModules R where
  obj := fun X ‚Ü¶ (ModuleCat.restrictScalars (Œ±.app X)).obj (M'.obj X)
  map := fun {X Y} f ‚Ü¶ ModuleCat.ofHom
    { toFun x := into _ (into _ (out _ ((M'.map f).hom (out _ x))))
      map_add' _ _ := by simp
      map_smul' r x := by
        ext
        dsimp
        rw [smul_def, out_into, M'.map_smul, smul_def]
        have eq := RingHom.congr_fun (Œ±.naturality f) r
        dsimp at eq
        rw [‚Üê eq]
        simp }

/-- The restriction of scalars functor `PresheafOfModules R' ‚•§ PresheafOfModules R`
induced by a morphism of presheaves of rings `R ‚ü∂ R'`. -/
@[simps]
noncomputable def restrictScalars (Œ± : R ‚ü∂ R') :
    PresheafOfModules.{v} R' ‚•§ PresheafOfModules.{v} R where
  obj M' := M'.restrictScalarsObj Œ±
  map œÜ' :=
    { app := fun X ‚Ü¶ (ModuleCat.restrictScalars (Œ±.app X)).map (Hom.app œÜ' X)
      naturality := fun {X Y} f ‚Ü¶ by
        ext x
        exact congr_arg (into _) (naturality_apply œÜ' f (out _ x)) }

instance (Œ± : R ‚ü∂ R') : (restrictScalars.{v} Œ±).Additive where

/-- The isomorphism `restrictScalars Œ± ‚ãô toPresheaf R ‚âÖ toPresheaf R'` for any
morphism of presheaves of rings `Œ± : R ‚ü∂ R'`. -/
noncomputable def restrictScalarsCompToPresheaf (Œ± : R ‚ü∂ R') :
    restrictScalars.{v} Œ± ‚ãô toPresheaf R ‚âÖ toPresheaf R' where
  hom.app X := { app M := AddCommGrp.ofHom <|
    (Module.RestrictScalars.outAddEquiv _ _).toAddMonoidHom }
  inv.app X := { app M := AddCommGrp.ofHom <|
    (Module.RestrictScalars.outAddEquiv _ _).symm.toAddMonoidHom }

def restrictScalarsId : ùü≠ (PresheafOfModules R) ‚âÖ restrictScalars (ùüô R) where
  hom.app X := { app M := ModuleCat.ofHom <| Module.RestrictScalars.idEquiv.symm.toLinearMap }
  inv.app X := { app M := ModuleCat.ofHom <| Module.RestrictScalars.idEquiv.toLinearMap }

instance : (restrictScalars (ùüô R)).Full :=
  CategoryTheory.Functor.Full.of_iso (F := (ùü≠ _)) restrictScalarsId

instance (Œ± : R ‚ü∂ R') : (restrictScalars Œ±).Faithful :=
  have _ : (restrictScalars Œ± ‚ãô toPresheaf R).Faithful := Functor.Faithful.of_iso
    (restrictScalarsCompToPresheaf _).symm
  Functor.Faithful.of_comp (restrictScalars Œ±) (toPresheaf R)

end PresheafOfModules
