/-
Copyright (c) 2024 Lean FRO. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.RingTheory.Coalgebra.Equiv
import Mathlib.Algebra.Category.ModuleCat.Basic

/-!
# Category instance for coalgebras over a commutative ring

We introduce the bundled category `CoalgebraCat` of coalgebras over a fixed commutative ring `R`
along with the forgetful functor to `ModuleCat`.

This is mostly a copy-paste from `Mathlib.Algebra.Category.AlgebraCat.Basic`.
-/

set_option linter.uppercaseLean3 false

open CategoryTheory

open CategoryTheory.Limits

universe v u

variable (R : Type u) [CommRing R]

/-- The category of R-coalgebras and their morphisms. -/
structure CoalgebraCat extends ModuleCat.{v} R where
  [isCoalgebra : Coalgebra R carrier]

attribute [instance] CoalgebraCat.isCoalgebra

initialize_simps_projections CoalgebraCat (-isCoalgebra)

namespace CoalgebraCat

instance : CoeSort (CoalgebraCat R) (Type v) :=
  ‚ü®fun C => C.carrier‚ü©

instance : Category (CoalgebraCat.{v} R) where
  Hom A B := A ‚Üí‚Çóc[R] B
  id A := CoalgHom.id R A
  comp f g := g.comp f

instance {M N : CoalgebraCat.{v} R} : FunLike (M ‚ü∂ N) M N :=
  CoalgHom.funLike

instance {M N : CoalgebraCat.{v} R} : CoalgHomClass (M ‚ü∂ N) R M N :=
  CoalgHom.coalgHomClass

instance : ConcreteCategory.{v} (CoalgebraCat.{v} R) where
  forget :=
    { obj := fun R => R
      map := fun f => f.toFun }
  forget_faithful := ‚ü®fun h => CoalgHom.ext (by intros x; dsimp at h; rw [h])‚ü©

instance {S : CoalgebraCat.{v} R} : Coalgebra R ((forget (CoalgebraCat R)).obj S) :=
  (inferInstance : Coalgebra R S.carrier)

instance hasForgetToModule : HasForget‚ÇÇ (CoalgebraCat.{v} R) (ModuleCat.{v} R) where
  forget‚ÇÇ :=
    { obj := fun M => ModuleCat.of R M
      map := fun f => ModuleCat.ofHom f.toLinearMap }

@[simp]
lemma forget‚ÇÇ_module_obj (X : CoalgebraCat.{v} R) :
    (forget‚ÇÇ (CoalgebraCat.{v} R) (ModuleCat.{v} R)).obj X = ModuleCat.of R X :=
  rfl

@[simp]
lemma forget‚ÇÇ_module_map {X Y : CoalgebraCat.{v} R} (f : X ‚ü∂ Y) :
    (forget‚ÇÇ (CoalgebraCat.{v} R) (ModuleCat.{v} R)).map f = ModuleCat.ofHom f.toLinearMap :=
  rfl

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. -/
def of (X : Type v) [AddCommGroup X] [Module R X] [Coalgebra R X] : CoalgebraCat.{v} R :=
  ‚ü®‚ü®X‚ü©‚ü©

/-- Typecheck a `CoalgHom` as a morphism in `CoalgebraCat R`. -/
def ofHom {R : Type u} [CommRing R] {X Y : Type v}
    [AddCommGroup X] [Module R X] [Coalgebra R X] [AddCommGroup Y] [Module R Y] [Coalgebra R Y]
    (f : X ‚Üí‚Çóc[R] Y) : of R X ‚ü∂ of R Y :=
  f

@[simp]
theorem ofHom_apply {R : Type u} [CommRing R] {X Y : Type v}
    [AddCommGroup X] [Module R X] [Coalgebra R X] [AddCommGroup Y] [Module R Y] [Coalgebra R Y]
    (f : X ‚Üí‚Çóc[R] Y) (x : X) : ofHom f x = f x :=
  rfl

noncomputable instance : Inhabited (CoalgebraCat R) :=
  ‚ü®of R R‚ü©

@[simp]
theorem coe_of (X : Type u) [AddCommGroup X] [Module R X] [Coalgebra R X] : (of R X : Type u) = X :=
  rfl

variable {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
coalgebra. -/
@[simps]
def ofSelfIso (M : CoalgebraCat.{v} R) : CoalgebraCat.of R M ‚âÖ M where
  hom := ùüô M
  inv := ùüô M

variable {M N U : ModuleCat.{v} R}

@[simp]
theorem id_apply (m : M) : (ùüô M : M ‚Üí M) m = m :=
  rfl

@[simp]
theorem coe_comp (f : M ‚ü∂ N) (g : N ‚ü∂ U) : (f ‚â´ g : M ‚Üí U) = g ‚àò f :=
  rfl

end CoalgebraCat

variable {X‚ÇÅ X‚ÇÇ : Type u}

/--
Build an isomorphism in the category `CoalgebraCat R` from a `CoalgEquiv` between `Coalgebra`s.
-/
@[simps]
def CoalgEquiv.toCoalgebraIso
    {g‚ÇÅ : AddCommGroup X‚ÇÅ} {g‚ÇÇ : AddCommGroup X‚ÇÇ} {_ : Module R X‚ÇÅ} {_ : Module R X‚ÇÇ}
    {m‚ÇÅ : Coalgebra R X‚ÇÅ} {m‚ÇÇ : Coalgebra R X‚ÇÇ}
    (e : X‚ÇÅ ‚âÉ‚Çóc[R] X‚ÇÇ) : CoalgebraCat.of R X‚ÇÅ ‚âÖ CoalgebraCat.of R X‚ÇÇ where
  hom := (e : X‚ÇÅ ‚Üí‚Çóc[R] X‚ÇÇ)
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çóc[R] X‚ÇÅ)
  hom_inv_id := by ext x; exact e.left_inv x
  inv_hom_id := by ext x; exact e.right_inv x

namespace CategoryTheory.Iso

/-- Build a `CoalgEquiv` from an isomorphism in the category `CoalgebraCat R`. -/
@[simps]
def toCoalgEquiv {X Y : CoalgebraCat R} (i : X ‚âÖ Y) : X ‚âÉ‚Çóc[R] Y :=
  { i.hom with
    toFun := i.hom
    invFun := i.inv
    left_inv := fun x => by
      change (i.hom ‚â´ i.inv) x = x
      simp only [Iso.hom_inv_id]
      erw [id_apply]
    right_inv := fun x => by
      change (i.inv ‚â´ i.hom) x = x
      simp only [Iso.inv_hom_id]
      erw [id_apply] }

end CategoryTheory.Iso

/-- Coalgebra equivalences between `Coalgebra`s are the same as (isomorphic to) isomorphisms in
`CoalgebraCat`. -/
@[simps]
def coalgEquivIsoCoalgebraIso {X Y : Type u}
    [AddCommGroup X] [Module R X] [Coalgebra R X] [AddCommGroup Y] [Module R Y] [Coalgebra R Y] :
    (X ‚âÉ‚Çóc[R] Y) ‚âÖ CoalgebraCat.of R X ‚âÖ CoalgebraCat.of R Y where
  hom e := e.toCoalgebraIso
  inv i := i.toCoalgEquiv

instance CoalgebraCat.forget_reflects_isos :
    (forget (CoalgebraCat.{u} R)).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forget (CoalgebraCat.{u} R)).map f)
    let e : X ‚âÉ‚Çóc[R] Y := { f, i.toEquiv with }
    exact ‚ü®(IsIso.of_iso e.toCoalgebraIso).1‚ü©

/-!
`@[simp]` lemmas for `CoalgHom.comp` and categorical identities.
-/

@[simp] theorem CoalgHom.comp_id_coalgebraCat
    {R} [CommRing R] {G : CoalgebraCat.{u} R}
    {H : Type u} [AddCommGroup H] [Module R H] [Coalgebra R H] (f : G ‚Üí‚Çóc[R] H) :
    f.comp (ùüô G) = f :=
  Category.id_comp (CoalgebraCat.ofHom f)
@[simp] theorem CoalgHom.id_coalgebraCat_comp
    {R} [CommRing R] {G : Type u} [AddCommGroup G] [Module R G] [Coalgebra R G]
    {H : CoalgebraCat.{u} R} (f : G ‚Üí‚Çóc[R] H) :
    CoalgHom.comp (ùüô H) f = f :=
  Category.comp_id (CoalgebraCat.ofHom f)
