import Mathlib.Algebra.Module.LinearMap.Basic
import Mathlib.CategoryTheory.ConcreteCategory.Basic

open CategoryTheory

universe v u

attribute [local instance] ConcreteCategory.instFunLike

variable (R : Type u) [Ring R]

structure ModuleCatNew where
  carrier : Type v
  addCommGroup : AddCommGroup carrier := by infer_instance
  module : Module R carrier := by infer_instance

namespace ModuleCatNew

variable {R}
variable (M M‚ÇÅ M‚ÇÇ M‚ÇÉ : ModuleCatNew.{v} R)

instance : CoeSort (ModuleCatNew.{v} R) (Type v) := ‚ü®carrier‚ü©
attribute [coe] carrier

instance : AddCommGroup M := M.addCommGroup
instance : Module R M := M.module

structure Hom where
  linearMap : M‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ

variable {M‚ÇÅ M‚ÇÇ M‚ÇÉ}

namespace Hom

@[simps]
def id : Hom M M where
  linearMap := LinearMap.id

@[simps]
def comp (f : Hom M‚ÇÅ M‚ÇÇ) (g : Hom M‚ÇÇ M‚ÇÉ) : Hom M‚ÇÅ M‚ÇÉ where
  linearMap := g.linearMap.comp f.linearMap

end Hom

instance : Category (ModuleCatNew.{v} R) where
  Hom := Hom
  id := Hom.id
  comp := Hom.comp

@[simp]
lemma id_linearMap : Hom.linearMap (ùüô M) = LinearMap.id := rfl

@[simp]
lemma comp_linearMap (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (g : M‚ÇÇ ‚ü∂ M‚ÇÉ) :
    (f ‚â´ g).linearMap = g.linearMap.comp f.linearMap := rfl

variable (R) in
@[simps obj]
def forget : ModuleCatNew.{v} R ‚•§ Type v where
  obj M := M
  map {M‚ÇÅ M‚ÇÇ} f x := f.linearMap x

instance : (forget.{v} R).Faithful where
  map_injective {M‚ÇÅ M‚ÇÇ f g} h:= by
    obtain ‚ü®f‚ü© := f
    obtain ‚ü®g‚ü© := g
    suffices f = g by rw [this]
    ext x
    exact congr_fun h x

instance : ConcreteCategory (ModuleCatNew.{v} R) where
  forget := forget R

variable (R) in
@[simps]
def of (X : Type v) [AddCommGroup X] [Module R X] : ModuleCatNew.{v} R where
  carrier := X

lemma unbundle (M : ModuleCatNew.{v} R) :
    ‚àÉ (X : Type v) (_ : AddCommGroup X) (_ : Module R X), M = of R X :=
  ‚ü®M, _, _, rfl‚ü©

section

variable {X Y : Type v} [AddCommGroup X] [AddCommGroup Y] [Module R X] [Module R Y]

@[simps]
def homMk (f : X ‚Üí‚Çó[R] Y) : of R X ‚ü∂ of R Y where
  linearMap := f

@[simp]
lemma homMk_apply (f : X ‚Üí‚Çó[R] Y) (x : X) : homMk f x = f x := rfl

lemma unbundle_hom (f : of R X ‚ü∂ of R Y) : ‚àÉ (œÜ : X ‚Üí‚Çó[R] Y), f = homMk œÜ := ‚ü®_, rfl‚ü©

end

@[ext 1100]
lemma hom_ext {M‚ÇÅ M‚ÇÇ : ModuleCatNew.{v} R} {f g : M‚ÇÅ ‚ü∂ M‚ÇÇ} (h : ‚àÄ (x : M‚ÇÅ), f x = g x) :
    f = g := by
  ext x
  exact h x

lemma hom_ext'
    {M‚ÇÅ M‚ÇÇ : ModuleCatNew.{v} R} {f g : M‚ÇÅ ‚ü∂ M‚ÇÇ} (h : f.linearMap = g.linearMap) :
    f = g :=
  by cases f; cases g; subst h; rfl

@[simp]
lemma hom_mk_apply {M‚ÇÅ M‚ÇÇ : ModuleCatNew.{u} R} (f : M‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ) (x : M‚ÇÅ) :
    letI g : M‚ÇÅ ‚ü∂ M‚ÇÇ := { linearMap := f }; g x = f x := rfl

example {M‚ÇÅ M‚ÇÇ M‚ÇÉ : ModuleCatNew.{v} R} (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (g : M‚ÇÇ ‚ü∂ M‚ÇÉ) (x : M‚ÇÅ) :
    (f ‚â´ g) x = g (f x) := by
  obtain ‚ü®M‚ÇÅ, _, _, rfl‚ü© := unbundle M‚ÇÅ
  obtain ‚ü®M‚ÇÇ, _, _, rfl‚ü© := unbundle M‚ÇÇ
  obtain ‚ü®M‚ÇÉ, _, _, rfl‚ü© := unbundle M‚ÇÉ
  obtain ‚ü®f, rfl‚ü© := unbundle_hom f
  obtain ‚ü®g, rfl‚ü© := unbundle_hom g
  simp

@[simp]
lemma id_apply {M : ModuleCatNew.{v} R} (x : M) :
    (ùüô M) x = x := rfl

example {M‚ÇÅ M‚ÇÇ M‚ÇÉ : ModuleCatNew.{v} R} (f : M‚ÇÅ ‚ü∂ M‚ÇÇ) (g : M‚ÇÇ ‚ü∂ M‚ÇÉ) (x : M‚ÇÅ) :
    (f ‚â´ g) x = g (f x) := by
  simp

end ModuleCatNew
