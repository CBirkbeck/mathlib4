/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.CategoryTheory.Monoidal.Rigid.Basic
import Mathlib.CategoryTheory.Monoidal.Subcategory
import Mathlib.LinearAlgebra.Coevaluation
import Mathlib.LinearAlgebra.FreeModule.Finite.Matrix
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Closed

/-!
# The category of finitely generated modules over a ring

This introduces `FGModuleCat R`, the category of finitely generated modules over a ring `R`.
It is implemented as a full subcategory on a subtype of `ModuleCat R`.

When `K` is a field,
`FGModuleCatCat K` is the category of finite dimensional vector spaces over `K`.

We first create the instance as a preadditive category.
When `R` is commutative we then give the structure as an `R`-linear monoidal category.
When `R` is a field we give it the structure of a closed monoidal category
and then as a right-rigid monoidal category.

## Future work

* Show that `FGModuleCat R` is abelian when `R` is (left)-noetherian.

-/


noncomputable section

open CategoryTheory ModuleCat.monoidalCategory

universe u

section Ring

variable (R : Type u) [Ring R]

/-- Define `FGModuleCat` as the subtype of `ModuleCat.{u} R` of finitely generated modules. -/
def FGModuleCat :=
  FullSubcategory fun V : ModuleCat.{u} R => Module.Finite R V
-- Porting note: still no derive handler via `dsimp`.
-- see https://github.com/leanprover-community/mathlib4/issues/5020
-- deriving LargeCategory, ConcreteCategory,Preadditive

variable {R}

/-- A synonym for `M.obj.carrier`, which we can mark with `@[coe]`. -/
def FGModuleCat.carrier (M : FGModuleCat R) : Type u := M.obj.carrier

instance : CoeSort (FGModuleCat R) (Type u) :=
  ‚ü®FGModuleCat.carrier‚ü©

attribute [coe] FGModuleCat.carrier

@[simp] lemma obj_carrier (M : FGModuleCat R) : M.obj.carrier = M.carrier := rfl

instance (M : FGModuleCat R) : AddCommGroup M := by
  change AddCommGroup M.obj
  infer_instance

instance (M : FGModuleCat R) : Module R M := by
  change Module R M.obj
  infer_instance

instance : LargeCategory (FGModuleCat R) := by
  dsimp [FGModuleCat]
  infer_instance

instance {M N : FGModuleCat R} : FunLike (M ‚ü∂ N) M N :=
  LinearMap.instFunLike

instance {M N : FGModuleCat R} : LinearMapClass (M ‚ü∂ N) R M N :=
  LinearMap.semilinearMapClass

instance : ConcreteCategory (FGModuleCat R) := by
  dsimp [FGModuleCat]
  infer_instance

instance : Preadditive (FGModuleCat R) := by
  dsimp [FGModuleCat]
  infer_instance

end Ring

namespace FGModuleCat

section Ring

variable (R : Type u) [Ring R]

instance finite (V : FGModuleCat R) : Module.Finite R V :=
  V.property

instance : Inhabited (FGModuleCat R) :=
  ‚ü®‚ü®ModuleCat.of R R, Module.Finite.self R‚ü©‚ü©

/-- Lift an unbundled finitely generated module to `FGModuleCat R`. -/
def of (V : Type u) [AddCommGroup V] [Module R V] [Module.Finite R V] : FGModuleCat R :=
  ‚ü®ModuleCat.of R V, by change Module.Finite R V; infer_instance‚ü©

instance (V : FGModuleCat R) : Module.Finite R V :=
  V.property

instance : HasForget‚ÇÇ (FGModuleCat.{u} R) (ModuleCat.{u} R) := by
  dsimp [FGModuleCat]
  infer_instance

instance : (forget‚ÇÇ (FGModuleCat R) (ModuleCat.{u} R)).Full where
  map_surjective f := ‚ü®f, rfl‚ü©

variable {R}

/-- Converts and isomorphism in the category `FGModuleCat R` to
a `LinearEquiv` between the underlying modules. -/
def isoToLinearEquiv {V W : FGModuleCat R} (i : V ‚âÖ W) : V ‚âÉ‚Çó[R] W :=
  ((forget‚ÇÇ (FGModuleCat.{u} R) (ModuleCat.{u} R)).mapIso i).toLinearEquiv

/-- Converts a `LinearEquiv` to an isomorphism in the category `FGModuleCat R`. -/
@[simps]
def _root_.LinearEquiv.toFGModuleCatIso
    {V W : Type u} [AddCommGroup V] [Module R V] [Module.Finite R V]
    [AddCommGroup W] [Module R W] [Module.Finite R W] (e : V ‚âÉ‚Çó[R] W) :
    FGModuleCat.of R V ‚âÖ FGModuleCat.of R W where
  hom := e.toLinearMap
  inv := e.symm.toLinearMap
  hom_inv_id := by ext x; exact e.left_inv x
  inv_hom_id := by ext x; exact e.right_inv x

end Ring

section CommRing

variable (R : Type u) [CommRing R]

instance : Linear R (FGModuleCat R) := by
  dsimp [FGModuleCat]
  infer_instance

open scoped MonoidalCategory in
@[simps]
instance : MonoidalCategoryStruct (FGModuleCat R) where
  tensorObj X Y := ‚ü®X.1 ‚äó Y.1, Module.Finite.tensorProduct R X Y‚ü©
  whiskerLeft X _ _ f := X.1 ‚óÅ f
  whiskerRight {X‚ÇÅ X‚ÇÇ} (f : X‚ÇÅ.1 ‚ü∂ X‚ÇÇ.1) Y := (f ‚ñ∑ Y.1 :)
  tensorHom {X Y W Z} (f : X.1 ‚ü∂ Y.1) (g : W.1 ‚ü∂ Z.1) := (f ‚äó g :)
  tensorUnit := ‚ü®ModuleCat.of R R, Module.Finite.self R‚ü©
  associator X Y Z := ‚ü®(Œ±_ X.1 Y.1 Z.1).hom, (Œ±_ X.1 Y.1 Z.1).inv,
      (Œ±_ X.1 Y.1 Z.1).hom_inv_id, (Œ±_ X.1 Y.1 Z.1).inv_hom_id‚ü©
  leftUnitor X :=
    let lid := (TensorProduct.lid R X.1).toModuleIso
    ‚ü®lid.hom, lid.inv, lid.hom_inv_id, lid.inv_hom_id‚ü©
  rightUnitor X :=
    let rid := (TensorProduct.rid R X.1).toModuleIso
    ‚ü®rid.hom, rid.inv, rid.hom_inv_id, rid.inv_hom_id‚ü©

@[simps]
def moduleCatInducingFunctorData :
    Monoidal.InducingFunctorData (forget‚ÇÇ (FGModuleCat R) (ModuleCat R)) where
  ŒºIso X Y := Iso.refl _
  ŒµIso := ModuleCat.tensorUnitIso R
  associator_eq X Y Z := TensorProduct.ext_threefold fun x y z => rfl
  leftUnitor_eq X := TensorProduct.ext rfl
  rightUnitor_eq X := TensorProduct.ext rfl

instance : MonoidalCategory (FGModuleCat R) :=
  Monoidal.induced (forget‚ÇÇ (FGModuleCat R) (ModuleCat R)) <| moduleCatInducingFunctorData R

/-- `forget‚ÇÇ (FGModuleCat R) (ModuleCat R)` as a monoidal functor. -/
def forget‚ÇÇMonoidal : MonoidalFunctor (FGModuleCat R) (ModuleCat R) := by
  unfold instMonoidalCategory
  exact Monoidal.fromInduced (forget‚ÇÇ (FGModuleCat R) (ModuleCat R)) _

instance : (forget‚ÇÇMonoidal R).Faithful :=
  forget‚ÇÇ_faithful _ _

instance forget‚ÇÇMonoidal_additive : (forget‚ÇÇMonoidal R).Additive := by
  dsimp [forget‚ÇÇMonoidal]
  -- Porting note (#11187): was `infer_instance`
  exact Functor.fullSubcategoryInclusion_additive _

instance forget‚ÇÇMonoidal_linear : (forget‚ÇÇMonoidal R).Linear R := by
  dsimp [forget‚ÇÇMonoidal]
  -- Porting note (#11187): was `infer_instance`
  exact Functor.fullSubcategoryInclusionLinear _ _

open MonoidalCategory

@[simp] lemma tensorUnit_obj : (ùüô_ (FGModuleCat R)).obj = ModuleCat.of R R := rfl
@[simp] lemma tensorObj_obj (M N : FGModuleCat.{u} R) : (M ‚äó N).obj = (M.obj ‚äó N.obj) := rfl

instance : BraidedCategory (FGModuleCat.{u} R) :=
  braidedCategoryOfFaithful (forget‚ÇÇMonoidal R)
    (fun X Y =>  ‚ü®(Œ≤_ X.1 Y.1).hom, (Œ≤_ X.1 Y.1).inv,
      (Œ≤_ X.1 Y.1).hom_inv_id, (Œ≤_ X.1 Y.1).inv_hom_id‚ü©)
    (by aesop_cat)

/-- `forget‚ÇÇ (FGModuleCat R) (ModuleCat R)` as a braided functor. -/
@[simps toMonoidalFunctor]
def forget‚ÇÇBraided : BraidedFunctor (FGModuleCat.{u} R) (ModuleCat.{u} R) where
  toMonoidalFunctor := forget‚ÇÇMonoidal R

instance : (forget‚ÇÇBraided R).Faithful :=
  forget‚ÇÇ_faithful _ _

instance instSymmetricCategory : SymmetricCategory (FGModuleCat.{u} R) :=
  symmetricCategoryOfFaithful (forget‚ÇÇBraided R)

instance : MonoidalPreadditive (FGModuleCat R) where

instance : MonoidalLinear R (FGModuleCat R) where

theorem Iso.conj_eq_conj {V W : FGModuleCat R} (i : V ‚âÖ W) (f : End V) :
    Iso.conj i f = LinearEquiv.conj (isoToLinearEquiv i) f :=
  rfl

end CommRing

section Field

variable (K : Type u) [Field K]

instance (V W : FGModuleCat K) : Module.Finite K (V ‚ü∂ W) :=
  (by infer_instance : Module.Finite K (V ‚Üí‚Çó[K] W))

instance closedPredicateModuleFinite :
    MonoidalCategory.ClosedPredicate fun V : ModuleCat.{u} K ‚Ü¶ Module.Finite K V where
  prop_ihom {X Y} _ _ := Module.Finite.linearMap K K X Y

instance : MonoidalClosed (FGModuleCat K) where
  closed X :=
    { rightAdj := FullSubcategory.lift _ (fullSubcategoryInclusion _ ‚ãô ihom X.1)
        fun Y => Module.Finite.linearMap K K X Y
      adj :=
        { unit :=
          { app := fun Y => (ihom.coev X.1).app Y.1
            naturality := fun _ _ f => ihom.coev_naturality X.1 f }
          counit :=
          { app := fun Y => (ihom.ev X.1).app Y.1
            naturality := fun _ _ f => ihom.ev_naturality X.1 f }
          left_triangle_components := fun _ ‚Ü¶ TensorProduct.ext' fun _ _ => rfl
          right_triangle_components := fun _ ‚Ü¶ rfl } }

variable (V W : FGModuleCat K)

@[simp]
theorem ihom_obj : (ihom V).obj W = FGModuleCat.of K (V ‚Üí‚Çó[K] W) :=
  rfl

/-- The dual module is the dual in the rigid monoidal category `FGModuleCat K`. -/
def FGModuleCatDual : FGModuleCat K :=
  ‚ü®ModuleCat.of K (Module.Dual K V), Subspace.instModuleDualFiniteDimensional‚ü©

@[simp] lemma FGModuleCatDual_obj : (FGModuleCatDual K V).obj = ModuleCat.of K (Module.Dual K V) :=
  rfl
@[simp] lemma FGModuleCatDual_coe : (FGModuleCatDual K V : Type u) = Module.Dual K V := rfl

open CategoryTheory.MonoidalCategory

/-- The coevaluation map is defined in `LinearAlgebra.coevaluation`. -/
def FGModuleCatCoevaluation : ùüô_ (FGModuleCat K) ‚ü∂ V ‚äó FGModuleCatDual K V :=
  coevaluation K V

theorem FGModuleCatCoevaluation_apply_one :
    FGModuleCatCoevaluation K V (1 : K) =
      ‚àë i : Basis.ofVectorSpaceIndex K V,
        (Basis.ofVectorSpace K V) i ‚äó‚Çú[K] (Basis.ofVectorSpace K V).coord i :=
  coevaluation_apply_one K V

/-- The evaluation morphism is given by the contraction map. -/
def FGModuleCatEvaluation : FGModuleCatDual K V ‚äó V ‚ü∂ ùüô_ (FGModuleCat K) :=
  contractLeft K V

@[simp]
theorem FGModuleCatEvaluation_apply (f : FGModuleCatDual K V) (x : V) :
    (FGModuleCatEvaluation K V) (f ‚äó‚Çú x) = f.toFun x :=
  contractLeft_apply f x

private theorem coevaluation_evaluation :
    letI V' : FGModuleCat K := FGModuleCatDual K V
    V' ‚óÅ FGModuleCatCoevaluation K V ‚â´ (Œ±_ V' V V').inv ‚â´ FGModuleCatEvaluation K V ‚ñ∑ V' =
      (œÅ_ V').hom ‚â´ (Œª_ V').inv := by
  apply contractLeft_assoc_coevaluation K V

private theorem evaluation_coevaluation :
    FGModuleCatCoevaluation K V ‚ñ∑ V ‚â´
        (Œ±_ V (FGModuleCatDual K V) V).hom ‚â´ V ‚óÅ FGModuleCatEvaluation K V =
      (Œª_ V).hom ‚â´ (œÅ_ V).inv := by
  apply contractLeft_assoc_coevaluation' K V

instance exactPairing : ExactPairing V (FGModuleCatDual K V) where
  coevaluation' := FGModuleCatCoevaluation K V
  evaluation' := FGModuleCatEvaluation K V
  coevaluation_evaluation' := coevaluation_evaluation K V
  evaluation_coevaluation' := evaluation_coevaluation K V

instance rightDual : HasRightDual V :=
  ‚ü®FGModuleCatDual K V‚ü©

instance rightRigidCategory : RightRigidCategory (FGModuleCat K) where

end Field

end FGModuleCat

/-!
`@[simp]` lemmas for `LinearMap.comp` and categorical identities.
-/

@[simp] theorem LinearMap.comp_id_fgModuleCat
    {R} [Ring R] {G : FGModuleCat.{u} R} {H : Type u} [AddCommGroup H] [Module R H]
    (f : G ‚Üí‚Çó[R] H) : f.comp (ùüô G) = f :=
  Category.id_comp (ModuleCat.ofHom f)
@[simp] theorem LinearMap.id_fgModuleCat_comp
    {R} [Ring R] {G : Type u} [AddCommGroup G] [Module R G] {H : FGModuleCat.{u} R}
    (f : G ‚Üí‚Çó[R] H) : LinearMap.comp (ùüô H) f = f :=
  Category.comp_id (ModuleCat.ofHom f)
