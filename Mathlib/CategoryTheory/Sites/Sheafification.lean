/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.CategoryTheory.Limits.Preserves.Finite
/-!

# Sheafification

Given a site `(C, J)` we define a typeclass `HasSheaf J A` saying that the inclusion functor from
`A`-valued sheaves on `C` to presheaves admits a left adjoint (sheafification).
-/

universe v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory

open Limits

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] (J : GrothendieckTopology C)
variable (A : Type u‚ÇÇ) [Category.{v‚ÇÇ} A]

/--
A proposition saying that the inclusion functor from sheaves to presheaves admits a left adjoint.
-/
abbrev HasWeakSheafify := Nonempty (IsRightAdjoint (sheafToPresheaf J A))

/--
`HasSheafify` means that the inclusion functor from sheaves to presheaves admits a left exact
left adjiont (sheafification).
-/
class HasSheafify : Prop where
  isRightAdjoint : HasWeakSheafify J A
  isLeftExact : letI := isRightAdjoint.some
    Nonempty (PreservesFiniteLimits (leftAdjoint (sheafToPresheaf J A)))

instance [HasSheafify J A] : HasWeakSheafify J A := HasSheafify.isRightAdjoint

instance [IsRightAdjoint <| sheafToPresheaf J A] : HasWeakSheafify J A := ‚ü®inferInstance‚ü©

noncomputable section

instance [h : HasWeakSheafify J A] : IsRightAdjoint (sheafToPresheaf J A) := h.some

instance [HasSheafify J A] : PreservesFiniteLimits (leftAdjoint (sheafToPresheaf J A)) :=
  HasSheafify.isLeftExact.some

theorem HasSheafify.mk' {F : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A} (adj : F ‚ä£ sheafToPresheaf J A)
    [PreservesFiniteLimits F] : HasSheafify J A where
  isRightAdjoint := ‚ü®‚ü®F, adj‚ü©‚ü©
  isLeftExact :=
    let i : (h : IsRightAdjoint (sheafToPresheaf J A) := ‚ü®F, adj‚ü©) ‚Üí
      F ‚âÖ leftAdjoint (sheafToPresheaf J A) := fun _ ‚Ü¶
      adj.leftAdjointUniq (Adjunction.ofRightAdjoint (sheafToPresheaf J A))
    ‚ü®‚ü®fun _ ‚Ü¶ preservesLimitsOfShapeOfNatIso (i _)‚ü©‚ü©

/-- The sheafification functor, left adjoint to the inclusion. -/
def presheafToSheaf [HasWeakSheafify J A] : (C·µí·µñ ‚•§ A) ‚•§ Sheaf J A :=
  leftAdjoint (sheafToPresheaf J A)
set_option linter.uppercaseLean3 false in
#align category_theory.presheaf_to_Sheaf CategoryTheory.presheafToSheaf

instance [HasSheafify J A] : PreservesFiniteLimits (presheafToSheaf J A) :=
  HasSheafify.isLeftExact.some

/-- The sheafification-inclusion adjunction. -/
def sheafificationAdjunction [HasWeakSheafify J A] :
    presheafToSheaf J A ‚ä£ sheafToPresheaf J A := IsRightAdjoint.adj
#align category_theory.sheafification_adjunction CategoryTheory.sheafificationAdjunction

instance [HasWeakSheafify J A] : IsLeftAdjoint <| presheafToSheaf J A where
  adj := sheafificationAdjunction J A

end

variable {D : Type*} [Category D] [HasWeakSheafify J D]

namespace GrothendieckTopology

/-- The sheafification of a presheaf `P`. -/
noncomputable abbrev sheafify (P : C·µí·µñ ‚•§ D) : C·µí·µñ ‚•§ D :=
  presheafToSheaf J D |>.obj P |>.val
#align category_theory.grothendieck_topology.sheafify CategoryTheory.GrothendieckTopology.sheafify

/-- The canonical map from `P` to its sheafification. -/
noncomputable abbrev toSheafify (P : C·µí·µñ ‚•§ D) : P ‚ü∂ J.sheafify P :=
  sheafificationAdjunction J D |>.unit.app P
#align category_theory.grothendieck_topology.to_sheafify CategoryTheory.GrothendieckTopology.toSheafify

/-- The canonical map on sheafifications induced by a morphism. -/
noncomputable abbrev sheafifyMap {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) : J.sheafify P ‚ü∂ J.sheafify Q :=
  presheafToSheaf J D |>.map Œ∑ |>.val
#align category_theory.grothendieck_topology.sheafify_map CategoryTheory.GrothendieckTopology.sheafifyMap

@[simp]
theorem sheafifyMap_id (P : C·µí·µñ ‚•§ D) : J.sheafifyMap (ùüô P) = ùüô (J.sheafify P) := by
  dsimp [sheafifyMap, sheafify]
  simp
#align category_theory.grothendieck_topology.sheafify_map_id CategoryTheory.GrothendieckTopology.sheafifyMap_id

@[simp]
theorem sheafifyMap_comp {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R) :
    J.sheafifyMap (Œ∑ ‚â´ Œ≥) = J.sheafifyMap Œ∑ ‚â´ J.sheafifyMap Œ≥ := by
  dsimp [sheafifyMap, sheafify]
  simp
#align category_theory.grothendieck_topology.sheafify_map_comp CategoryTheory.GrothendieckTopology.sheafifyMap_comp

@[reassoc (attr := simp)]
theorem toSheafify_naturality {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    Œ∑ ‚â´ J.toSheafify _ = J.toSheafify _ ‚â´ J.sheafifyMap Œ∑ :=
  sheafificationAdjunction J D |>.unit.naturality Œ∑
#align category_theory.grothendieck_topology.to_sheafify_naturality CategoryTheory.GrothendieckTopology.toSheafify_naturality

variable (D)

/-- The sheafification of a presheaf `P`, as a functor. -/
noncomputable abbrev sheafification : (C·µí·µñ ‚•§ D) ‚•§ C·µí·µñ ‚•§ D :=
  presheafToSheaf J D ‚ãô sheafToPresheaf J D
#align category_theory.grothendieck_topology.sheafification CategoryTheory.GrothendieckTopology.sheafification

theorem sheafification_obj (P : C·µí·µñ ‚•§ D) : (J.sheafification D).obj P = J.sheafify P :=
  rfl
#align category_theory.grothendieck_topology.sheafification_obj CategoryTheory.GrothendieckTopology.sheafification_obj

theorem sheafification_map {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) :
    (J.sheafification D).map Œ∑ = J.sheafifyMap Œ∑ :=
  rfl
#align category_theory.grothendieck_topology.sheafification_map CategoryTheory.GrothendieckTopology.sheafification_map

/-- The canonical map from `P` to its sheafification, as a natural transformation. -/
noncomputable abbrev toSheafification : ùü≠ _ ‚ü∂ sheafification J D :=
  sheafificationAdjunction J D |>.unit
#align category_theory.grothendieck_topology.to_sheafification CategoryTheory.GrothendieckTopology.toSheafification

@[simp]
theorem toSheafification_app (P : C·µí·µñ ‚•§ D) : (J.toSheafification D).app P = J.toSheafify P :=
  rfl
#align category_theory.grothendieck_topology.to_sheafification_app CategoryTheory.GrothendieckTopology.toSheafification_app

variable {D}

theorem isIso_toSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : IsIso (J.toSheafify P) := by
  refine ‚ü®(sheafificationAdjunction J D |>.counit.app ‚ü®P, hP‚ü©).val, ?_, ?_‚ü©
  ¬∑ change _ = (ùüô (sheafToPresheaf J D ‚ãô ùü≠ (C·µí·µñ ‚•§ D)) : _).app ‚ü®P, hP‚ü©
    rw [‚Üê sheafificationAdjunction J D |>.right_triangle]
    rfl
  ¬∑ change (sheafToPresheaf _ _).map _ ‚â´ _ = _
    change _ ‚â´ (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj ‚ü®P, hP‚ü©) = _
    erw [‚Üê inv_counit_map (sheafificationAdjunction J D) (X := ‚ü®P, hP‚ü©), comp_inv_eq_id]
#align category_theory.grothendieck_topology.is_iso_to_sheafify CategoryTheory.GrothendieckTopology.isIso_toSheafify

/-- If `P` is a sheaf, then `P` is isomorphic to `J.sheafify P`. -/
noncomputable def isoSheafify {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) : P ‚âÖ J.sheafify P :=
  letI := isIso_toSheafify J hP
  asIso (J.toSheafify P)
#align category_theory.grothendieck_topology.iso_sheafify CategoryTheory.GrothendieckTopology.isoSheafify

@[simp]
theorem isoSheafify_hom {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (J.isoSheafify hP).hom = J.toSheafify P :=
  rfl
#align category_theory.grothendieck_topology.iso_sheafify_hom CategoryTheory.GrothendieckTopology.isoSheafify_hom

/-- Given a sheaf `Q` and a morphism `P ‚ü∂ Q`, construct a morphism from `J.sheafify P` to `Q`. -/
noncomputable def sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    J.sheafify P ‚ü∂ Q :=
  (sheafificationAdjunction J D).homEquiv P ‚ü®Q, hQ‚ü© |>.symm Œ∑ |>.val
#align category_theory.grothendieck_topology.sheafify_lift CategoryTheory.GrothendieckTopology.sheafifyLift

@[reassoc (attr := simp)]
theorem toSheafify_sheafifyLift {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q) :
    J.toSheafify P ‚â´ sheafifyLift J Œ∑ hQ = Œ∑ := by
  rw [toSheafify, sheafifyLift, Adjunction.homEquiv_counit]
  change _ ‚â´ (sheafToPresheaf J D).map _ ‚â´ _ = _
  simp only [Adjunction.unit_naturality_assoc]
  change _ ‚â´ (sheafificationAdjunction J D).unit.app ((sheafToPresheaf J D).obj ‚ü®Q, hQ‚ü©) ‚â´ _ = _
  change _ ‚â´ _ ‚â´ (sheafToPresheaf J D).map _ = _
  rw [sheafificationAdjunction J D |>.right_triangle_components (Y := ‚ü®Q, hQ‚ü©)]
  simp
#align category_theory.grothendieck_topology.to_sheafify_sheafify_lift CategoryTheory.GrothendieckTopology.toSheafify_sheafifyLift

theorem sheafifyLift_unique {P Q : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (Œ≥ : J.sheafify P ‚ü∂ Q) : J.toSheafify P ‚â´ Œ≥ = Œ∑ ‚Üí Œ≥ = sheafifyLift J Œ∑ hQ := by
  intro h
  rw [toSheafify] at h
  rw [sheafifyLift]
  let Œ≥' : (presheafToSheaf J D).obj P ‚ü∂ ‚ü®Q, hQ‚ü© := ‚ü®Œ≥‚ü©
  change Œ≥'.val = _
  rw [‚Üê Sheaf.Hom.ext_iff, ‚Üê Adjunction.homEquiv_apply_eq, Adjunction.homEquiv_unit]
  exact h
#align category_theory.grothendieck_topology.sheafify_lift_unique CategoryTheory.GrothendieckTopology.sheafifyLift_unique

@[simp]
theorem isoSheafify_inv {P : C·µí·µñ ‚•§ D} (hP : Presheaf.IsSheaf J P) :
    (J.isoSheafify hP).inv = J.sheafifyLift (ùüô _) hP := by
  apply J.sheafifyLift_unique
  simp [Iso.comp_inv_eq]
#align category_theory.grothendieck_topology.iso_sheafify_inv CategoryTheory.GrothendieckTopology.isoSheafify_inv

theorem sheafify_hom_ext {P Q : C·µí·µñ ‚•§ D} (Œ∑ Œ≥ : J.sheafify P ‚ü∂ Q) (hQ : Presheaf.IsSheaf J Q)
    (h : J.toSheafify P ‚â´ Œ∑ = J.toSheafify P ‚â´ Œ≥) : Œ∑ = Œ≥ := by
  rw [sheafifyLift_unique J _ hQ _ h, ‚Üê h]
  exact (sheafifyLift_unique J _ hQ _ h.symm).symm
#align category_theory.grothendieck_topology.sheafify_hom_ext CategoryTheory.GrothendieckTopology.sheafify_hom_ext

@[reassoc (attr := simp)]
theorem sheafifyMap_sheafifyLift {P Q R : C·µí·µñ ‚•§ D} (Œ∑ : P ‚ü∂ Q) (Œ≥ : Q ‚ü∂ R)
    (hR : Presheaf.IsSheaf J R) :
    J.sheafifyMap Œ∑ ‚â´ J.sheafifyLift Œ≥ hR = J.sheafifyLift (Œ∑ ‚â´ Œ≥) hR := by
  apply J.sheafifyLift_unique
  rw [‚Üê Category.assoc, ‚Üê J.toSheafify_naturality, Category.assoc, toSheafify_sheafifyLift]
#align category_theory.grothendieck_topology.sheafify_map_sheafify_lift CategoryTheory.GrothendieckTopology.sheafifyMap_sheafifyLift

end GrothendieckTopology

open GrothendieckTopology

variable (D)

@[simp]
theorem sheafificationAdjunction_unit_app {P : C·µí·µñ ‚•§ D} :
    (sheafificationAdjunction J D).unit.app P = J.toSheafify P := rfl

@[simp]
theorem sheafificationAdjunction_counit_app_val (P : Sheaf J D) :
    ((sheafificationAdjunction J D).counit.app P).val = J.sheafifyLift (ùüô P.val) P.cond := by
  unfold sheafifyLift
  rw [Adjunction.homEquiv_counit]
  simp

variable {J D}

/-- A sheaf `P` is isomorphic to its own sheafification. -/
@[simps]
noncomputable def sheafificationIso (P : Sheaf J D) : P ‚âÖ (presheafToSheaf J D).obj P.val where
  hom := ‚ü®(J.isoSheafify P.2).hom‚ü©
  inv := ‚ü®(J.isoSheafify P.2).inv‚ü©
  hom_inv_id := by
    ext1
    apply (J.isoSheafify P.2).hom_inv_id
  inv_hom_id := by
    ext1
    apply (J.isoSheafify P.2).inv_hom_id
#align category_theory.sheafification_iso CategoryTheory.sheafificationIso

instance isIso_sheafificationAdjunction_counit (P : Sheaf J D) :
    IsIso ((sheafificationAdjunction J D).counit.app P) :=
  isIso_of_fully_faithful (sheafToPresheaf J D) _
#align category_theory.is_iso_sheafification_adjunction_counit CategoryTheory.isIso_sheafificationAdjunction_counit

instance sheafification_reflective : IsIso (sheafificationAdjunction J D).counit :=
  NatIso.isIso_of_isIso_app _
#align category_theory.sheafification_reflective CategoryTheory.sheafification_reflective

end CategoryTheory
