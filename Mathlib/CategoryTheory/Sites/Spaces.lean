/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import Mathlib.CategoryTheory.Sites.Grothendieck
import Mathlib.CategoryTheory.Sites.Pretopology
import Mathlib.CategoryTheory.Limits.Lattice
import Mathlib.Topology.Sets.Opens

#align_import category_theory.sites.spaces from "leanprover-community/mathlib"@"b6fa3beb29f035598cf0434d919694c5e98091eb"

/-!
# Grothendieck topology on a topological space

Define the Grothendieck topology and the pretopology associated to a topological space, and show
that the pretopology induces the topology.

The covering (pre)sieves on `X` are those for which the union of domains contains `X`.

## Tags

site, Grothendieck topology, space

## References

* [nLab, *Grothendieck topology*](https://ncatlab.org/nlab/show/Grothendieck+topology)
* [S. MacLane, I. Moerdijk, *Sheaves in Geometry and Logic*][MM92]

## Implementation notes

We define the two separately, rather than defining the Grothendieck topology as that generated
by the pretopology for the purpose of having nice definitional properties for the sieves.
-/


universe u

namespace Opens

variable (T : Type u) [TopologicalSpace T]

open CategoryTheory TopologicalSpace CategoryTheory.Limits

/-- The Grothendieck topology associated to a topological space. -/
def grothendieckTopology : GrothendieckTopology (Opens T)
    where
  sieves X S := ‚àÄ x ‚àà X, ‚àÉ (U : _) (f : U ‚ü∂ X), S f ‚àß x ‚àà U
  top_mem' X x hx := ‚ü®_, ùüô _, trivial, hx‚ü©
  pullback_stable' X Y S f hf y hy := by
    rcases hf y (f.le hy) with ‚ü®U, g, hg, hU‚ü©
    -- ‚ä¢ ‚àÉ U f_1, (Sieve.pullback f S).arrows f_1 ‚àß y ‚àà U
    refine' ‚ü®U ‚äì Y, homOfLE inf_le_right, _, hU, hy‚ü©
    -- ‚ä¢ (Sieve.pullback f S).arrows (homOfLE (_ : U ‚äì Y ‚â§ Y))
    apply S.downward_closed hg (homOfLE inf_le_left)
    -- üéâ no goals
  transitive' X S hS R hR x hx := by
    rcases hS x hx with ‚ü®U, f, hf, hU‚ü©
    -- ‚ä¢ ‚àÉ U f, R.arrows f ‚àß x ‚àà U
    rcases hR hf _ hU with ‚ü®V, g, hg, hV‚ü©
    -- ‚ä¢ ‚àÉ U f, R.arrows f ‚àß x ‚àà U
    exact ‚ü®_, g ‚â´ f, hg, hV‚ü©
    -- üéâ no goals
#align opens.grothendieck_topology Opens.grothendieckTopology

/-- The Grothendieck pretopology associated to a topological space. -/
def pretopology : Pretopology (Opens T)
    where
  coverings X R := ‚àÄ x ‚àà X, ‚àÉ (U : _) (f : U ‚ü∂ X), R f ‚àß x ‚àà U
  has_isos X Y f i x hx := ‚ü®_, _, Presieve.singleton_self _, (inv f).le hx‚ü©
  pullbacks X Y f S hS x hx := by
    rcases hS _ (f.le hx) with ‚ü®U, g, hg, hU‚ü©
    -- ‚ä¢ ‚àÉ U f_1, Presieve.pullbackArrows f S f_1 ‚àß x ‚àà U
    refine' ‚ü®_, _, Presieve.pullbackArrows.mk _ _ hg, _‚ü©
    -- ‚ä¢ x ‚àà pullback g f
    have : U ‚äì Y ‚â§ pullback g f
    -- ‚ä¢ U ‚äì Y ‚â§ pullback g f
    refine' leOfHom (pullback.lift (homOfLE inf_le_left) (homOfLE inf_le_right) rfl)
    -- ‚ä¢ x ‚àà pullback g f
    apply this ‚ü®hU, hx‚ü©
    -- üéâ no goals
  Transitive X S Ti hS hTi x hx := by
    rcases hS x hx with ‚ü®U, f, hf, hU‚ü©
    -- ‚ä¢ ‚àÉ U f, Presieve.bind S Ti f ‚àß x ‚àà U
    rcases hTi f hf x hU with ‚ü®V, g, hg, hV‚ü©
    -- ‚ä¢ ‚àÉ U f, Presieve.bind S Ti f ‚àß x ‚àà U
    exact ‚ü®_, _, ‚ü®_, g, f, hf, hg, rfl‚ü©, hV‚ü©
    -- üéâ no goals
#align opens.pretopology Opens.pretopology

/-- The pretopology associated to a space is the largest pretopology that
    generates the Grothendieck topology associated to the space. -/
@[simp]
theorem pretopology_ofGrothendieck :
    Pretopology.ofGrothendieck _ (Opens.grothendieckTopology T) = Opens.pretopology T := by
  apply le_antisymm
  -- ‚ä¢ Pretopology.ofGrothendieck (Opens T) (grothendieckTopology T) ‚â§ pretopology T
  ¬∑ intro X R hR x hx
    -- ‚ä¢ ‚àÉ U f, R f ‚àß x ‚àà U
    rcases hR x hx with ‚ü®U, f, ‚ü®V, g‚ÇÅ, g‚ÇÇ, hg‚ÇÇ, _‚ü©, hU‚ü©
    -- ‚ä¢ ‚àÉ U f, R f ‚àß x ‚àà U
    exact ‚ü®V, g‚ÇÇ, hg‚ÇÇ, g‚ÇÅ.le hU‚ü©
    -- üéâ no goals
  ¬∑ intro X R hR x hx
    -- ‚ä¢ ‚àÉ U f, (Sieve.generate R).arrows f ‚àß x ‚àà U
    rcases hR x hx with ‚ü®U, f, hf, hU‚ü©
    -- ‚ä¢ ‚àÉ U f, (Sieve.generate R).arrows f ‚àß x ‚àà U
    exact ‚ü®U, f, Sieve.le_generate R U hf, hU‚ü©
    -- üéâ no goals
#align opens.pretopology_of_grothendieck Opens.pretopology_ofGrothendieck

/-- The pretopology associated to a space induces the Grothendieck topology associated to the space.
-/
@[simp]
theorem pretopology_toGrothendieck :
    Pretopology.toGrothendieck _ (Opens.pretopology T) = Opens.grothendieckTopology T := by
  rw [‚Üê pretopology_ofGrothendieck]
  -- ‚ä¢ Pretopology.toGrothendieck (Opens T) (Pretopology.ofGrothendieck (Opens T) ( ‚Ä¶
  apply (Pretopology.gi (Opens T)).l_u_eq
  -- üéâ no goals
#align opens.pretopology_to_grothendieck Opens.pretopology_toGrothendieck

end Opens
