/-
Copyright (c) 2025 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
import Mathlib.CategoryTheory.Bicategory.Functor.LocallyDiscrete
import Mathlib.CategoryTheory.Sites.Descent.Morphisms
import Mathlib.CategoryTheory.Sites.Descent.CodescentData
import Mathlib.CategoryTheory.Sites.Descent.PullbackStruct

/-!
# Descent data

-/

universe t v' v u' u

namespace CategoryTheory

open Opposite Limits

namespace Pseudofunctor

section

@[simp]
lemma mapComp'_mapLocallyDiscrete_comp
    {C D : Type*} [Category C] [Category D] (F : C ‚•§ D)
    (G : Pseudofunctor (LocallyDiscrete D) Cat)
    {X Y Z : LocallyDiscrete C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (fg : X ‚ü∂ Z) (hfg : f ‚â´ g = fg) :
      ((mapLocallyDiscrete F).comp G).mapComp' f g fg hfg =
      G.mapComp' ((mapLocallyDiscrete F).map f) ((mapLocallyDiscrete F).map g)
        ((mapLocallyDiscrete F).map fg) (by aesop) := by
  ext
  subst hfg
  rw [mapComp'_eq_mapComp]
  rfl

end

variable {C : Type u} [Category.{v} C] (F : Pseudofunctor (LocallyDiscrete C·µí·µñ) Cat.{v', u'})
  {Œπ : Type t} {S : C} {X : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ S)

/-- If `F` is a pseudofunctor from `(LocallyDiscrete C·µí·µñ)` to `Cat` and `f i : X i ‚ü∂ S`
is a family of morphisms in `C`, this is the type of family of objects in `F.obj (X i)`
equipped with a descent datum relative to the morphisms `f i`. -/
abbrev DescentData :=
  ((mapLocallyDiscrete (Over.forget S).op).comp F).CodescentData
    (fun (i : Œπ) ‚Ü¶ .mk (op (Over.mk (f i))))

/-- The functor `F.obj (.mk (op S)) ‚•§ F.DescentData f`. -/
def toDescentData : F.obj (.mk (op S)) ‚•§ F.DescentData f :=
  ((mapLocallyDiscrete (Over.forget S).op).comp F).toCodescentDataOfIsInitial
    (fun (i : Œπ) ‚Ü¶ .mk (op (Over.mk (f i)))) (.mk (op (Over.mk (ùüô _))))
      (IsInitial.ofUniqueHom
        (fun Z ‚Ü¶ .toLoc (Quiver.Hom.op (Over.homMk Z.as.unop.hom)))
        (fun ‚ü®‚ü®Z‚ü©‚ü© ‚ü®‚ü®m‚ü©‚ü© ‚Ü¶ by
          congr
          ext
          simpa using Over.w m))

namespace DescentData

variable {F f}

@[simps!]
def mk' (obj : ‚àÄ i, F.obj (.mk (op (X i))))
    (hom : ‚àÄ ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
      (_hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (_hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q),
        (F.map f‚ÇÅ.op.toLoc).obj (obj i‚ÇÅ) ‚ü∂ (F.map f‚ÇÇ.op.toLoc).obj (obj i‚ÇÇ))
    (hom_comp' : ‚àÄ ‚¶ÉY Y' : C‚¶Ñ (g : Y' ‚ü∂ Y) (q : Y ‚ü∂ S) (q' : Y' ‚ü∂ S) (hq : g ‚â´ q = q')
      ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q)
      (gf‚ÇÅ : Y' ‚ü∂ X i‚ÇÅ) (gf‚ÇÇ : Y' ‚ü∂ X i‚ÇÇ) (hgf‚ÇÅ : g ‚â´ f‚ÇÅ = gf‚ÇÅ) (hgf‚ÇÇ : g ‚â´ f‚ÇÇ = gf‚ÇÇ),
      hom q' gf‚ÇÅ gf‚ÇÇ (by aesop_cat) (by aesop_cat) =
        (F.mapComp' f‚ÇÅ.op.toLoc g.op.toLoc gf‚ÇÅ.op.toLoc
          (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, hgf‚ÇÅ])).hom.app _ ‚â´
          (F.map (.toLoc g.op)).map (hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ) ‚â´
          (F.mapComp' f‚ÇÇ.op.toLoc g.op.toLoc gf‚ÇÇ.op.toLoc
          (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, hgf‚ÇÇ])).inv.app _)
    (hom_self : ‚àÄ ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi : Œπ‚¶Ñ (g : Y ‚ü∂ X i) (hg : g ‚â´ f i = q),
      hom q g g hg hg = ùüô _)
    (comp_hom : ‚àÄ ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
      (f‚ÇÉ : Y ‚ü∂ X i‚ÇÉ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q) (hf‚ÇÉ : f‚ÇÉ ‚â´ f i‚ÇÉ = q),
        hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ ‚â´ hom q f‚ÇÇ f‚ÇÉ hf‚ÇÇ hf‚ÇÉ = hom q f‚ÇÅ f‚ÇÉ hf‚ÇÅ hf‚ÇÉ) :
    F.DescentData f :=
  CodescentData.mk' obj
    (fun Y i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ ‚Ü¶ hom Y.as.unop.hom f‚ÇÅ.as.unop.left f‚ÇÇ.as.unop.left
      (Over.w f‚ÇÅ.as.unop) (Over.w f‚ÇÇ.as.unop))
    (fun Y' Y g i‚ÇÅ i‚ÇÇ f‚ÇÅ f‚ÇÇ f‚ÇÅg f‚ÇÇg hf‚ÇÅg hf‚ÇÇg ‚Ü¶ by
      simpa using hom_comp' g.as.unop.left Y.as.unop.hom Y'.as.unop.hom
        (Over.w g.as.unop) f‚ÇÅ.as.unop.left f‚ÇÇ.as.unop.left
        (Over.w f‚ÇÅ.as.unop) (Over.w f‚ÇÇ.as.unop) f‚ÇÅg.as.unop.left f‚ÇÇg.as.unop.left
        (by simp [‚Üê hf‚ÇÅg]) (by simp [‚Üê hf‚ÇÇg]))
    (fun _ _ _ ‚Ü¶ hom_self _ _ _)
    (fun Y i‚ÇÅ i‚ÇÇ i‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ ‚Ü¶ comp_hom _ _ _ _ _ _ _)

section mk''

variable (obj : ‚àÄ i, (F.obj (.mk (op (X i)))))
  (sq : ‚àÄ i j, ChosenPullback (f i) (f j))
  (hom : ‚àÄ (i j : Œπ), (F.map (sq i j).p‚ÇÅ.op.toLoc).obj (obj i) ‚ü∂
    (F.map (sq i j).p‚ÇÇ.op.toLoc).obj (obj j))
  (diag : ‚àÄ i, (sq i i).Diagonal)
  (hom_self : ‚àÄ i, (F.map (diag i).f.op.toLoc).map (hom i i) =
    (F.mapComp' (sq i i).p‚ÇÅ.op.toLoc (diag i).f.op.toLoc (ùüô _)
        (by simp [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp])).inv.app _ ‚â´
      (F.mapComp' (sq i i).p‚ÇÇ.op.toLoc (diag i).f.op.toLoc (ùüô _)
        (by simp [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp])).hom.app _)

noncomputable def mk''Hom ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q) :
    (F.map f‚ÇÅ.op.toLoc).obj (obj i‚ÇÅ) ‚ü∂ (F.map f‚ÇÇ.op.toLoc).obj (obj i‚ÇÇ) :=
  let p : Y ‚ü∂ (sq i‚ÇÅ i‚ÇÇ).pullback := (sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by aesop)
  (F.mapComp' (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ.op.toLoc p.op.toLoc f‚ÇÅ.op.toLoc
        (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_fst])).hom.app _ ‚â´
      (F.map (.toLoc p.op)).map (hom i‚ÇÅ i‚ÇÇ) ‚â´
      (F.mapComp' (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ.op.toLoc p.op.toLoc f‚ÇÇ.op.toLoc
        (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_snd])).inv.app _

lemma mk''Hom_eq ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q) (p : Y ‚ü∂ (sq i‚ÇÅ i‚ÇÇ).pullback)
    (hp‚ÇÅ : p ‚â´ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ = f‚ÇÅ) (hp‚ÇÇ : p ‚â´ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ = f‚ÇÇ) :
  mk''Hom obj sq hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ =
    (F.mapComp' (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ.op.toLoc p.op.toLoc f‚ÇÅ.op.toLoc (by aesop)).hom.app _ ‚â´
      (F.map (.toLoc p.op)).map (hom i‚ÇÅ i‚ÇÇ) ‚â´
      (F.mapComp' (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ.op.toLoc p.op.toLoc f‚ÇÇ.op.toLoc (by aesop)).inv.app _ := by
  obtain rfl : p = (sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by rw [hf‚ÇÅ, hf‚ÇÇ]) := by
    apply (sq i‚ÇÅ i‚ÇÇ).isPullback.hom_ext <;> aesop
  rfl

lemma mk''Hom_comp' ‚¶ÉY Y' : C‚¶Ñ (g : Y' ‚ü∂ Y) (q : Y ‚ü∂ S) (q' : Y' ‚ü∂ S) (hq : g ‚â´ q = q')
    ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q)
    (gf‚ÇÅ : Y' ‚ü∂ X i‚ÇÅ) (gf‚ÇÇ : Y' ‚ü∂ X i‚ÇÇ) (hgf‚ÇÅ : g ‚â´ f‚ÇÅ = gf‚ÇÅ) (hgf‚ÇÇ : g ‚â´ f‚ÇÇ = gf‚ÇÇ) :
    mk''Hom obj sq hom q' gf‚ÇÅ gf‚ÇÇ (by aesop) (by aesop) =
      (F.mapComp' f‚ÇÅ.op.toLoc g.op.toLoc gf‚ÇÅ.op.toLoc (by aesop)).hom.app (obj i‚ÇÅ) ‚â´
        (F.map g.op.toLoc).map (mk''Hom obj sq hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ) ‚â´
          (F.mapComp' f‚ÇÇ.op.toLoc g.op.toLoc gf‚ÇÇ.op.toLoc (by aesop)).inv.app (obj i‚ÇÇ) := by
  let p := (sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by aesop)
  dsimp
  rw [mk''Hom_eq _ _ _ _ _ _ _ _ p (by aesop) (by aesop),
    mk''Hom_eq _ _ _ _ _ _ _ _ (g ‚â´ p) (by aesop) (by aesop)]
  dsimp
  simp only [Functor.map_comp, Category.assoc]
  rw [‚Üê F.mapComp'_hom_app_comp_mapComp'_hom_app_map_obj_assoc
    _ _ _ _ _ _ (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_fst]) rfl
    (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, ‚Üê op_comp,
        Category.assoc, IsPullback.lift_fst, hgf‚ÇÅ])]
  rw [F.map_map_mapComp'_inv_app_comp_mapComp'_inv_app
    _ _ _ _ _ _ (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_snd]) rfl
    (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, hgf‚ÇÇ]),
    mapComp'_inv_naturality_assoc, Iso.hom_inv_id_app_assoc]

@[simp]
lemma mk''Hom_p‚ÇÅ_p‚ÇÇ (i : Œπ) :
    mk''Hom obj sq hom (sq i i).p (sq i i).p‚ÇÅ (sq i i).p‚ÇÇ (by simp) (by simp) = hom i i := by
  rw [mk''Hom_eq obj sq hom (sq i i).p (sq i i).p‚ÇÅ (sq i i).p‚ÇÇ (by simp) (by simp)
    (ùüô _) (by simp)  (by simp)]
  simp [mapComp'_comp_id_hom_app, mapComp'_comp_id_inv_app]

include hom_self in
lemma mk''Hom_self ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi : Œπ‚¶Ñ (g : Y ‚ü∂ X i) (hg : g ‚â´ f i = q) :
    mk''Hom obj sq hom q g g hg hg = ùüô _ := by
  rw [mk''Hom_comp' obj sq hom (g ‚â´ (diag i).f) (sq i i).p _ (by aesop) (sq i i).p‚ÇÅ (sq i i).p‚ÇÇ
    (by simp) (by simp) _ _ (by simp) (by simp), mk''Hom_p‚ÇÅ_p‚ÇÇ,
    ‚Üê F.mapComp'_naturality_2_assoc (diag i).f.op.toLoc g.op.toLoc _ (by simp), hom_self,
    Functor.map_comp_assoc]
  dsimp
  rw [F.mapComp'_inv_app_map_obj_comp_mapComp'_inv_app _ _ _ (ùüô _) _ _
    (by simp only [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, ChosenPullback.LiftStruct.f_p‚ÇÇ,
      Quiver.Hom.id_toLoc, op_id]) rfl
    (by simp only [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, Category.assoc,
      ChosenPullback.LiftStruct.f_p‚ÇÇ, Category.comp_id])]
  simp only [‚Üê Functor.map_comp_assoc, Category.assoc, Iso.hom_inv_id_app, Category.comp_id]
  rw [F.mapComp'_hom_app_comp_mapComp'_hom_app_map_obj_assoc _ _ _ (ùüô _) _ _
    (by simp only [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, ChosenPullback.LiftStruct.f_p‚ÇÅ,
      Quiver.Hom.id_toLoc, op_id]) rfl
    (by simp only [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, Category.assoc,
      ChosenPullback.LiftStruct.f_p‚ÇÅ, Category.comp_id]),
    ‚Üê Functor.map_comp_assoc]
  simp

variable (sq‚ÇÉ : ‚àÄ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ), ChosenPullback‚ÇÉ (sq i‚ÇÅ i‚ÇÇ) (sq i‚ÇÇ i‚ÇÉ) (sq i‚ÇÅ i‚ÇÉ))
  (hom_comp : ‚àÄ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ),
    mk''Hom obj sq hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (by simp) (by simp) ‚â´
    mk''Hom obj sq hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ (by simp) (by simp) =
    mk''Hom obj sq hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ (by simp) (by simp))

-- TODO @jriou: moving to DescentDataPrime.lean
noncomputable def mk'' : F.DescentData f :=
  DescentData.mk' obj (fun _ _ _ _ _ _ ‚Ü¶ mk''Hom _ _ _ _ _ _)
    (fun _ _ _ _ _ hq _ _ _ _ _ _ _ _ ‚Ü¶ mk''Hom_comp' _ _ _ _ _ _ hq _ _ _ _ _ _)
    (fun _ _ _ _ hg ‚Ü¶ mk''Hom_self obj sq hom diag hom_self _ _ hg)
    (fun Y q i‚ÇÅ i‚ÇÇ i‚ÇÉ f‚ÇÅ f‚ÇÇ f‚ÇÉ hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ ‚Ü¶ by
      obtain ‚ü®œÜ, _, _, _‚ü© := (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).exists_lift f‚ÇÅ f‚ÇÇ f‚ÇÉ q hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ
      dsimp
      rw [mk''Hom_comp' obj sq hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ _
            (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ _ _ _ _ _ _,
        mk''Hom_comp' obj sq hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ _ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ
          (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ _ _ _ _ _ _,
        mk''Hom_comp' obj sq hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ (by aesop) (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ
          (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ _ _ _ _ _ _]
      ¬∑ simp only [Category.assoc, Iso.inv_hom_id_app_assoc,
        ‚Üê Functor.map_comp_assoc, hom_comp]
      all_goals aesop)

end mk''

end DescentData

end Pseudofunctor

end CategoryTheory
