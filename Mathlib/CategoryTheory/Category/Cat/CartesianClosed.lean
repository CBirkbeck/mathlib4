/-
Copyright (c) 2025 Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Emily Riehl
-/
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Functor.Currying
import Mathlib.CategoryTheory.Monoidal.Cartesian.Cat

/-!
# Cartesian closed structure on `Cat`

The category of small categories is cartesian closed, with the exponential at a category `C`
defined by the functor category mapping out of `C`.

Adjoint transposition is defined by currying and uncurrying.

-/

universe v u v‚ÇÅ v‚ÇÇ v‚ÇÉ v‚ÇÑ u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

namespace CategoryTheory

open Functor

namespace Cat

variable (C : Type u) [Category.{v} C]

/-- A category `C` induces a functor from `Cat` to itself defined
by forming the category of functors out of `C`. -/
@[simps]
def exp : Cat ‚•§ Cat where
  obj D := Cat.of (C ‚•§ D)
  map F := (whiskeringRight _ _ _).obj F

end Cat

section

variable {B : Type u‚ÇÅ} [Category.{v‚ÇÅ} B] {C : Type u‚ÇÇ} [Category.{v‚ÇÇ} C] {D : Type u‚ÇÉ}
  [Category.{v‚ÇÉ} D] {E : Type u‚ÇÑ} [Category.{v‚ÇÑ} E]

/-- The isomorphism of categories of bifunctors given by currying. -/
@[simps]
def curryingIso : C ‚•§ D ‚•§ E ‚âÖ C √ó D ‚•§ E where
  hom F := uncurry.obj F
  inv G := curry.obj G
  hom_inv_id := types_ext _ _ (fun F ‚Ü¶ curry_obj_uncurry_obj F)
  inv_hom_id := types_ext _ _ (fun F ‚Ü¶ uncurry_obj_curry_obj F)

/-- The isomorphism of categories of bifunctors given by flipping the arguments. -/
@[simps]
def flippingIso : C ‚•§ D ‚•§ E ‚âÖ D ‚•§ C ‚•§ E where
  hom F := F.flip
  inv F := F.flip
  hom_inv_id := types_ext _ _ (fun _ ‚Ü¶ rfl)
  inv_hom_id := types_ext _ _ (fun _ ‚Ü¶ rfl)

/-- The equivalence of types of bifunctors given by currying. -/
@[simps!]
def curryingEquiv : C ‚•§ D ‚•§ E ‚âÉ C √ó D ‚•§ E :=
  curryingIso.toEquiv

/-- The flipped equivalence of types of bifunctors given by currying. -/
@[simps!]
def curryingFlipEquiv : D ‚•§ C ‚•§ E ‚âÉ C √ó D ‚•§ E :=
  (flippingIso ‚â™‚â´ curryingIso).toEquiv

/-- Natural isomorphism witnessing `comp_flip_uncurry_eq`. -/
@[simps!]
def compFlipUncurryIso (F : B ‚•§ D) (G : D ‚•§ C ‚•§ E) :
    uncurry.obj (F ‚ãô G).flip ‚âÖ (ùü≠ C).prod F ‚ãô (uncurry.obj G.flip) :=
  NatIso.ofComponents (fun _ ‚Ü¶ eqToIso rfl)

lemma comp_flip_uncurry_eq (F : B ‚•§ D) (G : D ‚•§ C ‚•§ E) :
    uncurry.obj (F ‚ãô G).flip = (ùü≠ C).prod F ‚ãô uncurry.obj G.flip :=
  Functor.ext_of_iso (compFlipUncurryIso F G) (by aesop_cat) (by aesop_cat)

end

section
variable {B C D E : Type u} [Category.{u} B] [Category.{u} C]
  [Category.{u} D] [Category.{u} E]

/-- Natural isomorphism witnessing `comp_flip_curry_eq`. -/
@[simps!]
def compFlipCurryIso (F : C √ó B ‚•§ D) (G : D ‚•§ E) :
    (curry.obj (F ‚ãô G)).flip ‚âÖ (curry.obj F).flip ‚ãô (Cat.exp (Cat.of C)).map G.toCatHom :=
  NatIso.ofComponents (fun _ ‚Ü¶ eqToIso rfl)

lemma comp_flip_curry_eq (F : C √ó B ‚•§ D) (G : D ‚•§ E) :
    (curry.obj (F ‚ãô G)).flip =
      (curry.obj F).flip ‚ãô (Cat.exp (Cat.of C)).map G.toCatHom :=
  Functor.ext_of_iso (compFlipCurryIso F G) (by aesop_cat) (by aesop_cat)

end

namespace Cat

section
variable (C : Type u) [Category.{u} C]

instance closed : Closed (Cat.of C) where
  rightAdj := exp C
  adj := Adjunction.mkOfHomEquiv {
    homEquiv _ _ := curryingFlipEquiv.symm
    homEquiv_naturality_left_symm :=
      comp_flip_uncurry_eq
    homEquiv_naturality_right :=
      comp_flip_curry_eq
  }

instance cartesianClosed : CartesianClosed Cat.{u, u} where
  closed C := closed C

end

end Cat

end CategoryTheory
