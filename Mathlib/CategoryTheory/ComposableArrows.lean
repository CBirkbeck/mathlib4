/-
Copyright (c) 2023 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.Category.Preorder
import Mathlib.CategoryTheory.EqToHom
import Mathlib.CategoryTheory.Functor.Const
import Mathlib.Tactic.FinCases
import Mathlib.Tactic.Linarith

/-!
# Composable arrows

If `C` is a category, the type of `n`-simplices in the nerve of `C` identifies
to the type of functors `Fin (n + 1) ‚•§ C`, which can be thought as families of `n` composable
arrows in `C`. In this file, we introduce and study this category `ComposableArrows C n`
of `n` composable arrows in `C`.

If `F : ComposableArrows C n`, we define `F.left` as the leftmost object, `F.right` as the
rightmost object, and `F.hom : F.left ‚ü∂ F.right` is the canonical map.

The most significant definition in this file is the constructor
`F.precomp f : ComposableArrows C (n + 1)` for `F : ComposableArrows C n` and `f : X ‚ü∂ F.left`:
"it shifts `F` towards the right and inserts `f` on the left". This `precomp` has
good definitional properties.

In the namespace `CategoryTheory.ComposableArrows`, we provide constructors
like `mk‚ÇÅ f`, `mk‚ÇÇ f g`, `mk‚ÇÉ f g h` for `ComposableArrows C n` for small `n`.

TODO (@joelriou):
* redefine `Arrow C` as `ComposableArrow C 1`?
* construct some elements in `ComposableArrows m (Fin (n + 1))` for small `n`
the precomposition with which shall induce funtors
`ComposableArrows C n ‚•§ ComposableArrows C m` which correspond to simplicial operations
(specifically faces) with good definitional properties (this might be necessary for
up to `n = 7` in order to formalize spectral sequences following Verdier)

-/

namespace CategoryTheory

open Category

variable (C : Type*) [Category C]

/-- `ComposableArrows C n` is the type of functors `Fin (n + 1) ‚•§ C`. -/
abbrev ComposableArrows (n : ‚Ñï) := Fin (n + 1) ‚•§ C

namespace ComposableArrows

variable {C} {n m : ‚Ñï}

section

variable (F G : ComposableArrows C n)

/-- The `i`th object (with `i : ‚Ñï` such that `i ‚â§ n`) of `F : ComposableArrows C n`. -/
@[simp]
abbrev obj' (i : ‚Ñï) (hi : i ‚â§ n := by linarith) : C := F.obj ‚ü®i, by linarith‚ü©

/-- The map `F.obj' i ‚ü∂ F.obj' j` when `F : ComposableArrows C n`, and `i` and `j`
are natural numbers such that `i ‚â§ j ‚â§ n`. -/
@[simp]
abbrev map' (i j : ‚Ñï) (hij : i ‚â§ j := by linarith) (hjn : j ‚â§ n := by linarith) :
  F.obj ‚ü®i, by linarith‚ü© ‚ü∂ F.obj ‚ü®j, by linarith‚ü© := F.map (homOfLE (by
    simp only [Fin.mk_le_mk]
    linarith))

lemma map'_self (i : ‚Ñï) (hi : i ‚â§ n := by linarith) :
    F.map' i i = ùüô _ := F.map_id _

lemma map'_comp (i j k : ‚Ñï) (hij : i ‚â§ j := by linarith)
    (hjk : j ‚â§ k := by linarith) (hk : k ‚â§ n := by linarith) :
    F.map' i k = F.map' i j ‚â´ F.map' j k :=
  F.map_comp _ _

/-- The leftmost object of `F : ComposableArrows C n`. -/
abbrev left := obj' F 0

/-- The rightmost object of `F : ComposableArrows C n`. -/
abbrev right := obj' F n

/-- The canonical map `F.left ‚ü∂ F.right` for `F : ComposableArrows C n`. -/
abbrev hom : F.left ‚ü∂ F.right := map' F 0 n

variable {F G}

/-- The map `F.obj' i ‚ü∂ G.obj' i` induced on `i`th objects by a morphism `F ‚ü∂ G`
in `ComposableArrows C n` when `i` is a natural number such that `i ‚â§ n`. -/
@[simp]
abbrev app' (œÜ : F ‚ü∂ G) (i : ‚Ñï) (hi : i ‚â§ n := by linarith) :
    F.obj' i ‚ü∂ G.obj' i := œÜ.app _

@[reassoc]
lemma naturality' (œÜ : F ‚ü∂ G) (i j : ‚Ñï)
    (hij : i ‚â§ j := by linarith) (hj : j ‚â§ n := by linarith) :
    F.map' i j ‚â´ app' œÜ j = app' œÜ i ‚â´ G.map' i j :=
  œÜ.naturality _

@[reassoc]
lemma naturality'_1 (œÜ : F ‚âÖ G) (i j : ‚Ñï)
    (hij : i ‚â§ j := by linarith) (hj : j ‚â§ n := by linarith) :
    app' œÜ.inv i ‚â´ F.map' i j ‚â´ app' œÜ.hom j = G.map' i j :=
  NatIso.naturality_1 œÜ _

@[reassoc]
lemma naturality'_2 (œÜ : F ‚âÖ G) (i j : ‚Ñï)
    (hij : i ‚â§ j := by linarith) (hj : j ‚â§ n := by linarith) :
    app' œÜ.hom i ‚â´ G.map' i j ‚â´ app' œÜ.inv j = F.map' i j :=
  NatIso.naturality_2 œÜ _

/-- Constructor for `ComposableArrows C 0`. -/
@[simps!]
def mk‚ÇÄ (X : C) : ComposableArrows C 0 := (Functor.const (Fin 1)).obj X

namespace Mk‚ÇÅ

variable (X‚ÇÄ X‚ÇÅ : C)

/-- The map which sends `0 : Fin 2` to `X‚ÇÄ` and `1` to `X‚ÇÅ`. -/
@[simp]
def obj : Fin 2 ‚Üí C
  | ‚ü®0, _‚ü© => X‚ÇÄ
  | ‚ü®1, _‚ü©  => X‚ÇÅ

variable {X‚ÇÄ X‚ÇÅ} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ)

/-- The obvious map `obj X‚ÇÄ X‚ÇÅ i ‚ü∂ obj X‚ÇÄ X‚ÇÅ j` whenever `i j : Fin 2` satisfy `i ‚â§ j`. -/
@[simp]
def map : ‚àÄ (i j : Fin 2) (_ : i ‚â§ j), obj X‚ÇÄ X‚ÇÅ i ‚ü∂ obj X‚ÇÄ X‚ÇÅ j
  | ‚ü®0, _‚ü©, ‚ü®0, _‚ü©, _ => ùüô _
  | ‚ü®0, _‚ü©, ‚ü®1, _‚ü©, _ => f
  | ‚ü®1, _‚ü©, ‚ü®1, _‚ü©, _ => ùüô _

lemma map_id (i : Fin 2) : map f i i (by simp) = ùüô _ :=
  match i with
    | 0 => rfl
    | 1 => rfl

lemma map_comp {i j k : Fin 2} (hij : i ‚â§ j) (hjk : j ‚â§ k) :
    map f i k (hij.trans hjk) = map f i j hij ‚â´ map f j k hjk :=
  match i with
    | 0 =>
        match j with
          | 0 => by rw [map_id, id_comp]
          | 1 => by
              obtain rfl : k = 1 := k.eq_one_of_neq_zero (by rintro rfl; simp at hjk)
              rw [map_id, comp_id]
    | 1 => by
        obtain rfl := j.eq_one_of_neq_zero (by rintro rfl; simp at hij)
        obtain rfl := k.eq_one_of_neq_zero (by rintro rfl; simp at hjk)
        rw [map_id, id_comp]

end Mk‚ÇÅ

/-- Constructor for `ComposableArrows C 1`. -/
@[simps]
def mk‚ÇÅ {X‚ÇÄ X‚ÇÅ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) : ComposableArrows C 1 where
  obj := Mk‚ÇÅ.obj X‚ÇÄ X‚ÇÅ
  map g := Mk‚ÇÅ.map f _ _ (leOfHom g)
  map_id := Mk‚ÇÅ.map_id f
  map_comp g g' := Mk‚ÇÅ.map_comp f (leOfHom g) (leOfHom g')

/-- Constructor for morphisms `F ‚ü∂ G` in `ComposableArrows C n` which takes as inputs
a family of morphisms `F.obj i ‚ü∂ G.obj i` and the naturality condition only for the
maps in `Fin (n + 1)` given by inequalities of the form `i ‚â§ i + 1`. -/
@[simps]
def homMk {F G : ComposableArrows C n} (app : ‚àÄ i, F.obj i ‚ü∂ G.obj i)
    (w : ‚àÄ (i : ‚Ñï) (hi : i < n), F.map' i (i + 1) ‚â´ app _ = app _ ‚â´ G.map' i (i + 1)) :
    F ‚ü∂ G where
  app := app
  naturality := by
    suffices ‚àÄ (k i j : ‚Ñï) (hj : i + k = j) (hj' : j ‚â§ n),
        F.map' i j ‚â´ app _ = app _ ‚â´ G.map' i j by
      rintro ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© hij
      have hij' := leOfHom hij
      simp only [Fin.mk_le_mk] at hij'
      obtain ‚ü®k, hk‚ü© := Nat.le.dest hij'
      exact this k i j hk (by linarith)
    intro k
    induction' k with k hk
    ¬∑ intro i j hj hj'
      simp only [Nat.zero_eq, add_zero] at hj
      obtain rfl := hj
      rw [F.map'_self i, G.map'_self i, id_comp, comp_id]
    ¬∑ intro i j hj hj'
      rw [Nat.succ_eq_add_one, ‚Üê add_assoc] at hj
      subst hj
      rw [F.map'_comp i (i + k) (i + k + 1), G.map'_comp i (i + k) (i + k + 1), assoc,
        w (i + k) (by linarith), reassoc_of% (hk i (i + k) rfl (by linarith))]

/-- Constructor for isomorphisms `F ‚âÖ G` in `ComposableArrows C n` which takes as inputs
a family of isomorphisms `F.obj i ‚âÖ G.obj i` and the naturality condition only for the
maps in `Fin (n + 1)` given by inequalities of the form `i ‚â§ i + 1`. -/
@[simps]
def isoMk {F G : ComposableArrows C n} (app : ‚àÄ i, F.obj i ‚âÖ G.obj i)
    (w : ‚àÄ (i : ‚Ñï) (hi : i < n),
      F.map' i (i + 1) ‚â´ (app _).hom = (app _).hom ‚â´ G.map' i (i + 1)) :
    F ‚âÖ G where
  hom := homMk (fun i => (app i).hom) w
  inv := homMk (fun i => (app i).inv) (fun i hi => by
    dsimp only
    rw [‚Üê cancel_epi ((app _).hom), ‚Üê reassoc_of% (w i hi), Iso.hom_inv_id, comp_id,
      Iso.hom_inv_id_assoc])

lemma ext {F G : ComposableArrows C n} (h : ‚àÄ i, F.obj i = G.obj i)
    (w : ‚àÄ (i : ‚Ñï) (hi : i < n), F.map' i (i + 1) =
      eqToHom (h _) ‚â´ G.map' i (i + 1) ‚â´ eqToHom (h _).symm) : F = G :=
  Functor.ext_of_iso
    (isoMk (fun i => eqToIso (h i)) (fun i hi => by simp [w i hi])) h (fun i => rfl)

/-- Constructor for morphisms in `ComposableArrows C 0`. -/
@[simps!]
def homMk‚ÇÄ {F G : ComposableArrows C 0} (f : F.obj' 0 ‚ü∂ G.obj' 0) : F ‚ü∂ G :=
  homMk (fun i => match i with
    | ‚ü®0, _‚ü© => f) (fun i hi => by simp at hi)

@[ext]
lemma hom_ext‚ÇÄ {F G : ComposableArrows C 0} {œÜ œÜ' : F ‚ü∂ G}
    (h : app' œÜ 0 = app' œÜ' 0) :
    œÜ = œÜ' := by
  ext i
  fin_cases i
  exact h

/-- Constructor for isomorphisms in `ComposableArrows C 0`. -/
@[simps!]
def isoMk‚ÇÄ {F G : ComposableArrows C 0} (e : F.obj' 0 ‚âÖ G.obj' 0) : F ‚âÖ G where
  hom := homMk‚ÇÄ e.hom
  inv := homMk‚ÇÄ e.inv

lemma ext‚ÇÄ {F G : ComposableArrows C 0} (h : F.obj' 0 = G.obj 0) : F = G :=
  ext (fun i => match i with
    | ‚ü®0, _‚ü© => h) (fun i hi => by simp at hi)

lemma mk‚ÇÄ_surjective (F : ComposableArrows C 0) : ‚àÉ (X : C), F = mk‚ÇÄ X :=
  ‚ü®F.obj' 0, ext‚ÇÄ rfl‚ü©

/-- Constructor for morphisms in `ComposableArrows C 1`. -/
@[simps!]
def homMk‚ÇÅ {F G : ComposableArrows C 1}
    (left : F.obj' 0 ‚ü∂ G.obj' 0) (right : F.obj' 1 ‚ü∂ G.obj' 1)
    (w : F.map' 0 1 ‚â´ right = left ‚â´ G.map' 0 1 := by aesop_cat) :
    F ‚ü∂ G :=
  homMk (fun i => match i with
      | ‚ü®0, _‚ü© => left
      | ‚ü®1, _‚ü© => right) (by
          intro i hi
          obtain rfl : i = 0 := by simpa using hi
          exact w)

@[ext]
lemma hom_ext‚ÇÅ {F G : ComposableArrows C 1} {œÜ œÜ' : F ‚ü∂ G}
    (h‚ÇÄ : app' œÜ 0 = app' œÜ' 0) (h‚ÇÅ : app' œÜ 1 = app' œÜ' 1) :
    œÜ = œÜ' := by
  ext i
  match i with
    | 0 => exact h‚ÇÄ
    | 1 => exact h‚ÇÅ

/-- Constructor for isomorphisms in `ComposableArrows C 1`. -/
@[simps!]
def isoMk‚ÇÅ {F G : ComposableArrows C 1}
    (left : F.obj' 0 ‚âÖ G.obj' 0) (right : F.obj' 1 ‚âÖ G.obj' 1)
    (w : F.map' 0 1 ‚â´ right.hom = left.hom ‚â´ G.map' 0 1 := by aesop_cat) :
    F ‚âÖ G where
  hom := homMk‚ÇÅ left.hom right.hom w
  inv := homMk‚ÇÅ left.inv right.inv (by
    rw [‚Üê cancel_mono right.hom, assoc, assoc, w, right.inv_hom_id, left.inv_hom_id_assoc]
    apply comp_id)

lemma map'_eq_hom‚ÇÅ (F : ComposableArrows C 1) : F.map' 0 1 = F.hom := rfl

lemma ext‚ÇÅ {F G : ComposableArrows C 1}
    (left : F.left = G.left) (right : F.right = G.right)
    (w : F.hom = eqToHom left ‚â´ G.hom ‚â´ eqToHom right.symm) : F = G :=
  Functor.ext_of_iso (isoMk‚ÇÅ (eqToIso left) (eqToIso right) (by simp [map'_eq_hom‚ÇÅ, w]))
    (fun i => by fin_cases i <;> assumption)
    (fun i => by fin_cases i <;> rfl)

lemma mk‚ÇÅ_surjective (X : ComposableArrows C 1) : ‚àÉ (X‚ÇÄ X‚ÇÅ : C) (f : X‚ÇÄ ‚ü∂ X‚ÇÅ), X = mk‚ÇÅ f :=
  ‚ü®_, _, X.map' 0 1, ext‚ÇÅ rfl rfl (by simp)‚ü©

variable (F)

/-- The `ith` arrow of `F : ComposableArrows C n` for `0 ‚â§ i < n`. -/
abbrev arrow (i : ‚Ñï) (hi : i < n := by linarith) : ComposableArrows C 1 := mk‚ÇÅ (F.map' i (i + 1))

lemma arrow_right_eq_arrow_succ_left (i : ‚Ñï) (hi : i + 1 < n := by linarith) :
    (F.arrow i).right = (F.arrow (i + 1)).left := rfl

lemma eq_arrow_zero (F : ComposableArrows C 1) : F = F.arrow 0 := by
  obtain ‚ü®_, _, _, rfl‚ü© := mk‚ÇÅ_surjective F
  rfl

namespace Precomp

variable (X : C)

/-- The map `Fin (n + 1 + 1) ‚Üí C` which "shifts" `F.obj'` to the right and inserts `X` in
the zeroth position. -/
def obj : Fin (n + 1 + 1) ‚Üí C
  | ‚ü®0, _‚ü© => X
  | ‚ü®i + 1, hi‚ü© => F.obj' i

@[simp]
lemma obj_zero : obj F X 0 = X := rfl

@[simp]
lemma obj_one : obj F X 1 = F.obj' 0 := rfl

@[simp]
lemma obj_succ (i : ‚Ñï) (hi : i + 1 < n + 1 + 1) : obj F X ‚ü®i + 1, hi‚ü© = F.obj' i := rfl

variable {X} (f : X ‚ü∂ F.left)

/-- Auxiliary definition for the action on maps of the functor `F.precomp f`.
It sends `0 ‚â§ 1` to `f` and `i + 1 ‚â§ j + 1` to `F.map' i j`. -/
def map : ‚àÄ (i j : Fin (n + 1 + 1)) (_ : i ‚â§ j), obj F X i ‚ü∂ obj F X j
  | ‚ü®0, _‚ü©, ‚ü®0, _‚ü©, _ => ùüô X
  | ‚ü®0, _‚ü©, ‚ü®1, _‚ü©, _ => f
  | ‚ü®0, _‚ü©, ‚ü®j + 2, hj‚ü©, _ => f ‚â´ F.map' 0 (j + 1)
  | ‚ü®i + 1, hi‚ü©, ‚ü®j + 1, hj‚ü©, hij => F.map' i j (by simpa using hij)

@[simp]
lemma map_zero_zero : map F f 0 0 (by simp) = ùüô X := rfl

@[simp]
lemma map_one_one : map F f 1 1 (by simp) = F.map (ùüô _) := rfl

@[simp]
lemma map_zero_one : map F f 0 1 (by simp) = f := rfl

@[simp]
lemma map_zero_one' : map F f 0 ‚ü®0 + 1, by simp‚ü© (by simp) = f := rfl

@[simp]
lemma map_zero_succ_succ (j : ‚Ñï) (hj : j + 2 < n + 1 + 1) :
    map F f 0 ‚ü®j + 2, hj‚ü© (by simp) = f ‚â´ F.map' 0 (j+1) := rfl

@[simp]
lemma map_succ_succ (i j : ‚Ñï) (hi : i + 1 < n + 1 + 1) (hj : j + 1 < n + 1 + 1)
    (hij : i + 1 ‚â§ j + 1) :
    map F f ‚ü®i + 1, hi‚ü© ‚ü®j + 1, hj‚ü© hij = F.map' i j := rfl

@[simp]
lemma map_one_succ (j : ‚Ñï) (hj : j + 1 < n + 1 + 1) :
    map F f 1 ‚ü®j + 1, hj‚ü© (by simp [Fin.le_def]) = F.map' 0 j := rfl

lemma map_id (i : Fin (n + 1 + 1)) : map F f i i (by simp) = ùüô _ := by
  obtain ‚ü®i, hi‚ü© := i
  cases i
  ¬∑ rfl
  ¬∑ apply F.map_id

lemma map_comp {i j k : Fin (n + 1 + 1)} (hij : i ‚â§ j) (hjk : j ‚â§ k) :
    map F f i k (hij.trans hjk) = map F f i j hij ‚â´ map F f j k hjk := by
  obtain ‚ü®i, hi‚ü© := i
  obtain ‚ü®j, hj‚ü© := j
  obtain ‚ü®k, hk‚ü© := k
  cases i
  ¬∑ obtain _ | _ | j := j
    ¬∑ dsimp
      rw [id_comp]
    ¬∑ obtain _ | _ | k := k
      ¬∑ simp at hjk
      ¬∑ dsimp
        rw [F.map_id, comp_id]
      ¬∑ rfl
    ¬∑ obtain _ | _ | k := k
      ¬∑ simp [Fin.ext_iff] at hjk
      ¬∑ simp [Fin.le_def, Nat.succ_eq_add_one] at hjk
      ¬∑ dsimp
        rw [assoc, ‚Üê F.map_comp, homOfLE_comp]
  ¬∑ obtain _ | j := j
    ¬∑ simp [Fin.ext_iff] at hij
    ¬∑ obtain _ | k := k
      ¬∑ simp [Fin.ext_iff] at hjk
      ¬∑ dsimp
        rw [‚Üê F.map_comp, homOfLE_comp]

end Precomp

/-- "Precomposition" of `F : ComposableArrows C n` by a morphism `f : X ‚ü∂ F.left`. -/
@[simps]
def precomp {X : C} (f : X ‚ü∂ F.left) : ComposableArrows C (n + 1) where
  obj := Precomp.obj F X
  map g := Precomp.map F f _ _ (leOfHom g)
  map_id := Precomp.map_id F f
  map_comp g g' := (Precomp.map_comp F f (leOfHom g) (leOfHom g'))

/-- Constructor for `ComposableArrows C 2`. -/
@[simp]
def mk‚ÇÇ {X‚ÇÄ X‚ÇÅ X‚ÇÇ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ) : ComposableArrows C 2 :=
  (mk‚ÇÅ g).precomp f

/-! It is certainly important to keep named constructors like `mk‚ÇÄ`, `mk‚ÇÅ` and `mk‚ÇÇ`,
but instead of the following `mk‚ÇÉ`, etc, it would be nice to introduce
a list-like notation in order to construct `n` composable arrows for arbitrary `n`. -/

/-- Constructor for `ComposableArrows C 3`. -/
@[simp]
def mk‚ÇÉ {X‚ÇÄ X‚ÇÅ X‚ÇÇ X‚ÇÉ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ) (h : X‚ÇÇ ‚ü∂ X‚ÇÉ) : ComposableArrows C 3 :=
  (mk‚ÇÇ g h).precomp f

/-- Constructor for `ComposableArrows C 4`. -/
@[simp]
def mk‚ÇÑ {X‚ÇÄ X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ) (h : X‚ÇÇ ‚ü∂ X‚ÇÉ) (i : X‚ÇÉ ‚ü∂ X‚ÇÑ) :
    ComposableArrows C 4 :=
  (mk‚ÇÉ g h i).precomp f

/-- Constructor for `ComposableArrows C 5`. -/
@[simp]
def mk‚ÇÖ {X‚ÇÄ X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ X‚ÇÖ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ) (h : X‚ÇÇ ‚ü∂ X‚ÇÉ)
    (i : X‚ÇÉ ‚ü∂ X‚ÇÑ) (j : X‚ÇÑ ‚ü∂ X‚ÇÖ) :
    ComposableArrows C 5 :=
  (mk‚ÇÑ g h i j).precomp f

section

variable {X‚ÇÄ X‚ÇÅ X‚ÇÇ X‚ÇÉ X‚ÇÑ : C} (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ) (h : X‚ÇÇ ‚ü∂ X‚ÇÉ) (i : X‚ÇÉ ‚ü∂ X‚ÇÑ)

/-! These examples are meant to test the good definitional properties of `precomp`,
and that `dsimp` can see through. -/

example : map' (mk‚ÇÇ f g) 0 1 = f := by dsimp
example : map' (mk‚ÇÇ f g) 1 2 = g := by dsimp
example : map' (mk‚ÇÇ f g) 0 2 = f ‚â´ g := by dsimp
example : (mk‚ÇÇ f g).hom = f ‚â´ g := by dsimp
example : map' (mk‚ÇÇ f g) 0 0 = ùüô _ := by dsimp
example : map' (mk‚ÇÇ f g) 1 1 = ùüô _ := by dsimp
example : map' (mk‚ÇÇ f g) 2 2 = ùüô _ := by dsimp

example : map' (mk‚ÇÉ f g h) 0 1 = f := by dsimp
example : map' (mk‚ÇÉ f g h) 1 2 = g := by dsimp
example : map' (mk‚ÇÉ f g h) 2 3 = h := by dsimp
example : map' (mk‚ÇÉ f g h) 0 3 = f ‚â´ g ‚â´ h := by dsimp
example : (mk‚ÇÉ f g h).hom = f ‚â´ g ‚â´ h := by dsimp
example : map' (mk‚ÇÉ f g h) 0 2 = f ‚â´ g := by dsimp
example : map' (mk‚ÇÉ f g h) 1 3 = g ‚â´ h := by dsimp

end

/-- The map `ComposableArrows C m ‚Üí ComposableArrows C n` obtained by precomposition with
a functor `Fin (n + 1) ‚•§ Fin (m + 1)`. -/
@[simps!]
def whiskerLeft (F : ComposableArrows C m) (Œ¶ : Fin (n + 1) ‚•§ Fin (m + 1)) :
    ComposableArrows C n := Œ¶ ‚ãô F

/-- The functor `ComposableArrows C m ‚•§ ComposableArrows C n` obtained by precomposition with
a functor `Fin (n + 1) ‚•§ Fin (m + 1)`. -/
@[simps!]
def whiskerLeftFunctor (Œ¶ : Fin (n + 1) ‚•§ Fin (m + 1)) :
    ComposableArrows C m ‚•§ ComposableArrows C n where
  obj F := F.whiskerLeft Œ¶
  map f := CategoryTheory.whiskerLeft Œ¶ f

/-- The functor `Fin n ‚•§ Fin (n + 1)` which sends `i` to `i.succ`. -/
@[simps]
def _root_.Fin.succFunctor (n : ‚Ñï) : Fin n ‚•§ Fin (n + 1) where
  obj i := i.succ
  map {i j} hij := homOfLE (Fin.succ_le_succ_iff.2 (leOfHom hij))

/-- The functor `ComposableArrows C (n + 1) ‚•§ ComposableArrows C n` which forgets
the first arrow. -/
@[simps!]
def Œ¥‚ÇÄFunctor : ComposableArrows C (n + 1) ‚•§ ComposableArrows C n :=
  whiskerLeftFunctor (Fin.succFunctor (n + 1))

/-- The `ComposableArrows C n` obtained by forgetting the first arrow. -/
abbrev Œ¥‚ÇÄ (F : ComposableArrows C (n + 1)) := Œ¥‚ÇÄFunctor.obj F

@[simp]
lemma precomp_Œ¥‚ÇÄ {X : C} (f : X ‚ü∂ F.left) : (F.precomp f).Œ¥‚ÇÄ = F := rfl

end

section

variable {F G : ComposableArrows C (n + 1)}

lemma hom_ext_succ {œÜ œÜ' : F ‚ü∂ G} (h : app' œÜ 0 = app' œÜ' 0)
    (h‚ÇÄ : Œ¥‚ÇÄFunctor.map œÜ = Œ¥‚ÇÄFunctor.map œÜ') : œÜ = œÜ' := by
  ext ‚ü®i, hi‚ü©
  cases' i with i
  ¬∑ exact h
  ¬∑ exact congr_app h‚ÇÄ ‚ü®i, by linarith‚ü©

variable (app‚ÇÄ : F.left ‚ü∂ G.left) (œÜ‚ÇÄ : F.Œ¥‚ÇÄ ‚ü∂ G.Œ¥‚ÇÄ)
  (w : F.map' 0 1 ‚â´ œÜ‚ÇÄ.app 0 = app‚ÇÄ ‚â´ G.map' 0 1)

/-- Inductive construction of morphisms in `ComposableArrows C (n + 1)`. -/
def homMkSucc : F ‚ü∂ G :=
  homMk (fun i => match i with
    | 0 => app‚ÇÄ
    | ‚ü®i + 1, _‚ü© => œÜ‚ÇÄ.app ‚ü®i, by linarith‚ü©) (fun i hi => by
      obtain _ | i := i
      ¬∑ exact w
      ¬∑ let œÜ : (‚ü®i, by linarith‚ü© : Fin (n + 1)) ‚â§ ‚ü®i + 1, by linarith‚ü© := by simp
        exact œÜ‚ÇÄ.naturality (homOfLE œÜ))

@[simp]
lemma homMkSucc_app_zero : (homMkSucc app‚ÇÄ œÜ‚ÇÄ w).app 0 = app‚ÇÄ := rfl

@[simp]
lemma homMkSucc_app_succ (i : ‚Ñï) (hi : i + 1 < n + 1 + 1) :
    (homMkSucc app‚ÇÄ œÜ‚ÇÄ w).app ‚ü®i + 1, hi‚ü© = œÜ‚ÇÄ.app ‚ü®i, by linarith‚ü© := rfl

/-- Inductive construction of isomorphisms in `ComposableArrows C (n + 1)`. -/
@[simps]
def isoMkSucc (app‚ÇÄ : F.left ‚âÖ G.left) (œÜ‚ÇÄ : F.Œ¥‚ÇÄ ‚âÖ G.Œ¥‚ÇÄ)
    (w : F.map' 0 1 ‚â´ œÜ‚ÇÄ.hom.app 0 = app‚ÇÄ.hom ‚â´ G.map' 0 1) : F ‚âÖ G where
  hom := homMkSucc app‚ÇÄ.hom œÜ‚ÇÄ.hom w
  inv := homMkSucc app‚ÇÄ.inv œÜ‚ÇÄ.inv (by
    rw [‚Üê cancel_epi app‚ÇÄ.hom, ‚Üê reassoc_of% w, œÜ‚ÇÄ.hom_inv_id_app,
      app‚ÇÄ.hom_inv_id_assoc]
    dsimp
    rw [comp_id])
  hom_inv_id := hom_ext_succ app‚ÇÄ.hom_inv_id œÜ‚ÇÄ.hom_inv_id
  inv_hom_id := hom_ext_succ app‚ÇÄ.inv_hom_id œÜ‚ÇÄ.inv_hom_id

/-! Together with `precomp`, the extensionality lemma `ext_succ` below essentially shows
that an element in `ComposableArrows C (n + 1)` can be uniquely written as a "composition"
of an element `F : ComposableArrows C 1` and of an element `G : ComposableArrows C n`
such that `F.right = G.left`. In other words, in the nerve of the category `C`
(see `AlgebraicTopology.Nerve`), the `n + 1`-simplices identify to a certain fiber
product of `1`-simplicies and `n`-simplices over `0`-simplices. This is a characteristic
property of nerves of categories, see
`https://ncatlab.org/nlab/show/Segal+condition#of_simplicial_nerves_of_small_categories`. -/

lemma ext_succ (h : F.arrow 0 = G.arrow 0) (h‚ÇÄ : F.Œ¥‚ÇÄ = G.Œ¥‚ÇÄ) : F = G :=
  ext
    (fun ‚ü®i, hi‚ü© => by
      obtain _ | i := i
      ¬∑ exact Functor.congr_obj h 0
      ¬∑ exact Functor.congr_obj h‚ÇÄ ‚ü®i, by linarith‚ü©)
    (fun i hi => by
      obtain _ | i := i
      ¬∑ simpa using (naturality'_2 (eqToIso h) 0 1).symm
      ¬∑ simpa using (naturality'_2 (eqToIso h‚ÇÄ) i (i + 1)).symm)

end

section

variable {F G : ComposableArrows C 2}

@[ext]
lemma hom_ext‚ÇÇ {œÜ œÜ' : F ‚ü∂ G}
    (h‚ÇÄ : app' œÜ 0 = app' œÜ' 0) (h‚ÇÅ : app' œÜ 1 = app' œÜ' 1) (h‚ÇÇ : app' œÜ 2 = app' œÜ' 2) :
    œÜ = œÜ' :=
  hom_ext_succ h‚ÇÄ (hom_ext‚ÇÅ h‚ÇÅ h‚ÇÇ)

variable
  (app‚ÇÄ : F.obj' 0 ‚ü∂ G.obj' 0) (app‚ÇÅ : F.obj' 1 ‚ü∂ G.obj' 1) (app‚ÇÇ : F.obj' 2 ‚ü∂ G.obj' 2)
  (w‚ÇÅ : F.map' 0 1 ‚â´ app‚ÇÅ = app‚ÇÄ ‚â´ G.map' 0 1)
  (w‚ÇÇ : F.map' 1 2 ‚â´ app‚ÇÇ = app‚ÇÅ ‚â´ G.map' 1 2)

/-- Constructor for morphisms in `ComposableArrows C 1`. -/
def homMk‚ÇÇ : F ‚ü∂ G :=
  homMkSucc app‚ÇÄ (homMk‚ÇÅ app‚ÇÅ app‚ÇÇ w‚ÇÇ) w‚ÇÅ

@[simp]
lemma homMk‚ÇÇ_app_zero : (homMk‚ÇÇ app‚ÇÄ app‚ÇÅ app‚ÇÇ w‚ÇÅ w‚ÇÇ).app 0 = app‚ÇÄ := rfl

@[simp]
lemma homMk‚ÇÇ_app_one : (homMk‚ÇÇ app‚ÇÄ app‚ÇÅ app‚ÇÇ w‚ÇÅ w‚ÇÇ).app 1 = app‚ÇÅ := rfl

@[simp]
lemma homMk‚ÇÇ_app_two' : (homMk‚ÇÇ app‚ÇÄ app‚ÇÅ app‚ÇÇ w‚ÇÅ w‚ÇÇ).app ‚ü®2, by linarith‚ü© = app‚ÇÇ := rfl

/-- Constructor for isomorphisms in `ComposableArrows C 1`. -/
@[simps]
def isoMk‚ÇÇ (app‚ÇÄ : F.obj' 0 ‚âÖ G.obj' 0) (app‚ÇÅ : F.obj' 1 ‚âÖ G.obj' 1)
    (app‚ÇÇ : F.obj' 2 ‚âÖ G.obj' 2)
    (w‚ÇÅ : F.map' 0 1 ‚â´ app‚ÇÅ.hom = app‚ÇÄ.hom ‚â´ G.map' 0 1)
    (w‚ÇÇ : F.map' 1 2 ‚â´ app‚ÇÇ.hom = app‚ÇÅ.hom ‚â´ G.map' 1 2) : F ‚âÖ G where
  hom := homMk‚ÇÇ app‚ÇÄ.hom app‚ÇÅ.hom app‚ÇÇ.hom w‚ÇÅ w‚ÇÇ
  inv := homMk‚ÇÇ app‚ÇÄ.inv app‚ÇÅ.inv app‚ÇÇ.inv
    (by rw [‚Üê cancel_epi app‚ÇÄ.hom, ‚Üê reassoc_of% w‚ÇÅ, app‚ÇÅ.hom_inv_id,
      comp_id, app‚ÇÄ.hom_inv_id_assoc])
    (by rw [‚Üê cancel_epi app‚ÇÅ.hom, ‚Üê reassoc_of% w‚ÇÇ, app‚ÇÇ.hom_inv_id,
      comp_id, app‚ÇÅ.hom_inv_id_assoc])

lemma ext‚ÇÇ (h‚ÇÄ : F.obj' 0 = G.obj' 0) (h‚ÇÅ : F.obj' 1 = G.obj' 1) (h‚ÇÇ : F.obj' 2 = G.obj' 2)
    (w‚ÇÅ : F.map' 0 1 = eqToHom h‚ÇÄ ‚â´ G.map' 0 1 ‚â´ eqToHom h‚ÇÅ.symm)
    (w‚ÇÇ : F.map' 1 2 = eqToHom h‚ÇÅ ‚â´ G.map' 1 2 ‚â´ eqToHom h‚ÇÇ.symm) : F = G :=
  ext_succ (ext‚ÇÅ h‚ÇÄ h‚ÇÅ w‚ÇÅ) (ext‚ÇÅ h‚ÇÅ h‚ÇÇ w‚ÇÇ)

variable (F)

lemma mk‚ÇÇ_surjective : ‚àÉ (X‚ÇÄ X‚ÇÅ X‚ÇÇ : C) (f : X‚ÇÄ ‚ü∂ X‚ÇÅ) (g : X‚ÇÅ ‚ü∂ X‚ÇÇ), F = mk‚ÇÇ f g :=
  ‚ü®_, _, _, F.map' 0 1, F.map' 1 2, ext‚ÇÇ rfl rfl rfl (by simp) (by simp)‚ü©

end

/-! The lemmas `segal_condition_uniq` and `segal_condition_exists` essentially prove
that nerves of categories (see `AlgebraicTopology.Nerve`) satisfy the
Segal condition, see
https://ncatlab.org/nlab/show/Segal+condition#of_simplicial_nerves_of_small_categories -/

lemma segal_condition_uniq {F G : ComposableArrows C (n + 1)}
    (h : ‚àÄ (i : ‚Ñï) (hi : i ‚â§ n), F.arrow i = G.arrow i) : F = G := by
  revert F G h
  induction' n with n hn
  ¬∑ intro F G h
    rw [F.eq_arrow_zero, G.eq_arrow_zero, h 0 (by rfl)]
  ¬∑ intro F G h
    apply ext_succ
    ¬∑ exact h 0 (by linarith)
    ¬∑ exact hn (fun i hi => h (i + 1) (by linarith))

lemma segal_condition_exists (œÜ : Fin (n + 1) ‚Üí ComposableArrows C 1)
    (hœÜ : ‚àÄ (i : Fin n), (œÜ i.castSucc).right = (œÜ i.succ).left) :
      ‚àÉ (F : ComposableArrows C (n + 1)),
        ‚àÄ (i : ‚Ñï) (hi : i ‚â§ n), F.arrow i = œÜ ‚ü®i, by linarith‚ü© := by
  revert œÜ
  induction' n with n hn
  ¬∑ intro œÜ _
    refine' ‚ü®œÜ 0, fun i hi => _‚ü©
    obtain rfl : i = 0 := by linarith
    exact (eq_arrow_zero _).symm
  ¬∑ intro œÜ hœÜ
    obtain ‚ü®F, hF‚ü© := hn (fun ‚ü®i, hi‚ü© => œÜ ‚ü®i + 1, by linarith‚ü©) (fun i => hœÜ i.succ)
    have : (œÜ 0).right = F.obj' 0 := (hœÜ 0).trans (by
      change _ = (F.arrow 0).left
      rw [hF 0 (by linarith)]
      rfl)
    refine' ‚ü®F.precomp ((œÜ 0).hom ‚â´ eqToHom this), fun i hi => _‚ü©
    obtain _ | i := i
    ¬∑ exact ext‚ÇÅ rfl this.symm (by simp)
    ¬∑ exact hF i (by linarith)

/-- Given a functor `F : C ‚•§ D` and for all `X : C`, an object `obj X : D` that is
isomorphic to `F.obj X`, this is the functor isomorphic to `F` obtained by replacing
`F.obj` by `obj`. -/
@[simps]
def _root_.CategoryTheory.Functor.mkOfIsoObj {C D : Type*} [Category C] [Category D]
    (F : C ‚•§ D) (obj : C ‚Üí D) (e : ‚àÄ X, F.obj X ‚âÖ obj X) : C ‚•§ D where
  obj := obj
  map f := (e _).inv ‚â´ F.map f ‚â´ (e _).hom

/-- The isomorphism to the functor constructed by `Functor.mkOfIsoObj`. -/
@[simps!]
def _root_.CategoryTheory.Functor.isoMkOfIsoObj {C D : Type*} [Category C] [Category D]
    (F : C ‚•§ D) (obj : C ‚Üí D) (e : ‚àÄ X, F.obj X ‚âÖ obj X) :
    F ‚âÖ F.mkOfIsoObj obj e :=
  NatIso.ofComponents e (by aesop_cat)

section

variable (obj : Fin (n + 1) ‚Üí C) (mapSucc : ‚àÄ (i : Fin n), obj i.castSucc ‚ü∂ obj i.succ)

lemma mkOfObjMapSucc_aux :
    ‚àÉ (F : ComposableArrows C n) (iso : ‚àÄ (i : Fin (n + 1)), F.obj i ‚âÖ obj i),
      ‚àÄ (i : ‚Ñï) (hi : i < n), F.map' i (i + 1) =
        (iso _).hom ‚â´ mapSucc ‚ü®i, hi‚ü© ‚â´ (iso _).inv := by
  obtain _ | n := n
  ¬∑ exact ‚ü®mk‚ÇÄ (obj 0), fun 0 => Iso.refl _, fun i hi => by simp at hi‚ü©
  ¬∑ obtain ‚ü®F, hF‚ü© := (segal_condition_exists (fun i => mk‚ÇÅ (mapSucc i)) (fun i => rfl))
    have : ‚àÄ (i : Fin (n + 2)), F.obj i = obj i := fun ‚ü®i, hi‚ü© => by
      obtain _ | i := i
      ¬∑ exact congr_arg left (hF 0 (by linarith))
      ¬∑ exact congr_arg right (hF i (by linarith))
    exact ‚ü®F, fun i => eqToIso (this i), fun i hi =>
      by simpa using (naturality'_2 (eqToIso (hF i (by linarith))) 0 1).symm‚ü©

/-- This is the functor in `ComposableArrows C n` that is obtained from
objects `obj : Fin (n +1) ‚Üí C` and morphisms `obj i.castSucc ‚ü∂ obj i.succ`.
It has good definitional properties on objects. -/
@[simps! obj]
noncomputable def mkOfObjMapSucc :
    ComposableArrows C n :=
  (mkOfObjMapSucc_aux obj mapSucc).choose.mkOfIsoObj obj
    (mkOfObjMapSucc_aux obj mapSucc).choose_spec.choose

lemma mkOfObjMapSucc_map'_succ (i : ‚Ñï) (hi : i < n := by linarith) :
    (mkOfObjMapSucc obj mapSucc).map' i (i + 1) = mapSucc ‚ü®i, hi‚ü© := by
  let iso := (mkOfObjMapSucc_aux obj mapSucc).choose_spec.choose
  change (iso _).inv ‚â´ _ ‚â´ (iso _).hom = _
  rw [‚Üê cancel_mono (iso _).inv, ‚Üê cancel_epi (iso _).hom, assoc, assoc,
    Iso.hom_inv_id, comp_id, Iso.hom_inv_id_assoc]
  apply (mkOfObjMapSucc_aux obj mapSucc).choose_spec.choose_spec

lemma mkOfObjMapSucc_arrow (i : ‚Ñï) (hi : i < n) :
    (mkOfObjMapSucc obj mapSucc).arrow i = mk‚ÇÅ (mapSucc ‚ü®i, hi‚ü©) := by
  rw [‚Üê mkOfObjMapSucc_map'_succ obj mapSucc i]

end

end ComposableArrows

variable {C}

/-- The functor `ComposableArrows C n ‚•§ ComposableArrows D n` obtained by postcomposition
with a functor `C ‚•§ D`. -/
@[simps!]
def Functor.mapComposableArrows {D : Type*} [Category D] (G : C ‚•§ D) (n : ‚Ñï) :
    ComposableArrows C n ‚•§ ComposableArrows D n :=
  (whiskeringRight _ _ _).obj G

end CategoryTheory
