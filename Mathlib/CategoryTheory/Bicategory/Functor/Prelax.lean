/-
Copyright (c) 2024 Calle S√∂nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne
-/

import Mathlib.CategoryTheory.Bicategory.Basic

/-!

# Prelax functors

This file defines lax prefunctors and prelax functors between bicategories. The point of these
definitions is to give some API that will be helpful in both the development of Lax and Oplax
functors.

A lax prefunctor `F` between quivers `B` and `C`, equipped with quiver structures on the hom types,
consists of
* a function between objects `F.obj : B ‚ü∂ C`,
* a family of functions between 1-morphisms `F.map : (a ‚ü∂ b) ‚Üí (F.obj a ‚ü∂ F.obj b)`,
* a family of functions between 2-morphisms `F.map‚ÇÇ : (f ‚ü∂ g) ‚Üí (F.map f ‚ü∂ F.map g)`,

A prelax functor is a lax prefunctor such that `map‚ÇÇ` is a functor. Namely, it satisfies
* `F.map‚ÇÇ (ùüô f) = ùüô (F.map f)`,
* `F.map‚ÇÇ (Œ∑ ‚â´ Œ∏) = F.map‚ÇÇ Œ∑ ‚â´ F.map‚ÇÇ Œ∏`.

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe w‚ÇÅ w‚ÇÇ w‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

section

variable {B : Type u‚ÇÅ} [Quiver.{v‚ÇÅ + 1} B] [‚àÄ a b : B, Quiver.{w‚ÇÅ + 1} (a ‚ü∂ b)]
variable {C : Type u‚ÇÇ} [Quiver.{v‚ÇÇ + 1} C] [‚àÄ a b : C, Quiver.{w‚ÇÇ + 1} (a ‚ü∂ b)]
variable {D : Type u‚ÇÉ} [Quiver.{v‚ÇÉ + 1} D] [‚àÄ a b : D, Quiver.{w‚ÇÉ + 1} (a ‚ü∂ b)]

/-- A lax prefunctor between bicategories consists of functions between objects,
1-morphisms, and 2-morphisms. This structure will be extended to define `PrelaxFunctor`.
-/
structure LaxPreFunctor (B : Type u‚ÇÅ) [Quiver.{v‚ÇÅ + 1} B] [‚àÄ a b : B, Quiver.{w‚ÇÅ + 1} (a ‚ü∂ b)]
  (C : Type u‚ÇÇ) [Quiver.{v‚ÇÇ + 1} C] [‚àÄ a b : C, Quiver.{w‚ÇÇ + 1} (a ‚ü∂ b)] extends
  Prefunctor B C where
  /-- The action of a prelax functor on 2-morphisms. -/
  map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} : (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g)
#align category_theory.prelax_functor CategoryTheory.LaxPreFunctor

initialize_simps_projections LaxPreFunctor (+toPrefunctor, -obj, -map)

/-- The prefunctor between the underlying quivers. -/
add_decl_doc LaxPreFunctor.toPrefunctor

namespace LaxPreFunctor

attribute [coe] CategoryTheory.LaxPreFunctor.toPrefunctor

instance hasCoeToPrefunctor : Coe (LaxPreFunctor B C) (Prefunctor B C) :=
  ‚ü®toPrefunctor‚ü©
#align category_theory.prelax_functor.has_coe_to_prefunctor CategoryTheory.LaxPreFunctor.hasCoeToPrefunctor

variable (F : LaxPreFunctor B C)

-- Porting note: deleted syntactic tautologies `toPrefunctor_eq_coe : F.toPrefunctor = F`
-- and `to_prefunctor_obj : (F : Prefunctor B C).obj = F.obj`
-- and `to_prefunctor_map`
#noalign category_theory.prelax_functor.to_prefunctor_eq_coe
#noalign category_theory.prelax_functor.to_prefunctor_obj
#noalign category_theory.prelax_functor.to_prefunctor_map

/-- The identity prelax functor. -/
@[simps]
def id (B : Type u‚ÇÅ) [Quiver.{v‚ÇÅ + 1} B] [‚àÄ a b : B, Quiver.{w‚ÇÅ + 1} (a ‚ü∂ b)] : LaxPreFunctor B B :=
  { Prefunctor.id B with map‚ÇÇ := fun Œ∑ => Œ∑ }
#align category_theory.prelax_functor.id CategoryTheory.LaxPreFunctor.id

instance : Inhabited (LaxPreFunctor B B) :=
  ‚ü®LaxPreFunctor.id B‚ü©

-- Porting note: `by exact` was not necessary in mathlib3
/-- Composition of prelax functors. -/
@[simps]
def comp (F : LaxPreFunctor B C) (G : LaxPreFunctor C D) : LaxPreFunctor B D :=
  { (F : Prefunctor B C).comp ‚ÜëG with map‚ÇÇ := fun Œ∑ => by exact G.map‚ÇÇ (F.map‚ÇÇ Œ∑) }
#align category_theory.prelax_functor.comp CategoryTheory.LaxPreFunctor.comp

end LaxPreFunctor

end

/-- A prelax functor between bicategories is a lax prefunctor such that `map‚ÇÇ` is a functor.
This structure will be extended to define `LaxFunctor` and `OplaxFunctor`.
-/
structure PrelaxFunctor (B: Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] (C : Type u‚ÇÇ) [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
    extends LaxPreFunctor B C where
  /-- Prelax functors preserve identity 2-morphisms. -/
  map‚ÇÇ_id : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) := by aesop
  /-- Prelax functors preserve compositions. -/
  map‚ÇÇ_comp :
    ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h), map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ := by
    aesop_cat

namespace PrelaxFunctor

attribute [simp] map‚ÇÇ_id
attribute [reassoc (attr := simp)] map‚ÇÇ_comp

variable {B : Type u‚ÇÅ} [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] {C : Type u‚ÇÇ} [Bicategory.{w‚ÇÇ, v‚ÇÇ} C]
variable {D : Type u‚ÇÉ} [Bicategory.{w‚ÇÉ, v‚ÇÉ} D]

attribute [coe] CategoryTheory.LaxPreFunctor.toPrefunctor

instance hasCoeToLaxPreFunctor : Coe (PrelaxFunctor B C) (LaxPreFunctor B C) :=
  ‚ü®toLaxPreFunctor‚ü©

-- TODO: what simps to include here...?
/-- The identity prelax functor. -/
@[simps!]
def id (B : Type u‚ÇÅ) [Bicategory.{w‚ÇÅ, v‚ÇÅ} B] : PrelaxFunctor B B where
  toLaxPreFunctor := LaxPreFunctor.id B

instance : Inhabited (LaxPreFunctor B B) :=
  ‚ü®LaxPreFunctor.id B‚ü©

variable (F : PrelaxFunctor B C)

/-- Composition of prelax functors. -/
@[simps!]
def comp (G : PrelaxFunctor C D) : PrelaxFunctor B D where
  toLaxPreFunctor := LaxPreFunctor.comp F.toLaxPreFunctor G.toLaxPreFunctor

/-- Function between 1-morphisms as a functor. -/
@[simps]
def mapFunctor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) where
  obj f := F.map f
  map Œ∑ := F.map‚ÇÇ Œ∑

section

variable {a b : B}

/-- An oplax functor `F : B ‚•§ C` sends 2-isomorphisms `Œ∑ : f ‚âÖ f` to 2-isomorphisms
`F.map f ‚âÖ F.map g` -/
@[simps!]
abbrev map‚ÇÇIso {f g : a ‚ü∂ b} (Œ∑ : f ‚âÖ g) : F.map f ‚âÖ F.map g :=
  (F.mapFunctor a b).mapIso Œ∑

instance map‚ÇÇ_isIso {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) [IsIso Œ∑] : IsIso (F.map‚ÇÇ Œ∑) :=
  (F.map‚ÇÇIso (asIso Œ∑)).isIso_hom

@[simp]
lemma map‚ÇÇ_inv {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) [IsIso Œ∑] : F.map‚ÇÇ (inv Œ∑) = inv (F.map‚ÇÇ Œ∑) := by
  apply IsIso.eq_inv_of_hom_inv_id
  simp [‚Üê F.map‚ÇÇ_comp Œ∑ (inv Œ∑)]

@[reassoc]
lemma map‚ÇÇ_hom_inv {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) [IsIso Œ∑] :
    F.map‚ÇÇ Œ∑ ‚â´ F.map‚ÇÇ (inv Œ∑) = ùüô (F.map f) := by
  simp

@[reassoc]
lemma map‚ÇÇ_inv_hom {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) [IsIso Œ∑] :
    F.map‚ÇÇ (inv Œ∑) ‚â´ F.map‚ÇÇ Œ∑ = ùüô (F.map g) := by
  simp

end

end PrelaxFunctor
