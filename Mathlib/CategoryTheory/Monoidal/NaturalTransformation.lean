/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Adjunction.FullyFaithful
import Mathlib.CategoryTheory.Monoidal.Functor
import Mathlib.CategoryTheory.FullSubcategory

/-!
# Monoidal natural transformations

Natural transformations between (lax) monoidal functors must satisfy
an additional compatibility relation with the tensorators:
`F.Œº X Y ‚â´ app (X ‚äó Y) = (app X ‚äó app Y) ‚â´ G.Œº X Y`.

(Lax) monoidal functors between a fixed pair of monoidal categories
themselves form a category.
-/

open CategoryTheory

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

open CategoryTheory.Category

open CategoryTheory.Functor

namespace CategoryTheory
section

open SemigroupalCategory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [SemigroupalCategory.{v‚ÇÅ} C] {D : Type u‚ÇÇ}
  [Category.{v‚ÇÇ} D] [SemigroupalCategory.{v‚ÇÇ} D]

/-- A semigroupal natural transformation is a natural transformation between (lax) semigroupal functors
additionally satisfying:
`F.Œº X Y ‚â´ app (X ‚äó Y) = (app X ‚äó app Y) ‚â´ G.Œº X Y`
-/
@[ext]
structure SemigroupalNatTrans (F G : LaxSemigroupalFunctor C D) extends
  NatTrans F.toFunctor G.toFunctor where
  /-- The tensor condition for a semigroupal natural transformation. -/
  tensor : ‚àÄ X Y, F.Œº _ _ ‚â´ app (X ‚äó Y) = (app X ‚äó app Y) ‚â´ G.Œº _ _ := by aesop_cat

-- Porting note: `reassoc (attr := simp)` seems to add a `simp`
-- attribute to the original lemma as well.
attribute [reassoc (attr := simp)] SemigroupalNatTrans.tensor

initialize_simps_projections SemigroupalNatTrans (+toNatTrans, -app)

namespace SemigroupalNatTrans

/-- The identity semigroupal natural transformation. -/
@[simps!]
def id (F : LaxSemigroupalFunctor C D) : SemigroupalNatTrans F F :=
  { ùüô F.toFunctor with }

instance (F : LaxSemigroupalFunctor C D) : Inhabited (SemigroupalNatTrans F F) :=
  ‚ü®id F‚ü©

/-- Vertical composition of semigroupal natural transformations. -/
@[simps!]
def vcomp {F G H : LaxSemigroupalFunctor C D}
    (Œ± : SemigroupalNatTrans F G) (Œ≤ : SemigroupalNatTrans G H) :
    SemigroupalNatTrans F H :=
  { NatTrans.vcomp Œ±.toNatTrans Œ≤.toNatTrans with }

instance categoryLaxSemigroupalFunctor : Category (LaxSemigroupalFunctor C D) where
  Hom := SemigroupalNatTrans
  id := id
  comp Œ± Œ≤ := vcomp Œ± Œ≤

@[simp]
theorem comp_toNatTrans_lax {F G H : LaxSemigroupalFunctor C D} {Œ± : F ‚ü∂ G} {Œ≤ : G ‚ü∂ H} :
    (Œ± ‚â´ Œ≤).toNatTrans = @CategoryStruct.comp (C ‚•§ D) _ _ _ _ Œ±.toNatTrans Œ≤.toNatTrans :=
  rfl

instance categorySemigroupalFunctor : Category (SemigroupalFunctor C D) :=
  InducedCategory.category SemigroupalFunctor.toLaxSemigroupalFunctor

-- Porting note: added, as `SemigroupalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : LaxSemigroupalFunctor C D} {Œ± Œ≤ : F ‚ü∂ G}
    (w : ‚àÄ X : C, Œ±.app X = Œ≤.app X) : Œ± = Œ≤ :=
  SemigroupalNatTrans.ext (funext w)

@[simp]
theorem comp_toNatTrans {F G H : SemigroupalFunctor C D} {Œ± : F ‚ü∂ G} {Œ≤ : G ‚ü∂ H} :
    (Œ± ‚â´ Œ≤).toNatTrans = @CategoryStruct.comp (C ‚•§ D) _ _ _ _ Œ±.toNatTrans Œ≤.toNatTrans :=
  rfl

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] [SemigroupalCategory.{v‚ÇÉ} E]

/-- Horizontal composition of semigroupal natural transformations. -/
@[simps]
def hcomp {F G : LaxSemigroupalFunctor C D}
    {H K : LaxSemigroupalFunctor D E} (Œ± : SemigroupalNatTrans F G)
    (Œ≤ : SemigroupalNatTrans H K) : SemigroupalNatTrans (F ‚äó‚ãô H) (G ‚äó‚ãô K) :=
  { NatTrans.hcomp Œ±.toNatTrans Œ≤.toNatTrans with
    tensor := fun X Y => by
      dsimp; simp
      conv_lhs => rw [‚Üê K.toFunctor.map_comp, Œ±.tensor, K.toFunctor.map_comp] }

section

attribute [local simp] NatTrans.naturality SemigroupalNatTrans.tensor

/-- The cartesian product of two semigroupal natural transformations is semigroupal. -/
@[simps]
def prod {F G : LaxSemigroupalFunctor C D} {H K : LaxSemigroupalFunctor C E}
    (Œ± : SemigroupalNatTrans F G) (Œ≤ : SemigroupalNatTrans H K) :
    SemigroupalNatTrans (F.prod' H) (G.prod' K) where
  app X := (Œ±.app X, Œ≤.app X)

end

end SemigroupalNatTrans

namespace SemigroupalNatIso

variable {F G : LaxSemigroupalFunctor C D}

/-- Construct a semigroupal natural isomorphism from object level isomorphisms,
and the semigroupal naturality in the forward direction. -/
def ofComponents (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X)
    (naturality' :
      ‚àÄ {X Y : C} (f : X ‚ü∂ Y), F.map f ‚â´ (app Y).hom = (app X).hom ‚â´ G.map f := by aesop_cat)
    (tensor' :
      ‚àÄ X Y, F.Œº X Y ‚â´ (app (X ‚äó Y)).hom = ((app X).hom ‚äó (app Y).hom) ‚â´ G.Œº X Y := by aesop_cat) :
    F ‚âÖ G where
  hom := { app := fun X => (app X).hom }
  inv := {
    (NatIso.ofComponents app @naturality').inv with
    app := fun X => (app X).inv
    tensor := fun X Y => by
      dsimp
      rw [Iso.comp_inv_eq, assoc, tensor', ‚Üê tensor_comp_assoc,
        Iso.inv_hom_id, Iso.inv_hom_id, tensor_id, id_comp] }

@[simp]
theorem ofComponents.hom_app (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X) (naturality) (tensor) (X) :
    (ofComponents app naturality tensor).hom.app X = (app X).hom :=
  rfl

@[simp]
theorem ofComponents.inv_app (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X) (naturality) (tensor) (X) :
    (ofComponents app naturality tensor).inv.app X = (app X).inv := by simp [ofComponents]

instance isIso_of_isIso_app (Œ± : F ‚ü∂ G) [‚àÄ X : C, IsIso (Œ±.app X)] : IsIso Œ± :=
  (ofComponents (fun X => asIso (Œ±.app X)) (fun f => Œ±.toNatTrans.naturality f) Œ±.tensor).isIso_hom

end SemigroupalNatIso

variable (F : SemigroupalFunctor C D) {G : D ‚•§ C} (h : F.toFunctor ‚ä£ G)

/-- The unit of a adjunction can be upgraded to a semigroupal natural transformation. -/
def semigroupalUnit  :
    LaxSemigroupalFunctor.id C ‚ü∂ F.toLaxSemigroupalFunctor ‚äó‚ãô (semigroupalAdjoint F h) where
  toNatTrans := h.unit
  tensor X Y := by
    dsimp
    simp only [id_comp, comp_id, assoc, Adjunction.homEquiv_unit,
      ‚Üê h.unit_naturality_assoc, ‚Üê Functor.map_comp,
      F.map_tensor, IsIso.hom_inv_id_assoc, ‚Üê tensor_comp_assoc,
      Adjunction.left_triangle_components, tensorHom_id, id_whiskerRight,
      IsIso.inv_hom_id, map_id]

/-- The unit of a adjunction can be upgraded to a semigroupal natural transformation. -/
@[simps]
def semigroupalCounit :
    (semigroupalAdjoint F h) ‚äó‚ãô F.toLaxSemigroupalFunctor ‚ü∂ LaxSemigroupalFunctor.id D where
  toNatTrans := h.counit
  tensor X Y := by
    have eq := h.counit_naturality (F.Œº (G.obj X) (G.obj Y)) =‚â´ inv (F.Œº _ _)
    simp only [assoc, IsIso.hom_inv_id, comp_id] at eq
    dsimp
    simp only [Adjunction.homEquiv_unit, comp_id, assoc,
      map_comp, map_inv, h.counit_naturality, ‚Üê eq,
      h.left_triangle_components_assoc,
      IsIso.inv_hom_id_assoc, IsIso.hom_inv_id_assoc]

instance [F.IsEquivalence] : IsIso (semigroupalUnit F h) := by
  dsimp [semigroupalUnit]
  infer_instance

instance [F.IsEquivalence] : IsIso (semigroupalCounit F h) := by
  dsimp [semigroupalCounit]
  infer_instance

end
open MonoidalCategory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] [MonoidalCategory.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  [MonoidalCategory.{v‚ÇÇ} D]

/-- A monoidal natural transformation is a natural transformation between (lax) monoidal functors
additionally satisfying:
`F.Œº X Y ‚â´ app (X ‚äó Y) = (app X ‚äó app Y) ‚â´ G.Œº X Y`
-/
@[ext]
structure MonoidalNatTrans (F G : LaxMonoidalFunctor C D) extends
    SemigroupalNatTrans F.1 G.1 where
  /-- The unit condition for a monoidal natural transformation. -/
  unit : F.Œµ ‚â´ app (ùüô_ C) = G.Œµ := by aesop_cat

-- Porting note: `reassoc (attr := simp)` seems to add a `simp`
-- attribute to the original lemma as well.
attribute [reassoc (attr := simp)] MonoidalNatTrans.unit

initialize_simps_projections MonoidalNatTrans (+toNatTrans, -app)

namespace MonoidalNatTrans

/-- The identity monoidal natural transformation. -/
@[simps!]
def id (F : LaxMonoidalFunctor C D) : MonoidalNatTrans F F :=
  { ùüô F.toFunctor with }

instance (F : LaxMonoidalFunctor C D) : Inhabited (MonoidalNatTrans F F) :=
  ‚ü®id F‚ü©

/-- Vertical composition of monoidal natural transformations. -/
@[simps!]
def vcomp {F G H : LaxMonoidalFunctor C D} (Œ± : MonoidalNatTrans F G) (Œ≤ : MonoidalNatTrans G H) :
    MonoidalNatTrans F H :=
  { NatTrans.vcomp Œ±.toNatTrans Œ≤.toNatTrans with }

instance categoryLaxMonoidalFunctor : Category (LaxMonoidalFunctor C D) where
  Hom := MonoidalNatTrans
  id := id
  comp Œ± Œ≤ := vcomp Œ± Œ≤

@[simp]
theorem comp_toNatTrans_lax {F G H : LaxMonoidalFunctor C D} {Œ± : F ‚ü∂ G} {Œ≤ : G ‚ü∂ H} :
    (Œ± ‚â´ Œ≤).toNatTrans = @CategoryStruct.comp (C ‚•§ D) _ _ _ _ Œ±.toNatTrans Œ≤.toNatTrans :=
  rfl

instance categoryMonoidalFunctor : Category (MonoidalFunctor C D) :=
  InducedCategory.category MonoidalFunctor.toLaxMonoidalFunctor

-- Porting note: added, as `MonoidalNatTrans.ext` does not apply to morphisms.
@[ext]
lemma ext' {F G : LaxMonoidalFunctor C D} {Œ± Œ≤ : F ‚ü∂ G} (w : ‚àÄ X : C, Œ±.app X = Œ≤.app X) : Œ± = Œ≤ :=
  MonoidalNatTrans.ext (funext w)

@[simp]
theorem comp_toNatTrans {F G H : MonoidalFunctor C D} {Œ± : F ‚ü∂ G} {Œ≤ : G ‚ü∂ H} :
    (Œ± ‚â´ Œ≤).toNatTrans = @CategoryStruct.comp (C ‚•§ D) _ _ _ _ Œ±.toNatTrans Œ≤.toNatTrans :=
  rfl

variable {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E] [MonoidalCategory.{v‚ÇÉ} E]

/-- Horizontal composition of monoidal natural transformations. -/
@[simps]
def hcomp {F G : LaxMonoidalFunctor C D} {H K : LaxMonoidalFunctor D E} (Œ± : MonoidalNatTrans F G)
    (Œ≤ : MonoidalNatTrans H K) : MonoidalNatTrans (F ‚äó‚ãô H) (G ‚äó‚ãô K) :=
  { NatTrans.hcomp Œ±.toNatTrans Œ≤.toNatTrans with
    unit := by
      dsimp; simp
      conv_lhs => rw [‚Üê K.toFunctor.map_comp, Œ±.unit]
    tensor := fun X Y => by
      dsimp; simp
      conv_lhs => rw [‚Üê K.toFunctor.map_comp, Œ±.tensor, K.toFunctor.map_comp] }

section

attribute [local simp] NatTrans.naturality MonoidalNatTrans.unit

/-- The cartesian product of two monoidal natural transformations is monoidal. -/
@[simps]
def prod {F G : LaxMonoidalFunctor C D} {H K : LaxMonoidalFunctor C E} (Œ± : MonoidalNatTrans F G)
    (Œ≤ : MonoidalNatTrans H K) : MonoidalNatTrans (F.prod' H) (G.prod' K) where
  app X := (Œ±.app X, Œ≤.app X)

end

end MonoidalNatTrans

namespace MonoidalNatIso

variable {F G : LaxMonoidalFunctor C D}

open SemigroupalCategory

/-- Construct a monoidal natural isomorphism from object level isomorphisms,
and the monoidal naturality in the forward direction. -/
def ofComponents (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X)
    (naturality' :
      ‚àÄ {X Y : C} (f : X ‚ü∂ Y), F.map f ‚â´ (app Y).hom = (app X).hom ‚â´ G.map f := by aesop_cat)
    (unit' : F.Œµ ‚â´ (app (ùüô_ C)).hom = G.Œµ := by aesop_cat)
    (tensor' :
      ‚àÄ X Y, F.Œº X Y ‚â´ (app (X ‚äó Y)).hom = ((app X).hom ‚äó (app Y).hom) ‚â´ G.Œº X Y := by aesop_cat) :
    F ‚âÖ G where
  hom := { app := fun X => (app X).hom }
  inv := {
    (NatIso.ofComponents app @naturality').inv,
    (SemigroupalNatIso.ofComponents app naturality' tensor').inv with
    app := fun X => (app X).inv
    unit := by
      dsimp
      rw [‚Üê unit', assoc, Iso.hom_inv_id, comp_id] }

@[simp]
theorem ofComponents.hom_app (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X) (naturality) (unit) (tensor) (X) :
    (ofComponents app naturality unit tensor).hom.app X = (app X).hom :=
  rfl

@[simp]
theorem ofComponents.inv_app (app : ‚àÄ X : C, F.obj X ‚âÖ G.obj X) (naturality) (unit) (tensor) (X) :
    (ofComponents app naturality unit tensor).inv.app X = (app X).inv := by simp [ofComponents]

instance isIso_of_isIso_app (Œ± : F ‚ü∂ G) [‚àÄ X : C, IsIso (Œ±.app X)] : IsIso Œ± :=
  (ofComponents (fun X => asIso (Œ±.app X)) (fun f => Œ±.toNatTrans.naturality f)
    Œ±.unit Œ±.tensor).isIso_hom

end MonoidalNatIso

variable (F : MonoidalFunctor C D) {G : D ‚•§ C} (h : F.toFunctor ‚ä£ G)

/-- The unit of a adjunction can be upgraded to a monoidal natural transformation. -/
def monoidalUnit  :
    LaxMonoidalFunctor.id C ‚ü∂ F.toLaxMonoidalFunctor ‚äó‚ãô (monoidalAdjoint F h) where
  toSemigroupalNatTrans := semigroupalUnit F.toSemigroupalFunctor h

/-- The unit of a adjunction can be upgraded to a monoidal natural transformation. -/
@[simps]
def monoidalCounit :
    (monoidalAdjoint F h) ‚äó‚ãô F.toLaxMonoidalFunctor ‚ü∂ LaxMonoidalFunctor.id D :=
{ semigroupalCounit F.toSemigroupalFunctor h with
  toNatTrans := h.counit
  unit := by
    have eq := h.counit.naturality F.Œµ
    dsimp at eq ‚ä¢
    rw [map_inv, map_comp, assoc, assoc, map_inv, ‚Üê cancel_mono F.Œµ, assoc, assoc, assoc, ‚Üê eq,
      IsIso.inv_hom_id_assoc, Adjunction.left_triangle_components, comp_id, id_comp] }

instance [F.IsEquivalence] : IsIso (monoidalUnit F h) := by
  dsimp [monoidalUnit, semigroupalUnit]
  infer_instance

instance [F.IsEquivalence] : IsIso (monoidalCounit F h) := by
  dsimp [monoidalCounit, semigroupalCounit]
  infer_instance

end CategoryTheory
