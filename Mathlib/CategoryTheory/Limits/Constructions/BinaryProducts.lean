/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Andrew Yang
-/
import Mathlib.CategoryTheory.Limits.Shapes.Terminal
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Pullbacks
import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Terminal

#align_import category_theory.limits.constructions.binary_products from "leanprover-community/mathlib"@"3424a5932a77dcec2c177ce7d805acace6149299"

/-!
# Constructing binary product from pullbacks and terminal object.

The product is the pullback over the terminal objects. In particular, if a category
has pullbacks and a terminal object, then it has binary products.

We also provide the dual.
-/


universe v v' u u'

open CategoryTheory CategoryTheory.Category CategoryTheory.Limits

variable {C : Type u} [Category.{v} C] {D : Type u'} [Category.{v'} D] (F : C ‚•§ D)

/-- If a span is the pullback span over the terminal object, then it is a binary product. -/
def isBinaryProductOfIsTerminalIsPullback (F : Discrete WalkingPair ‚•§ C) (c : Cone F) {X : C}
    (hX : IsTerminal X) (f : F.obj ‚ü®WalkingPair.left‚ü© ‚ü∂ X) (g : F.obj ‚ü®WalkingPair.right‚ü© ‚ü∂ X)
    (hc : IsLimit
      (PullbackCone.mk (c.œÄ.app ‚ü®WalkingPair.left‚ü©) (c.œÄ.app ‚ü®WalkingPair.right‚ü© : _) <|
        hX.hom_ext (_ ‚â´ f) (_ ‚â´ g))) : IsLimit c where
  lift s :=
    hc.lift
      (PullbackCone.mk (s.œÄ.app ‚ü®WalkingPair.left‚ü©) (s.œÄ.app ‚ü®WalkingPair.right‚ü©) (hX.hom_ext _ _))
  fac s j :=
    Discrete.casesOn j fun j =>
      WalkingPair.casesOn j (hc.fac _ WalkingCospan.left) (hc.fac _ WalkingCospan.right)
  uniq s m J := by
    let c' :=
      PullbackCone.mk (m ‚â´ c.œÄ.app ‚ü®WalkingPair.left‚ü©) (m ‚â´ c.œÄ.app ‚ü®WalkingPair.right‚ü© : _)
        (hX.hom_ext (_ ‚â´ f) (_ ‚â´ g))
    dsimp; rw [‚Üê J, ‚Üê J]
    -- ‚ä¢ m = IsLimit.lift hc (PullbackCone.mk (NatTrans.app s.œÄ { as := WalkingPair.l ‚Ä¶
           -- ‚ä¢ m = IsLimit.lift hc (PullbackCone.mk (m ‚â´ NatTrans.app c.œÄ { as := WalkingPa ‚Ä¶
    apply hc.hom_ext
    -- ‚ä¢ ‚àÄ (j : WalkingCospan), m ‚â´ NatTrans.app (PullbackCone.mk (NatTrans.app c.œÄ { ‚Ä¶
    rintro (_ | (_ | _)) <;> simp only [PullbackCone.mk_œÄ_app_one, PullbackCone.mk_œÄ_app]
                             -- ‚ä¢ m ‚â´ NatTrans.app c.œÄ { as := WalkingPair.left } ‚â´ f = IsLimit.lift hc (Pullb ‚Ä¶
                             -- ‚ä¢ m ‚â´ NatTrans.app c.œÄ { as := WalkingPair.left } = IsLimit.lift hc (PullbackC ‚Ä¶
                             -- ‚ä¢ m ‚â´ NatTrans.app c.œÄ { as := WalkingPair.right } = IsLimit.lift hc (Pullback ‚Ä¶
    exacts [(Category.assoc _ _ _).symm.trans (hc.fac_assoc c' WalkingCospan.left f).symm,
      (hc.fac c' WalkingCospan.left).symm, (hc.fac c' WalkingCospan.right).symm]
#align is_binary_product_of_is_terminal_is_pullback isBinaryProductOfIsTerminalIsPullback

/-- The pullback over the terminal object is the product -/
def isProductOfIsTerminalIsPullback {W X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (H‚ÇÅ : IsTerminal Z)
    (H‚ÇÇ : IsLimit (PullbackCone.mk _ _ (show h ‚â´ f = k ‚â´ g from H‚ÇÅ.hom_ext _ _))) :
    IsLimit (BinaryFan.mk h k) := by
  apply isBinaryProductOfIsTerminalIsPullback _ _ H‚ÇÅ
  exact H‚ÇÇ
  -- üéâ no goals
#align is_product_of_is_terminal_is_pullback isProductOfIsTerminalIsPullback

/-- The product is the pullback over the terminal object. -/
def isPullbackOfIsTerminalIsProduct {W X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (H‚ÇÅ : IsTerminal Z) (H‚ÇÇ : IsLimit (BinaryFan.mk h k)) :
    IsLimit (PullbackCone.mk _ _ (show h ‚â´ f = k ‚â´ g from H‚ÇÅ.hom_ext _ _)) := by
  apply PullbackCone.isLimitAux'
  -- ‚ä¢ (s : PullbackCone f g) ‚Üí { l // l ‚â´ PullbackCone.fst (PullbackCone.mk h k (_ ‚Ä¶
  intro s
  -- ‚ä¢ { l // l ‚â´ PullbackCone.fst (PullbackCone.mk h k (_ : h ‚â´ f = k ‚â´ g)) = Pull ‚Ä¶
  use H‚ÇÇ.lift (BinaryFan.mk s.fst s.snd)
  -- ‚ä¢ IsLimit.lift H‚ÇÇ (BinaryFan.mk (PullbackCone.fst s) (PullbackCone.snd s)) ‚â´ P ‚Ä¶
  use H‚ÇÇ.fac (BinaryFan.mk s.fst s.snd) ‚ü®WalkingPair.left‚ü©
  -- ‚ä¢ IsLimit.lift H‚ÇÇ (BinaryFan.mk (PullbackCone.fst s) (PullbackCone.snd s)) ‚â´ P ‚Ä¶
  use H‚ÇÇ.fac (BinaryFan.mk s.fst s.snd) ‚ü®WalkingPair.right‚ü©
  -- ‚ä¢ ‚àÄ {m : s.pt ‚ü∂ (PullbackCone.mk h k (_ : h ‚â´ f = k ‚â´ g)).pt}, m ‚â´ PullbackCon ‚Ä¶
  intro m h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ m = IsLimit.lift H‚ÇÇ (BinaryFan.mk (PullbackCone.fst s) (PullbackCone.snd s))
  apply H‚ÇÇ.hom_ext
  -- ‚ä¢ ‚àÄ (j : Discrete WalkingPair), m ‚â´ NatTrans.app (BinaryFan.mk h k).œÄ j = IsLi ‚Ä¶
  rintro ‚ü®‚ü®‚ü©‚ü©
  -- ‚ä¢ m ‚â´ NatTrans.app (BinaryFan.mk h k).œÄ { as := WalkingPair.left } = IsLimit.l ‚Ä¶
  ¬∑ exact h‚ÇÅ.trans (H‚ÇÇ.fac (BinaryFan.mk s.fst s.snd) ‚ü®WalkingPair.left‚ü©).symm
    -- üéâ no goals
  ¬∑ exact h‚ÇÇ.trans (H‚ÇÇ.fac (BinaryFan.mk s.fst s.snd) ‚ü®WalkingPair.right‚ü©).symm
    -- üéâ no goals
#align is_pullback_of_is_terminal_is_product isPullbackOfIsTerminalIsProduct

/-- Any category with pullbacks and a terminal object has a limit cone for each walking pair. -/
noncomputable def limitConeOfTerminalAndPullbacks [HasTerminal C] [HasPullbacks C]
    (F : Discrete WalkingPair ‚•§ C) : LimitCone F
    where
  cone :=
    { pt :=
        pullback (terminal.from (F.obj ‚ü®WalkingPair.left‚ü©))
          (terminal.from (F.obj ‚ü®WalkingPair.right‚ü©))
      œÄ :=
        Discrete.natTrans fun x =>
          Discrete.casesOn x fun x => WalkingPair.casesOn x pullback.fst pullback.snd }
  isLimit :=
    isBinaryProductOfIsTerminalIsPullback F _ terminalIsTerminal _ _ (pullbackIsPullback _ _)
#align limit_cone_of_terminal_and_pullbacks limitConeOfTerminalAndPullbacks

variable (C)

-- This is not an instance, as it is not always how one wants to construct binary products!
/-- Any category with pullbacks and terminal object has binary products. -/
theorem hasBinaryProducts_of_hasTerminal_and_pullbacks [HasTerminal C] [HasPullbacks C] :
    HasBinaryProducts C :=
  { has_limit := fun F => HasLimit.mk (limitConeOfTerminalAndPullbacks F) }
#align has_binary_products_of_has_terminal_and_pullbacks hasBinaryProducts_of_hasTerminal_and_pullbacks

variable {C}

/-- A functor that preserves terminal objects and pullbacks preserves binary products. -/
noncomputable def preservesBinaryProductsOfPreservesTerminalAndPullbacks [HasTerminal C]
    [HasPullbacks C] [PreservesLimitsOfShape (Discrete.{0} PEmpty) F]
    [PreservesLimitsOfShape WalkingCospan F] : PreservesLimitsOfShape (Discrete WalkingPair) F :=
  ‚ü®fun {K} =>
    preservesLimitOfPreservesLimitCone (limitConeOfTerminalAndPullbacks K).2
      (by
        apply
          isBinaryProductOfIsTerminalIsPullback _ _ (isLimitOfHasTerminalOfPreservesLimit F)
        apply isLimitOfHasPullbackOfPreservesLimit)‚ü©
        -- üéâ no goals
#align preserves_binary_products_of_preserves_terminal_and_pullbacks preservesBinaryProductsOfPreservesTerminalAndPullbacks

/-- In a category with a terminal object and pullbacks,
a product of objects `X` and `Y` is isomorphic to a pullback. -/
noncomputable def prodIsoPullback [HasTerminal C] [HasPullbacks C] (X Y : C)
    [HasBinaryProduct X Y] : X ‚®Ø Y ‚âÖ pullback (terminal.from X) (terminal.from Y) :=
  limit.isoLimitCone (limitConeOfTerminalAndPullbacks _)
#align prod_iso_pullback prodIsoPullback

/-- If a cospan is the pushout cospan under the initial object, then it is a binary coproduct. -/
def isBinaryCoproductOfIsInitialIsPushout (F : Discrete WalkingPair ‚•§ C) (c : Cocone F) {X : C}
    (hX : IsInitial X) (f : X ‚ü∂ F.obj ‚ü®WalkingPair.left‚ü©) (g : X ‚ü∂ F.obj ‚ü®WalkingPair.right‚ü©)
    (hc :
      IsColimit
        (PushoutCocone.mk (c.Œπ.app ‚ü®WalkingPair.left‚ü©) (c.Œπ.app ‚ü®WalkingPair.right‚ü© : _) <|
          hX.hom_ext (f ‚â´ _) (g ‚â´ _))) :
    IsColimit c
    where
  desc s :=
    hc.desc
      (PushoutCocone.mk (s.Œπ.app ‚ü®WalkingPair.left‚ü©) (s.Œπ.app ‚ü®WalkingPair.right‚ü©) (hX.hom_ext _ _))
  fac s j :=
    Discrete.casesOn j fun j =>
      WalkingPair.casesOn j (hc.fac _ WalkingSpan.left) (hc.fac _ WalkingSpan.right)
  uniq s m J := by
    let c' :=
      PushoutCocone.mk (c.Œπ.app ‚ü®WalkingPair.left‚ü© ‚â´ m) (c.Œπ.app ‚ü®WalkingPair.right‚ü© ‚â´ m)
        (hX.hom_ext (f ‚â´ _) (g ‚â´ _))
    dsimp; rw [‚Üê J, ‚Üê J]
    -- ‚ä¢ m = IsColimit.desc hc (PushoutCocone.mk (NatTrans.app s.Œπ { as := WalkingPai ‚Ä¶
           -- ‚ä¢ m = IsColimit.desc hc (PushoutCocone.mk (NatTrans.app c.Œπ { as := WalkingPai ‚Ä¶
    apply hc.hom_ext
    -- ‚ä¢ ‚àÄ (j : WalkingSpan), NatTrans.app (PushoutCocone.mk (NatTrans.app c.Œπ { as : ‚Ä¶
    rintro (_ | (_ | _)) <;>
      simp only [PushoutCocone.mk_Œπ_app_zero, PushoutCocone.mk_Œπ_app, Category.assoc]
      -- ‚ä¢ f ‚â´ NatTrans.app c.Œπ { as := WalkingPair.left } ‚â´ m = f ‚â´ NatTrans.app c.Œπ { ‚Ä¶
      -- ‚ä¢ NatTrans.app c.Œπ { as := WalkingPair.left } ‚â´ m = NatTrans.app c.Œπ { as := W ‚Ä¶
      -- ‚ä¢ NatTrans.app c.Œπ { as := WalkingPair.right } ‚â´ m = NatTrans.app c.Œπ { as :=  ‚Ä¶
    congr 1
    exacts [(hc.fac c' WalkingSpan.left).symm, (hc.fac c' WalkingSpan.left).symm,
      (hc.fac c' WalkingSpan.right).symm]
#align is_binary_coproduct_of_is_initial_is_pushout isBinaryCoproductOfIsInitialIsPushout

/-- The pushout under the initial object is the coproduct -/
def isCoproductOfIsInitialIsPushout {W X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (H‚ÇÅ : IsInitial W)
    (H‚ÇÇ : IsColimit (PushoutCocone.mk _ _ (show h ‚â´ f = k ‚â´ g from H‚ÇÅ.hom_ext _ _))) :
    IsColimit (BinaryCofan.mk f g) := by
  apply isBinaryCoproductOfIsInitialIsPushout _ _ H‚ÇÅ
  exact H‚ÇÇ
  -- üéâ no goals
#align is_coproduct_of_is_initial_is_pushout isCoproductOfIsInitialIsPushout

/-- The coproduct is the pushout under the initial object. -/
def isPushoutOfIsInitialIsCoproduct {W X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h : W ‚ü∂ X) (k : W ‚ü∂ Y)
    (H‚ÇÅ : IsInitial W) (H‚ÇÇ : IsColimit (BinaryCofan.mk f g)) :
    IsColimit (PushoutCocone.mk _ _ (show h ‚â´ f = k ‚â´ g from H‚ÇÅ.hom_ext _ _)) := by
  apply PushoutCocone.isColimitAux'
  -- ‚ä¢ (s : PushoutCocone h k) ‚Üí { l // PushoutCocone.inl (PushoutCocone.mk f g (_  ‚Ä¶
  intro s
  -- ‚ä¢ { l // PushoutCocone.inl (PushoutCocone.mk f g (_ : h ‚â´ f = k ‚â´ g)) ‚â´ l = Pu ‚Ä¶
  use H‚ÇÇ.desc (BinaryCofan.mk s.inl s.inr)
  -- ‚ä¢ PushoutCocone.inl (PushoutCocone.mk f g (_ : h ‚â´ f = k ‚â´ g)) ‚â´ IsColimit.des ‚Ä¶
  use H‚ÇÇ.fac (BinaryCofan.mk s.inl s.inr) ‚ü®WalkingPair.left‚ü©
  -- ‚ä¢ PushoutCocone.inr (PushoutCocone.mk f g (_ : h ‚â´ f = k ‚â´ g)) ‚â´ IsColimit.des ‚Ä¶
  use H‚ÇÇ.fac (BinaryCofan.mk s.inl s.inr) ‚ü®WalkingPair.right‚ü©
  -- ‚ä¢ ‚àÄ {m : (PushoutCocone.mk f g (_ : h ‚â´ f = k ‚â´ g)).pt ‚ü∂ s.pt}, PushoutCocone. ‚Ä¶
  intro m h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ m = IsColimit.desc H‚ÇÇ (BinaryCofan.mk (PushoutCocone.inl s) (PushoutCocone.i ‚Ä¶
  apply H‚ÇÇ.hom_ext
  -- ‚ä¢ ‚àÄ (j : Discrete WalkingPair), NatTrans.app (BinaryCofan.mk f g).Œπ j ‚â´ m = Na ‚Ä¶
  rintro ‚ü®‚ü®‚ü©‚ü©
  -- ‚ä¢ NatTrans.app (BinaryCofan.mk f g).Œπ { as := WalkingPair.left } ‚â´ m = NatTran ‚Ä¶
  ¬∑ exact h‚ÇÅ.trans (H‚ÇÇ.fac (BinaryCofan.mk s.inl s.inr) ‚ü®WalkingPair.left‚ü©).symm
    -- üéâ no goals
  ¬∑ exact h‚ÇÇ.trans (H‚ÇÇ.fac (BinaryCofan.mk s.inl s.inr) ‚ü®WalkingPair.right‚ü©).symm
    -- üéâ no goals
#align is_pushout_of_is_initial_is_coproduct isPushoutOfIsInitialIsCoproduct

/-- Any category with pushouts and an initial object has a colimit cocone for each walking pair. -/
noncomputable def colimitCoconeOfInitialAndPushouts [HasInitial C] [HasPushouts C]
    (F : Discrete WalkingPair ‚•§ C) : ColimitCocone F where
  cocone :=
    { pt := pushout (initial.to (F.obj ‚ü®WalkingPair.left‚ü©)) (initial.to (F.obj ‚ü®WalkingPair.right‚ü©))
      Œπ :=
        Discrete.natTrans fun x =>
          Discrete.casesOn x fun x => WalkingPair.casesOn x pushout.inl pushout.inr }
  isColimit := isBinaryCoproductOfIsInitialIsPushout F _ initialIsInitial _ _ (pushoutIsPushout _ _)
#align colimit_cocone_of_initial_and_pushouts colimitCoconeOfInitialAndPushouts

variable (C)

-- This is not an instance, as it is not always how one wants to construct binary coproducts!
/-- Any category with pushouts and initial object has binary coproducts. -/
theorem hasBinaryCoproducts_of_hasInitial_and_pushouts [HasInitial C] [HasPushouts C] :
    HasBinaryCoproducts C :=
  { has_colimit := fun F => HasColimit.mk (colimitCoconeOfInitialAndPushouts F) }
#align has_binary_coproducts_of_has_initial_and_pushouts hasBinaryCoproducts_of_hasInitial_and_pushouts

variable {C}

/-- A functor that preserves initial objects and pushouts preserves binary coproducts. -/
noncomputable def preservesBinaryCoproductsOfPreservesInitialAndPushouts [HasInitial C]
    [HasPushouts C] [PreservesColimitsOfShape (Discrete.{0} PEmpty) F]
    [PreservesColimitsOfShape WalkingSpan F] : PreservesColimitsOfShape (Discrete WalkingPair) F :=
  ‚ü®fun {K} =>
    preservesColimitOfPreservesColimitCocone (colimitCoconeOfInitialAndPushouts K).2 (by
      apply
        isBinaryCoproductOfIsInitialIsPushout _ _
          (isColimitOfHasInitialOfPreservesColimit F)
      apply isColimitOfHasPushoutOfPreservesColimit)‚ü©
      -- üéâ no goals
#align preserves_binary_coproducts_of_preserves_initial_and_pushouts preservesBinaryCoproductsOfPreservesInitialAndPushouts

/-- In a category with an initial object and pushouts,
a coproduct of objects `X` and `Y` is isomorphic to a pushout. -/
noncomputable def coprodIsoPushout [HasInitial C] [HasPushouts C] (X Y : C)
    [HasBinaryCoproduct X Y] : X ‚®ø Y ‚âÖ pushout (initial.to X) (initial.to Y) :=
  colimit.isoColimitCocone (colimitCoconeOfInitialAndPushouts _)
#align coprod_iso_pushout coprodIsoPushout
