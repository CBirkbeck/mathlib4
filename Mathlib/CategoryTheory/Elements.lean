/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.StructuredArrow
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.PUnit

#align_import category_theory.elements from "leanprover-community/mathlib"@"8a318021995877a44630c898d0b2bc376fceef3b"

/-!
# The category of elements

This file defines the category of elements, also known as (a special case of) the Grothendieck
construction.

Given a functor `F : C ‚•§ Type`, an object of `F.Elements` is a pair `(X : C, x : F.obj X)`.
A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.

## Implementation notes

This construction is equivalent to a special case of a comma construction, so this is mostly just a
more convenient API. We prove the equivalence in
`CategoryTheory.CategoryOfElements.structuredArrowEquivalence`.

## References
* [Emily Riehl, *Category Theory in Context*, Section 2.4][riehl2017]
* <https://en.wikipedia.org/wiki/Category_of_elements>
* <https://ncatlab.org/nlab/show/category+of+elements>

## Tags
category of elements, Grothendieck construction, comma category
-/


namespace CategoryTheory

universe w v u

variable {C : Type u} [Category.{v} C]

/-- The type of objects for the category of elements of a functor `F : C ‚•§ Type`
is a pair `(X : C, x : F.obj X)`.
-/
def Functor.Elements (F : C ‚•§ Type w) :=
  Œ£c : C, F.obj c
#align category_theory.functor.elements CategoryTheory.Functor.Elements

-- porting note: added because Sigma.ext would be triggered automatically
lemma Functor.Elements.ext {F : C ‚•§ Type w} (x y : F.Elements) (h‚ÇÅ : x.fst = y.fst)
    (h‚ÇÇ : F.map (eqToHom h‚ÇÅ) x.snd = y.snd) : x = y := by
  cases x
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù } = y
  cases y
  -- ‚ä¢ { fst := fst‚úù¬π, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
  cases h‚ÇÅ
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
  simp only [eqToHom_refl, FunctorToTypes.map_id_apply] at h‚ÇÇ
  -- ‚ä¢ { fst := fst‚úù, snd := snd‚úù¬π } = { fst := fst‚úù, snd := snd‚úù }
  simp [h‚ÇÇ]
  -- üéâ no goals

/-- The category structure on `F.Elements`, for `F : C ‚•§ Type`.
    A morphism `(X, x) ‚ü∂ (Y, y)` is a morphism `f : X ‚ü∂ Y` in `C`, so `F.map f` takes `x` to `y`.
 -/
instance categoryOfElements (F : C ‚•§ Type w) : Category.{v} F.Elements where
  Hom p q := { f : p.1 ‚ü∂ q.1 // (F.map f) p.2 = q.2 }
  id p := ‚ü®ùüô p.1, by aesop_cat‚ü©
                     -- üéâ no goals
  comp {X Y Z} f g := ‚ü®f.val ‚â´ g.val, by simp [f.2, g.2]‚ü©
                                         -- üéâ no goals
#align category_theory.category_of_elements CategoryTheory.categoryOfElements

namespace CategoryOfElements

@[ext]
theorem ext (F : C ‚•§ Type w) {x y : F.Elements} (f g : x ‚ü∂ y) (w : f.val = g.val) : f = g :=
  Subtype.ext_val w
#align category_theory.category_of_elements.ext CategoryTheory.CategoryOfElements.ext

@[simp]
theorem comp_val {F : C ‚•§ Type w} {p q r : F.Elements} {f : p ‚ü∂ q} {g : q ‚ü∂ r} :
    (f ‚â´ g).val = f.val ‚â´ g.val :=
  rfl
#align category_theory.category_of_elements.comp_val CategoryTheory.CategoryOfElements.comp_val

@[simp]
theorem id_val {F : C ‚•§ Type w} {p : F.Elements} : (ùüô p : p ‚ü∂ p).val = ùüô p.1 :=
  rfl
#align category_theory.category_of_elements.id_val CategoryTheory.CategoryOfElements.id_val

end CategoryOfElements

noncomputable instance groupoidOfElements {G : Type u} [Groupoid.{v} G] (F : G ‚•§ Type w) :
    Groupoid F.Elements
    where
  inv {p q} f :=
    ‚ü®inv f.val,
      calc
        F.map (inv f.val) q.2 = F.map (inv f.val) (F.map f.val p.2) := by rw [f.2]
                                                                          -- üéâ no goals
        _ = (F.map f.val ‚â´ F.map (inv f.val)) p.2 := rfl
        _ = p.2 := by
          rw [‚Üê F.map_comp]
          -- ‚ä¢ F.map (‚Üëf ‚â´ inv ‚Üëf) p.snd = p.snd
          simp
          -- üéâ no goals
        ‚ü©
  inv_comp _ := by
    ext
    -- ‚ä¢ ‚Üë((fun {p q} f => { val := inv ‚Üëf, property := (_ : F.map (inv ‚Üëf) q.snd = p ‚Ä¶
    simp
    -- üéâ no goals
  comp_inv _ := by
    ext
    -- ‚ä¢ ‚Üë(x‚úù ‚â´ (fun {p q} f => { val := inv ‚Üëf, property := (_ : F.map (inv ‚Üëf) q.sn ‚Ä¶
    simp
    -- üéâ no goals
#align category_theory.groupoid_of_elements CategoryTheory.groupoidOfElements

namespace CategoryOfElements

variable (F : C ‚•§ Type w)

/-- The functor out of the category of elements which forgets the element. -/
@[simps]
def œÄ : F.Elements ‚•§ C where
  obj X := X.1
  map f := f.val
#align category_theory.category_of_elements.œÄ CategoryTheory.CategoryOfElements.œÄ

/-- A natural transformation between functors induces a functor between the categories of elements.
-/
@[simps]
def map {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : F‚ÇÅ.Elements ‚•§ F‚ÇÇ.Elements
    where
  obj t := ‚ü®t.1, Œ±.app t.1 t.2‚ü©
  map {t‚ÇÅ t‚ÇÇ} k := ‚ü®k.1, by simpa [‚Üê k.2] using (FunctorToTypes.naturality _ _ Œ± k.1 t‚ÇÅ.2).symm‚ü©
                            -- üéâ no goals
#align category_theory.category_of_elements.map CategoryTheory.CategoryOfElements.map

@[simp]
theorem map_œÄ {F‚ÇÅ F‚ÇÇ : C ‚•§ Type w} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) : map Œ± ‚ãô œÄ F‚ÇÇ = œÄ F‚ÇÅ :=
  rfl
#align category_theory.category_of_elements.map_œÄ CategoryTheory.CategoryOfElements.map_œÄ

/-- The forward direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def toStructuredArrow : F.Elements ‚•§ StructuredArrow PUnit F where
  obj X := StructuredArrow.mk fun _ => X.2
  map {X Y} f := StructuredArrow.homMk f.val (by funext; simp [f.2])
                                                 -- ‚ä¢ (((fun X => StructuredArrow.mk fun x => X.snd) X).hom ‚â´ F.map ‚Üëf) x‚úù = Comma ‚Ä¶
                                                         -- üéâ no goals
#align category_theory.category_of_elements.to_structured_arrow CategoryTheory.CategoryOfElements.toStructuredArrow

@[simp]
theorem toStructuredArrow_obj (X) :
    (toStructuredArrow F).obj X =
      { left := ‚ü®‚ü®‚ü©‚ü©
        right := X.1
        hom := fun _ => X.2 } :=
  rfl
#align category_theory.category_of_elements.to_structured_arrow_obj CategoryTheory.CategoryOfElements.toStructuredArrow_obj

@[simp]
theorem to_comma_map_right {X Y} (f : X ‚ü∂ Y) : ((toStructuredArrow F).map f).right = f.val :=
  rfl
#align category_theory.category_of_elements.to_comma_map_right CategoryTheory.CategoryOfElements.to_comma_map_right

/-- The reverse direction of the equivalence `F.Elements ‚âÖ (*, F)`. -/
def fromStructuredArrow : StructuredArrow PUnit F ‚•§ F.Elements where
  obj X := ‚ü®X.right, X.hom PUnit.unit‚ü©
  map f := ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü©
#align category_theory.category_of_elements.from_structured_arrow CategoryTheory.CategoryOfElements.fromStructuredArrow

@[simp]
theorem fromStructuredArrow_obj (X) : (fromStructuredArrow F).obj X = ‚ü®X.right, X.hom PUnit.unit‚ü© :=
  rfl
#align category_theory.category_of_elements.from_structured_arrow_obj CategoryTheory.CategoryOfElements.fromStructuredArrow_obj

@[simp]
theorem fromStructuredArrow_map {X Y} (f : X ‚ü∂ Y) :
    (fromStructuredArrow F).map f = ‚ü®f.right, congr_fun f.w.symm PUnit.unit‚ü© :=
  rfl
#align category_theory.category_of_elements.from_structured_arrow_map CategoryTheory.CategoryOfElements.fromStructuredArrow_map

/-- The equivalence between the category of elements `F.Elements`
    and the comma category `(*, F)`. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_hom
  unitIso_inv counitIso_hom counitIso_inv]
def structuredArrowEquivalence : F.Elements ‚âå StructuredArrow PUnit F :=
  Equivalence.mk (toStructuredArrow F) (fromStructuredArrow F)
    (NatIso.ofComponents fun X => eqToIso (by aesop_cat))
                                              -- üéâ no goals
    (NatIso.ofComponents fun X => StructuredArrow.isoMk (Iso.refl _))
#align category_theory.category_of_elements.structured_arrow_equivalence CategoryTheory.CategoryOfElements.structuredArrowEquivalence

open Opposite

/-- The forward direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaSections`.
-/
@[simps]
def toCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : F.Elements·µí·µñ ‚•§ CostructuredArrow yoneda F
    where
  obj X := CostructuredArrow.mk ((yonedaSections (unop (unop X).fst) F).inv (ULift.up (unop X).2))
  map f := by
    fapply CostructuredArrow.homMk
    -- ‚ä¢ ((fun X => CostructuredArrow.mk ((yonedaSections X.unop.fst.unop F).inv { do ‚Ä¶
    ¬∑ exact f.unop.val.unop
      -- üéâ no goals
    ¬∑ ext Z y
      -- ‚ä¢ NatTrans.app (yoneda.map (‚Üëf.unop).unop ‚â´ ((fun X => CostructuredArrow.mk (( ‚Ä¶
      dsimp
      -- ‚ä¢ F.map (‚Üëf.unop ‚â´ y.op) Y‚úù.unop.snd = F.map y.op X‚úù.unop.snd
      simp only [FunctorToTypes.map_comp_apply, ‚Üê f.unop.2]
      -- üéâ no goals
#align category_theory.category_of_elements.to_costructured_arrow CategoryTheory.CategoryOfElements.toCostructuredArrow

/-- The reverse direction of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)`,
given by `CategoryTheory.yonedaEquiv`.
-/
@[simps]
def fromCostructuredArrow (F : C·µí·µñ ‚•§ Type v) : (CostructuredArrow yoneda F)·µí·µñ ‚•§ F.Elements where
  obj X := ‚ü®op (unop X).1, yonedaEquiv.1 (unop X).3‚ü©
  map {X Y} f :=
    ‚ü®f.unop.1.op, by
      convert (congr_fun ((unop X).hom.naturality f.unop.left.op) (ùüô _)).symm
      -- ‚ä¢ ((fun X => { fst := op X.unop.left, snd := Equiv.toFun yonedaEquiv X.unop.ho ‚Ä¶
      simp only [Equiv.toFun_as_coe, Quiver.Hom.unop_op, yonedaEquiv_apply, types_comp_apply,
        Category.comp_id, yoneda_obj_map]
      have : yoneda.map f.unop.left ‚â´ (unop X).hom = (unop Y).hom := by
        convert f.unop.3
      erw [‚Üê this]
      -- ‚ä¢ NatTrans.app (yoneda.map f.unop.left ‚â´ X.unop.hom) (op Y.unop.left) (ùüô Y.uno ‚Ä¶
      simp only [yoneda_map_app, FunctorToTypes.comp]
      -- ‚ä¢ NatTrans.app X.unop.hom (op Y.unop.left) (ùüô Y.unop.left ‚â´ f.unop.left) = Nat ‚Ä¶
      erw [Category.id_comp]‚ü©
      -- üéâ no goals
#align category_theory.category_of_elements.from_costructured_arrow CategoryTheory.CategoryOfElements.fromCostructuredArrow

@[simp]
theorem fromCostructuredArrow_obj_mk (F : C·µí·µñ ‚•§ Type v) {X : C} (f : yoneda.obj X ‚ü∂ F) :
    (fromCostructuredArrow F).obj (op (CostructuredArrow.mk f)) = ‚ü®op X, yonedaEquiv.1 f‚ü© :=
  rfl
#align category_theory.category_of_elements.from_costructured_arrow_obj_mk CategoryTheory.CategoryOfElements.fromCostructuredArrow_obj_mk

/-- The unit of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
theorem from_toCostructuredArrow_eq (F : C·µí·µñ ‚•§ Type v) :
    (toCostructuredArrow F).rightOp ‚ãô fromCostructuredArrow F = ùü≠ _ := by
  refine' Functor.ext _ _
  -- ‚ä¢ ‚àÄ (X : Functor.Elements F), ((toCostructuredArrow F).rightOp ‚ãô fromCostructu ‚Ä¶
  ¬∑ intro X
    -- ‚ä¢ ((toCostructuredArrow F).rightOp ‚ãô fromCostructuredArrow F).obj X = (ùü≠ (Func ‚Ä¶
    exact Functor.Elements.ext _ _ rfl (by simp [yonedaEquiv])
    -- üéâ no goals
  ¬∑ intro X Y f
    -- ‚ä¢ ((toCostructuredArrow F).rightOp ‚ãô fromCostructuredArrow F).map f = eqToHom  ‚Ä¶
    have : ‚àÄ {a b : F.Elements} (H : a = b),
        (eqToHom H).1 = eqToHom (show a.fst = b.fst by cases H; rfl) := by
      rintro _ _ rfl
      simp
    ext
    -- ‚ä¢ ‚Üë(((toCostructuredArrow F).rightOp ‚ãô fromCostructuredArrow F).map f) = ‚Üë(eqT ‚Ä¶
    simp [this]
    -- üéâ no goals
#align category_theory.category_of_elements.from_to_costructured_arrow_eq CategoryTheory.CategoryOfElements.from_toCostructuredArrow_eq

/-- The counit of the equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` is indeed iso. -/
theorem to_fromCostructuredArrow_eq (F : C·µí·µñ ‚•§ Type v) :
    (fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F = ùü≠ _ := by
  refine' Functor.ext _ _
  -- ‚ä¢ ‚àÄ (X : CostructuredArrow yoneda F), ((fromCostructuredArrow F).rightOp ‚ãô toC ‚Ä¶
  ¬∑ intro X
    -- ‚ä¢ ((fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F).obj X = (ùü≠ (Cost ‚Ä¶
    cases' X with X_left X_right X_hom
    -- ‚ä¢ ((fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F).obj { left := X_ ‚Ä¶
    cases X_right
    -- ‚ä¢ ((fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F).obj { left := X_ ‚Ä¶
    simp only [Functor.id_obj, Functor.rightOp_obj, toCostructuredArrow_obj, Functor.comp_obj,
      CostructuredArrow.mk]
    congr
    -- ‚ä¢ (yonedaSections (op ((fromCostructuredArrow F).obj (op { left := X_left, rig ‚Ä¶
    ext x f
    -- ‚ä¢ NatTrans.app ((yonedaSections (op ((fromCostructuredArrow F).obj (op { left  ‚Ä¶
    convert congr_fun (X_hom.naturality f.op).symm (ùüô X_left)
    -- ‚ä¢ NatTrans.app X_hom x f = ((yoneda.obj X_left).map f.op ‚â´ NatTrans.app X_hom  ‚Ä¶
    simp
    -- üéâ no goals
  ¬∑ intro X Y f
    -- ‚ä¢ ((fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F).map f = eqToHom  ‚Ä¶
    ext
    -- ‚ä¢ (((fromCostructuredArrow F).rightOp ‚ãô toCostructuredArrow F).map f).left = ( ‚Ä¶
    simp [CostructuredArrow.eqToHom_left]
    -- üéâ no goals
#align category_theory.category_of_elements.to_from_costructured_arrow_eq CategoryTheory.CategoryOfElements.to_fromCostructuredArrow_eq

/-- The equivalence `F.Elements·µí·µñ ‚âÖ (yoneda, F)` given by yoneda lemma. -/
@[simps! functor_obj functor_map inverse_obj inverse_map unitIso_inv counitIso_hom counitIso_inv]
def costructuredArrowYonedaEquivalence (F : C·µí·µñ ‚•§ Type v) :
    F.Elements·µí·µñ ‚âå CostructuredArrow yoneda F :=
  Equivalence.mk (toCostructuredArrow F) (fromCostructuredArrow F).rightOp
    (NatIso.op (eqToIso (from_toCostructuredArrow_eq F))) (eqToIso <| to_fromCostructuredArrow_eq F)
#align category_theory.category_of_elements.costructured_arrow_yoneda_equivalence CategoryTheory.CategoryOfElements.costructuredArrowYonedaEquivalence

-- Porting note:
-- Running `@[simps! unitIso_hom]` is mysteriously slow.
-- We separate it out to avoid needing to increase the maxHeartbeats.
attribute [simps! unitIso_hom] costructuredArrowYonedaEquivalence

/-- The equivalence `(-.Elements)·µí·µñ ‚âÖ (yoneda, -)` of is actually a natural isomorphism of functors.
-/
theorem costructuredArrow_yoneda_equivalence_naturality {F‚ÇÅ F‚ÇÇ : C·µí·µñ ‚•§ Type v} (Œ± : F‚ÇÅ ‚ü∂ F‚ÇÇ) :
    (map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ = toCostructuredArrow F‚ÇÅ ‚ãô CostructuredArrow.map Œ± := by
  fapply Functor.ext
  -- ‚ä¢ ‚àÄ (X : (Functor.Elements F‚ÇÅ)·µí·µñ), ((map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ).obj X ‚Ä¶
  ¬∑ intro X
    -- ‚ä¢ ((map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ).obj X = (toCostructuredArrow F‚ÇÅ ‚ãô Cost ‚Ä¶
    simp only [CostructuredArrow.map_mk, toCostructuredArrow_obj, Functor.op_obj,
      Functor.comp_obj]
    congr
    -- ‚ä¢ (yonedaSections (op ((map Œ±).obj X.unop)).unop.fst.unop F‚ÇÇ).inv { down := (o ‚Ä¶
    ext _ f
    -- ‚ä¢ NatTrans.app ((yonedaSections (op ((map Œ±).obj X.unop)).unop.fst.unop F‚ÇÇ).in ‚Ä¶
    simpa using congr_fun (Œ±.naturality f.op).symm (unop X).snd
    -- üéâ no goals
  ¬∑ intro X Y f
    -- ‚ä¢ ((map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ).map f = eqToHom (_ : ((map Œ±).op ‚ãô toC ‚Ä¶
    ext
    -- ‚ä¢ (((map Œ±).op ‚ãô toCostructuredArrow F‚ÇÇ).map f).left = (eqToHom (_ : ((map Œ±). ‚Ä¶
    simp [CostructuredArrow.eqToHom_left]
    -- üéâ no goals
#align category_theory.category_of_elements.costructured_arrow_yoneda_equivalence_naturality CategoryTheory.CategoryOfElements.costructuredArrow_yoneda_equivalence_naturality

end CategoryOfElements

end CategoryTheory
