/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Calle S√∂nne, Jo√´l Riou, Ravi Vakil
-/
import Mathlib.AlgebraicGeometry.Gluing
import Mathlib.CategoryTheory.Sites.OneHypercover
import Mathlib.AlgebraicGeometry.Sites.BigZariski

/-!
# Gluing Hypercovers

In this file ...

-/
universe v u

open CategoryTheory Opposite Limits

namespace CategoryTheory

-- this should be moved to CategoryTheory.Limits.Shapes.Types
namespace Limits

variable (I : MulticospanIndex (Type u))

/-- TODO -/
@[ext]
structure MulticospanIndex.sections where
  val (i : I.L) : I.left i
  property (r : I.R) : I.fst r (val _) = I.snd r (val _)

/-- TODO -/
@[simps]
def MulticospanIndex.sectionsEquiv :
    I.sections ‚âÉ I.multicospan.sections where
  toFun s :=
    { val := fun i ‚Ü¶ match i with
        | .left i => s.val i
        | .right j => I.fst j (s.val _)
      property := by
        rintro _ _ (_|_|r)
        ¬∑ rfl
        ¬∑ rfl
        ¬∑ exact (s.property r).symm }
  invFun s :=
    { val := fun i ‚Ü¶ s.val (.left i)
      property := fun r ‚Ü¶ (s.property (.fst r)).trans (s.property (.snd r)).symm }
  left_inv _ := rfl
  right_inv s := by
    ext (_|r)
    ¬∑ rfl
    ¬∑ exact s.property (.fst r)

namespace Multifork

variable {I}
variable (c : Multifork I)

/-- TODO -/
@[simps]
def toSections (x : c.pt) : I.sections where
  val i := c.Œπ i x
  property r := congr_fun (c.condition r) x

lemma toSections_fac : I.sectionsEquiv.symm ‚àò Types.sectionOfCone c = c.toSections := rfl

lemma isLimit_types_iff : Nonempty (IsLimit c) ‚Üî Function.Bijective c.toSections := by
  rw [Types.isLimit_iff_bijective_sectionOfCone, ‚Üê toSections_fac, EquivLike.comp_bijective]

namespace IsLimit

variable {c}
variable (hc : IsLimit c)

/-- TODO -/
noncomputable def sectionsEquiv : I.sections ‚âÉ c.pt :=
  (Equiv.ofBijective _ (c.isLimit_types_iff.1 ‚ü®hc‚ü©)).symm

@[simp]
lemma sectionsEquiv_symm_apply_val (x : c.pt) (i : I.L) :
    ((sectionsEquiv hc).symm x).val i = c.Œπ i x := rfl

@[simp]
lemma sectionsEquiv_apply_val (s : I.sections) (i : I.L) :
    c.Œπ i (sectionsEquiv hc s) = s.val i := by
  obtain ‚ü®x, rfl‚ü© := (sectionsEquiv hc).symm.surjective s
  simp

end IsLimit

end Multifork

end Limits

end CategoryTheory

namespace AlgebraicGeometry.Scheme.GlueData

variable (D : Scheme.GlueData.{u})

/-- TODO -/
@[simps]
noncomputable def oneHypercover : Scheme.zariskiTopology.OneHypercover D.glued where
  I‚ÇÄ := D.J
  X := D.U
  f := D.Œπ
  I‚ÇÅ _ _ := PUnit
  Y i‚ÇÅ i‚ÇÇ _ := D.V (i‚ÇÅ, i‚ÇÇ)
  p‚ÇÅ i‚ÇÅ i‚ÇÇ _ := D.f i‚ÇÅ i‚ÇÇ
  p‚ÇÇ i‚ÇÅ i‚ÇÇ j := D.t i‚ÇÅ i‚ÇÇ ‚â´ D.f i‚ÇÇ i‚ÇÅ
  w i‚ÇÅ i‚ÇÇ _ := by simp only [Category.assoc, Scheme.GlueData.glue_condition]
  mem‚ÇÄ := by
    refine zariskiTopology.superset_covering ?_ (zariskiTopology_openCover D.openCover)
    rw [Sieve.sets_iff_generate] -- the name of this lemma should be changed!
    rintro W _ ‚ü®i‚ü©
    exact ‚ü®_, ùüô _, _, ‚ü®i‚ü©, by simp; rfl‚ü©
  mem‚ÇÅ i‚ÇÅ i‚ÇÇ W p‚ÇÅ p‚ÇÇ fac := by
    dsimp at p‚ÇÅ p‚ÇÇ fac ‚ä¢
    refine zariskiTopology.superset_covering ?_ (zariskiTopology.top_mem _)
    intro T g _
    dsimp
    have ‚ü®œÜ, h‚ÇÅ, h‚ÇÇ‚ü© := PullbackCone.IsLimit.lift' (D.vPullbackConeIsLimit i‚ÇÅ i‚ÇÇ)
      (g ‚â´ p‚ÇÅ) (g ‚â´ p‚ÇÇ) (by simpa using g ‚â´= fac)
    exact ‚ü®‚ü®‚ü©, œÜ, h‚ÇÅ.symm, h‚ÇÇ.symm‚ü©

variable {F : Sheaf Scheme.zariskiTopology (Type v)}

section

variable (s : ‚àÄ (j : D.J), F.val.obj (op (D.U j)))
  (h : ‚àÄ (i j : D.J), F.val.map (D.f i j).op (s i) =
    F.val.map ((D.f j i).op ‚â´ (D.t i j).op) (s j))

/-- TODO -/
noncomputable def sheafValGluedMk : F.val.obj (op D.glued) :=
  Multifork.IsLimit.sectionsEquiv (D.oneHypercover.isLimitMultifork F)
    { val := s
      property := fun _ ‚Ü¶ h _ _ }

@[simp]
lemma sheafValGluedMk_val (j : D.J) : F.val.map (D.Œπ j).op (D.sheafValGluedMk s h) = s j :=
  Multifork.IsLimit.sectionsEquiv_apply_val (D.oneHypercover.isLimitMultifork F) _ _

end


end AlgebraicGeometry.Scheme.GlueData
