/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro
-/
import Mathlib.Data.Multiset.Powerset

#align_import data.multiset.antidiagonal from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# The antidiagonal on a multiset.

The antidiagonal of a multiset `s` consists of all pairs `(t‚ÇÅ, t‚ÇÇ)`
such that `t‚ÇÅ + t‚ÇÇ = s`. These pairs are counted with multiplicities.
-/

universe u

namespace Multiset

open List

variable {Œ± Œ≤ : Type*}

/-- The antidiagonal of a multiset `s` consists of all pairs `(t‚ÇÅ, t‚ÇÇ)`
    such that `t‚ÇÅ + t‚ÇÇ = s`. These pairs are counted with multiplicities. -/
def antidiagonal (s : Multiset Œ±) : Multiset (Multiset Œ± √ó Multiset Œ±) :=
  Quot.liftOn s (fun l ‚Ü¶ (revzip (powersetAux l) : Multiset (Multiset Œ± √ó Multiset Œ±)))
    fun _ _ h ‚Ü¶ Quot.sound (revzip_powersetAux_perm h)
#align multiset.antidiagonal Multiset.antidiagonal

theorem antidiagonal_coe (l : List Œ±) : @antidiagonal Œ± l = revzip (powersetAux l) :=
  rfl
#align multiset.antidiagonal_coe Multiset.antidiagonal_coe

@[simp]
theorem antidiagonal_coe' (l : List Œ±) : @antidiagonal Œ± l = revzip (powersetAux' l) :=
  Quot.sound revzip_powersetAux_perm_aux'
#align multiset.antidiagonal_coe' Multiset.antidiagonal_coe'

/- Porting note: `simp` seemed to be applying `antidiagonal_coe'` instead of `antidiagonal_coe`
in what used to be `simp [antidiagonal_coe]`. -/
/-- A pair `(t‚ÇÅ, t‚ÇÇ)` of multisets is contained in `antidiagonal s`
    if and only if `t‚ÇÅ + t‚ÇÇ = s`. -/
@[simp]
theorem mem_antidiagonal {s : Multiset Œ±} {x : Multiset Œ± √ó Multiset Œ±} :
    x ‚àà antidiagonal s ‚Üî x.1 + x.2 = s :=
  Quotient.inductionOn s <| fun l ‚Ü¶ by
    dsimp only [quot_mk_to_coe, antidiagonal_coe]
    -- ‚ä¢ x ‚àà ‚Üë(revzip (powersetAux l)) ‚Üî x.fst + x.snd = ‚Üël
    refine' ‚ü®fun h => revzip_powersetAux h, fun h ‚Ü¶ _‚ü©
    -- ‚ä¢ x ‚àà ‚Üë(revzip (powersetAux l))
    haveI := Classical.decEq Œ±
    -- ‚ä¢ x ‚àà ‚Üë(revzip (powersetAux l))
    simp only [revzip_powersetAux_lemma l revzip_powersetAux, h.symm, ge_iff_le, mem_coe,
      List.mem_map, mem_powersetAux]
    cases' x with x‚ÇÅ x‚ÇÇ
    -- ‚ä¢ ‚àÉ a, a ‚â§ (x‚ÇÅ, x‚ÇÇ).fst + (x‚ÇÅ, x‚ÇÇ).snd ‚àß (a, (x‚ÇÅ, x‚ÇÇ).fst + (x‚ÇÅ, x‚ÇÇ).snd - a)  ‚Ä¶
    exact ‚ü®x‚ÇÅ, le_add_right _ _, by rw [add_tsub_cancel_left x‚ÇÅ x‚ÇÇ]‚ü©
    -- üéâ no goals
#align multiset.mem_antidiagonal Multiset.mem_antidiagonal

@[simp]
theorem antidiagonal_map_fst (s : Multiset Œ±) : (antidiagonal s).map Prod.fst = powerset s :=
  Quotient.inductionOn s <| fun l ‚Ü¶ by simp [powersetAux'];
                                       -- üéâ no goals
#align multiset.antidiagonal_map_fst Multiset.antidiagonal_map_fst

@[simp]
theorem antidiagonal_map_snd (s : Multiset Œ±) : (antidiagonal s).map Prod.snd = powerset s :=
  Quotient.inductionOn s <| fun l ‚Ü¶ by simp [powersetAux']
                                       -- üéâ no goals
#align multiset.antidiagonal_map_snd Multiset.antidiagonal_map_snd

@[simp]
theorem antidiagonal_zero : @antidiagonal Œ± 0 = {(0, 0)} :=
  rfl
#align multiset.antidiagonal_zero Multiset.antidiagonal_zero

@[simp]
theorem antidiagonal_cons (a : Œ±) (s) :
    antidiagonal (a ::‚Çò s) =
      map (Prod.map id (cons a)) (antidiagonal s) + map (Prod.map (cons a) id) (antidiagonal s) :=
  Quotient.inductionOn s <| fun l ‚Ü¶ by
    simp only [revzip, reverse_append, quot_mk_to_coe, coe_eq_coe, powersetAux'_cons, cons_coe,
      coe_map, antidiagonal_coe', coe_add]
    rw [‚Üê zip_map, ‚Üê zip_map, zip_append, (_ : _ ++ _ = _)]
    -- ‚ä¢ zip (powersetAux' l) (reverse (List.map (cons a) (powersetAux' l))) ++ zip ( ‚Ä¶
    ¬∑ congr; simp; rw [map_reverse]; simp
             -- ‚ä¢ reverse (List.map (cons a) (powersetAux' l)) = List.map (cons a) (reverse (p ‚Ä¶
                   -- ‚ä¢ reverse (powersetAux' l) = List.map id (reverse (powersetAux' l))
                                     -- üéâ no goals
    ¬∑ simp
      -- üéâ no goals
#align multiset.antidiagonal_cons Multiset.antidiagonal_cons

theorem antidiagonal_eq_map_powerset [DecidableEq Œ±] (s : Multiset Œ±) :
    s.antidiagonal = s.powerset.map fun t ‚Ü¶ (s - t, t) := by
  induction' s using Multiset.induction_on with a s hs
  -- ‚ä¢ antidiagonal 0 = map (fun t => (0 - t, t)) (powerset 0)
  ¬∑ simp only [antidiagonal_zero, powerset_zero, zero_tsub, map_singleton]
    -- üéâ no goals
  ¬∑ simp_rw [antidiagonal_cons, powerset_cons, map_add, hs, map_map, Function.comp, Prod.map_mk,
      id.def, sub_cons, erase_cons_head]
    rw [add_comm]
    -- ‚ä¢ map (fun x => (a ::‚Çò (s - x), x)) (powerset s) + map (fun x => (s - x, a ::‚Çò ‚Ä¶
    congr 1
    -- ‚ä¢ map (fun x => (a ::‚Çò (s - x), x)) (powerset s) = map (fun x => (a ::‚Çò s - x, ‚Ä¶
    refine' Multiset.map_congr rfl fun x hx ‚Ü¶ _
    -- ‚ä¢ (a ::‚Çò (s - x), x) = (a ::‚Çò s - x, x)
    rw [cons_sub_of_le _ (mem_powerset.mp hx)]
    -- üéâ no goals
#align multiset.antidiagonal_eq_map_powerset Multiset.antidiagonal_eq_map_powerset

@[simp]
theorem card_antidiagonal (s : Multiset Œ±) : card (antidiagonal s) = 2 ^ card s := by
  have := card_powerset s
  -- ‚ä¢ ‚Üëcard (antidiagonal s) = 2 ^ ‚Üëcard s
  rwa [‚Üê antidiagonal_map_fst, card_map] at this
  -- üéâ no goals
#align multiset.card_antidiagonal Multiset.card_antidiagonal

theorem prod_map_add [CommSemiring Œ≤] {s : Multiset Œ±} {f g : Œ± ‚Üí Œ≤} :
    prod (s.map fun a ‚Ü¶ f a + g a) =
      sum ((antidiagonal s).map fun p ‚Ü¶ (p.1.map f).prod * (p.2.map g).prod) := by
  refine' s.induction_on _ _
  -- ‚ä¢ prod (map (fun a => f a + g a) 0) = sum (map (fun p => prod (map f p.fst) *  ‚Ä¶
  ¬∑ simp only [map_zero, prod_zero, antidiagonal_zero, map_singleton, mul_one, sum_singleton]
    -- üéâ no goals
  ¬∑ intro a s ih
    -- ‚ä¢ prod (map (fun a => f a + g a) (a ::‚Çò s)) = sum (map (fun p => prod (map f p ‚Ä¶
    simp only [map_cons, prod_cons, ih, sum_map_mul_left.symm, add_mul, mul_left_comm (f a),
      mul_left_comm (g a), sum_map_add, antidiagonal_cons, Prod_map, id_eq, map_add, map_map,
      Function.comp_apply, mul_assoc, sum_add]
    exact add_comm _ _
    -- üéâ no goals
#align multiset.prod_map_add Multiset.prod_map_add

end Multiset
