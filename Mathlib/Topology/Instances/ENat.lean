/-
Copyright (c) 2024 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.Topology.Order.MonotoneContinuity
import Mathlib.Topology.Algebra.Order.LiminfLimsup
import Mathlib.Topology.Instances.ENNReal
import Mathlib.Topology.EMetricSpace.Lipschitz
import Mathlib.Topology.Metrizable.Basic
import Mathlib.Topology.Order.T5
import Mathlib.Data.Real.ENatENNReal

/-!
# Topology on extended natural numbers
-/

noncomputable section

open Set Filter Metric Function
open scoped Topology ENNReal NNReal ENat

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}

namespace ENat

variable {a b c d : ‚Ñï‚àû} {r p q : ‚Ñï} {x y z : ‚Ñï‚àû} {s : Set ‚Ñï‚àû}

-- TODO: Add in a suitable file.
lemma toNat_eq_toNat (ha : a ‚â† ‚ä§) (hb : b ‚â† ‚ä§) : a.toNat = b.toNat ‚Üî a = b :=
  ‚ü®fun h ‚Ü¶ by simpa [ha, hb] using WithTop.untop'_eq_untop'_iff.mp h, fun h ‚Ü¶ congrArg toNat h‚ü©

-- TODO: Add in a suitable file.
lemma range_nat_cast : Set.range ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) = Iio (‚ä§ : ‚Ñï‚àû) := by
  ext n
  simp only [mem_Iio]
  exact ‚ü®fun ‚ü®m, hm‚ü© ‚Ü¶ hm.symm ‚ñ∏ coe_lt_top m, fun h ‚Ü¶ Option.ne_none_iff_exists.mp h.ne_top‚ü©

-- TODO: Add in a suitable file.
lemma Ico_eq_Iio (b : ‚Ñï‚àû) : Ico 0 b = Iio b := by ext x; simp

-- TODO: Add in a suitable file.
lemma Icc_eq_Iic (b : ‚Ñï‚àû) : Icc 0 b = Iic b := by ext x; simp

section TopologicalSpace

open TopologicalSpace

/-- Topology on `‚Ñï‚àû`.

Note: this is different from the `EMetricSpace` topology. The `EMetricSpace` topology has
`IsOpen {‚àû}`, while all neighborhoods of `‚àû` in `‚Ñï‚àû` contain infinite intervals. -/
instance : TopologicalSpace ‚Ñï‚àû := Preorder.topology ‚Ñï‚àû

instance : OrderTopology ‚Ñï‚àû := ‚ü®rfl‚ü©

example : OrderClosedTopology ‚Ñï‚àû := by infer_instance

-- short-circuit type class inference
instance : T2Space ‚Ñï‚àû := inferInstance
instance : T5Space ‚Ñï‚àû := inferInstance
instance : T4Space ‚Ñï‚àû := inferInstance

example : StrictMono ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) := by exact Nat.strictMono_cast

theorem embedding_coe : Embedding ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) :=
  Nat.strictMono_cast.embedding_of_ordConnected <| by rw [range_nat_cast]; exact ordConnected_Iio

theorem isOpen_ne_top : IsOpen {a : ‚Ñï‚àû | a ‚â† ‚ä§} := isOpen_ne

theorem isOpen_Ico_zero : IsOpen (Ico 0 b) := by rw [ENat.Ico_eq_Iio]; exact isOpen_Iio

theorem openEmbedding_coe : OpenEmbedding ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) :=
  ‚ü®embedding_coe, by rw [range_nat_cast]; exact isOpen_Iio‚ü©

theorem coe_range_mem_nhds : range ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) ‚àà ùìù (r : ‚Ñï‚àû) :=
  IsOpen.mem_nhds openEmbedding_coe.isOpen_range <| mem_range_self _

@[fun_prop]
theorem continuous_coe : Continuous ((‚Üë) : ‚Ñï ‚Üí ‚Ñï‚àû) :=
  embedding_coe.continuous

theorem continuous_coe_iff {Œ±} [TopologicalSpace Œ±] {f : Œ± ‚Üí ‚Ñï} :
    (Continuous fun a ‚Ü¶ (f a : ‚Ñï‚àû)) ‚Üî Continuous f :=
  embedding_coe.continuous_iff.symm

theorem nhds_coe {r : ‚Ñï} : ùìù (r : ‚Ñï‚àû) = (ùìù r).map (‚Üë) :=
  (openEmbedding_coe.map_nhds_eq r).symm

theorem tendsto_nhds_coe_iff {Œ± : Type*} {l : Filter Œ±} {x : ‚Ñï} {f : ‚Ñï‚àû ‚Üí Œ±} :
    Tendsto f (ùìù ‚Üëx) l ‚Üî Tendsto (f ‚àò (‚Üë) : ‚Ñï ‚Üí Œ±) (ùìù x) l := by
  rw [nhds_coe, tendsto_map'_iff]

theorem continuousAt_coe_iff {Œ± : Type*} [TopologicalSpace Œ±] {x : ‚Ñï} {f : ‚Ñï‚àû ‚Üí Œ±} :
    ContinuousAt f ‚Üëx ‚Üî ContinuousAt (f ‚àò (‚Üë) : ‚Ñï ‚Üí Œ±) x :=
  tendsto_nhds_coe_iff

theorem nhds_coe_coe {r p : ‚Ñï} :
    ùìù ((r : ‚Ñï‚àû), (p : ‚Ñï‚àû)) = (ùìù (r, p)).map fun p : ‚Ñï √ó ‚Ñï => (‚Üëp.1, ‚Üëp.2) :=
  ((openEmbedding_coe.prod openEmbedding_coe).map_nhds_eq (r, p)).symm

theorem tendsto_toNat {a : ‚Ñï‚àû} (ha : a ‚â† ‚ä§) :
    Tendsto ENat.toNat (ùìù a) (ùìù a.toNat) := by
  lift a to ‚Ñï using ha
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id

theorem eventuallyEq_of_toNat_eventuallyEq {l : Filter Œ±} {f g : Œ± ‚Üí ‚Ñï‚àû}
    (hfi : ‚àÄ·∂† x in l, f x ‚â† ‚ä§) (hgi : ‚àÄ·∂† x in l, g x ‚â† ‚ä§)
    (hfg : (fun x => (f x).toNat) =·∂†[l] fun x => (g x).toNat) : f =·∂†[l] g := by
  filter_upwards [hfi, hgi, hfg] with _ hfx hgx _
  rwa [‚Üê ENat.toNat_eq_toNat hfx hgx]

theorem continuousOn_toNat : ContinuousOn ENat.toNat {a | a ‚â† ‚ä§} := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toNat ha)

lemma continuousAt_toNat (hx : x ‚â† ‚ä§) : ContinuousAt ENat.toNat x :=
  continuousOn_toNat.continuousAt (isOpen_ne_top.mem_nhds_iff.mpr hx)

theorem nhds_top : ùìù (‚ä§ : ‚Ñï‚àû) = ‚®Ö (a) (_ : a ‚â† ‚ä§), ùìü (Ioi a) :=
  nhds_top_order.trans <| by simp [lt_top_iff_ne_top, Ioi]

theorem nhds_zero : ùìù (0 : ‚Ñï‚àû) = ‚®Ö (a) (_ : a ‚â† 0), ùìü (Iio a) :=
  nhds_bot_order.trans <| by simp [pos_iff_ne_zero, Iio]

theorem nhds_zero_basis : (ùìù (0 : ‚Ñï‚àû)).HasBasis (fun a : ‚Ñï‚àû => 0 < a) fun a => Iio a :=
  nhds_bot_basis

end TopologicalSpace

end ENat
