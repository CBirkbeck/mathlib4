/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Category.Ring.Instances
import Mathlib.Algebra.Category.Ring.FilteredColimits
import Mathlib.RingTheory.Localization.Basic
import Mathlib.Topology.Sheaves.Stalks

#align_import topology.sheaves.operations from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!

# Operations on sheaves

## Main definition

- `SubmonoidPresheaf` : A subpresheaf with a submonoid structure on each of the components.
- `LocalizationPresheaf` : The localization of a presheaf of commrings at a `SubmonoidPresheaf`.
- `TotalQuotientPresheaf` : The presheaf of total quotient rings.

-/

-- Porting note: all aligns here start with `Top.`
set_option linter.uppercaseLean3 false

open scoped nonZeroDivisors

open TopologicalSpace Opposite CategoryTheory

universe v u w

namespace TopCat

namespace Presheaf

variable {X : TopCat.{w}} {C : Type u} [Category.{v} C] [ConcreteCategory C]

attribute [local instance 1000] ConcreteCategory.hasCoeToSort

/-- A subpresheaf with a submonoid structure on each of the components. -/
structure SubmonoidPresheaf [‚àÄ X : C, MulOneClass X] [‚àÄ X Y : C, MonoidHomClass (X ‚ü∂ Y) X Y]
    (F : X.Presheaf C) where
  obj : ‚àÄ U, Submonoid (F.obj U)
  map : ‚àÄ {U V : (Opens X)·µí·µñ} (i : U ‚ü∂ V), obj U ‚â§ (obj V).comap (F.map i)
#align Top.presheaf.submonoid_presheaf TopCat.Presheaf.SubmonoidPresheaf

variable {F : X.Presheaf CommRingCat.{w}} (G : F.SubmonoidPresheaf)

/-- The localization of a presheaf of `CommRing`s with respect to a `SubmonoidPresheaf`. -/
protected noncomputable def SubmonoidPresheaf.localizationPresheaf : X.Presheaf CommRingCat where
  obj U := CommRingCat.of <| Localization (G.obj U)
  map {U V} i := CommRingCat.ofHom <| IsLocalization.map _ (F.map i) (G.map i)
  map_id U := by
    simp_rw [F.map_id]
    -- ‚ä¢ CommRingCat.ofHom (IsLocalization.map (Localization (obj G U)) (ùüô (F.obj U)) ‚Ä¶
    ext x
    -- ‚ä¢ ‚Üë(CommRingCat.ofHom (IsLocalization.map (Localization (obj G U)) (ùüô (F.obj U ‚Ä¶
    -- Porting note : `M` and `S` needs to be specified manually
    exact IsLocalization.map_id (M := G.obj U) (S := Localization (G.obj U)) x
    -- üéâ no goals
  map_comp {U V W} i j := by
    delta CommRingCat.ofHom CommRingCat.of Bundled.of
    -- ‚ä¢ { obj := fun U => Bundled.mk (Localization (obj G U)), map := fun {U V} i => ‚Ä¶
    simp_rw [F.map_comp, CommRingCat.comp_eq_ring_hom_comp]
    -- ‚ä¢ IsLocalization.map (Localization (obj G W)) (RingHom.comp (F.map j) (F.map i ‚Ä¶
    rw [IsLocalization.map_comp_map]
    -- üéâ no goals
#align Top.presheaf.submonoid_presheaf.localization_presheaf TopCat.Presheaf.SubmonoidPresheaf.localizationPresheaf

-- Porting note : this instance can't be synthesized
instance (U) : Algebra ((forget CommRingCat).obj (F.obj U)) (G.localizationPresheaf.obj U) :=
  show Algebra _ (Localization (G.obj U)) from inferInstance

-- Porting note : this instance can't be synthesized
instance (U) : IsLocalization (G.obj U) (G.localizationPresheaf.obj U) :=
  show IsLocalization (G.obj U) (Localization (G.obj U)) from inferInstance

/-- The map into the localization presheaf. -/
@[simps app]
def SubmonoidPresheaf.toLocalizationPresheaf : F ‚ü∂ G.localizationPresheaf where
  app U := CommRingCat.ofHom <| algebraMap (F.obj U) (Localization <| G.obj U)
  naturality {_ _} i := (IsLocalization.map_comp (G.map i)).symm
#align Top.presheaf.submonoid_presheaf.to_localization_presheaf TopCat.Presheaf.SubmonoidPresheaf.toLocalizationPresheaf

instance epi_toLocalizationPresheaf : Epi G.toLocalizationPresheaf :=
  @NatTrans.epi_of_epi_app _ _ _ _ _ _ G.toLocalizationPresheaf fun U => Localization.epi' (G.obj U)

variable (F)

/-- Given a submonoid at each of the stalks, we may define a submonoid presheaf consisting of
sections whose restriction onto each stalk falls in the given submonoid. -/
@[simps]
noncomputable def submonoidPresheafOfStalk (S : ‚àÄ x : X, Submonoid (F.stalk x)) :
    F.SubmonoidPresheaf where
  obj U := ‚®Ö x : U.unop, Submonoid.comap (F.germ x) (S x)
  map {U V} i := by
    intro s hs
    -- ‚ä¢ s ‚àà Submonoid.comap (F.map i) ((fun U => ‚®Ö (x : { x // x ‚àà U.unop }), Submon ‚Ä¶
    simp only [Submonoid.mem_comap, Submonoid.mem_iInf] at hs ‚ä¢
    -- ‚ä¢ ‚àÄ (i_1 : { x // x ‚àà V.unop }), ‚Üë(germ F i_1) (‚Üë(F.map i) s) ‚àà S ‚Üëi_1
    intro x
    -- ‚ä¢ ‚Üë(germ F x) (‚Üë(F.map i) s) ‚àà S ‚Üëx
    change (F.map i.unop.op ‚â´ F.germ x) s ‚àà _
    -- ‚ä¢ ‚Üë(F.map i.unop.op ‚â´ germ F x) s ‚àà S ‚Üëx
    rw [F.germ_res]
    -- ‚ä¢ ‚Üë(germ F ((fun x => { val := ‚Üëx, property := (_ : ‚Üëx ‚àà ‚ÜëU.unop) }) x)) s ‚àà S ‚Ä¶
    exact hs _
    -- üéâ no goals
#align Top.presheaf.submonoid_presheaf_of_stalk TopCat.Presheaf.submonoidPresheafOfStalk

noncomputable instance : Inhabited F.SubmonoidPresheaf :=
  ‚ü®F.submonoidPresheafOfStalk fun _ => ‚ä•‚ü©

/-- The localization of a presheaf of `CommRing`s at locally non-zero-divisor sections. -/
noncomputable def totalQuotientPresheaf : X.Presheaf CommRingCat.{w} :=
  (F.submonoidPresheafOfStalk fun x => (F.stalk x)‚Å∞).localizationPresheaf
#align Top.presheaf.total_quotient_presheaf TopCat.Presheaf.totalQuotientPresheaf

/-- The map into the presheaf of total quotient rings -/
noncomputable def toTotalQuotientPresheaf : F ‚ü∂ F.totalQuotientPresheaf :=
  SubmonoidPresheaf.toLocalizationPresheaf _
#align Top.presheaf.to_total_quotient_presheaf TopCat.Presheaf.toTotalQuotientPresheaf

-- Porting note : deriving `Epi` failed
instance : Epi (toTotalQuotientPresheaf F) := epi_toLocalizationPresheaf _

instance (F : X.Sheaf CommRingCat.{w}) : Mono F.presheaf.toTotalQuotientPresheaf := by
  -- Porting note : was an `apply (config := { instances := false })`
  -- See https://github.com/leanprover/lean4/issues/2273
  suffices : ‚àÄ (U : (Opens ‚ÜëX)·µí·µñ), Mono (F.presheaf.toTotalQuotientPresheaf.app U)
  -- ‚ä¢ Mono (toTotalQuotientPresheaf (Sheaf.presheaf F))
  ¬∑ apply NatTrans.mono_of_mono_app
    -- üéâ no goals
  intro U
  -- ‚ä¢ Mono (NatTrans.app (toTotalQuotientPresheaf (Sheaf.presheaf F)) U)
  apply ConcreteCategory.mono_of_injective
  -- ‚ä¢ Function.Injective ‚Üë(NatTrans.app (toTotalQuotientPresheaf (Sheaf.presheaf F ‚Ä¶
  dsimp [toTotalQuotientPresheaf, CommRingCat.ofHom]
  -- ‚ä¢ Function.Injective ‚Üë(algebraMap (‚Üë((Sheaf.presheaf F).obj U)) (Localization  ‚Ä¶
  -- Porting note : this is a hack to make the `refine` below works
  set m := _
  -- ‚ä¢ Function.Injective ‚Üë(algebraMap (‚Üë((Sheaf.presheaf F).obj U)) (Localization  ‚Ä¶
  change Function.Injective (algebraMap _ (Localization m))
  -- ‚ä¢ Function.Injective ‚Üë(algebraMap ((forget CommRingCat).obj ((Sheaf.presheaf F ‚Ä¶
  change Function.Injective (algebraMap (F.presheaf.obj U) _)
  -- ‚ä¢ Function.Injective ‚Üë(algebraMap (‚Üë((Sheaf.presheaf F).obj U)) (Localization  ‚Ä¶
  haveI : IsLocalization _ (Localization m) := Localization.isLocalization
  -- ‚ä¢ Function.Injective ‚Üë(algebraMap (‚Üë((Sheaf.presheaf F).obj U)) (Localization  ‚Ä¶
  -- Porting note : `M` and `S` need to be specified manually, so used a hack to save some typing
  refine IsLocalization.injective (M := m) (S := Localization m) ?_
  -- ‚ä¢ m ‚â§ ((forget CommRingCat).obj ((Sheaf.presheaf F).obj U))‚Å∞
  intro s hs t e
  -- ‚ä¢ t = 0
  apply section_ext F (unop U)
  -- ‚ä¢ ‚àÄ (x : { x // x ‚àà U.unop }), ‚Üë(germ (Sheaf.presheaf F) x) t = ‚Üë(germ (Sheaf. ‚Ä¶
  intro x
  -- ‚ä¢ ‚Üë(germ (Sheaf.presheaf F) x) t = ‚Üë(germ (Sheaf.presheaf F) x) 0
  rw [map_zero]
  -- ‚ä¢ ‚Üë(germ (Sheaf.presheaf F) x) t = 0
  apply Submonoid.mem_iInf.mp hs x
  -- ‚ä¢ ‚Üë(germ (Sheaf.presheaf F) x) t * ‚Üë(germ (Sheaf.presheaf F) x) s = 0
  -- Porting note : added `dsimp` to make `rw ‚Üêmap_mul` work
  dsimp
  -- ‚ä¢ ‚Üë(germ (Sheaf.presheaf F) x) t * ‚Üë(germ (Sheaf.presheaf F) x) s = 0
  rw [‚Üêmap_mul, e, map_zero]
  -- üéâ no goals

end Presheaf

end TopCat
