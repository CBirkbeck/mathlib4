/-
Copyright (c) 2024 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.CategoryTheory.MorphismProperty.Limits

/-!
# Anodyne extensions

-/

universe w v u

namespace CategoryTheory

open Category Limits

namespace MorphismProperty

variable {C : Type u} [Category.{v} C] (W : MorphismProperty C)

def IsStableUnderColimitsOfShape (J : Type _) [Category J] : Prop :=
  ‚àÄ (X‚ÇÅ X‚ÇÇ : J ‚•§ C) (c‚ÇÅ : Cocone X‚ÇÅ) (c‚ÇÇ : Cocone X‚ÇÇ)
    (h‚ÇÅ : IsColimit c‚ÇÅ) (_ : IsColimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ) (_ : W.functorCategory J f),
      W (h‚ÇÅ.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ.Œπ)))

variable {W}

lemma IsStableUnderColimitsOfShape.lim_map {J : Type _} [Category J]
  (hW : W.IsStableUnderColimitsOfShape J) {X Y : J ‚•§ C}
  (f : X ‚ü∂ Y) [HasColimitsOfShape J C]
  (hf : W.functorCategory _ f) : W (colim.map f) :=
  hW X Y _ _ (colimit.isColimit X) (colimit.isColimit Y) f hf

variable (W)

abbrev IsStableUnderCoproductsOfShape (J : Type _) :=
  W.IsStableUnderColimitsOfShape (Discrete J)

def IsStableUnderCoproductOfSize := ‚àÄ (J : Type w), W.IsStableUnderCoproductsOfShape J

abbrev IsStableUnderCoproducts := IsStableUnderCoproductOfSize.{v} W

structure IsStableUnderRetract : Prop where
  mem_of_retract' (f g : Arrow C) (i : f ‚ü∂ g) (p : g ‚ü∂ f)
    (hip : i ‚â´ p = ùüô f) (hg : W g.hom) : W f.hom

variable (C) in
lemma IsStableUnderRetract.monomorphisms : (monomorphisms C).IsStableUnderRetract where
  mem_of_retract' f g i p hip hg :=
    { right_cancellation := fun a b h => by
        have : Mono g.hom := hg
        have : a ‚â´ i.left = b ‚â´ i.left := by
          rw [‚Üê cancel_mono g.hom]
          simp [reassoc_of% h]
        replace this := this =‚â´ p.left
        replace hip : i.left ‚â´ p.left = ùüô _ := by
          rw [‚Üê Arrow.comp_left, hip, Arrow.id_left]
        simpa only [Functor.id_obj, assoc, hip, comp_id] using this }

variable {W} in
lemma IsStableUnderRetract.mem_of_retract (hW : W.IsStableUnderRetract)
    {X Y X' Y' : C} (f : X ‚ü∂ Y) (f' : X' ‚ü∂ Y')
    (i : Arrow.mk f ‚ü∂ Arrow.mk f') (p : Arrow.mk f' ‚ü∂ Arrow.mk f) (hip: i ‚â´ p = ùüô _)
    (hf' : W f') : W f :=
  hW.mem_of_retract' _ _ i p hip hf'

structure IsStableUnderInfiniteComposition : Prop where
  mem (X : ‚Ñï ‚•§ C) (hX : ‚àÄ (n : ‚Ñï), W (X.map (homOfLE (Nat.le_add_right n 1))))
    (c : Cocone X) (hc : IsColimit c) : W (c.Œπ.app 0)

class IsGabrielZismanSaturated [W.IsMultiplicative] where
  subset_mono : W ‚â§ monomorphisms C
  iso_subset : isomorphisms C ‚â§ W
  isStableUnderCobaseChange : W.IsStableUnderCobaseChange := by infer_instance
  isStableUnderRetract : W.IsStableUnderRetract
  isStableUnderCoproducts : W.IsStableUnderCoproducts := by infer_instance
  isStableUnderInfiniteComposition : W.IsStableUnderInfiniteComposition := by infer_instance

namespace IsGabrielZismanSaturated

attribute [instance] isStableUnderCobaseChange isStableUnderCoproducts
  isStableUnderInfiniteComposition

end IsGabrielZismanSaturated

inductive gabrielZismanSaturation : MorphismProperty C where
  | of_mem {X Y : C} (f : X ‚ü∂ Y) (hf : W f) : gabrielZismanSaturation f
  | of_iso {X Y : C} (e : X ‚âÖ Y) : gabrielZismanSaturation e.hom
  | of_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (hf : gabrielZismanSaturation f)
      (hg : gabrielZismanSaturation g) : gabrielZismanSaturation (f ‚â´ g)
  | of_isPushout {A A' B B' : C} (f : A ‚ü∂ A') (g : A ‚ü∂ B) (f' : B ‚ü∂ B') (g' : A' ‚ü∂ B')
      (h : IsPushout g f f' g') (hf : gabrielZismanSaturation f) : gabrielZismanSaturation f'
  | of_retract {X Y X' Y' : C} (f : X ‚ü∂ Y) (f' : X' ‚ü∂ Y') (i : Arrow.mk f ‚ü∂ Arrow.mk f')
      (p : Arrow.mk f' ‚ü∂ Arrow.mk f) (hip : i ‚â´ p = ùüô _) (hf' : gabrielZismanSaturation f') :
      gabrielZismanSaturation f
  | of_infinite_composition (X : ‚Ñï ‚•§ C)
      (hX : ‚àÄ (n : ‚Ñï), gabrielZismanSaturation (X.map (homOfLE (Nat.le_add_right n 1))))
      (c : Cocone X) (hc : IsColimit c) : gabrielZismanSaturation (c.Œπ.app 0)
  | of_coproduct {J : Type v} (X‚ÇÅ X‚ÇÇ : Discrete J ‚•§ C) (c‚ÇÅ : Cocone X‚ÇÅ) (c‚ÇÇ : Cocone X‚ÇÇ)
    (h‚ÇÅ : IsColimit c‚ÇÅ) (_ : IsColimit c‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ)
      (_ : gabrielZismanSaturation.functorCategory (Discrete J) f):
      gabrielZismanSaturation (h‚ÇÅ.desc (Cocone.mk _ (f ‚â´ c‚ÇÇ.Œπ)))

instance : W.gabrielZismanSaturation.ContainsIdentities where
  id_mem X := gabrielZismanSaturation.of_iso (Iso.refl X)

instance : W.gabrielZismanSaturation.IsMultiplicative where
  comp_mem _ _ hf hg :=
    gabrielZismanSaturation.of_comp _ _ hf hg

section

instance : ContainsIdentities (monomorphisms C) where
  id_mem _ := monomorphisms.infer_property _

instance : IsMultiplicative (monomorphisms C) where
  comp_mem f g hf hg := by
    have : Mono f := hf
    have : Mono g := hg
    exact mono_comp f g

lemma IsGabrielZismanSaturated.monomorphisms
    (hmono‚ÇÅ : (monomorphisms C).IsStableUnderCobaseChange)
    (hmono‚ÇÇ : (monomorphisms C).IsStableUnderCoproducts)
    (hmono‚ÇÉ : (monomorphisms C).IsStableUnderInfiniteComposition) :
    IsGabrielZismanSaturated (monomorphisms C) where
  subset_mono _ _ _ := id
  iso_subset _ _ f hf := by
    have : IsIso f := hf
    apply monomorphisms.infer_property
  isStableUnderCobaseChange := hmono‚ÇÅ
  isStableUnderRetract := IsStableUnderRetract.monomorphisms C
  isStableUnderCoproducts := hmono‚ÇÇ
  isStableUnderInfiniteComposition := hmono‚ÇÉ

end

lemma subset_gabrielZismanSaturation : W ‚â§ W.gabrielZismanSaturation :=
  fun _ _ _ hf => gabrielZismanSaturation.of_mem _ hf

lemma gabrielZismanSaturation_subset_iff (W‚ÇÅ W‚ÇÇ : MorphismProperty C) [W‚ÇÇ.IsMultiplicative]
    [W‚ÇÇ.IsGabrielZismanSaturated] :
    W‚ÇÅ.gabrielZismanSaturation ‚â§ W‚ÇÇ ‚Üî W‚ÇÅ ‚â§ W‚ÇÇ := by
  constructor
  ¬∑ intro h X Y f hf
    exact h _ (subset_gabrielZismanSaturation _ _ hf)
  ¬∑ intro h X Y f hf
    induction hf with
      | of_mem f hf => exact h _ hf
      | of_iso f => exact IsGabrielZismanSaturated.iso_subset _ (isomorphisms.infer_property _)
      | of_comp f g _ _ hf hg => exact W‚ÇÇ.comp_mem _ _ hf hg
      | of_isPushout f g f' g' h _ hf => exact MorphismProperty.of_isPushout h hf
      | of_retract f f' i p hip _ h =>
          exact IsGabrielZismanSaturated.isStableUnderRetract.mem_of_retract f f' i p hip h
      | of_infinite_composition X _ c hc h =>
          exact IsGabrielZismanSaturated.isStableUnderInfiniteComposition.mem X h c hc
      | of_coproduct X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f _ h =>
          exact IsGabrielZismanSaturated.isStableUnderCoproducts _ X‚ÇÅ X‚ÇÇ c‚ÇÅ c‚ÇÇ h‚ÇÅ h‚ÇÇ f h

lemma gabrielZismanSaturation_isGabrielZismanSaturated
    (hW : W ‚â§ monomorphisms C)
    [(monomorphisms C).IsGabrielZismanSaturated] :
    (W.gabrielZismanSaturation).IsGabrielZismanSaturated where
  subset_mono := by simpa only [gabrielZismanSaturation_subset_iff] using hW
  iso_subset _ _ f hf := by
    have : IsIso f := hf
    exact gabrielZismanSaturation.of_iso (asIso f)
  isStableUnderCobaseChange := ‚ü®by
    rintro _ _ _ _ f g f' g' h hf
    exact gabrielZismanSaturation.of_isPushout _ _ _ _ h hf‚ü©
  isStableUnderRetract :=
    ‚ü®fun f g i p hip hg => gabrielZismanSaturation.of_retract _ _ _ _ hip hg‚ü©
  isStableUnderCoproducts J := gabrielZismanSaturation.of_coproduct
  isStableUnderInfiniteComposition :=
    ‚ü®gabrielZismanSaturation.of_infinite_composition‚ü©

end MorphismProperty

end CategoryTheory
