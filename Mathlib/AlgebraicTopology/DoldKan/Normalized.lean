/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
import Mathlib.AlgebraicTopology.DoldKan.FunctorN

#align_import algebraic_topology.dold_kan.normalized from "leanprover-community/mathlib"@"32a7e535287f9c73f2e4d2aef306a39190f0b504"

/-!

# Comparison with the normalized Moore complex functor

In this file, we show that when the category `A` is abelian,
there is an isomorphism `N‚ÇÅ_iso_normalizedMooreComplex_comp_toKaroubi` between
the functor `N‚ÇÅ : SimplicialObject A ‚•§ Karoubi (ChainComplex A ‚Ñï)`
defined in `FunctorN.lean` and the composition of
`normalizedMooreComplex A` with the inclusion
`ChainComplex A ‚Ñï ‚•§ Karoubi (ChainComplex A ‚Ñï)`.

This isomorphism shall be used in `equivalence.lean` in order to obtain
the Dold-Kan equivalence
`CategoryTheory.Abelian.DoldKan.equivalence : SimplicialObject A ‚âå ChainComplex A ‚Ñï`
with a functor (definitionally) equal to `normalizedMooreComplex A`.

(See `Equivalence.lean` for the general strategy of proof of the Dold-Kan equivalence.)

-/


open CategoryTheory CategoryTheory.Category CategoryTheory.Limits
  CategoryTheory.Subobject CategoryTheory.Idempotents DoldKan

noncomputable section

namespace AlgebraicTopology

namespace DoldKan

universe v

variable {A : Type*} [Category A] [Abelian A] {X : SimplicialObject A}

theorem HigherFacesVanish.inclusionOfMooreComplexMap (n : ‚Ñï) :
    HigherFacesVanish (n + 1) ((inclusionOfMooreComplexMap X).f (n + 1)) := fun j _ => by
  dsimp [AlgebraicTopology.inclusionOfMooreComplexMap, NormalizedMooreComplex.objX]
  -- ‚ä¢ arrow (Finset.inf Finset.univ fun k => kernelSubobject (SimplicialObject.Œ¥ X ‚Ä¶
  rw [‚Üê factorThru_arrow _ _ (finset_inf_arrow_factors Finset.univ _ j
    (by simp only [Finset.mem_univ])), assoc, kernelSubobject_arrow_comp, comp_zero]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.higher_faces_vanish.inclusion_of_Moore_complex_map AlgebraicTopology.DoldKan.HigherFacesVanish.inclusionOfMooreComplexMap

theorem factors_normalizedMooreComplex_PInfty (n : ‚Ñï) :
    Subobject.Factors (NormalizedMooreComplex.objX X n) (PInfty.f n) := by
  rcases n with _|n
  -- ‚ä¢ Factors (NormalizedMooreComplex.objX X Nat.zero) (HomologicalComplex.Hom.f P ‚Ä¶
  ¬∑ apply top_factors
    -- üéâ no goals
  ¬∑ rw [PInfty_f, NormalizedMooreComplex.objX, finset_inf_factors]
    -- ‚ä¢ ‚àÄ (i : Fin (n + 1)), i ‚àà Finset.univ ‚Üí Factors (kernelSubobject (SimplicialO ‚Ä¶
    intro i _
    -- ‚ä¢ Factors (kernelSubobject (SimplicialObject.Œ¥ X (Fin.succ i))) (HomologicalCo ‚Ä¶
    apply kernelSubobject_factors
    -- ‚ä¢ HomologicalComplex.Hom.f (P (Nat.succ n)) (Nat.succ n) ‚â´ SimplicialObject.Œ¥  ‚Ä¶
    exact (HigherFacesVanish.of_P (n + 1) n) i le_add_self
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.factors_normalized_Moore_complex_P_infty AlgebraicTopology.DoldKan.factors_normalizedMooreComplex_PInfty

/-- `PInfty` factors through the normalized Moore complex -/
@[simps!]
def PInftyToNormalizedMooreComplex (X : SimplicialObject A) : K[X] ‚ü∂ N[X] :=
  ChainComplex.ofHom _ _ _ _ _ _
    (fun n => factorThru _ _ (factors_normalizedMooreComplex_PInfty n)) fun n => by
    rw [‚Üê cancel_mono (NormalizedMooreComplex.objX X n).arrow, assoc, assoc, factorThru_arrow,
      ‚Üê inclusionOfMooreComplexMap_f, ‚Üê normalizedMooreComplex_objD,
      ‚Üê (inclusionOfMooreComplexMap X).comm (n + 1) n, inclusionOfMooreComplexMap_f,
      factorThru_arrow_assoc, ‚Üê alternatingFaceMapComplex_obj_d]
    exact PInfty.comm (n + 1) n
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_infty_to_normalized_Moore_complex AlgebraicTopology.DoldKan.PInftyToNormalizedMooreComplex

@[reassoc (attr := simp)]
theorem PInftyToNormalizedMooreComplex_comp_inclusionOfMooreComplexMap (X : SimplicialObject A) :
    PInftyToNormalizedMooreComplex X ‚â´ inclusionOfMooreComplexMap X = PInfty := by aesop_cat
                                                                                   -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_infty_to_normalized_Moore_complex_comp_inclusion_of_Moore_complex_map AlgebraicTopology.DoldKan.PInftyToNormalizedMooreComplex_comp_inclusionOfMooreComplexMap

@[reassoc (attr := simp)]
theorem PInftyToNormalizedMooreComplex_naturality {X Y : SimplicialObject A} (f : X ‚ü∂ Y) :
    AlternatingFaceMapComplex.map f ‚â´ PInftyToNormalizedMooreComplex Y =
      PInftyToNormalizedMooreComplex X ‚â´ NormalizedMooreComplex.map f :=
  by aesop_cat
     -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_infty_to_normalized_Moore_complex_naturality AlgebraicTopology.DoldKan.PInftyToNormalizedMooreComplex_naturality

@[reassoc (attr := simp)]
theorem PInfty_comp_PInftyToNormalizedMooreComplex (X : SimplicialObject A) :
    PInfty ‚â´ PInftyToNormalizedMooreComplex X = PInftyToNormalizedMooreComplex X := by aesop_cat
                                                                                       -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.P_infty_comp_P_infty_to_normalized_Moore_complex AlgebraicTopology.DoldKan.PInfty_comp_PInftyToNormalizedMooreComplex

@[reassoc (attr := simp)]
theorem inclusionOfMooreComplexMap_comp_PInfty (X : SimplicialObject A) :
    inclusionOfMooreComplexMap X ‚â´ PInfty = inclusionOfMooreComplexMap X := by
  ext (_|n)
  -- ‚ä¢ HomologicalComplex.Hom.f (inclusionOfMooreComplexMap X ‚â´ PInfty) Nat.zero =  ‚Ä¶
  ¬∑ dsimp
    -- ‚ä¢ HomologicalComplex.Hom.f (inclusionOfMooreComplexMap X) 0 ‚â´ ùüô (X.obj (Opposi ‚Ä¶
    simp only [comp_id]
    -- üéâ no goals
  ¬∑ exact (HigherFacesVanish.inclusionOfMooreComplexMap n).comp_P_eq_self
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.inclusion_of_Moore_complex_map_comp_P_infty AlgebraicTopology.DoldKan.inclusionOfMooreComplexMap_comp_PInfty

instance : Mono (inclusionOfMooreComplexMap X) :=
  ‚ü®fun _ _ hf => by
    ext n
    -- ‚ä¢ HomologicalComplex.Hom.f x‚úù¬π n = HomologicalComplex.Hom.f x‚úù n
    dsimp
    -- ‚ä¢ HomologicalComplex.Hom.f x‚úù¬π n = HomologicalComplex.Hom.f x‚úù n
    ext
    -- ‚ä¢ HomologicalComplex.Hom.f x‚úù¬π n ‚â´ arrow (NormalizedMooreComplex.objX X n) = H ‚Ä¶
    exact HomologicalComplex.congr_hom hf n‚ü©
    -- üéâ no goals

/-- `inclusionOfMooreComplexMap X` is a split mono. -/
def splitMonoInclusionOfMooreComplexMap (X : SimplicialObject A) :
    SplitMono (inclusionOfMooreComplexMap X) where
  retraction := PInftyToNormalizedMooreComplex X
  id := by
    simp only [‚Üê cancel_mono (inclusionOfMooreComplexMap X), assoc, id_comp,
      PInftyToNormalizedMooreComplex_comp_inclusionOfMooreComplexMap,
      inclusionOfMooreComplexMap_comp_PInfty]
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.split_mono_inclusion_of_Moore_complex_map AlgebraicTopology.DoldKan.splitMonoInclusionOfMooreComplexMap

variable (A)

/-- When the category `A` is abelian,
the functor `N‚ÇÅ : SimplicialObject A ‚•§ Karoubi (ChainComplex A ‚Ñï)` defined
using `PInfty` identifies to the composition of the normalized Moore complex functor
and the inclusion in the Karoubi envelope. -/
def N‚ÇÅ_iso_normalizedMooreComplex_comp_toKaroubi : N‚ÇÅ ‚âÖ normalizedMooreComplex A ‚ãô toKaroubi _ where
  hom :=
    { app := fun X =>
        { f := PInftyToNormalizedMooreComplex X
          comm := by erw [comp_id, PInfty_comp_PInftyToNormalizedMooreComplex] }
                     -- üéâ no goals
      naturality := fun X Y f => by
        simp only [Functor.comp_map, normalizedMooreComplex_map,
          PInftyToNormalizedMooreComplex_naturality, Karoubi.hom_ext_iff, Karoubi.comp_f, N‚ÇÅ_map_f,
          PInfty_comp_PInftyToNormalizedMooreComplex_assoc, toKaroubi_map_f, assoc] }
  inv :=
    { app := fun X =>
        { f := inclusionOfMooreComplexMap X
          comm := by erw [inclusionOfMooreComplexMap_comp_PInfty, id_comp] }
                     -- üéâ no goals
      naturality := fun X Y f => by
        ext
        -- ‚ä¢ HomologicalComplex.Hom.f ((normalizedMooreComplex A ‚ãô toKaroubi (ChainComple ‚Ä¶
        simp only [Functor.comp_map, normalizedMooreComplex_map, Karoubi.comp_f, toKaroubi_map_f,
          HomologicalComplex.comp_f, NormalizedMooreComplex.map_f,
          inclusionOfMooreComplexMap_f, factorThru_arrow, N‚ÇÅ_map_f,
          inclusionOfMooreComplexMap_comp_PInfty_assoc, AlternatingFaceMapComplex.map_f] }
  hom_inv_id := by
    ext X : 3
    -- ‚ä¢ (NatTrans.app ((NatTrans.mk fun X => Karoubi.Hom.mk (PInftyToNormalizedMoore ‚Ä¶
    simp only [PInftyToNormalizedMooreComplex_comp_inclusionOfMooreComplexMap,
      NatTrans.comp_app, Karoubi.comp_f, N‚ÇÅ_obj_p, NatTrans.id_app, Karoubi.id_eq]
  inv_hom_id := by
    ext X : 3
    -- ‚ä¢ (NatTrans.app ((NatTrans.mk fun X => Karoubi.Hom.mk (inclusionOfMooreComplex ‚Ä¶
    rw [‚Üê cancel_mono (inclusionOfMooreComplexMap X)]
    -- ‚ä¢ (NatTrans.app ((NatTrans.mk fun X => Karoubi.Hom.mk (inclusionOfMooreComplex ‚Ä¶
    simp only [NatTrans.comp_app, Karoubi.comp_f, assoc, NatTrans.id_app, Karoubi.id_eq,
      PInftyToNormalizedMooreComplex_comp_inclusionOfMooreComplexMap,
      inclusionOfMooreComplexMap_comp_PInfty]
    dsimp only [Functor.comp_obj, toKaroubi]
    -- ‚ä¢ inclusionOfMooreComplexMap X = ùüô ((normalizedMooreComplex A).obj X) ‚â´ inclus ‚Ä¶
    erw [id_comp]
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align algebraic_topology.dold_kan.N‚ÇÅ_iso_normalized_Moore_complex_comp_to_karoubi AlgebraicTopology.DoldKan.N‚ÇÅ_iso_normalizedMooreComplex_comp_toKaroubi

end DoldKan

end AlgebraicTopology
