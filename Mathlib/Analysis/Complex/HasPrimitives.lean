/-
Copyright (c) 2024 Ian Jauslin and Alex Kontorovich. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ian Jauslin, Alex Kontorovich
-/
import Mathlib.Analysis.Complex.CauchyIntegral

/-!
# Primitives of Holomorphic Functions

In this file, we give conditions under which holomorphic functions have primitives. The main goal
is to prove that holomorphic functions on simply connected domains have primitives. As a first step,
we prove that holomorphic functions on discs have primitives. The approach is based on Moreira's
theorem, that a continuous function (on a disc) whose `RectangleIntegral` vanishes on all
rectangles contained in the disc has a primitive. (Coupled with the fact that holomorphic functions
have this propoerty.) To prove Moreira's theorem, we first define the `WedgeInt`, which is the
integral of a function over a "wedge" (a horizontal segment followed by a vertical segment in the
disc).

## Main results

* `VanishesOnRectanglesInDisc.diff_of_wedges`: If a function `f` vanishes on all rectangles in a
  disc with center `c`, then the wedge integral from `c` to `w` minus the wedge integral from
  `c` to `z` is equal to the wedge integral from `z` to `w`.

* `deriv_of_wedgeInt`: The derivative of the wedge integral is the function being integrated.

* `moreiras_theorem`: A function which is continuous on a disc and whose integral on
  rectangles in the disc vanishes has a primitive on the disc, defined by the wedge integral.

* `hasPrimitives_of_disc`: A holomorphic function on a disc has primitives.

## Tags
  Holomorphic functions, primitives

TODO: Extend to holomorphic functions on simply connected domains.
-/

open Complex Topology Set Metric

set_option autoImplicit true

open scoped Interval

namespace Asymptotics

-- TO DO: move to `Mathlib.Topology.Basic` near `ContinuousAt.tendsto`
/-- `f : Œ± ‚Üí E` is `ContinuousAt` `x` iff the map `y ‚Ü¶ f y - f x` is littleO of 1 as `y ‚Üí x`. -/
theorem continuousAt_iff_isLittleO {Œ± : Type*} {E : Type*} [NormedRing E] [NormOneClass E]
    [TopologicalSpace Œ±] {f : Œ± ‚Üí E} {x : Œ±} :
    (ContinuousAt f x) ‚Üî (fun (y : Œ±) ‚Ü¶ f y - f x) =o[ùìù x] (fun (_ : Œ±) ‚Ü¶ (1 : E)) := by
  convert (Asymptotics.isLittleO_one_iff (f' := fun (y : Œ±) => f y - f x) (l := ùìù x) (F := E)).symm
  exact tendsto_sub_nhds_zero_iff.symm

end Asymptotics

namespace Set

-- TO DO: move to `Mathlib.Data.Intervals.UnorderedInterval`
def uIoo {Œ± : Type*} [LinearOrder Œ±]  : Œ± ‚Üí Œ± ‚Üí Set Œ± := fun a b => Ioo (a ‚äì b) (a ‚äî b)

-- TO DO: move to `Mathlib.Data.Intervals.UnorderedInterval`
theorem uIoo_comm {Œ± : Type*} [LinearOrder Œ±] (a : Œ±) (b : Œ±) :
    uIoo a b = uIoo b a := by simp [uIoo, inf_comm, sup_comm]

-- TO DO: move to `Mathlib.Data.Set.Intervals.UnorderedInterval`
theorem uIoo_subset_uIcc {Œ± : Type*} [LinearOrder Œ±] (a : Œ±) (b : Œ±) :
    uIoo a b ‚äÜ uIcc a b := by simp [uIoo, uIcc, Ioo_subset_Icc_self]

end Set

namespace Complex

section Asymptotics

/-- As `w ‚Üí z`, `w.re - z.re` is big-O of `w - z`. -/
lemma re_isBigO {z : ‚ÑÇ} :
  (fun (w : ‚ÑÇ) => w.re - z.re) =O[ùìù z] fun w => w - z := by
  rw [Asymptotics.isBigO_iff]
  use 1
  filter_upwards
  intro w
  simp only [Real.norm_eq_abs, Complex.norm_eq_abs, one_mul]
  rw [‚Üê Complex.sub_re]
  exact Complex.abs_re_le_abs (w - z)

/-- As `w ‚Üí z`, `w.im - z.im` is big-O of `w - z`. -/
lemma im_isBigO {z : ‚ÑÇ} :
  (fun (w : ‚ÑÇ) => w.im - z.im) =O[ùìù z] fun w => w - z := by
  rw [Asymptotics.isBigO_iff]
  use 1
  filter_upwards
  intro w
  simp only [Real.norm_eq_abs, Complex.norm_eq_abs, one_mul]
  rw [‚Üê Complex.sub_im]
  exact Complex.abs_im_le_abs (w - z)

end Asymptotics

section reProdIm

/-- This lemma shows the equality between the convext hull of a complex product set and
  the complex product of convex hulls. -/
lemma convexHull_reProdIm (s t : Set ‚Ñù) :
    convexHull ‚Ñù (s √ó‚ÑÇ t) = convexHull ‚Ñù s √ó‚ÑÇ convexHull ‚Ñù t :=
  calc
    convexHull ‚Ñù (equivRealProdLm ‚Åª¬π' (s √óÀ¢ t)) = equivRealProdLm ‚Åª¬π' (convexHull ‚Ñù (s √óÀ¢ t)) := by
      simpa only [‚Üê LinearEquiv.image_symm_eq_preimage]
        using equivRealProdLm.symm.toLinearMap.convexHull_image (s √óÀ¢ t)
    _ = convexHull ‚Ñù s √ó‚ÑÇ convexHull ‚Ñù t := by rw [convexHull_prod]; rfl

/-- The preimage under `equivRealProd` of `s √óÀ¢ t` is `s √ó‚ÑÇ t`. -/
lemma preimage_equivRealProd_prod (s t : Set ‚Ñù) : equivRealProd ‚Åª¬π' (s √óÀ¢ t) = s √ó‚ÑÇ t := rfl

/-- The inequality `s √ó t ‚äÜ s‚ÇÅ √ó t‚ÇÅ` holds in `‚ÑÇ` iff it holds in `‚Ñù √ó ‚Ñù`. -/
lemma reProdIm_subset_iff {s s‚ÇÅ t t‚ÇÅ : Set ‚Ñù} : s √ó‚ÑÇ t ‚äÜ s‚ÇÅ √ó‚ÑÇ t‚ÇÅ ‚Üî s √óÀ¢ t ‚äÜ s‚ÇÅ √óÀ¢ t‚ÇÅ := by
  rw [‚Üê @preimage_equivRealProd_prod s t, ‚Üê @preimage_equivRealProd_prod s‚ÇÅ t‚ÇÅ]
  exact Equiv.preimage_subset equivRealProd _ _

/-- If `s ‚äÜ s‚ÇÅ ‚äÜ ‚Ñù` and `t ‚äÜ t‚ÇÅ ‚äÜ ‚Ñù`, then `s √ó t ‚äÜ s‚ÇÅ √ó t‚ÇÅ` in `‚ÑÇ`. -/
lemma reProdIm_subset_iff' {s s‚ÇÅ t t‚ÇÅ : Set ‚Ñù} :
    s √ó‚ÑÇ t ‚äÜ s‚ÇÅ √ó‚ÑÇ t‚ÇÅ ‚Üî s ‚äÜ s‚ÇÅ ‚àß t ‚äÜ t‚ÇÅ ‚à® s = ‚àÖ ‚à® t = ‚àÖ := by
  convert prod_subset_prod_iff
  exact reProdIm_subset_iff

/-- The axis-parallel complex rectangle with opposite corners `z` and `w` is complex product
  of two intervals, which is also the convex hull of the four corners. -/
lemma segment_reProdIm_segment_eq_convexHull (z w : ‚ÑÇ) :
    [[z.re, w.re]] √ó‚ÑÇ [[z.im, w.im]] = convexHull ‚Ñù {z, z.re + w.im * I, w.re + z.im * I, w} := by
  simp_rw [‚Üê segment_eq_uIcc, ‚Üê convexHull_pair, ‚Üê convexHull_reProdIm,
    ‚Üê preimage_equivRealProd_prod, insert_prod, singleton_prod, image_pair,
    insert_union, ‚Üê insert_eq, preimage_equiv_eq_image_symm, image_insert_eq, image_singleton,
    equivRealProd_symm_apply, re_add_im]

end reProdIm

section Rectangle

/-- A `Rectangle` is an axis-parallel rectangle with corners `z` and `w`. -/
def Rectangle (z w : ‚ÑÇ) : Set ‚ÑÇ := [[z.re, w.re]] √ó‚ÑÇ [[z.im, w.im]]

/-- If the four corners of a rectangle are contained in a convex set `U`, then the whole
  rectangle is. -/
lemma rectangle_in_convex {U : Set ‚ÑÇ} (U_convex : Convex ‚Ñù U) {z w : ‚ÑÇ} (hz : z ‚àà U)
    (hw : w ‚àà U) (hzw : (z.re + w.im * I) ‚àà U) (hwz : (w.re + z.im * I) ‚àà U) :
    Rectangle z w ‚äÜ U := by
  rw [Rectangle, segment_reProdIm_segment_eq_convexHull]
  convert convexHull_min ?_ (U_convex)
  refine insert_subset hz (insert_subset hzw (insert_subset hwz ?_))
  exact singleton_subset_iff.mpr hw

/-- If `z` is in a ball centered at `c`, then `z.re + c.im * I` is in the ball. -/
lemma cornerRectangle_in_disc {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
    z.re + c.im * I ‚àà ball c r := by
  simp only [mem_ball] at hz ‚ä¢
  rw [dist_of_im_eq] <;> simp only [add_re, I_re, mul_zero, I_im, zero_add, add_im,
    add_zero, sub_self, mul_re, mul_one, ofReal_im, mul_im, ofReal_re]
  apply lt_of_le_of_lt ?_ hz
  rw [dist_eq_re_im, Real.dist_eq]
  apply Real.le_sqrt_of_sq_le
  simp only [_root_.sq_abs, le_add_iff_nonneg_right, ge_iff_le, sub_nonneg]
  exact sq_nonneg _

end Rectangle

section Segments

/-- A real segment `[a‚ÇÅ, a‚ÇÇ]` translated by `b * I` is the complex line segment. -/
lemma horizontalSegment_eq (a‚ÇÅ a‚ÇÇ b : ‚Ñù) :
    (fun x => ‚Üëx + ‚Üëb * I) '' [[a‚ÇÅ, a‚ÇÇ]] = [[a‚ÇÅ, a‚ÇÇ]] √ó‚ÑÇ {b} := by
  rw [‚Üê preimage_equivRealProd_prod]
  ext x
  constructor
  ¬∑ intro hx
    obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, hx‚ÇÅ'‚ü© := hx
    simp [‚Üê hx‚ÇÅ', mem_preimage, mem_prod, hx‚ÇÅ]
  ¬∑ intro hx
    obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, hx‚ÇÅ', hx‚ÇÅ''‚ü© := hx
    refine ‚ü®x.re, x‚ÇÅ, by simp‚ü©

/-- A vertical segment `[b‚ÇÅ, b‚ÇÇ]` translated by `a` is the complex line segment. -/
lemma verticalSegment_eq (a b‚ÇÅ b‚ÇÇ : ‚Ñù) :
    (fun y => ‚Üëa + ‚Üëy * I) '' [[b‚ÇÅ, b‚ÇÇ]] = {a} √ó‚ÑÇ [[b‚ÇÅ, b‚ÇÇ]] := by
  rw [‚Üê preimage_equivRealProd_prod]
  ext x
  constructor
  ¬∑ intro hx
    obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, hx‚ÇÅ'‚ü© := hx
    simp [‚Üê hx‚ÇÅ', mem_preimage, mem_prod, hx‚ÇÅ]
  ¬∑ intro hx
    simp only [equivRealProd_apply, singleton_prod, mem_image, Prod.mk.injEq,
      exists_eq_right_right, mem_preimage] at hx
    obtain ‚ü®x‚ÇÅ, hx‚ÇÅ, hx‚ÇÅ', hx‚ÇÅ''‚ü© := hx
    refine ‚ü®x.im, x‚ÇÅ, by simp‚ü©

end Segments

section SubsetBall_Aux

/- Auxiliary lemmata about subsets of balls -/

lemma mem_ball_re_aux {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ} :
    (Ioo (z.re - (r - dist z c)) (z.re + (r - dist z c))) √ó‚ÑÇ {z.im} ‚äÜ ball z (r - dist z c) := by
  intro x hx
  obtain ‚ü®xRe, xIm‚ü© := hx
  simp only [mem_preimage, mem_singleton_iff, mem_Ioo] at xRe xIm
  simp only [mem_ball]
  rw [dist_eq_re_im, xIm]
  simp only [sub_self, ne_eq, not_false_eq_true, zero_pow', add_zero, Real.sqrt_sq_eq_abs, abs_lt]
  refine ‚ü®by linarith, by linarith‚ü©

lemma mem_ball_re_aux' {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ} (_ : z ‚àà ball c r) {x : ‚Ñù}
    (hx : x ‚àà Ioo (z.re - (r - dist z c)) (z.re + (r - dist z c))) :
    x + z.im * I ‚àà ball c r := by
  set r‚ÇÅ := r - dist z c
  set s := Ioo (z.re - r‚ÇÅ) (z.re + r‚ÇÅ)
  have s_ball‚ÇÅ : s √ó‚ÑÇ {z.im} ‚äÜ ball z r‚ÇÅ := mem_ball_re_aux
  have s_ball : s √ó‚ÑÇ {z.im} ‚äÜ ball c r := s_ball‚ÇÅ.trans (by apply ball_subset_ball'; simp)
  apply s_ball
  rw [mem_reProdIm]
  simp only [add_re, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self,
    add_zero, gt_iff_lt, not_lt, ge_iff_le, mem_Ioo, add_im, mul_im, zero_add, mem_singleton_iff,
    and_true]
  apply hx

lemma mem_closedBall_aux {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ} (z_in_ball : z ‚àà closedBall c r)
    {y : ‚Ñù} (y_in_I : y ‚àà Œô c.im z.im) : z.re + y * I ‚àà closedBall c r := by
  rw [mem_closedBall] at z_in_ball ‚ä¢
  rw [mem_uIoc] at y_in_I
  apply le_trans ?_ z_in_ball
  rw [dist_eq_re_im, dist_eq_re_im]
  apply Real.le_sqrt_of_sq_le
  rw [Real.sq_sqrt (by positivity)]
  simp only [add_re, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self,
    add_zero, add_im, mul_im, zero_add, add_le_add_iff_left]
  cases y_in_I <;> nlinarith

lemma mem_ball_of_map_re_aux {c : ‚ÑÇ} {r : ‚Ñù} {a‚ÇÅ a‚ÇÇ b : ‚Ñù} (ha‚ÇÅ : a‚ÇÅ + b * I ‚àà ball c r)
    (ha‚ÇÇ : a‚ÇÇ + b * I ‚àà ball c r) : (fun (x : ‚Ñù) => x + b * I) '' [[a‚ÇÅ, a‚ÇÇ]] ‚äÜ ball c r := by
  convert rectangle_in_convex (convex_ball c r) ha‚ÇÅ ha‚ÇÇ ?_ ?_ using 1 <;>
    simp only [add_re, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self,
      add_zero, add_im, mul_im, zero_add, ha‚ÇÅ, ha‚ÇÇ, Rectangle]
  simp [horizontalSegment_eq a‚ÇÅ a‚ÇÇ b]

lemma mem_ball_of_map_im_aux {c : ‚ÑÇ} {r : ‚Ñù} {a b‚ÇÅ b‚ÇÇ : ‚Ñù} (hb‚ÇÅ : a + b‚ÇÅ * I ‚àà ball c r)
    (hb‚ÇÇ : a + b‚ÇÇ * I ‚àà ball c r) : (fun (y : ‚Ñù) => a + y * I) '' [[b‚ÇÅ, b‚ÇÇ]] ‚äÜ ball c r := by
  convert rectangle_in_convex (convex_ball c r) hb‚ÇÅ hb‚ÇÇ ?_ ?_ using 1 <;>
  simp only [add_re, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self,
    add_zero, add_im, mul_im, zero_add, hb‚ÇÅ, hb‚ÇÇ, Rectangle]
  simp [verticalSegment_eq a b‚ÇÅ b‚ÇÇ]
-- NOTE: I don't know why these `simp`s can't be combined.

lemma mem_ball_of_map_im_aux' {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ}
    (_ : z ‚àà ball c r) {w : ‚ÑÇ} (hw : w ‚àà ball z (r - dist z c)) :
    (fun (y : ‚Ñù) ‚Ü¶ w.re + y * I) '' [[z.im, w.im]] ‚äÜ ball c r := by
  apply mem_ball_of_map_im_aux <;>
  apply mem_of_subset_of_mem (ball_subset_ball' (by simp) : ball z (r - dist z c) ‚äÜ ball c r)
  ¬∑ exact cornerRectangle_in_disc hw
  ¬∑ convert hw; simp

end SubsetBall_Aux

end Complex

section ContinuousOn_Aux
/- Auxiliary lemmata about continuity of various occurring functions -/

lemma ContinuousOn.re_aux_1 {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r)) {z : ‚ÑÇ}
    (hz : z ‚àà ball c r) : ContinuousOn (fun (x : ‚Ñù) => f (x + z.im * I))
                            (Ioo (z.re - (r - dist z c)) (z.re + (r - dist z c))) := by
  apply (hf.comp ((continuous_add_right _).comp continuous_ofReal).continuousOn)
  intro x hx
  change x + z.im * I ‚àà ball c r
  exact mem_ball_re_aux' hz hx

lemma ContinuousOn.re_aux_2 {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (f_cont : ContinuousOn f (ball c r)) {a‚ÇÅ a‚ÇÇ b : ‚Ñù} (ha‚ÇÅ : a‚ÇÅ + b * I ‚àà ball c r)
    (ha‚ÇÇ : a‚ÇÇ + b * I ‚àà ball c r) : ContinuousOn (fun x => f (‚Üëx + ‚Üëb * I)) [[a‚ÇÅ, a‚ÇÇ]] := by
  convert ContinuousOn.comp (g := f) (f := fun (x : ‚Ñù) => (x : ‚ÑÇ) + b * I) (s := uIcc a‚ÇÅ a‚ÇÇ)
    (t := (fun (x : ‚Ñù) => (x : ‚ÑÇ) + b * I) '' (uIcc a‚ÇÅ a‚ÇÇ)) ?_ ?_ (mapsTo_image _ _)
  ¬∑ apply f_cont.mono (mem_ball_of_map_re_aux ha‚ÇÅ ha‚ÇÇ)
  ¬∑ exact Continuous.continuousOn (Continuous.comp (continuous_add_right _) continuous_ofReal)

lemma ContinuousOn.im_aux_1 {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r)) {z : ‚ÑÇ}
    (hz : z ‚àà ball c r) {w : ‚ÑÇ} (hw : w ‚àà ball z (r - dist z c)) :
    ContinuousOn (fun (y : ‚Ñù) => f (w.re + y * I)) [[z.im, w.im]] := by
  convert ContinuousOn.comp (g := f) (f := fun (y : ‚Ñù) => (w.re : ‚ÑÇ) + y * I) (s := uIcc z.im w.im)
    (t := (fun (y : ‚Ñù) => (w.re : ‚ÑÇ) + y * I) '' (uIcc z.im w.im)) ?_ ?_ (mapsTo_image _ _)
  ¬∑ apply hf.mono (mem_ball_of_map_im_aux' hz hw)
  ¬∑ apply Continuous.continuousOn
    exact ((continuous_add_left _).comp (continuous_mul_right _)).comp continuous_ofReal

lemma ContinuousOn.im_aux {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (f_cont : ContinuousOn f (ball c r)) {a b‚ÇÅ b‚ÇÇ : ‚Ñù} (hb‚ÇÅ : a + b‚ÇÅ * I ‚àà ball c r)
    (hb‚ÇÇ : a + b‚ÇÇ * I ‚àà ball c r) : ContinuousOn (fun (y : ‚Ñù) => f (a + y * I)) [[b‚ÇÅ, b‚ÇÇ]] := by
  convert ContinuousOn.comp (g := f) (f := fun (y : ‚Ñù) => (a : ‚ÑÇ) + y * I) (s := uIcc b‚ÇÅ b‚ÇÇ)
    (t := (fun (y : ‚Ñù) => (a : ‚ÑÇ) + y * I) '' (uIcc b‚ÇÅ b‚ÇÇ)) ?_ ?_ (mapsTo_image _ _)
  ¬∑ apply f_cont.mono (mem_ball_of_map_im_aux hb‚ÇÅ hb‚ÇÇ)
  ¬∑ apply Continuous.continuousOn
    exact ((continuous_add_left _).comp (continuous_mul_right _)).comp continuous_ofReal

end ContinuousOn_Aux

namespace Complex

section MainDefinitions

/-- A set `U` `HasPrimitives` if, every holomorphic function on `U` has a primitive -/
def HasPrimitives (U : Set ‚ÑÇ) : Prop :=
  ‚àÄ f : ‚ÑÇ ‚Üí ‚ÑÇ, DifferentiableOn ‚ÑÇ f U ‚Üí ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, ‚àÄ z ‚àà U, HasDerivAt g (f z) z

/-- The wedge integral from `z` to `w` of a function `f` -/
noncomputable def WedgeInt (z w : ‚ÑÇ) (f : ‚ÑÇ ‚Üí ‚ÑÇ) : ‚ÑÇ :=
  (‚à´ x : ‚Ñù in z.re..w.re, f (x + z.im * I)) + I ‚Ä¢ (‚à´ y : ‚Ñù in z.im..w.im, f (re w + y * I))

/-- A `RectangleIntegral` of a function `f` is one over a rectangle determined by
  `z` and `w` in `‚ÑÇ`. -/
noncomputable def RectangleIntegral (f : ‚ÑÇ ‚Üí ‚ÑÇ) (z w : ‚ÑÇ) : ‚ÑÇ :=
    (‚à´ x : ‚Ñù in z.re..w.re, f (x + z.im * I)) - (‚à´ x : ‚Ñù in z.re..w.re, f (x + w.im * I))
     + I ‚Ä¢ (‚à´ y : ‚Ñù in z.im..w.im, f (w.re + y * I)) - I ‚Ä¢ ‚à´ y : ‚Ñù in z.im..w.im, f (z.re + y * I)

/-- A function `f` `VanishesOnRectanglesInDisc` if, for any rectangle contained in a disc,
  the integral of `f` over the rectangle is zero. -/
def VanishesOnRectanglesInDisc (c : ‚ÑÇ) (r : ‚Ñù) (f : ‚ÑÇ ‚Üí ‚ÑÇ) : Prop :=
    ‚àÄ z w, z ‚àà ball c r ‚Üí w ‚àà ball c r ‚Üí (z.re + w.im * I) ‚àà ball c r ‚Üí
    (w.re + z.im * I) ‚àà ball c r ‚Üí RectangleIntegral f z w = 0

end MainDefinitions

/-- If a function `f` `VanishesOnRectanglesInDisc` of center `c`, then, for all `w` in a
  neighborhood of `z`, the wedge integral from `c` to `w` minus the wedge integral from `c` to `z`
  is equal to the wedge integral from `z` to `w`. -/
lemma VanishesOnRectanglesInDisc.diff_of_wedges {c : ‚ÑÇ} {r : ‚Ñù} {z : ‚ÑÇ}
    (hz : z ‚àà ball c r) {f : ‚ÑÇ ‚Üí ‚ÑÇ} (f_cont : ContinuousOn f (ball c r))
    (hf : VanishesOnRectanglesInDisc c r f) :
    ‚àÄ·∂† (w : ‚ÑÇ) in ùìù z,
      WedgeInt c w f - WedgeInt c z f = WedgeInt z w f := by
  have hr : 0 < r := pos_of_mem_ball hz
  let r‚ÇÅ := r - dist z c
  have r‚ÇÅ_pos : 0 < r‚ÇÅ := by simp only [mem_ball, gt_iff_lt] at hz ‚ä¢; linarith
  have z_ball : ball z r‚ÇÅ ‚äÜ ball c r := ball_subset_ball' (by simp)
  filter_upwards [ball_mem_nhds z r‚ÇÅ_pos]
  intro w w_in_z_ball
  have hzPlusH : w ‚àà ball c r := mem_of_subset_of_mem z_ball w_in_z_ball
  simp only [WedgeInt]
  set intI := ‚à´ x : ‚Ñù in c.re..(w).re, f (x + c.im * I)
  set intII := I ‚Ä¢ ‚à´ y : ‚Ñù in c.im..w.im, f (w.re + y * I)
  set intIII := ‚à´ x : ‚Ñù in c.re..z.re, f (x + c.im * I)
  set intIV := I ‚Ä¢ ‚à´ y : ‚Ñù in c.im..z.im, f (z.re + y * I)
  set intV := ‚à´ x : ‚Ñù in z.re..w.re, f (x + z.im * I)
  set intVI := I ‚Ä¢ ‚à´ y : ‚Ñù in z.im..w.im, f (w.re + y * I)
  let intVII := ‚à´ x : ‚Ñù in z.re..w.re, f (x + c.im * I)
  let intVIII := I ‚Ä¢ ‚à´ y : ‚Ñù in c.im..z.im, f (w.re + y * I)
  have integrableHoriz : ‚àÄ a‚ÇÅ a‚ÇÇ b : ‚Ñù, a‚ÇÅ + b * I ‚àà ball c r ‚Üí a‚ÇÇ + b * I ‚àà ball c r
    ‚Üí IntervalIntegrable (fun x => f (x + b * I)) MeasureTheory.volume a‚ÇÅ a‚ÇÇ :=
      fun a‚ÇÅ a‚ÇÇ b ha‚ÇÅ ha‚ÇÇ ‚Ü¶
        ContinuousOn.intervalIntegrable (f_cont.re_aux_2 ha‚ÇÅ ha‚ÇÇ)
  have integrableVert : ‚àÄ a b‚ÇÅ b‚ÇÇ : ‚Ñù, a + b‚ÇÅ * I ‚àà ball c r ‚Üí a + b‚ÇÇ * I ‚àà ball c r
    ‚Üí IntervalIntegrable (fun y => f (a + y * I)) MeasureTheory.volume b‚ÇÅ b‚ÇÇ
  ¬∑ intro a b‚ÇÅ b‚ÇÇ hb‚ÇÅ hb‚ÇÇ
    apply ContinuousOn.intervalIntegrable (f_cont.im_aux hb‚ÇÅ hb‚ÇÇ)
  have intIdecomp : intI = intIII + intVII
  ¬∑ rw [intervalIntegral.integral_add_adjacent_intervals] <;> apply integrableHoriz
    ¬∑ simp only [re_add_im, mem_ball, dist_self, hr]
    ¬∑ exact cornerRectangle_in_disc hz
    ¬∑ exact cornerRectangle_in_disc hz
    ¬∑ exact cornerRectangle_in_disc hzPlusH
  have intIIdecomp : intII = intVIII + intVI
  ¬∑ rw [‚Üê smul_add, intervalIntegral.integral_add_adjacent_intervals] <;> apply integrableVert
    ¬∑ exact cornerRectangle_in_disc hzPlusH
    ¬∑ apply mem_of_subset_of_mem z_ball (cornerRectangle_in_disc w_in_z_ball)
    ¬∑ apply mem_of_subset_of_mem z_ball (cornerRectangle_in_disc w_in_z_ball)
    ¬∑ convert hzPlusH; simp
  have rectZero : intVIII = - intVII + intV + intIV
  ¬∑ rw [‚Üê sub_eq_zero]
    have : intVII - intV + intVIII - intIV = 0 := by
      have wzInBall : w.re + z.im * I ‚àà ball c r :=
        by exact mem_of_subset_of_mem z_ball (cornerRectangle_in_disc w_in_z_ball)
      have wcInBall : w.re + c.im * I ‚àà ball c r := cornerRectangle_in_disc hzPlusH
      convert hf (z.re + c.im * I) (w.re + z.im * I) (cornerRectangle_in_disc hz) wzInBall
          (by simpa using hz) (by simpa using wcInBall) using 1
      rw [RectangleIntegral]
      congr <;> simp
    rw [‚Üê this]
    ring
  rw [intIdecomp, intIIdecomp, rectZero]
  ring

/-- The integral of a continuous function `f` from `z` to `x + z.im * I` is equal to
  `(x - z.re) * f z` up to `o(x - z.re)`. -/
lemma deriv_of_wedgeInt_re' {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r))
  {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
  (fun (x : ‚Ñù) ‚Ü¶ (‚à´ t in z.re..x, f (t + z.im * I)) - (x - z.re) * f z)
    =o[ùìù z.re] (fun (x : ‚Ñù)  ‚Ü¶ x - z.re) := by
  let r‚ÇÅ := r - dist z c
  have : 0 < r‚ÇÅ := by simp only [mem_ball, gt_iff_lt] at hz ‚ä¢; linarith
  let s : Set ‚Ñù := Ioo (z.re - r‚ÇÅ) (z.re + r‚ÇÅ)
  have zRe_mem_s : z.re ‚àà s := by simp [mem_ball.mp hz]
  have s_open : IsOpen s := isOpen_Ioo
  have f_contOn : ContinuousOn (fun (x : ‚Ñù) => f (x + z.im * I)) s := hf.re_aux_1 hz
  have int1 : IntervalIntegrable (fun (x : ‚Ñù) => f (x + z.im * I)) MeasureTheory.volume z.re z.re
  ¬∑ apply ContinuousOn.intervalIntegrable
    apply f_contOn.mono
    simp [mem_ball.mp hz]
  have int2 : StronglyMeasurableAtFilter (fun (x : ‚Ñù) => f (x + z.im * I)) (ùìù z.re) :=
    ContinuousOn.stronglyMeasurableAtFilter s_open f_contOn _ zRe_mem_s
  have int3 : ContinuousAt (fun (x : ‚Ñù) => f (x + z.im * I)) z.re :=
    s_open.continuousOn_iff.mp f_contOn zRe_mem_s
  have := @intervalIntegral.integral_hasDerivAt_right (f := fun (x : ‚Ñù) ‚Ü¶ f (x + z.im * I))
    (a := z.re) (b := z.re) _ _ _ int1 int2 int3
  dsimp [HasDerivAt, HasDerivAtFilter] at this
  rw [hasFDerivAtFilter_iff_isLittleO] at this
  simp only [intervalIntegral.integral_same, sub_zero, re_add_im, map_sub] at this
  convert this using 3
  ring_nf
  congr

/- The horizontal integral of `f` from `z` to `z.re + w.im * I` is equal to `(w - z).re * f z`
  up to `o(w - z)`, as `w` tends to `z`. -/
lemma deriv_of_wedgeInt_re {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r))
  {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
  (fun (w : ‚ÑÇ) ‚Ü¶ (‚à´ x in z.re..w.re, f (x + z.im * I)) - ((w - z).re) * f z)
    =o[ùìù z] (fun w ‚Ü¶ w - z) := by
  have zReTendsTo : Filter.Tendsto (fun (w : ‚ÑÇ) ‚Ü¶ w.re) (ùìù z) (ùìù z.re) :=
    by apply Continuous.tendsto Complex.continuous_re
  have := (deriv_of_wedgeInt_re' hf hz).comp_tendsto zReTendsTo
  have := this.trans_isBigO re_isBigO
  convert this using 2
  congr
  simp

/-- If `f` is continuous on a ball containing `z`, then the integral from `z.im` to `w.im` of
  `f (w.re + y * I)` is equal to `(w - z).im * f z` up to `o(w - z)`, as `w` tends to `z`. -/
lemma deriv_of_wedgeInt_im' {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r))
  {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
  (fun w ‚Ü¶ ‚à´ y in z.im..w.im, f (w.re + y * I) - f z)
    =o[ùìù z] fun w ‚Ü¶ w - z := by
  have : (fun w ‚Ü¶ f w - f z) =o[ùìù z] fun (_ : ‚ÑÇ) ‚Ü¶ (1 : ‚ÑÇ)
  ¬∑ refine (Asymptotics.continuousAt_iff_isLittleO (f := f) (x := z)).mp ((hf z hz).continuousAt ?_)
    exact (IsOpen.mem_nhds_iff isOpen_ball).mpr hz
  rw [Asymptotics.IsLittleO] at this ‚ä¢
  intro Œµ Œµ_pos
  have := this Œµ_pos
  simp only [Asymptotics.isBigOWith_iff, Pi.one_apply, norm_one, mul_one ] at this ‚ä¢
  have : ‚àÄ·∂† (w : ‚ÑÇ) in ùìù z, ‚àÄ y ‚àà Œô z.im w.im, ‚Äñf (w.re + y * I) - f z‚Äñ ‚â§ Œµ
  ¬∑ rw [Metric.nhds_basis_closedBall.eventually_iff] at this ‚ä¢
    obtain ‚ü®i, i_pos, hi‚ü© := this
    refine ‚ü®i, i_pos, fun w w_in_ball y y_in_I ‚Ü¶ hi (mem_closedBall_aux w_in_ball y_in_I)‚ü©
  apply this.mono (fun w hw ‚Ü¶ ?_)
  calc
    _ ‚â§ Œµ * |w.im - z.im|  := intervalIntegral.norm_integral_le_of_norm_le_const hw
    _ = Œµ * |(w - z).im| := by simp
    _ ‚â§ Œµ  * ‚Äñw - z‚Äñ := by gcongr; apply abs_im_le_abs

/--   The vertical integral of `f` from `w.re + z.im * I` to `w` is equal to `(w - z).im * f z`
  up to `o(w - z)`, as `w` tends to `z`. -/
lemma deriv_of_wedgeInt_im {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ContinuousOn f (ball c r))
  {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
  (fun w ‚Ü¶ (‚à´ y in z.im..w.im, f (w.re + y * I)) - (w - z).im * f z)
    =o[ùìù z] fun w ‚Ü¶ w - z := by
  calc
    _ = (fun w:‚ÑÇ ‚Ü¶ (‚à´ y in z.im..w.im, f (w.re + y * I)) - (‚à´ _ in z.im..w.im, f z)) :=
      by congr! 2; simp
    _ =·∂†[ùìù z] (fun w ‚Ü¶ ‚à´ y in z.im..w.im, f (w.re + y * I) - f z) := ?_
    _ =o[ùìù z] fun w => w - z := deriv_of_wedgeInt_im' hf hz
  let r‚ÇÅ := r - dist z c
  have : 0 < r‚ÇÅ := by simp only [mem_ball, gt_iff_lt] at hz ‚ä¢; linarith
  filter_upwards [ball_mem_nhds z this]
  intro w hw
  rw [intervalIntegral.integral_sub ?_ continuousOn_const.intervalIntegrable]
  exact (hf.im_aux_1 hz hw).intervalIntegrable

/-- The `WedgeInt` has derivative at `z` equal to `f z`. -/
theorem deriv_of_wedgeInt {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (f_cont : ContinuousOn f (ball c r)) (hf : VanishesOnRectanglesInDisc c r f)
    {z : ‚ÑÇ} (hz : z ‚àà ball c r) :
    HasDerivAt (fun w => WedgeInt c w f) (f z) z := by
  have : 0 < r := pos_of_mem_ball hz
  dsimp [HasDerivAt, HasDerivAtFilter]
  rw [hasFDerivAtFilter_iff_isLittleO]
  calc
    _ =·∂†[ùìù z] (fun w ‚Ü¶ WedgeInt z w f - (w - z) * f z) := ?_
    _ = (fun w ‚Ü¶ (‚à´ x in z.re..w.re, f (‚Üëx + ‚Üëz.im * I)) - (w - z).re * f z)
        + I ‚Ä¢ (fun w ‚Ü¶ (‚à´ y in z.im..w.im, f (w.re + y * I)) - (w - z).im * f z) := ?_
    _ =o[ùìù z] fun w ‚Ü¶ w - z :=
      (deriv_of_wedgeInt_re f_cont hz).add ((deriv_of_wedgeInt_im f_cont hz).const_smul_left I)
  ¬∑ filter_upwards [VanishesOnRectanglesInDisc.diff_of_wedges hz f_cont hf]
    exact fun _ ha ‚Ü¶ by rw [ha]; congr
  ext1 w
  simp only [WedgeInt, smul_eq_mul, sub_re, ofReal_sub, sub_im, Pi.add_apply, Pi.smul_apply]
  set intI := ‚à´ (x : ‚Ñù) in z.re..w.re, f (x + z.im * I)
  set intII := ‚à´ (y : ‚Ñù) in z.im..w.im, f (w.re + y * I)
  calc
    _ = intI + I * intII - ((w - z).re + (w - z).im * I) * f z := by congr; rw [re_add_im]
    _ = intI + I * intII - ((w.re - z.re) + (w.im - z.im) * I) * f z := by simp
    _ = intI - (w.re - z.re) * f z + I * (intII - (w.im - z.im) * f z) := by ring

/-- *** Moreira's theorem *** A function which is continuous on a disc and whose integral on
  rectangles in the disc vanishes has a primitive on the disc. -/
theorem moreiras_theorem {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (hf : ContinuousOn f (ball c r))
    (hf‚ÇÇ : VanishesOnRectanglesInDisc c r f) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, ‚àÄ z ‚àà (ball c r), HasDerivAt g (f z) z :=
  ‚ü®fun z ‚Ü¶ WedgeInt c z f, fun _ hz ‚Ü¶ deriv_of_wedgeInt hf hf‚ÇÇ hz‚ü©

/-- If `f` is `DifferentiableOn` a set `U`, then the rectangle integral of `f` vanishes, for any
  rectangle in `U`. -/
theorem vanishesOnRectangles_of_holomorphic {f : ‚ÑÇ ‚Üí ‚ÑÇ} {U : Set ‚ÑÇ} {z w : ‚ÑÇ}
    (hf : DifferentiableOn ‚ÑÇ f U)
    (hU : Rectangle z w ‚äÜ U) :
    RectangleIntegral f z w = 0 := by
  convert integral_boundary_rect_eq_zero_of_differentiable_on_off_countable f z w ‚àÖ (by simp)
    ((hf.mono hU).continuousOn) ?_ using 1
  intro x hx
  apply hf.differentiableAt
  rw [_root_.mem_nhds_iff]
  refine ‚ü®Ioo (min z.re w.re) (max z.re w.re) √ó‚ÑÇ Ioo (min z.im w.im) (max z.im w.im), ?_, ?_, ?_‚ü©
  ¬∑ apply subset_trans ?_ hU
    rw [Rectangle]
    apply reProdIm_subset_iff'.mpr
    left
    constructor <;> convert uIoo_subset_uIcc _ _ using 1
  ¬∑ exact IsOpen.reProdIm isOpen_Ioo isOpen_Ioo
  ¬∑ convert hx using 1; simp

/-- If `f` is `DifferentiableOn` a disc, then `f` vanishes on rectangles in the disc. -/
theorem vanishesOnRectanglesInDisc_of_holomorphic {c : ‚ÑÇ} {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (hf : DifferentiableOn ‚ÑÇ f (ball c r)) :
    VanishesOnRectanglesInDisc c r f := fun _ _ hz hw hz' hw' ‚Ü¶
  vanishesOnRectangles_of_holomorphic hf (rectangle_in_convex (convex_ball c r) hz hw hz' hw')

/-- *** Holomorphic functions on discs have Primitives *** A holomorphic function on a disc has
  primitives. -/
theorem hasPrimitives_of_disc (c : ‚ÑÇ) {r : ‚Ñù} : HasPrimitives (ball c r) :=
  fun _ hf ‚Ü¶ moreiras_theorem hf.continuousOn (vanishesOnRectanglesInDisc_of_holomorphic hf)

end Complex
