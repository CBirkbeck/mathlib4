/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Data.Polynomial.UnitTrinomial
import Mathlib.FieldTheory.PolynomialGaloisGroup
import Mathlib.NumberTheory.NumberField.Discriminant
import Mathlib.RingTheory.Polynomial.GaussLemma
import Mathlib.Tactic.LinearCombination

#align_import ring_theory.polynomial.selmer from "leanprover-community/mathlib"@"3e00d81bdcbf77c8188bbd18f5524ddc3ed8cac6"

/-!
# Irreducibility of Selmer Polynomials

This file proves irreducibility of the Selmer polynomials `X ^ n - X - 1`.

## Main results

- `X_pow_sub_X_sub_one_irreducible`: The Selmer polynomials `X ^ n - X - 1` are irreducible.

TODO: Show that the Selmer polynomials have full Galois group.
-/


namespace Polynomial

open scoped Polynomial

variable {n : ℕ}

theorem X_pow_sub_X_sub_one_irreducible_aux (z : ℂ) : ¬(z ^ n = z + 1 ∧ z ^ n + z ^ 2 = 0) := by
  rintro ⟨h1, h2⟩
  replace h3 : z ^ 3 = 1
  · linear_combination (1 - z - z ^ 2 - z ^ n) * h1 + (z ^ n - 2) * h2
  have key : z ^ n = 1 ∨ z ^ n = z ∨ z ^ n = z ^ 2 := by
    rw [← Nat.mod_add_div n 3, pow_add, pow_mul, h3, one_pow, mul_one]
    have : n % 3 < 3 := Nat.mod_lt n zero_lt_three
    interval_cases n % 3 <;>
    simp only [this, pow_zero, pow_one, eq_self_iff_true, or_true_iff, true_or_iff]
  have z_ne_zero : z ≠ 0 := fun h =>
    zero_ne_one ((zero_pow zero_lt_three).symm.trans (show (0 : ℂ) ^ 3 = 1 from h ▸ h3))
  rcases key with (key | key | key)
  · exact z_ne_zero (by rwa [key, self_eq_add_left] at h1)
  · exact one_ne_zero (by rwa [key, self_eq_add_right] at h1)
  · exact z_ne_zero (pow_eq_zero (by rwa [key, add_self_eq_zero] at h2))
set_option linter.uppercaseLean3 false in
#align polynomial.X_pow_sub_X_sub_one_irreducible_aux Polynomial.X_pow_sub_X_sub_one_irreducible_aux

theorem X_pow_sub_X_sub_one_irreducible (hn1 : n ≠ 1) : Irreducible (X ^ n - X - 1 : ℤ[X]) := by
  by_cases hn0 : n = 0
  · rw [hn0, pow_zero, sub_sub, add_comm, ← sub_sub, sub_self, zero_sub]
    exact Associated.irreducible ⟨-1, mul_neg_one X⟩ irreducible_X
  have hn : 1 < n := Nat.one_lt_iff_ne_zero_and_ne_one.mpr ⟨hn0, hn1⟩
  have hp : (X ^ n - X - 1 : ℤ[X]) = trinomial 0 1 n (-1) (-1) 1 := by
    simp only [trinomial, C_neg, C_1]; ring
  rw [hp]
  apply IsUnitTrinomial.irreducible_of_coprime' ⟨0, 1, n, zero_lt_one, hn, -1, -1, 1, rfl⟩
  rintro z ⟨h1, h2⟩
  apply X_pow_sub_X_sub_one_irreducible_aux z
  rw [trinomial_mirror zero_lt_one hn (-1 : ℤˣ).ne_zero (1 : ℤˣ).ne_zero] at h2
  simp_rw [trinomial, aeval_add, aeval_mul, aeval_X_pow, aeval_C,
    Units.val_neg, Units.val_one, map_neg, map_one] at h1 h2
  replace h1 : z ^ n = z + 1 := by linear_combination h1
  replace h2 := mul_eq_zero_of_left h2 z
  rw [add_mul, add_mul, add_zero, mul_assoc (-1 : ℂ), ← pow_succ', Nat.sub_add_cancel hn.le] at h2
  rw [h1] at h2 ⊢
  exact ⟨rfl, by linear_combination -h2⟩
set_option linter.uppercaseLean3 false in
#align polynomial.X_pow_sub_X_sub_one_irreducible Polynomial.X_pow_sub_X_sub_one_irreducible

theorem X_pow_sub_X_sub_one_irreducible_rat (hn1 : n ≠ 1) : Irreducible (X ^ n - X - 1 : ℚ[X]) := by
  by_cases hn0 : n = 0
  · rw [hn0, pow_zero, sub_sub, add_comm, ← sub_sub, sub_self, zero_sub]
    exact Associated.irreducible ⟨-1, mul_neg_one X⟩ irreducible_X
  have hp : (X ^ n - X - 1 : ℤ[X]) = trinomial 0 1 n (-1) (-1) 1 := by
    simp only [trinomial, C_neg, C_1]; ring
  have hn : 1 < n := Nat.one_lt_iff_ne_zero_and_ne_one.mpr ⟨hn0, hn1⟩
  have h := (IsPrimitive.Int.irreducible_iff_irreducible_map_cast ?_).mp
    (X_pow_sub_X_sub_one_irreducible hn1)
  · rwa [Polynomial.map_sub, Polynomial.map_sub, Polynomial.map_pow, Polynomial.map_one,
      Polynomial.map_X] at h
  · exact hp.symm ▸ (trinomial_monic zero_lt_one hn).isPrimitive
set_option linter.uppercaseLean3 false in
#align polynomial.X_pow_sub_X_sub_one_irreducible_rat Polynomial.X_pow_sub_X_sub_one_irreducible_rat

open Equiv Pointwise

lemma swap_mem_stabilizer_iff {α : Type*} [DecidableEq α] (S : Set α) (a b : α) :
    swap a b ∈ MulAction.stabilizer (Perm α) S ↔ (a ∈ S ↔ b ∈ S) := by
  rw [MulAction.mem_stabilizer_iff, Set.ext_iff, ← swap_inv]
  simp_rw [Set.mem_inv_smul_set_iff, Perm.smul_def, swap_apply_def]
  exact ⟨fun h ↦ by simpa [Iff.comm] using h a, by intros; split_ifs <;> simp [*]⟩

/-- A transitive permutation group generated by transpositions must be the whole symmetric group -/
lemma closure_of_isSwap_of_isPretransitive {α : Type*} [DecidableEq α] [Finite α]
    (S : Set (Perm α)) (hS : ∀ σ ∈ S, σ.IsSwap) [MulAction.IsPretransitive (Subgroup.closure S) α] :
    Subgroup.closure S = ⊤ := by
  -- first separate out the case when `α` is empty
  rcases isEmpty_or_nonempty α with _ | ⟨⟨a0⟩⟩
  · apply Subsingleton.elim
  -- `P T` says that `closure S` contains all transpositions of elements of `T`
  let P : Set α → Prop := fun T ↦ ∀ x ∈ T, ∀ y ∈ T, x ≠ y → swap x y ∈ Subgroup.closure S
  -- it suffices to show `P Set.univ`, since then `closure S` contains all transpositions
  suffices : P Set.univ
  · rw [eq_top_iff, ← Perm.closure_isSwap, Subgroup.closure_le]
    rintro - ⟨x, y, hxy, rfl⟩
    exact this x (Set.mem_univ x) y (Set.mem_univ y) hxy
  -- we will prove `P Set.univ` by induction, adding one element at a time
  apply Set.Finite.induction_to_univ ∅ (by simp)
  -- we assume `P T`, and must prove `P ({b} ∪ T)` for some `b ∉ T`
  intro T hT ih
  -- first separate out the case where `T` is empty
  rcases T.eq_empty_or_nonempty with rfl | hT'
  · exact ⟨a0, Set.not_mem_empty a0, by simp⟩
  -- we can show that `S` does not stabilize `T`
  have key0 : ¬ Subgroup.closure S ≤ MulAction.stabilizer (Perm α) T
  · -- we can find `a ∈ T` and `b ∉ T`
    obtain ⟨a, ha⟩ := hT'
    obtain ⟨b, hb⟩ := T.ne_univ_iff_exists_not_mem.mp hT
    -- the action is transitive, so we can find `σ ∈ closure S` with `σ a = b`
    obtain ⟨⟨σ, hσ⟩, rfl⟩ := MulAction.exists_smul_eq (Subgroup.closure S) a b
    exact fun h ↦ hb (h hσ ▸ Set.smul_mem_smul_set ha)
  -- we can find `swap a b ∈ closure S` with `a ∈ T` and `b ∉ T`
  have key : ∃ a b, swap a b ∈ S ∧ a ∈ T ∧ b ∉ T
  · rw [Subgroup.closure_le, Set.not_subset] at key0
    -- we can find `τ = swap x y ∈ S` with `τ ∉ MulAction.stabilizer (Perm α) T`
    obtain ⟨τ, hτ, hτ'⟩ := key0
    obtain ⟨x, y, -, rfl⟩ := hS τ hτ
    rw [SetLike.mem_coe, swap_mem_stabilizer_iff, not_iff] at hτ'
    by_cases hx : x ∈ T
    · exact ⟨x, y, hτ, hx, fun hy ↦ hτ'.mpr hy hx⟩
    · exact ⟨y, x, swap_comm x y ▸ hτ, hτ'.mp hx, hx⟩
  -- now we have `a ∈ T` and `b ∉ T` and `swap a b ∈ Subgroup.closure S`
  obtain ⟨a, b, hab, ha, hb⟩ := key
  replace hab := Subgroup.subset_closure hab
  refine' ⟨b, hb, _⟩
  -- we will prove `P ({b} ∪ T)` with a case bash
  rintro x (rfl | hx) y (rfl | hy) hxy
  · contradiction
  · rcases eq_or_ne y a with rfl | h
    · exact swap_comm x y ▸ hab
    rw [← swap_mul_swap_mul_swap h hxy.symm]
    exact Subgroup.mul_mem _ (Subgroup.mul_mem _ hab (ih y hy a ha h)) hab
  · rcases eq_or_ne x a with rfl | h
    · exact swap_comm x y ▸ hab
    rw [swap_comm, ← swap_mul_swap_mul_swap h hxy]
    exact Subgroup.mul_mem _ (Subgroup.mul_mem _ hab (ih x hx a ha h)) hab
  · exact ih x hx y hy hxy

/- A transitive permutation group generated by transpositions must be the entire symmetric group -/
lemma surjective_of_isSwap_of_isPretransitive
    {α β : Type*} [Group α] [MulAction α β] [DecidableEq β] [Finite β]
    [h : MulAction.IsPretransitive α β] (S : Set α)
    (hS1 : ∀ σ ∈ S, Perm.IsSwap (MulAction.toPermHom α β σ)) (hS2 : Subgroup.closure S = ⊤) :
    Function.Surjective (MulAction.toPermHom α β) := by
  rw [← MonoidHom.range_top_iff_surjective]
  have := MulAction.IsPretransitive.of_compHom (α := β) (MulAction.toPermHom α β).rangeRestrict
  rw [MonoidHom.range_eq_map, ← hS2, MonoidHom.map_closure] at this ⊢
  exact closure_of_isSwap_of_isPretransitive _ (Set.ball_image_iff.mpr hS1)

open IntermediateField

attribute [local instance] Gal.splits_ℚ_ℂ

instance {α β : Type*} [Monoid α] [Subsingleton β] [MulAction α β] :
    MulAction.IsPretransitive α β :=
  ⟨fun _ _ ↦ ⟨1, Subsingleton.elim _ _⟩⟩

open NumberField

def reshom {K : Type*} [Field K] (σ : K →+* K) : 𝓞 K →+* 𝓞 K :=
  σ.restrict (𝓞 K) (𝓞 K) (fun _ ↦ map_isIntegral_int σ)

def res' {K : Type*} [Field K] (σ : K ≃+* K) : 𝓞 K ≃+* 𝓞 K :=
  RingEquiv.ofHomInv (reshom σ) (reshom σ.symm)
    (by ext x; exact σ.symm_apply_apply x) (by ext x; exact σ.apply_symm_apply x)

def res {K : Type*} [Field K] {τ : Type*} [RingEquivClass τ K K] (σ : τ) : 𝓞 K ≃+* 𝓞 K :=
  res' (RingEquivClass.toRingEquiv σ)

theorem X_pow_sub_X_sub_one_gal :
    Function.Bijective (Gal.galActionHom (X ^ n - X - 1 : ℚ[X]) ℂ) := by
  let f : ℚ[X] := X ^ n - X - 1
  change Function.Bijective (Gal.galActionHom f ℂ)
  have : MulAction.IsPretransitive f.Gal (f.rootSet ℂ)
  · rcases eq_or_ne n 1 with rfl | hn
    · have : IsEmpty (rootSet f ℂ) := by simp
      infer_instance
    exact Gal.galAction_isPretransitive _ _ (X_pow_sub_X_sub_one_irreducible_rat hn)
  let K := f.SplittingField
  let R := 𝓞 K
  let S0 : Set f.Gal := ⋃ (q : Ideal R) (hq : q.IsMaximal), {σ | ∀ x : R, res σ x - x ∈ q}
  let S : Set f.Gal := S0 \ {1}
  have hS0 : Subgroup.closure S0 = ⊤
  · sorry
  have hS1 : Subgroup.closure S = ⊤
  · have h : Subgroup.closure (S0 ∩ {1}) = ⊥
    · rw [eq_bot_iff, ← Subgroup.closure_singleton_one]
      exact Subgroup.closure_mono (Set.inter_subset_right S0 {1})
    rw [← hS0, ← Set.diff_union_inter S0 {1}, Subgroup.closure_union, h, sup_bot_eq]
  have hS2 : ∀ σ ∈ S, Perm.IsSwap (MulAction.toPermHom f.Gal (f.rootSet ℂ) σ)
  · rintro σ ⟨hσ, hσ1 : σ ≠ 1⟩
    rw [Set.mem_iUnion] at hσ
    obtain ⟨q, hσ⟩ := hσ
    rw [Set.mem_iUnion] at hσ
    obtain ⟨hq, hσ : ∀ x : R, res σ x - x ∈ q⟩ := hσ
    let F := R ⧸ q
    let π : R →+* F := Ideal.Quotient.mk q
    have : Field F := Ideal.Quotient.field q
    -- finite field, might not need to consider the characteristic
    -- reduce to action on roots in R
    sorry
  exact ⟨Gal.galActionHom_injective f ℂ, surjective_of_isSwap_of_isPretransitive S hS2 hS1⟩

  -- have : ∀ p : Nat.Primes, ∀ q : factors (map (algebraMap ℤ R) p)
  -- roots lie in the ring of integers OK
  -- if q is a prime idea of OK, then there is a ring homomorphism to the finite field OK/q
  -- the whole Galois group acts on OK
  -- the decomposition group acts on OK/q
  -- the inertia group acts trivially on OK/q
  --
  -- there are n roots in OK
  -- there are n or n-1 roots in OK/q (possible double root)
  -- Let σ(x) = x (mod p) for all x in OK
  -- If there are n roots in OK/q, then σ must act trivially on the roots in OK
  -- If x and y collapse (mod p), then maybe σ swaps x and y, but no more
  -- Now run through p's and σ's

  -- the key is proving closure/generating
  -- we need to know that if a subgroup contains every σ(x) = x (mod p) for every p, then it's ⊤
  -- we need to know that if a subfield is fixed by ..., then it's ⊥
  -- key facts from algebraic number theory: p divides discriminant implies ramified
  -- ramified means there exists σ(x) = x (mod p)

#check NumberField.discr_gt_one

end Polynomial
