/-
Copyright (c) 2025 Jiedong Jiang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiedong Jiang
-/

import Mathlib.RingTheory.Perfectoid.Untilt
import Mathlib.RingTheory.WittVector.Complete
import Mathlib.LinearAlgebra.Quotient.Defs
import Mathlib.RingTheory.WittVector.TeichmullerSeries
import Mathlib.RingTheory.AdicCompletion.Basic
import Mathlib.RingTheory.Localization.Away.Basic
import Mathlib.RingTheory.AdicCompletion.Algebra


/-!
# Fontaine's θ map
In this file, we define Fontaine's `θ` map, which is a ring
homomorphism from the Witt vector `𝕎(A^♭)` of the tilt of a perfectoid ring `O`
to `O` itself. Our definition of `θ` does not require that `O` is perfectoid in the first place.
We only need `O` to be `p`-adically complete.

## Main definitions
* `fontaineTheta` : Fontaine's θ map, which is a ring homomorphism from `𝕎(O^♭)` to `O`.
* `BDeRhamPlus` : The period ring `B_{dR}^+`.

## Main theorems
* `fontaineTheta_surjective` : Fontaine's θ map is surjective.

## Tags
Fontaine's theta map, period rings, perfectoid theory, p-adic Hodge theory

## TODO
Currently, the period ring `B_{dR}^+` takes the ring of integers `O` as the input.
After the perfectoid theory is developed, we should modify it to
take a perfectoid field as the input.
-/

universe u

open Ideal Quotient PreTilt WittVector
noncomputable section

variable {O : Type u} [CommRing O] {p : ℕ} [Fact (Nat.Prime p)]
    [Fact ¬IsUnit (p : O)] [IsAdicComplete (span {(p : O)}) O]

local notation A "^♭" => PreTilt A p
local notation "𝕎" => WittVector p

/-!
## θ as a ring homomorphism
In this section, we first define the ring homomorphism
`fontaineThetaModPPow : 𝕎 (O^♭) →+* O ⧸ span {(p : O)} ^ (n + 1)`.
Then we show they are compatible with each other and lift to a
ring homomorphism `fontaineTheta : 𝕎 (O^♭) →+* O`.

To prove this, we prove that `fontaineThetaFun` mod `p^n` is a ring homomorphism by
decompose it as a composition of several ring homomorphisms as below.
`𝕎(O^♭) --𝕎(Frob^-n)->  𝕎(O^♭) --𝕎(coeff 0)-> 𝕎(O/p) --gh_n-> O/p^(n+1)`
Here, the ring map `gh_n` fits in the following diagram.

```
𝕎(A)  --ghost_n->   A
|                   |
v                   v
𝕎(A/p) --gh_n-> A/p^(n+1)
```

-/

namespace WittVector

-- this file

variable (n : ℕ)

omit [Fact ¬IsUnit (p : O)] [IsAdicComplete (span {(p : O)}) O] in
theorem ker_map_le_ker_mk_comp_ghostComponent (n : ℕ) :
    RingHom.ker (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)}) ≤
    RingHom.ker (((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n)) := by
  intro x
  simp only [RingHom.mem_ker, map_eq_zero_iff, RingHom.comp_apply]
  intro h
  simp only [ghostComponent]
  apply_fun Ideal.quotEquivOfEq (Ideal.span_singleton_pow (p : O) (n + 1))
  simp only [RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply, ghostMap_apply,
    quotEquivOfEq_mk, map_zero]
  simp only [eq_zero_iff_dvd] at h ⊢
  exact pow_dvd_ghostComponent_of_dvd_coeff (fun _ _ ↦ h _)

def ghostComponentModPPow (n : ℕ): 𝕎 (O ⧸ span {(p : O)}) →+* O ⧸ span {(p : O)}^(n + 1) :=
  RingHom.liftOfSurjective (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)})
    (map_surjective _ Ideal.Quotient.mk_surjective)
    ⟨((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
    (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n⟩

end WittVector

-- `Untilt`, after `mk_comp_untilt_eq_coeff_zero`
namespace WittVector
-- @[simp]
-- theorem map_mk_teichmuller_frobeniusEquiv_symm_pow_untilt (n : ℕ) (x : O^♭) :
--     WittVector.map (Ideal.Quotient.mk (span {(p : O)}))
--     (teichmuller p ((((_root_.frobeniusEquiv _ p).symm ^ n) x).untilt)) =
--     (teichmuller p (Perfection.coeff (ModP O p) _ n x)) := by
--   simp only [RingAut.coe_pow, map_teichmuller, mk_untilt_eq_coeff_zero,
--     coeff_iterate_frobeniusEquiv_symm, zero_add]

open RingHom

@[simp]
theorem ghostComponentModPPow_teichmuller_coeff (n : ℕ) (x : O^♭) :
    ghostComponentModPPow n (teichmuller p (Perfection.coeff (ModP O p) _ n x)) =
    Ideal.Quotient.mk (span {(p : O)} ^ (n + 1)) x.untilt := by
  simpa [ghostComponentModPPow] using RingHom.liftOfSurjective_comp_apply
      (WittVector.map <| Ideal.Quotient.mk <| span {(p : O)})
      (map_surjective _ Ideal.Quotient.mk_surjective)
      ⟨((Ideal.Quotient.mk <| span {(p : O)} ^ (n + 1))).comp
      (WittVector.ghostComponent (p := p) n), ker_map_le_ker_mk_comp_ghostComponent n⟩
      (teichmuller p ((((_root_.frobeniusEquiv _ p).symm ^ n) x).untilt))

variable (O p) in
def fontaineThetaModPPow (n : ℕ): 𝕎 (O^♭) →+* O ⧸ span {(p : O)} ^ (n + 1) :=
  (ghostComponentModPPow n).comp
      (((WittVector.map (Perfection.coeff _ p 0))).comp
          (WittVector.map ((frobeniusEquiv (O^♭) p).symm ^ n : O^♭ →+* O^♭)))

@[simp]
theorem fontaineThetaModPPow_teichmuller (n : ℕ) (x : O^♭) :
    fontaineThetaModPPow O p n (teichmuller p x) = Ideal.Quotient.mk _ x.untilt := by
  simp [fontaineThetaModPPow, PreTilt]

theorem factorPowSucc_comp_fontaineThetaModPPow (n : ℕ) :
    (factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow O p (n + 1)) =
    fontaineThetaModPPow O p n:= by
  apply eq_of_apply_teichmuller_eq
      ((factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow O p (n + 1)))
      (fontaineThetaModPPow O p n)
  · use n + 1
    have : (p : (O ⧸ span {(p : O)} ^ (n + 1))) = mk (span {(p : O)} ^ (n + 1)) (p : O) := by
      simp only [map_natCast]
    rw [this, ← map_pow, Ideal.Quotient.eq_zero_iff_mem]
    exact Ideal.pow_mem_pow (mem_span_singleton_self _) _
  intro (x : Ring.Perfection (ModP O p) p)
  simp [PreTilt, fontaineThetaModPPow]

theorem factorPowSucc_fontaineThetaModPPow_eq (n : ℕ) (x : 𝕎 (O^♭)) :
    (factorPowSucc _ (n + 1)).comp (fontaineThetaModPPow O p (n + 1)) x =
    fontaineThetaModPPow O p n x:= by
  rw [← factorPowSucc_comp_fontaineThetaModPPow n]

def fontaineTheta : 𝕎 (O^♭) →+* O :=
  IsAdicComplete.limRingHom Order.succ_strictMono (factorPowSucc_fontaineThetaModPPow_eq _ _).symm

theorem mk_pow_fontaineTheta (n : ℕ) (x : 𝕎 (O^♭)) :
    Ideal.Quotient.mk (span {(p : O)} ^ (n + 1)) (fontaineTheta x) = fontaineThetaModPPow O p n x :=
  IsAdicComplete.mk_limRingHom Order.succ_strictMono
      (factorPowSucc_fontaineThetaModPPow_eq _ _).symm n x

-- move this lemma
omit [Fact (Nat.Prime p)]
  [Fact ¬IsUnit (p : O)]
  [IsAdicComplete (span {(p : O)}) O] in
private theorem _root_.Ideal.span_pow_zero_add_one : span {(p : O)} ^ (0 + 1) = span {(p : O)} := by
  simp

omit
  [Fact ¬IsUnit (p : O)]
  [IsAdicComplete (span {(p : O)}) O] in
@[simp]
theorem quotEquivOfEq_ghostComponentModPPow (x : 𝕎 (O ⧸ span {(p : O)})) :
    quotEquivOfEq (Ideal.span_pow_zero_add_one) (ghostComponentModPPow 0 x) =
    ghostComponent 0 x := by
  simp only [Nat.reduceAdd, ghostComponentModPPow, RingHom.liftOfSurjective]
  have surj : Function.Surjective (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : O)}))) :=
    map_surjective _ Ideal.Quotient.mk_surjective
  choose y hy using surj x
  have := RingHom.liftOfSurjective_comp_apply
      (WittVector.map (p := p) (Ideal.Quotient.mk (span {(p : O)})))
      surj ⟨((Ideal.Quotient.mk <| span {(p : O)} ^ (0 + 1))).comp
      (WittVector.ghostComponent (p := p) 0), ker_map_le_ker_mk_comp_ghostComponent 0⟩ y
  rw [hy] at this
  rw [this]
  simp [← hy, ghostComponent_apply]

theorem mk_fontaineTheta (x : 𝕎 (O^♭)) :
    Ideal.Quotient.mk (span {(p : O)}) (fontaineTheta x) =
    Perfection.coeff (ModP O p) _ 0 (x.coeff 0) := by
  have := mk_pow_fontaineTheta 0 x
  simp only [Nat.reduceAdd] at this
  apply_fun Ideal.quotEquivOfEq (pow_one (p : O) ▸ Ideal.span_singleton_pow (p : O) 1) at this
  simp only [quotEquivOfEq_mk] at this
  rw [this]
  simp only [fontaineThetaModPPow, Nat.reduceAdd, pow_zero, RingHom.one_def, WittVector.map_id,
    RingHomCompTriple.comp_eq, RingHom.coe_comp, Function.comp_apply,
    quotEquivOfEq_ghostComponentModPPow, ghostComponent_apply, wittPolynomial_zero,
    MvPolynomial.aeval_X]
  -- Note to reviewers : This line was designed to be `simp [fontaineThetaModPPow]`
  -- However, `WittVector.map_coeff` should, but does not work here.
  rfl

-- theorem modPPow

-- Teichmuller lifts

theorem fontaineTheta_teichmuller (x : O^♭) : fontaineTheta (teichmuller p x) = x.untilt := by
  rw [IsHausdorff.eq_iff_smodEq' (I := span {(p : O)})]
  intro n
  cases n
  · simp
  · simp [SModEq, mk_pow_fontaineTheta]

end WittVector


-- theorem fontaineTheta_p : fontaineTheta (p : 𝕎 (O^♭)) = p := by simp

-- AdicComplete
theorem IsAdicComplete.surjective_of_surjective_mkQ_comp {R M N: Type*} [CommRing R]
    [AddCommGroup M] [AddCommGroup N] [Module R M]
    [Module R N] (I : Ideal R) [IsAdicComplete I M] [IsHausdorff I N] (f : M →ₗ[R] N)
    (hf : Function.Surjective ((Submodule.mkQ (I • ⊤ : Submodule R N)).comp f)) :
    Function.Surjective f := sorry

-- this is a lemma from #20431 by Andrew Yang
section

variable {R S : Type*} [CommRing R] [CommRing S] {I : Ideal R} {J : Ideal S}

variable (M : Type*) [AddCommGroup M] [Module R M] [Module S M]

lemma SModEq.of_toAddSubgroup_le {U : Submodule R M} {V : Submodule S M}
    (h : U.toAddSubgroup ≤ V.toAddSubgroup) {x y : M} (hxy : x ≡ y [SMOD U]) : x ≡ y [SMOD V] := by
  simp only [SModEq, Submodule.Quotient.eq] at hxy ⊢
  exact h hxy

-- `Mathlib.Algebra.Module.Submodule.Basic` after `Submodule.toAddSubgroup_mono`
@[simp]
theorem Submodule.toAddSubgroup_toAddSubmonoid {R : Type*}  {M : Type*}  [Ring R]
    [AddCommGroup M] {module_M : Module R M}
    (p : Submodule R M) : p.toAddSubgroup.toAddSubmonoid = p.toAddSubmonoid := by
  ext
  simp

-- -- `Mathlib.Algebra.Group.Submonoid.Pointwise` after `AddSubmonoid.smul_iSup`
-- theorem foo {R S A: Type} [AddMonoid A] [CommSemiring R] [Semiring S] [DistribSMul R A]
-- [DistribSMul S A] [Algebra R S] [IsScalarTower R S A] (hIJ : I.map f ≤ J)
-- (p : AddSubmonoid M) :
-- I.toAddSubmonoid • p ≤ J • p := sorry

-- Note: after #20431 this lemma should be moved to the file `RingTheory.AdicCompletion.Mono`,
-- after `IsHausdorff.mono`
variable [Algebra R S]  [IsScalarTower R S M] (hIJ : I.map (algebraMap R S) ≤ J)

include hIJ in
lemma IsHausdorff.map [IsHausdorff J M] : IsHausdorff I M := by
  refine ⟨fun x h ↦ IsHausdorff.haus ‹_› x fun n ↦ ?_⟩ -- fun n ↦ ((h n).of_toAddSubgroup_le ?_)
  apply SModEq.of_toAddSubgroup_le
      (U := (I ^ n • ⊤ : Submodule R M)) (V := (J ^ n • ⊤ : Submodule S M))
  · -- show (I ^ n • ⊤ : Submodule R M).toAddSubmonoid ≤ (J ^ n • ⊤ : Submodule S M).toAddSubmonoid
    rw [← AddSubgroup.toAddSubmonoid_le]
    simp only [Submodule.toAddSubgroup_toAddSubmonoid, Submodule.smul_toAddSubmonoid,
      Submodule.top_toAddSubmonoid]
    rw [AddSubmonoid.smul_le]
    intro r hr m _
    rw [← algebraMap_smul S r m]
    apply AddSubmonoid.smul_mem_smul
    · have := Ideal.mem_map_of_mem (algebraMap R S) hr
      simp only [Ideal.map_pow] at this
      apply Ideal.pow_right_mono (I :=  I.map (algebraMap R S)) hIJ n this
    · trivial
  · exact h n
end

theorem surjective_fontaineTheta : Function.Surjective (fontaineTheta : 𝕎 (O^♭) → O) := by
  let I := span {(p : 𝕎 (O^♭))}
  haveI : IsAdicComplete I (𝕎 (O^♭)) := inferInstance
  letI : Algebra (𝕎 (O^♭)) O := RingHom.toAlgebra fontaineTheta
  haveI : IsHausdorff I O := sorry
  let f : 𝕎 (O^♭) →ₐ[𝕎 (O^♭)] O := Algebra.ofId _ _
  have : ⇑f.toLinearMap = ⇑fontaineTheta := rfl
  -- have : (I • ⊤).mkQ ∘ₗ f.toLinearMap = (I • ⊤).mkQ
  rw [← this]
  apply IsAdicComplete.surjective_of_surjective_mkQ_comp I f.toLinearMap
  intro x
  simp? [f, Algebra.ofId_apply, RingHom.algebraMap_toAlgebra]


def fontaineThetaInvertP :
    Localization.Away (M := 𝕎 (O^♭)) (p : 𝕎 (O^♭)) →+* Localization.Away (p : O) :=
  Localization.awayLift ((algebraMap O _).comp fontaineTheta) (p : 𝕎 (O^♭))
      (by simpa using IsLocalization.Away.algebraMap_isUnit (p : O))

section PeriodRing

variable (R : Type*) [CommRing R] (f : R)
#synth CommRing (Localization.Away (M := 𝕎 (O^♭)) (p : 𝕎 (O^♭)))

-- import Mathlib.RingTheory.Localization.Away.Basic
#check Localization.awayLift
variable (O p) in
def BDeRhamPlus : Type u :=
  AdicCompletion (R := (Localization.Away (M := 𝕎 (O^♭)) (p : 𝕎 (O^♭))))
      (RingHom.ker fontaineThetaInvertP) (Localization.Away (M := 𝕎 (O^♭)) (p : 𝕎 (O^♭)))

-- Mathlib.RingTheory.AdicCompletion.Algebra
instance : CommRing (BDeRhamPlus O p) := AdicCompletion.instCommRing _

end PeriodRing
def BDeRham : Type u := FractionRing (BDeRhamPlus O p)
notation "𝔹_dR^+(" O ")" => BDeRhamPlus O

end

-- lemmas about frobenius and untilt another PR
-- fontaine theta and Bdr PR
-- surjective to another PR
