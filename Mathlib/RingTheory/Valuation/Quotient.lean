/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Johan Commelin, Patrick Massot
-/
import Mathlib.RingTheory.Valuation.Basic
import Mathlib.RingTheory.Ideal.QuotientOperations

#align_import ring_theory.valuation.quotient from "leanprover-community/mathlib"@"da420a8c6dd5bdfb85c4ced85c34388f633bc6ff"

/-!
# The valuation on a quotient ring

The support of a valuation `v : Valuation R Œì‚ÇÄ` is `supp v`. If `J` is an ideal of `R`
with `h : J ‚äÜ supp v` then the induced valuation
on `R / J` = `Ideal.Quotient J` is `onQuot v h`.

-/


namespace Valuation

variable {R Œì‚ÇÄ : Type*} [CommRing R] [LinearOrderedCommMonoidWithZero Œì‚ÇÄ]

variable (v : Valuation R Œì‚ÇÄ)

/-- If `hJ : J ‚äÜ supp v` then `onQuotVal hJ` is the induced function on `R / J` as a function.
Note: it's just the function; the valuation is `onQuot hJ`. -/
def onQuotVal {J : Ideal R} (hJ : J ‚â§ supp v) : R ‚ß∏ J ‚Üí Œì‚ÇÄ := fun q =>
  Quotient.liftOn' q v fun a b h =>
    calc
      v a = v (b + -(-a + b)) := by simp
                                    -- üéâ no goals
      _ = v b :=
        v.map_add_supp b <| (Ideal.neg_mem_iff _).2 <| hJ <| QuotientAddGroup.leftRel_apply.mp h
#align valuation.on_quot_val Valuation.onQuotVal

/-- The extension of valuation `v` on `R` to valuation on `R / J` if `J ‚äÜ supp v`. -/
def onQuot {J : Ideal R} (hJ : J ‚â§ supp v) : Valuation (R ‚ß∏ J) Œì‚ÇÄ where
  toFun := v.onQuotVal hJ
  map_zero' := v.map_zero
  map_one' := v.map_one
  map_mul' xbar ybar := Quotient.ind‚ÇÇ' v.map_mul xbar ybar
  map_add_le_max' xbar ybar := Quotient.ind‚ÇÇ' v.map_add xbar ybar
#align valuation.on_quot Valuation.onQuot

@[simp]
theorem onQuot_comap_eq {J : Ideal R} (hJ : J ‚â§ supp v) :
    (v.onQuot hJ).comap (Ideal.Quotient.mk J) = v :=
  ext fun _ => rfl
#align valuation.on_quot_comap_eq Valuation.onQuot_comap_eq

theorem self_le_supp_comap (J : Ideal R) (v : Valuation (R ‚ß∏ J) Œì‚ÇÄ) :
    J ‚â§ (v.comap (Ideal.Quotient.mk J)).supp := by
  rw [comap_supp, ‚Üê Ideal.map_le_iff_le_comap]
  -- ‚ä¢ Ideal.map (Ideal.Quotient.mk J) J ‚â§ supp v
  simp
  -- üéâ no goals
#align valuation.self_le_supp_comap Valuation.self_le_supp_comap

@[simp]
theorem comap_onQuot_eq (J : Ideal R) (v : Valuation (R ‚ß∏ J) Œì‚ÇÄ) :
    (v.comap (Ideal.Quotient.mk J)).onQuot (v.self_le_supp_comap J) = v :=
  ext <| by
    rintro ‚ü®x‚ü©
    -- ‚ä¢ ‚Üë(onQuot (comap (Ideal.Quotient.mk J) v) (_ : J ‚â§ supp (comap (Ideal.Quotien ‚Ä¶
    rfl
    -- üéâ no goals
#align valuation.comap_on_quot_eq Valuation.comap_onQuot_eq

/-- The quotient valuation on `R / J` has support `(supp v) / J` if `J ‚äÜ supp v`. -/
theorem supp_quot {J : Ideal R} (hJ : J ‚â§ supp v) :
    supp (v.onQuot hJ) = (supp v).map (Ideal.Quotient.mk J) := by
  apply le_antisymm
  -- ‚ä¢ supp (onQuot v hJ) ‚â§ Ideal.map (Ideal.Quotient.mk J) (supp v)
  ¬∑ rintro ‚ü®x‚ü© hx
    -- ‚ä¢ Quot.mk Setoid.r x ‚àà Ideal.map (Ideal.Quotient.mk J) (supp v)
    apply Ideal.subset_span
    -- ‚ä¢ Quot.mk Setoid.r x ‚àà ‚Üë(Ideal.Quotient.mk J) '' ‚Üë(supp v)
    exact ‚ü®x, hx, rfl‚ü©
    -- üéâ no goals
  ¬∑ rw [Ideal.map_le_iff_le_comap]
    -- ‚ä¢ supp v ‚â§ Ideal.comap (Ideal.Quotient.mk J) (supp (onQuot v hJ))
    intro x hx
    -- ‚ä¢ x ‚àà Ideal.comap (Ideal.Quotient.mk J) (supp (onQuot v hJ))
    exact hx
    -- üéâ no goals
#align valuation.supp_quot Valuation.supp_quot

theorem supp_quot_supp : supp (v.onQuot le_rfl) = 0 := by
  rw [supp_quot]
  -- ‚ä¢ Ideal.map (Ideal.Quotient.mk (supp v)) (supp v) = 0
  exact Ideal.map_quotient_self _
  -- üéâ no goals
#align valuation.supp_quot_supp Valuation.supp_quot_supp

end Valuation

namespace AddValuation

variable {R Œì‚ÇÄ : Type*}

variable [CommRing R] [LinearOrderedAddCommMonoidWithTop Œì‚ÇÄ]

variable (v : AddValuation R Œì‚ÇÄ)

-- attribute [local reducible] AddValuation -- Porting note: reducible not supported

/-- If `hJ : J ‚äÜ supp v` then `onQuotVal hJ` is the induced function on `R / J` as a function.
Note: it's just the function; the valuation is `onQuot hJ`. -/
def onQuotVal {J : Ideal R} (hJ : J ‚â§ supp v) : R ‚ß∏ J ‚Üí Œì‚ÇÄ :=
  Valuation.onQuotVal v hJ
#align add_valuation.on_quot_val AddValuation.onQuotVal

/-- The extension of valuation `v` on `R` to valuation on `R / J` if `J ‚äÜ supp v`. -/
def onQuot {J : Ideal R} (hJ : J ‚â§ supp v) : AddValuation (R ‚ß∏ J) Œì‚ÇÄ :=
  Valuation.onQuot v hJ
#align add_valuation.on_quot AddValuation.onQuot

@[simp]
theorem onQuot_comap_eq {J : Ideal R} (hJ : J ‚â§ supp v) :
    (v.onQuot hJ).comap (Ideal.Quotient.mk J) = v :=
  Valuation.onQuot_comap_eq v hJ
#align add_valuation.on_quot_comap_eq AddValuation.onQuot_comap_eq

theorem comap_supp {S : Type*} [CommRing S] (f : S ‚Üí+* R) :
    supp (v.comap f) = Ideal.comap f v.supp :=
  Valuation.comap_supp v f
#align add_valuation.comap_supp AddValuation.comap_supp

theorem self_le_supp_comap (J : Ideal R) (v : AddValuation (R ‚ß∏ J) Œì‚ÇÄ) :
    J ‚â§ (v.comap (Ideal.Quotient.mk J)).supp :=
  Valuation.self_le_supp_comap J v
#align add_valuation.self_le_supp_comap AddValuation.self_le_supp_comap

@[simp]
theorem comap_onQuot_eq (J : Ideal R) (v : AddValuation (R ‚ß∏ J) Œì‚ÇÄ) :
    (v.comap (Ideal.Quotient.mk J)).onQuot (v.self_le_supp_comap J) = v :=
  Valuation.comap_onQuot_eq J v
#align add_valuation.comap_on_quot_eq AddValuation.comap_onQuot_eq

/-- The quotient valuation on `R / J` has support `(supp v) / J` if `J ‚äÜ supp v`. -/
theorem supp_quot {J : Ideal R} (hJ : J ‚â§ supp v) :
    supp (v.onQuot hJ) = (supp v).map (Ideal.Quotient.mk J) :=
  Valuation.supp_quot v hJ
#align add_valuation.supp_quot AddValuation.supp_quot

theorem supp_quot_supp : supp ((Valuation.onQuot v) le_rfl) = 0 :=
  Valuation.supp_quot_supp v
#align add_valuation.supp_quot_supp AddValuation.supp_quot_supp

end AddValuation
