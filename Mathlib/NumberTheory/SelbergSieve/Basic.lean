/-
Copyright (c) 2023 Arend Mellendijk. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Arend Mellendijk
-/
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.NumberTheory.SelbergSieve.Temp

/-!
# The Selberg Sieve

We set up the working assumptions of the Selberg sieve and define the notion of an upper bound sieve
and show that every upper bound sieve yields an upper bound on the size of the sifted set. We also
define the Œõ¬≤ sieve and prove that Œõ¬≤ sieves are upper bound sieves. We then diagonalise the main
term of the Œõ¬≤ sieve.

We mostly follow the treatment outlined by Heath-Brown in the notes to an old graduate course. One
minor notational difference is that we write $\nu(n)$ in place of $\frac{\omega(n)}{n}$.

## Results

 * `siftedSum_le_mainSum_errSum_of_UpperBoundSieve` - Every upper bound sieve gives an upper bound
 on the size of the sifted set in terms of `mainSum` and `errSum`
 * `upperMoebius_of_lambda_sq` - Lambda squared weights produce upper bound sieves
 * `lambdaSquared_mainSum_eq_diag_quad_form` - The main sum of a Œõ¬≤ sieve has a nice diagonalisation

## Notation
The `SelbergSieve.Notation` namespace includes common shorthand for the variables included in the
`SelbergSieve` structure.
 * `A` for `support`
 * `ùíú d` for `multSum d`
 * `P` for `prodPrimes`
 * `a` for `weights`
 * `X` for `totalMass`
 * `ŒΩ` for `nu`
 * `y` for `level`
 * `R d` for `rem d`
 * `g d` for `selbergTerms d`

## References

 * [Heath-Brown, *Lectures on sieves*][heathbrown2002lecturessieves]
 * [Koukoulopoulos, *The Distribution of Prime Numbers*][MR3971232]

-/

noncomputable section

open scoped BigOperators ArithmeticFunction

open Finset Real Nat

/--
We set up the Selberg sieve as follows. Take a finite set of natural numbers `A`, whose elements
are weighted by a sequence `a n`. Also take a finite set of primes `P`, represented as a squarefree
natural number. These are the primes that we will sift from our set `A`. Suppose we can approximate
`‚àë n ‚àà {k ‚àà A | d ‚à£ k}, a n = ŒΩ d * X + R d`, where `X` is an approximation to the total size of `A`
and `ŒΩ` is a multiplicative arithmetic function such that `0 < ŒΩ p < 1` for all primes `p ‚à£ P`.

Then the fundamental theorem of the Selberg sieve will give us an upper bound on the size of the
sifted sum `‚àë n ‚àà {k ‚àà support | k.Coprime P}, a n`, obtained by removing any elements of `A` that
are a multiple of a prime in `P`.
-/
class SelbergSieve where
  support : Finset ‚Ñï
  prodPrimes : ‚Ñï
  prodPrimes_squarefree : Squarefree prodPrimes
  weights : ‚Ñï ‚Üí ‚Ñù
  weights_nonneg : ‚àÄ n : ‚Ñï, 0 ‚â§ weights n
  totalMass : ‚Ñù
  nu : ArithmeticFunction ‚Ñù
  nu_mult : nu.IsMultiplicative
  nu_pos_of_prime : ‚àÄ p : ‚Ñï, p.Prime ‚Üí p ‚à£ prodPrimes ‚Üí 0 < nu p
  nu_lt_one_of_prime : ‚àÄ p : ‚Ñï, p.Prime ‚Üí p ‚à£ prodPrimes ‚Üí nu p < 1
  level : ‚Ñù
  one_le_level : 1 ‚â§ level

attribute [arith_mult] SelbergSieve.nu_mult

namespace SelbergSieve

namespace Notation

scoped notation3 "ŒΩ" => nu
scoped notation3 "P" => prodPrimes
scoped notation3 "a" => weights
scoped notation3 "X" => totalMass
scoped notation3 "A" => support
scoped notation3 "y" => level
scoped notation3 "hy" => one_le_level

end Notation

open Notation

section SelbergSieve

variable [s : SelbergSieve]

@[simp]
def multSum (d : ‚Ñï) : ‚Ñù :=
  ‚àë n ‚àà A, if d ‚à£ n then a n else 0

scoped notation3 "ùíú" => multSum

-- A_d = ŒΩ (d)/d X + R_d
@[simp]
def rem (d : ‚Ñï) : ‚Ñù :=
  ùíú d - ŒΩ d * X

scoped notation3 "R" => rem

def siftedSum : ‚Ñù :=
  ‚àë d ‚àà A with Coprime P d, a d

/--
These are the terms that appear in the sum `S` in the main term of the fundamental theorem.

$S = ‚àë_{l|P, l‚â§\sqrt{y}} g(l)$
-/
def selbergTerms : ArithmeticFunction ‚Ñù :=
  nu.pmul (.prodPrimeFactors fun p =>  1 / (1 - ŒΩ p))

scoped notation3 "g" => selbergTerms

theorem selbergTerms_apply (d : ‚Ñï) :
    g d = ŒΩ d * ‚àè p ‚àà d.primeFactors, 1 / (1 - ŒΩ p) := by
  unfold selbergTerms
  by_cases h : d=0
  ¬∑ rw [h]; simp
  rw [ArithmeticFunction.pmul_apply, ArithmeticFunction.prodPrimeFactors_apply h]

def mainSum (ŒºPlus : ‚Ñï ‚Üí ‚Ñù) : ‚Ñù :=
  ‚àë d ‚àà divisors P, ŒºPlus d * ŒΩ d

def errSum (ŒºPlus : ‚Ñï ‚Üí ‚Ñù) : ‚Ñù :=
  ‚àë d ‚àà divisors P, |ŒºPlus d| * |R d|

end SelbergSieve

section UpperBoundSieve

def UpperMoebius (Œº_plus : ‚Ñï ‚Üí ‚Ñù) : Prop :=
  ‚àÄ n : ‚Ñï, (if n=1 then 1 else 0) ‚â§ ‚àë d ‚àà n.divisors, Œº_plus d

structure UpperBoundSieve where mk ::
  ŒºPlus : ‚Ñï ‚Üí ‚Ñù
  hŒºPlus : UpperMoebius ŒºPlus

instance ubToŒºPlus : CoeFun UpperBoundSieve fun _ => ‚Ñï ‚Üí ‚Ñù where coe ub := ub.ŒºPlus

end UpperBoundSieve

section SieveLemmas

variable [s : SelbergSieve]

theorem prodPrimes_ne_zero : P ‚â† 0 :=
  Squarefree.ne_zero prodPrimes_squarefree

theorem squarefree_of_dvd_prodPrimes {d : ‚Ñï} (hd : d ‚à£ P) : Squarefree d :=
  Squarefree.squarefree_of_dvd hd prodPrimes_squarefree

theorem squarefree_of_mem_divisors_prodPrimes {d : ‚Ñï} (hd : d ‚àà divisors P) : Squarefree d := by
  simp only [Nat.mem_divisors] at hd
  exact Squarefree.squarefree_of_dvd hd.left prodPrimes_squarefree

theorem prod_primeFactors_nu {d : ‚Ñï} (hd : d ‚à£ P) : ‚àè p ‚àà d.primeFactors, ŒΩ p = ŒΩ d := by
  rw [‚Üê nu_mult.map_prod_of_subset_primeFactors _ _ subset_rfl,
    Nat.prod_primeFactors_of_squarefree <| Squarefree.squarefree_of_dvd hd prodPrimes_squarefree]

theorem nu_pos_of_dvd_prodPrimes {d : ‚Ñï} (hd : d ‚à£ P) : 0 < ŒΩ d := by
  calc
    0 < ‚àè p ‚àà d.primeFactors, ŒΩ p := by
      apply prod_pos
      intro p hpd
      have hp_prime : p.Prime := prime_of_mem_primeFactors hpd
      have hp_dvd : p ‚à£ P := (dvd_of_mem_primeFactors hpd).trans hd
      exact nu_pos_of_prime p hp_prime hp_dvd
    _ = ŒΩ d := prod_primeFactors_nu hd

theorem nu_ne_zero {d : ‚Ñï} (hd : d ‚à£ P) : ŒΩ d ‚â† 0 := by
  apply _root_.ne_of_gt
  exact nu_pos_of_dvd_prodPrimes hd

theorem nu_ne_zero_of_mem_divisors_prodPrimes {d : ‚Ñï} (hd : d ‚àà divisors P) : ŒΩ d ‚â† 0 := by
  apply _root_.ne_of_gt
  rw [mem_divisors] at hd
  apply nu_pos_of_dvd_prodPrimes hd.left

theorem multSum_eq_main_err (d : ‚Ñï) : multSum d = ŒΩ d * X + R d := by
  dsimp [rem]
  ring

theorem siftedSum_as_delta : siftedSum = ‚àë d ‚àà support, a d * if Nat.gcd P d = 1 then 1 else 0 :=
  by
  dsimp only [siftedSum]
  simp_rw [mul_ite, mul_one, mul_zero, sum_filter]

theorem nu_lt_self_of_dvd_prodPrimes (d : ‚Ñï) (hdP : d ‚à£ P) (hd_ne_one : d ‚â† 1) : ŒΩ d < 1 := by
  have hd_sq : Squarefree d := Squarefree.squarefree_of_dvd hdP prodPrimes_squarefree
  have := hd_sq.ne_zero
  calc
    ŒΩ d = ‚àè p ‚àà d.primeFactors, ŒΩ p := (prod_primeFactors_nu hdP).symm
    _ < ‚àè p ‚àà d.primeFactors, 1 := by
      apply prod_lt_prod_of_nonempty
      ¬∑ intro p hp
        simp only [mem_primeFactors] at hp
        apply nu_pos_of_prime p (by aesop) (hp.2.1.trans hdP)
      ¬∑ intro p hpd; rw [mem_primeFactors_of_ne_zero hd_sq.ne_zero] at hpd
        apply nu_lt_one_of_prime p hpd.left (hpd.2.trans hdP)
      ¬∑ simp only [nonempty_primeFactors, show 1 < d by omega]
    _ = 1 := by
      simp

section SelbergTerms
/-!
Now follow some important identities involving `g`
-/

theorem selbergTerms_pos (l : ‚Ñï) (hl : l ‚à£ P) : 0 < g l := by
  rw [selbergTerms_apply]
  apply mul_pos <| nu_pos_of_dvd_prodPrimes hl
  apply prod_pos
  intro p hp
  rw [one_div_pos]
  have hp_prime : p.Prime := prime_of_mem_primeFactors hp
  have hp_dvd : p ‚à£ P := (Nat.dvd_of_mem_primeFactors hp).trans hl
  linarith only [nu_lt_one_of_prime p hp_prime hp_dvd]

theorem selbergTerms_mult : ArithmeticFunction.IsMultiplicative g := by
  unfold selbergTerms
  arith_mult

theorem one_div_selbergTerms_eq_conv_moebius_nu (l : ‚Ñï) (hl : Squarefree l)
    (hnu_nonzero : ŒΩ l ‚â† 0) : 1 / g l = ‚àë ‚ü®d, e‚ü© ‚àà l.divisorsAntidiagonal, (Œº <| d) * (ŒΩ e)‚Åª¬π :=
  by
  simp only [selbergTerms_apply, one_div, mul_inv, inv_div, inv_inv, Finset.prod_congr,
    Finset.prod_inv_distrib, (nu_mult).prodPrimeFactors_one_sub_of_squarefree _ hl, mul_sum]
  apply symm
  rw [‚Üê Nat.sum_divisorsAntidiagonal fun i _ : ‚Ñï => (ŒΩ l)‚Åª¬π * (‚Üë(Œº i) * ŒΩ i)]
  apply sum_congr rfl; intro ‚ü®d, e‚ü© hd
  simp only [mem_divisorsAntidiagonal, ne_eq] at hd
  obtain ‚ü®rfl, _‚ü© := hd
  have : ŒΩ e ‚â† 0 := by
    revert hnu_nonzero; contrapose!
    exact nu_mult.eq_zero_of_squarefree_of_dvd_eq_zero hl (Nat.dvd_mul_left e d)
  simp only [squarefree_mul_iff] at hl ‚ä¢
  field_simp
  rw [nu_mult.map_mul_of_coprime hl.1, mul_comm (ŒΩ d)]
  ring

theorem nu_eq_conv_one_div_selbergTerms (d : ‚Ñï) (hdP : d ‚à£ P) :
    (ŒΩ d)‚Åª¬π = ‚àë l ‚àà divisors P with l ‚à£ d, 1 / g l := by
  apply symm
  rw [Nat.divisors_filter_dvd_of_dvd prodPrimes_ne_zero hdP]
  have hd_pos : 0 < d := Nat.pos_of_ne_zero <| ne_zero_of_dvd_ne_zero prodPrimes_ne_zero hdP
  revert hdP; revert d
  apply (ArithmeticFunction.sum_eq_iff_sum_mul_moebius_eq_on _ (fun _ _ => Nat.dvd_trans)).mpr
  intro l _ hlP
  apply symm
  exact one_div_selbergTerms_eq_conv_moebius_nu l
    (Squarefree.squarefree_of_dvd hlP prodPrimes_squarefree)
    (ne_of_gt <| nu_pos_of_dvd_prodPrimes hlP)

theorem conv_selbergTerms_eq_selbergTerms_mul_nu {d : ‚Ñï} (hd : d ‚à£ P) :
    (‚àë l ‚àà divisors P with l ‚à£ d, g l) = g d * (ŒΩ d)‚Åª¬π := by
  calc
    (‚àë l ‚àà divisors P with l ‚à£ d, g l) =
        ‚àë l ‚àà divisors P with l ‚à£ d, g (d / l) := by
      rw [divisors_filter_dvd_of_dvd prodPrimes_ne_zero hd,
        ‚Üê Nat.sum_divisorsAntidiagonal fun x _ => g x, Nat.sum_divisorsAntidiagonal' fun x _ => g x,
        sum_over_dvd_ite prodPrimes_ne_zero hd]
    _ = g d * ‚àë l ‚àà divisors P with l ‚à£ d, 1 / g l := by
      simp_rw [mul_sum]; apply sum_congr rfl; intro l hl
      simp only [mem_filter, mem_divisors, ne_eq] at hl
      rw [selbergTerms_mult.map_div_of_coprime hl.2]
      ¬∑ ring
      ¬∑ apply coprime_of_squarefree_mul <|
          (Nat.div_mul_cancel hl.2).symm ‚ñ∏ (squarefree_of_dvd_prodPrimes hd)
      ¬∑ exact (selbergTerms_pos _ hl.1.1).ne.symm
    _ = g d * (ŒΩ d)‚Åª¬π := by rw [‚Üê nu_eq_conv_one_div_selbergTerms d hd]

end SelbergTerms

theorem upper_bound_of_UpperBoundSieve (ŒºPlus : UpperBoundSieve) :
    siftedSum ‚â§ ‚àë d ‚àà divisors P, ŒºPlus d * multSum d := by
  have hŒº : ‚àÄ n, (if n = 1 then 1 else 0) ‚â§ ‚àë d ‚àà n.divisors, ŒºPlus d := ŒºPlus.hŒºPlus
  calc siftedSum ‚â§
    ‚àë n ‚àà support, a n * ‚àë d ‚àà (Nat.gcd P n).divisors, ŒºPlus d := ?caseA
    _ = ‚àë n ‚àà support, ‚àë d ‚àà divisors P with d ‚à£ n, a n * ŒºPlus d := ?caseB
    _ = ‚àë d ‚àà divisors P, ŒºPlus d * multSum d := ?caseC
  case caseA =>
    rw [siftedSum_as_delta]
    apply Finset.sum_le_sum; intro n _
    exact mul_le_mul_of_nonneg_left (hŒº (Nat.gcd P n)) (weights_nonneg n)
  case caseB =>
    simp_rw [mul_sum]
    congr with n
    congr
    ¬∑ rw [‚Üê divisors_filter_dvd_of_dvd prodPrimes_ne_zero (Nat.gcd_dvd_left _ _)]
      ext x; simp +contextual [dvd_gcd_iff]
  case caseC =>
    rw [sum_comm_filter]
    simp_rw [multSum, ‚Üê sum_filter, mul_sum, mul_comm]

theorem siftedSum_le_mainSum_errSum_of_UpperBoundSieve (ŒºPlus : UpperBoundSieve) :
    siftedSum ‚â§ X * mainSum ŒºPlus + errSum ŒºPlus := by
  calc siftedSum ‚â§ ‚àë d ‚àà divisors P, ŒºPlus d * multSum d := by apply upper_bound_of_UpperBoundSieve
   _ ‚â§ X * ‚àë d ‚àà divisors P, ŒºPlus d * ŒΩ d + ‚àë d ‚àà divisors P, ŒºPlus d * R d := ?caseA
   _ ‚â§ _ := ?caseB
  case caseA =>
    apply le_of_eq
    rw [mul_sum, ‚Üêsum_add_distrib]
    congr with d
    dsimp only [rem]; ring
  case caseB =>
    apply _root_.add_le_add (le_rfl)
    apply sum_le_sum; intro d _
    rw [‚Üêabs_mul]
    exact le_abs_self (UpperBoundSieve.ŒºPlus ŒºPlus d * R d)

end SieveLemmas

section LambdaSquared

def lambdaSquared (weights : ‚Ñï ‚Üí ‚Ñù) : ‚Ñï ‚Üí ‚Ñù := fun d =>
  ‚àë d1 ‚àà d.divisors, ‚àë d2 ‚àà d.divisors with d = Nat.lcm d1 d2, weights d1 * weights d2

private theorem lambdaSquared_eq_zero_of_support_wlog {w : ‚Ñï ‚Üí ‚Ñù} {height : ‚Ñù}
  (hw : ‚àÄ (d : ‚Ñï), ¬¨d ^ 2 ‚â§ height ‚Üí w d = 0) {d : ‚Ñï} (hd : ¬¨‚Üëd ‚â§ height) (d1 : ‚Ñï) (d2 : ‚Ñï)
  (h : d = Nat.lcm d1 d2) (hle : d1 ‚â§ d2) :
    w d1 * w d2 = 0 := by
  rw [hw d2]
  ¬∑ ring
  by_contra hyp; apply hd
  apply le_trans _ hyp
  norm_cast
  calc _ ‚â§ (d1.lcm d2) := by rw [h]
      _ ‚â§ (d1*d2) := Nat.div_le_self _ _
      _ ‚â§ _       := ?_
  ¬∑ rw [sq]; gcongr

theorem lambdaSquared_eq_zero_of_not_le_height (w : ‚Ñï ‚Üí ‚Ñù) (height : ‚Ñù)
    (hw : ‚àÄ d : ‚Ñï, ¬¨d ^ 2 ‚â§ height ‚Üí w d = 0) (d : ‚Ñï) (hd : ¬¨d ‚â§ height) :
    lambdaSquared w d = 0 := by
  dsimp only [lambdaSquared]
  by_cases hheight : 0 ‚â§ height
  swap
  ¬∑ push_neg at hd hheight
    have : ‚àÄ d' : ‚Ñï, w d' = 0 := by
      intro d'; apply hw
      have : (0:‚Ñù) ‚â§ (d') ^ 2 := by norm_num
      linarith
    apply sum_eq_zero; intro d1 _
    apply sum_eq_zero; intro d2 _
    rw [this d1, this d2]
    simp only [ite_self, eq_self_iff_true, MulZeroClass.mul_zero]
  apply sum_eq_zero; intro d1 _; apply sum_eq_zero; intro d2 h
  simp only [mem_filter, mem_divisors, ne_eq] at h
  obtain (hle | hle) :=  Nat.le_or_le d1 d2
  ¬∑ apply lambdaSquared_eq_zero_of_support_wlog hw hd d1 d2 h.2 hle
  ¬∑ rw[mul_comm]
    apply lambdaSquared_eq_zero_of_support_wlog hw hd d2 d1 (Nat.lcm_comm d1 d2 ‚ñ∏ h.2) hle

theorem upperMoebius_lambdaSquared (weights : ‚Ñï ‚Üí ‚Ñù) (hw : weights 1 = 1) :
    UpperMoebius <| lambdaSquared weights := by
  dsimp [UpperMoebius, lambdaSquared]
  intro n
  have h_sq :
    (‚àë d ‚àà n.divisors, ‚àë d1 ‚àà d.divisors, ‚àë d2 ‚àà d.divisors with
      d = Nat.lcm d1 d2, weights d1 * weights d2) =
      (‚àë d ‚àà n.divisors, weights d) ^ 2 := by
    rw [sq, mul_sum, conv_lambda_sq_larger_sum _ n, sum_comm]
    apply sum_congr rfl; intro d1 hd1
    rw [sum_mul, sum_comm_filter]
    apply sum_congr rfl; intro d2 hd2
    rw [sum_filter, sum_ite_eq_of_mem']
    ring
    rw [mem_divisors, Nat.lcm_dvd_iff]
    exact ‚ü®‚ü®dvd_of_mem_divisors hd1, dvd_of_mem_divisors hd2‚ü©, (mem_divisors.mp hd1).2‚ü©
  rw [h_sq]
  split_ifs with hn
  ¬∑ rw [hn]; simp [hw]
  ¬∑ apply sq_nonneg

variable [s : SelbergSieve]

theorem lambdaSquared_mainSum_eq_quad_form (w : ‚Ñï ‚Üí ‚Ñù) :
    mainSum (lambdaSquared w) =
      ‚àë d1 ‚àà divisors P, ‚àë d2 ‚àà divisors P,
        ŒΩ d1 * w d1 * ŒΩ d2 * w d2 * (ŒΩ (d1.gcd d2))‚Åª¬π := by
  calc mainSum (lambdaSquared w)
      = ‚àë d ‚àà divisors P, ‚àë d1 ‚àà divisors d, ‚àë d2 ‚àà divisors d with d = d1.lcm d2,
          w d1 * w d2 * ŒΩ d := ?caseA
    _ = ‚àë d ‚àà divisors P, ‚àë d1 ‚àà divisors P, ‚àë d2 ‚àà divisors P with d = d1.lcm d2,
          w d1 * w d2 * ŒΩ d := by apply conv_lambda_sq_larger_sum
    _ = ‚àë d1 ‚àà divisors P, ‚àë d2 ‚àà divisors P,
          ŒΩ d1 * w d1 * ŒΩ d2 * w d2 * (ŒΩ (d1.gcd d2))‚Åª¬π := ?caseB
  case caseA =>
    dsimp only [mainSum, lambdaSquared]
    simp_rw [sum_mul]
  case caseB =>
    rw [sum_comm, sum_congr rfl]; intro d1 hd1
    rw [sum_comm_filter, sum_congr rfl]; intro d2 hd2
    have h : d1.lcm d2 ‚à£ P := Nat.lcm_dvd_iff.mpr ‚ü®dvd_of_mem_divisors hd1, dvd_of_mem_divisors hd2‚ü©
    rw [sum_filter, sum_ite_eq_of_mem' (divisors P) (d1.lcm d2) _
      (mem_divisors.mpr ‚ü®h, prodPrimes_ne_zero‚ü© ), nu_mult.map_lcm]
    ¬∑ ring
    refine _root_.ne_of_gt (nu_pos_of_dvd_prodPrimes ?_)
    trans d1
    ¬∑ exact Nat.gcd_dvd_left d1 d2
    ¬∑ exact dvd_of_mem_divisors hd1

theorem lambdaSquared_mainSum_eq_diag_quad_form  (w : ‚Ñï ‚Üí ‚Ñù) :
    mainSum (lambdaSquared w) =
      ‚àë l ‚àà divisors P,
        1 / g l * (‚àë d ‚àà divisors P with l ‚à£ d, ŒΩ d * w d) ^ 2 := by
  calc mainSum (lambdaSquared w) =
    ‚àë d1 ‚àà divisors P, ‚àë d2 ‚àà divisors P, (‚àë l ‚àà divisors P with l ‚à£ d1.gcd d2,
          1 / g l * (ŒΩ d1 * w d1) * (ŒΩ d2 * w d2)) := ?caseA
    _ = ‚àë l ‚àà divisors P, ‚àë d1 ‚àà divisors P, ‚àë d2 ‚àà divisors P with
        l ‚à£ Nat.gcd d1 d2, 1 / g l * (ŒΩ d1 * w d1) * (ŒΩ d2 * w d2) := ?caseB
    _ = ‚àë l ‚àà divisors P,
        1 / g l * (‚àë d ‚àà divisors P with l ‚à£ d, ŒΩ d * w d) ^ 2 := ?caseC
  case caseA =>
    rw [lambdaSquared_mainSum_eq_quad_form w]
    apply sum_congr rfl; intro d1 hd1; apply sum_congr rfl; intro d2 _
    have hgcd_dvd: d1.gcd d2 ‚à£ P := (Nat.gcd_dvd_left d1 d2).trans (dvd_of_mem_divisors hd1)
    simp_rw [nu_eq_conv_one_div_selbergTerms _ hgcd_dvd, mul_sum]
    congr with l
    ring
  case caseB =>
    apply symm; rw [sum_comm, sum_congr rfl]; intro d1 _; rw[sum_comm_filter];
  case caseC =>
    congr with l
    simp_rw [sq, sum_mul, mul_sum, sum_filter_sum, filter_filter]
    congr with d1
    congr with d2
    ¬∑ rw [dvd_gcd_iff, and_comm]
    ¬∑ ring

end LambdaSquared

end SelbergSieve
