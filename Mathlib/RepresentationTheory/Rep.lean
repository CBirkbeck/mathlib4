/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Category.ModuleCat.Adjunctions
import Mathlib.Algebra.Category.ModuleCat.Limits
import Mathlib.Algebra.Category.ModuleCat.Colimits
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Closed
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Symmetric
import Mathlib.CategoryTheory.Elementwise
import Mathlib.RepresentationTheory.Action.Monoidal
import Mathlib.RepresentationTheory.Basic

/-!
# `Rep k G` is the category of `k`-linear representations of `G`.

If `V : Rep k G`, there is a coercion that allows you to treat `V` as a type,
and this type comes equipped with a `Module k V` instance.
Also `V.œÅ` gives the homomorphism `G ‚Üí* (V ‚Üí‚Çó[k] V)`.

Conversely, given a homomorphism `œÅ : G ‚Üí* (V ‚Üí‚Çó[k] V)`,
you can construct the bundled representation as `Rep.of œÅ`.

We construct the categorical equivalence `Rep k G ‚âå ModuleCat (MonoidAlgebra k G)`.
We verify that `Rep k G` is a `k`-linear abelian symmetric monoidal category with all (co)limits.
-/

suppress_compilation

universe v u w

open CategoryTheory

namespace ModuleCat

theorem hom_def {R : Type u} [Ring R] {X Y : ModuleCat R} : (X ‚ü∂ Y) = (X ‚Üí‚Çó[R] Y) := rfl

end ModuleCat

open CategoryTheory.Limits

/-- The category of `k`-linear representations of a monoid `G`. -/
abbrev Rep (k G : Type u) [Ring k] [Monoid G] :=
  Action (ModuleCat.{u} k) G

instance (k G : Type u) [CommRing k] [Monoid G] : Linear k (Rep k G) := by infer_instance

namespace Rep

variable {k G : Type u} [CommRing k]

section

variable [Monoid G]

instance : CoeSort (Rep k G) (Type u) :=
  ConcreteCategory.hasCoeToSort _

instance (V : Rep k G) : AddCommGroup V := by
  change AddCommGroup ((forget‚ÇÇ (Rep k G) (ModuleCat k)).obj V); infer_instance

instance (V : Rep k G) : Module k V := by
  change Module k ((forget‚ÇÇ (Rep k G) (ModuleCat k)).obj V)
  infer_instance

lemma coe_def {V : Rep k G} : V.V = (V : Type u) := rfl
export Action (œÅ)

/-- Specialize the existing `Action.œÅ`, changing the type to `Representation k G V`.
-/
def œÅ (V : Rep k G) : Representation k G V :=
-- Porting note: was `V.œÅ`
  Action.œÅ V

/-- Lift an unbundled representation to `Rep`. -/
def of {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) : Rep k G :=
  ‚ü®ModuleCat.of k V, œÅ‚ü©

@[simp]
theorem coe_of {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) :
    (of œÅ : Type u) = V :=
  rfl

@[simp]
theorem of_V {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) :
    (of œÅ).V = V :=
  rfl

@[simp]
theorem of_œÅ {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) : (of œÅ).œÅ = œÅ :=
  rfl

def hom {A B : Rep k G} (f : A ‚ü∂ B) : A ‚Üí‚Çó[k] B := f.hom

theorem hom_def {A B : Rep k G} (f : A ‚ü∂ B) : f.hom = hom f := rfl

theorem hom_ext {A B : Rep k G} {f g : A ‚ü∂ B} (h : hom f = hom g) : f = g :=
  Action.hom_ext _ _ h

@[simp]
theorem hom_id (A : Rep k G) : hom (ùüô A) = LinearMap.id := rfl

@[simps]
def mkHom' {A B : Rep k G} (f : A ‚Üí‚Çó[k] B) (h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f) :
    A ‚ü∂ B where
  hom := f
  comm g := h g

@[simp]
theorem mkHom'_hom' {A B : Rep k G} (f : A ‚Üí‚Çó[k] B) {h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f} :
    hom (mkHom' f h) = f := rfl

lemma mkHom_hom' {A B : Rep k G} (f : A ‚Üí‚Çó[k] B) (h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f) :
    @DFunLike.coe (no_index (A ‚Üí‚Çó[k] B)) A (fun _ => B) _
      (mkHom' f h).hom = f := rfl

abbrev mkIso' {A B : Rep k G} (f : A ‚âÉ‚Çó[k] B) (h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f) :
    A ‚âÖ B :=
  Action.mkIso f.toModuleIso h

@[simp]
theorem mkIso'_hom_hom' {A B : Rep k G} (f : A ‚âÉ‚Çó[k] B) {h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f} :
    hom (mkIso' f h).hom = f := rfl

@[simp]
theorem mkIso'_inv_hom' {A B : Rep k G} (f : A ‚âÉ‚Çó[k] B) {h : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f} :
    hom (mkIso' f h).inv = f.symm := rfl

theorem Action_œÅ_eq_œÅ {A : Rep k G} : Action.œÅ A = A.œÅ :=
  rfl

@[simp]
theorem œÅ_inv_self_apply {G : Type u} [Group G] (A : Rep k G) (g : G) (x : A) :
    A.œÅ g‚Åª¬π (A.œÅ g x) = x :=
  show (A.œÅ g‚Åª¬π * A.œÅ g) x = x by rw [‚Üê map_mul, inv_mul_cancel, map_one, LinearMap.one_apply]

@[simp]
theorem œÅ_self_inv_apply {G : Type u} [Group G] {A : Rep k G} (g : G) (x : A) :
    A.œÅ g (A.œÅ g‚Åª¬π x) = x :=
  show (A.œÅ g * A.œÅ g‚Åª¬π) x = x by rw [‚Üê map_mul, mul_inv_cancel, map_one, LinearMap.one_apply]

theorem hom_comm_apply {A B : Rep k G} (f : A ‚ü∂ B) (g : G) (x : A) :
    f.hom (A.œÅ g x) = B.œÅ g (f.hom x) :=
  LinearMap.ext_iff.1 (f.comm g) x

theorem hom_comm_apply' {A B : Rep k G} (f : A ‚ü∂ B) (g : G) (x : A) :
    @DFunLike.coe (no_index A ‚Üí‚Çó[k] B) A (fun _ => no_index B) _ f.hom
      (@DFunLike.coe (no_index A ‚Üí‚Çó[k] A) A (fun _ => no_index A) _ (A.œÅ g) x)
      = B.œÅ g (f.hom x) :=
  LinearMap.ext_iff.1 (f.comm g) x

theorem hom_comm_apply'' {A B : Rep k G} (f : A ‚ü∂ B) (g : G) (x : A) :
    hom f (A.œÅ g x) = B.œÅ g (hom f x) :=
  hom_comm_apply f g x

@[simp]
theorem comp_hom {A B C : Rep k G} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
    hom (f ‚â´ g) = hom g ‚àò‚Çó (hom f) :=
  rfl

@[simp]
theorem zero_hom {A B : Rep k G} : hom (0 : A ‚ü∂ B) = 0 := rfl

@[simp]
theorem add_hom {A B : Rep k G} (f g : A ‚ü∂ B) : hom (f + g) = hom f + hom g := rfl

@[simp]
theorem neg_hom {A B : Rep k G} (f : A ‚ü∂ B) : hom (-f) = -hom f := rfl

@[simp]
theorem sum_hom {A B : Rep k G} {Œπ : Type w} (s : Finset Œπ) (f : Œπ ‚Üí (A ‚ü∂ B)) :
    hom (‚àë i in s, f i) = ‚àë i in s, hom (f i) := Action.sum_hom _ _

@[simp]
theorem smul_hom {A B : Rep k G} (r : k) (f : A ‚ü∂ B) : hom (r ‚Ä¢ f) = r ‚Ä¢ hom f := rfl

variable (k G)

/-- The trivial `k`-linear `G`-representation on a `k`-module `V.` -/
abbrev trivial (V : Type u) [AddCommGroup V] [Module k V] : Rep k G :=
  Rep.of (@Representation.trivial k G V _ _ _ _)

variable {k G}

theorem trivial_def {V : Type u} [AddCommGroup V] [Module k V] (g : G) (v : V) :
    (trivial k G V).œÅ g v = v :=
  rfl

/-- A predicate for representations that fix every element. -/
abbrev IsTrivial (A : Rep k G) := Representation.IsTrivial A.œÅ

instance {V : Type u} [AddCommGroup V] [Module k V] :
    IsTrivial (Rep.trivial k G V) where

instance {V : Type u} [AddCommGroup V] [Module k V] (œÅ : Representation k G V) [œÅ.IsTrivial] :
    IsTrivial (Rep.of œÅ) where

noncomputable def trivialFunctor : ModuleCat k ‚•§ Rep k G where
  obj V := trivial k G V
  map f := { hom := f, comm := fun g => rfl }

-- Porting note: the two following instances were found automatically in mathlib3
noncomputable instance : PreservesLimits (forget‚ÇÇ (Rep k G) (ModuleCat.{u} k)) :=
  Action.instPreservesLimitsForget.{u} _ _

noncomputable instance : PreservesColimits (forget‚ÇÇ (Rep k G) (ModuleCat.{u} k)) :=
  Action.instPreservesColimitsForget.{u} _ _

/- Porting note: linter complains `simp` unfolds some types in the LHS, so
have removed `@[simp]`. -/
theorem MonoidalCategory.braiding_hom_apply {A B : Rep k G} (x : A) (y : B) :
    Action.Hom.hom (Œ≤_ A B).hom (TensorProduct.tmul k x y) = TensorProduct.tmul k y x :=
  rfl

/- Porting note: linter complains `simp` unfolds some types in the LHS, so
have removed `@[simp]`. -/
theorem MonoidalCategory.braiding_inv_apply {A B : Rep k G} (x : A) (y : B) :
    Action.Hom.hom (Œ≤_ A B).inv (TensorProduct.tmul k y x) = TensorProduct.tmul k x y :=
  rfl

section Res

variable {H : Type u} [Monoid H] (f : G ‚Üí* H)

lemma coe_res_obj (A : Rep k H) :
    ((Action.res _ f).obj A : Type u) = A := rfl

@[simp]
lemma res_obj_œÅ (A : Rep k H) :
    ((Action.res _ f).obj A).œÅ = A.œÅ.comp f := rfl

@[simp]
lemma res_map_hom {A B : Rep k H} (œÜ : A ‚ü∂ B) :
    hom ((Action.res _ f).map œÜ) = hom œÜ := rfl

variable (k) in
def resFunctor : Grp·µí·µñ ‚•§ Cat where
  obj := fun G ‚Ü¶ Cat.of (Rep k G.unop)
  map := fun f ‚Ü¶ Action.res (ModuleCat k) f.unop

variable (k) in
def opResFunctor (k : Type u) [CommRing k] : Grp·µí·µñ ‚•§ Cat where
  obj := fun G ‚Ü¶ Cat.of (Rep k G.unop)·µí·µñ
  map := fun f ‚Ü¶ (Action.res (ModuleCat k) f.unop).op

end Res

section Linearization

variable (k G)

/-- The monoidal functor sending a type `H` with a `G`-action to the induced `k`-linear
`G`-representation on `k[H].` -/
noncomputable def linearization : MonoidalFunctor (Action (Type u) G) (Rep k G) :=
  (ModuleCat.monoidalFree k).mapAction G

variable {k G}

theorem linearization_obj_V (X : Action (Type u) G) :
    ((linearization k G).obj X).V = ModuleCat.of k (X.V ‚Üí‚ÇÄ k) :=
  rfl

theorem coe_linearization_obj (X : Action (Type u) G) :
    (linearization k G).obj X = (X.V ‚Üí‚ÇÄ k) := rfl

@[simp]
theorem linearization_obj_œÅ (X : Action (Type u) G) (g : G) :
    @DFunLike.coe (no_index ((linearization k G).obj X ‚Üí‚Çó[k] (linearization k G).obj X))
      (no_index (linearization k G).obj X)
      (no_index fun _ => (linearization k G).obj X) _
       (((linearization k G).obj X).œÅ g) = ‚áë(Finsupp.lmapDomain k k (X.œÅ g)) :=
  rfl

@[simp]
theorem linearization_obj_œÅ' (X : Action (Type u) G) (g : G) :
    ((linearization k G).obj X).œÅ g = Finsupp.lmapDomain k k (X.typeœÅ g) := rfl

theorem linearization_of (X : Action (Type u) G) (g : G) (x : X.V) :
    ((linearization k G).obj X).œÅ g (Finsupp.single x (1 : k))
      = Finsupp.single (X.œÅ g x) (1 : k) := by
  rw [linearization_obj_œÅ (k := k) (G := G) X g, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single]

-- Porting note (#11041): helps fixing `linearizationTrivialIso` since change in behaviour of `ext`.
theorem linearization_single (X : Action (Type u) G) (g : G) (x : X.V) (r : k) :
    ((linearization k G).obj X).œÅ g (Finsupp.single x r) = Finsupp.single (X.œÅ g x) r := by
  rw [linearization_obj_œÅ, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single]

variable {X Y : Action (Type u) G} (f : X ‚ü∂ Y)

@[simp]
theorem linearization_map_hom : ((linearization k G).map f).hom = Finsupp.lmapDomain k k f.hom :=
  rfl

@[simp]
theorem linearization_map_hom' :
    hom ((linearization k G).map f) = Finsupp.lmapDomain k k (Action.hom f) :=
  rfl

theorem linearization_map_hom_single (x : X.V) (r : k) :
    ((linearization k G).map f).hom (Finsupp.single x r) = Finsupp.single (f.hom x) r :=
  Finsupp.mapDomain_single

@[simp]
theorem linearization_Œº_hom (X Y : Action (Type u) G) :
    ((linearization k G).Œº X Y).hom = (finsuppTensorFinsupp' k X.V Y.V).toLinearMap :=
  rfl

@[simp]
theorem linearization_Œº_inv_hom (X Y : Action (Type u) G) :
    (inv ((linearization k G).Œº X Y)).hom = (finsuppTensorFinsupp' k X.V Y.V).symm.toLinearMap := by
-- Porting note (#11039): broken proof was
/- simp_rw [‚Üê Action.forget_map, Functor.map_inv, Action.forget_map, linearization_Œº_hom]
  apply IsIso.inv_eq_of_hom_inv_id _
  exact LinearMap.ext fun x => LinearEquiv.symm_apply_apply _ _-/
  rw [‚Üê Action.forget_map, Functor.map_inv]
  apply IsIso.inv_eq_of_hom_inv_id
  exact LinearMap.ext fun x => LinearEquiv.symm_apply_apply (finsuppTensorFinsupp' k X.V Y.V) x

@[simp]
theorem linearization_Œµ_hom : (linearization k G).Œµ.hom = Finsupp.lsingle PUnit.unit :=
  rfl

theorem linearization_Œµ_inv_hom_apply (r : k) :
    (inv (linearization k G).Œµ).hom (Finsupp.single PUnit.unit r) = r :=
  IsIso.hom_inv_id_apply (linearization k G).Œµ r

variable (k G)

/-- The linearization of a type `X` on which `G` acts trivially is the trivial `G`-representation
on `k[X]`. -/
@[simps!]
noncomputable def linearizationTrivialIso (X : Type u) :
    (linearization k G).obj (Action.mk X 1) ‚âÖ trivial k G (X ‚Üí‚ÇÄ k) :=
  Action.mkIso (Iso.refl _) fun _ => Finsupp.lhom_ext' fun _ => LinearMap.ext
    fun _ => linearization_single ..

/-- Given a `G`-action on `H`, this is `k[H]` bundled with the natural representation
`G ‚Üí* End(k[H])` as a term of type `Rep k G`. -/
noncomputable abbrev ofMulAction (H : Type u) [MulAction G H] : Rep k G :=
  of <| Representation.ofMulAction k G H

/-- The `k`-linear `G`-representation on `k[G]`, induced by left multiplication. -/
noncomputable abbrev leftRegular : Rep k G :=
  ofMulAction k G G

/-- The `k`-linear `G`-representation on `k[G‚Åø]`, induced by left multiplication. -/
noncomputable abbrev diagonal (n : ‚Ñï) : Rep k G :=
  ofMulAction k G (Fin n ‚Üí G)

/-- The linearization of a type `H` with a `G`-action is definitionally isomorphic to the
`k`-linear `G`-representation on `k[H]` induced by the `G`-action on `H`. -/
noncomputable def linearizationOfMulActionIso (H : Type u) [MulAction G H] :
    (linearization k G).obj (Action.ofMulAction G H) ‚âÖ ofMulAction k G H :=
  Iso.refl _

section

variable (k G A : Type u) [CommRing k] [Monoid G] [AddCommGroup A]
  [Module k A] [DistribMulAction G A] [SMulCommClass G k A]

/-- Turns a `k`-module `A` with a compatible `DistribMulAction` of a monoid `G` into a
`k`-linear `G`-representation on `A`. -/
def ofDistribMulAction : Rep k G := Rep.of (Representation.ofDistribMulAction k G A)

@[simp] theorem ofDistribMulAction_œÅ_apply_apply (g : G) (a : A) :
    (ofDistribMulAction k G A).œÅ g a = g ‚Ä¢ a := rfl

/-- Given an `R`-algebra `S`, the `‚Ñ§`-linear representation associated to the natural action of
`S ‚âÉ‚Çê[R] S` on `S`. -/
@[simp] def ofAlgebraAut (R S : Type) [CommRing R] [CommRing S] [Algebra R S] :
    Rep ‚Ñ§ (S ‚âÉ‚Çê[R] S) := ofDistribMulAction ‚Ñ§ (S ‚âÉ‚Çê[R] S) S

end
section
variable (M G : Type) [Monoid M] [CommGroup G] [MulDistribMulAction M G]

/-- Turns a `CommGroup` `G` with a `MulDistribMulAction` of a monoid `M` into a
`‚Ñ§`-linear `M`-representation on `Additive G`. -/
def ofMulDistribMulAction : Rep ‚Ñ§ M := Rep.of (Representation.ofMulDistribMulAction M G)

@[simp] theorem ofMulDistribMulAction_œÅ_apply_apply (g : M) (a : Additive G) :
    (ofMulDistribMulAction M G).œÅ g a = Additive.ofMul (g ‚Ä¢ Additive.toMul a) := rfl

/-- Given an `R`-algebra `S`, the `‚Ñ§`-linear representation associated to the natural action of
`S ‚âÉ‚Çê[R] S` on `SÀ£`. -/
@[simp] def ofAlgebraAutOnUnits (R S : Type) [CommRing R] [CommRing S] [Algebra R S] :
    Rep ‚Ñ§ (S ‚âÉ‚Çê[R] S) := Rep.ofMulDistribMulAction (S ‚âÉ‚Çê[R] S) SÀ£

end

variable {k G}

/-- Given an element `x : A`, there is a natural morphism of representations `k[G] ‚ü∂ A` sending
`g ‚Ü¶ A.œÅ(g)(x).` -/
abbrev leftRegularHom (A : Rep k G) (x : A) : leftRegular k G ‚ü∂ A :=
  mkHom' (Finsupp.lift A k G fun g => A.œÅ g x)
    fun g => Finsupp.lhom_ext' fun y => LinearMap.ext_ring <| by simp

@[simp]
theorem leftRegularHom_single {A : Rep k G} (g : G) (x : A) (r : k) :
    (leftRegularHom A x).hom (Finsupp.single g r) = r ‚Ä¢ A.œÅ g x := by
  simp [ModuleCat.hom_def, of_V, coe_def]

@[simp] theorem leftRegularHom_single' {A : Rep k G} (g : G) (x : A) (r : k) :
    hom (leftRegularHom A x) (Finsupp.single g r) = r ‚Ä¢ A.œÅ g x := by
  simp

/-- Given a `k`-linear `G`-representation `A`, there is a `k`-linear isomorphism between
representation morphisms `Hom(k[G], A)` and `A`. -/
@[simps]
noncomputable def leftRegularHomEquiv (A : Rep k G) : (leftRegular k G ‚ü∂ A) ‚âÉ‚Çó[k] A where
  toFun f := hom f (Finsupp.single 1 1)
  map_add' x y := rfl
  map_smul' r x := rfl
  invFun x := leftRegularHom A x
  left_inv f := hom_ext <| Finsupp.lhom_ext' fun x : G => LinearMap.ext_ring <| by
    simpa using (hom_comm_apply f x (Finsupp.single 1 1)).symm
  right_inv x := by simp

theorem leftRegularHomEquiv_symm_single {A : Rep k G} (x : A) (g : G) (r : k) :
    ((leftRegularHomEquiv A).symm x).hom (Finsupp.single g r) = r ‚Ä¢ A.œÅ g x :=
  leftRegularHom_single _ _ _

@[simp]
theorem leftRegularHomEquiv_symm' {A : Rep k G} (x : A) :
    hom ((leftRegularHomEquiv A).symm x) = hom (leftRegularHom A x) := rfl

end Linearization

abbrev finsupp (Œ± : Type u) (A : Rep k G) : Rep k G :=
  Rep.of (Representation.finsupp A.œÅ Œ±)

variable (k G) in
abbrev free (Œ± : Type u) : Rep k G :=
  finsupp Œ± (leftRegular k G)

abbrev lsingle (A : Rep k G) {Œ± : Type u} (a : Œ±) :
    A ‚ü∂ (A.finsupp Œ±) :=
  mkHom' (Finsupp.lsingle a) fun g => by ext x; simp [coe_def, of_V]

abbrev finsuppHom (A : Rep k G) {Œ± Œ≤ : Type u} (f : Œ± ‚Üí Œ≤) :
    (A.finsupp Œ±) ‚ü∂ (A.finsupp Œ≤) :=
  mkHom' (Finsupp.lmapDomain A k f) fun g => Finsupp.lhom_ext fun i x => by simp

abbrev freeHom {Œ± Œ≤ : Type u} (f : Œ± ‚Üí Œ≤) :
    free k G Œ± ‚ü∂ free k G Œ≤ := finsuppHom _ f

@[simp] lemma _root_.Finsupp.finsuppProdLEquiv_single {Œ± Œ≤ R M : Type*}
    [Semiring R] [AddCommMonoid M] [Module R M] (a : Œ± √ó Œ≤) (m : M) :
    Finsupp.finsuppProdLEquiv R (Finsupp.single a m)
      = Finsupp.single a.1 (Finsupp.single a.2 m) := by
  show Finsupp.curry _ = _
  simp only [Finsupp.curry, Finsupp.single_zero, Finsupp.sum_single_index]

@[simp] lemma _root_.Finsupp.finsuppProdLEquiv_symm_single_single {Œ± Œ≤ R M : Type*}
    [Semiring R] [AddCommMonoid M] [Module R M] (a : Œ±) (b : Œ≤) (m : M) :
    (Finsupp.finsuppProdLEquiv R).symm (Finsupp.single a (Finsupp.single b m))
      = Finsupp.single (a, b) m := by
  show Finsupp.uncurry _ = _
  simp only [Finsupp.uncurry, Finsupp.sum_zero_index, Finsupp.sum_single_index, Finsupp.single_zero]

abbrev freeLift {Œ± : Type u} (A : Rep k G) (f : Œ± ‚Üí A) :
    free k G Œ± ‚ü∂ A :=
  mkHom' (Finsupp.linearCombination k (fun x => A.œÅ x.2 (f x.1))
    ‚àò‚Çó (Finsupp.finsuppProdLEquiv k).symm.toLinearMap)
    fun g => Finsupp.lhom_ext' fun i => Finsupp.lhom_ext fun j y => by simp

@[simp]
lemma freeLift_hom_single_single {Œ± : Type u} (A : Rep k G)
    (f : Œ± ‚Üí A) (i : Œ±) (g : G) (r : k) :
    (freeLift A f).hom (Finsupp.single i (Finsupp.single g r)) = r ‚Ä¢ A.œÅ g (f i) := by
  simp [of_V, coe_def, ModuleCat.hom_def]

@[simp]
lemma freeLift_hom_single_single' {Œ± : Type u} (A : Rep k G)
    (f : Œ± ‚Üí A) (i : Œ±) (g : G) (r : k) :
    hom (freeLift A f) (Finsupp.single i (Finsupp.single g r)) = r ‚Ä¢ A.œÅ g (f i) := by
  simp

@[simps] def freeLiftEquiv (Œ± : Type u) (A : Rep k G) :
    (free k G Œ± ‚ü∂ A) ‚âÉ‚Çó[k] (Œ± ‚Üí A) where
  toFun f i := hom f (Finsupp.single i (Finsupp.single 1 1))
  invFun := freeLift A
  left_inv x := hom_ext <| Finsupp.lhom_ext' fun i =>
    Finsupp.lhom_ext fun j y => by
      have := (hom_comm_apply'' x j (Finsupp.single i (Finsupp.single 1 1))).symm
      simp_all [‚Üê map_smul]
  right_inv x := by ext; simp
  map_add' x y := rfl
  map_smul' r x := rfl

lemma free_ext {Œ± : Type u} {A : Rep k G} (f g : (free k G Œ±) ‚ü∂ A)
  (h : ‚àÄ i : Œ±, hom f (Finsupp.single i (Finsupp.single 1 1))
    = hom g (Finsupp.single i (Finsupp.single 1 1))) : f = g :=
  (freeLiftEquiv Œ± A).injective (Function.funext_iff.2 h)

lemma freeLiftEquiv_naturality {Œ± Œ≤ : Type u} (A : Rep k G)
    (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí A) :
    (freeLiftEquiv Œ± A).symm (g ‚àò f) = freeHom f ‚â´ (freeLiftEquiv Œ≤ A).symm g :=
  free_ext _ _ fun i => by simp

variable (B C : Action (Type u) G)
open MonoidalCategory in
example : (B ‚äó C).V = (B.V √ó C.V) := rfl

open MonoidalCategory in
theorem coe_tensor {A B : Rep k G} : (A ‚äó B : Rep k G) = TensorProduct k A B := rfl

open MonoidalCategory in
theorem tensor_V {A B : Rep k G} : (A ‚äó B).V = TensorProduct k A B := rfl

open MonoidalCategory in
@[simp]
theorem tensor_œÅ {A B : Rep k G} (g : G) : (A ‚äó B).œÅ g = TensorProduct.map (A.œÅ g) (B.œÅ g) := rfl

open MonoidalCategory in
theorem tensor_œÅ' {A B : Rep k G} : @DFunLike.coe
    (no_index (G ‚Üí* (TensorProduct k A B ‚Üí‚Çó[k] TensorProduct k A B)))
    G (fun _ => no_index (TensorProduct k A B ‚Üí‚Çó[k] TensorProduct k A B)) _
    (A ‚äó B).œÅ = fun g => TensorProduct.map (A.œÅ g) (B.œÅ g) := rfl

variable (k G) in
open MonoidalCategory in
abbrev tprodIsoFree (Œ± : Type u) :
    leftRegular k G ‚äó trivial k G (Œ± ‚Üí‚ÇÄ k) ‚âÖ free k G Œ± :=
  mkIso' (finsuppTensorFinsupp' k G Œ±
    ‚â™‚â´‚Çó Finsupp.domLCongr (Equiv.prodComm G Œ±)
    ‚â™‚â´‚Çó Finsupp.finsuppProdLEquiv k)
    fun g => TensorProduct.ext <| Finsupp.lhom_ext fun x r => Finsupp.lhom_ext fun y s => by
    simp only [tensor_œÅ]
    simp [ModuleCat.coe_of, coe_tensor,
      ModuleCat.MonoidalCategory.instMonoidalCategoryStruct_tensorObj,
      ModuleCat.MonoidalCategory.tensorObj]

variable {Œ± : Type u} (i : Œ±)

@[simp]
lemma tprodIsoFree_inv_hom_single_single' {Œ± : Type u} (i : Œ±) (g : G) (r : k) :
    hom (tprodIsoFree k G Œ±).inv (Finsupp.single i (Finsupp.single g r)) =
      TensorProduct.tmul k (Finsupp.single g r) (Finsupp.single i 1) := by
  simp [finsuppTensorFinsupp'_symm_single_eq_tmul_single_one, coe_tensor]

@[simp] lemma tprodIsoFree_inv_hom_single_single {Œ± : Type u} (i : Œ±) (g : G) (r : k) :
    (tprodIsoFree k G Œ±).inv.hom (Finsupp.single i (Finsupp.single g r)) =
      TensorProduct.tmul k (Finsupp.single g r) (Finsupp.single i 1) :=
  tprodIsoFree_inv_hom_single_single' _ _ _

@[simp] lemma tprodIsoFree_hom_hom_single_tmul_single' {Œ± : Type u} (i : Œ±) (g : G) (r s : k) :
    hom (tprodIsoFree k G Œ±).hom (Finsupp.single g r ‚äó‚Çú Finsupp.single i s)
      = Finsupp.single i (Finsupp.single g (r * s)) := by
  simp [coe_tensor, of_V]

@[simp] lemma tprodIsoFree_hom_hom_single_tmul_single {Œ± : Type u} (i : Œ±) (g : G) (r s : k) :
    (tprodIsoFree k G Œ±).hom.hom (Finsupp.single g r ‚äó‚Çú Finsupp.single i s)
      = Finsupp.single i (Finsupp.single g (r * s)) :=
  tprodIsoFree_hom_hom_single_tmul_single' _ _ _ _

end
section

variable [Group G] [Fintype G] (A : Rep k G)

@[simp]
theorem _root_.ModuleCat.coeFn_sum {R : Type*} [Ring R] {M N : ModuleCat R}
    {Œπ : Type*} (t : Finset Œπ) (f : Œπ ‚Üí (M ‚ü∂ N)) :
    ‚áë(‚àë i in t, f i) = ‚àë i in t, ‚áë(f i) :=
  LinearMap.coeFn_sum _ _

abbrev norm (A : Rep k G) : A ‚ü∂ A :=
  mkHom' (‚àë g : G, A.œÅ g) fun g => by
    ext x
    simpa using Fintype.sum_bijective (Œ± := A) (fun x => g‚Åª¬π * x * g)
      ((Group.mulRight_bijective g).comp (Group.mulLeft_bijective g‚Åª¬π)) _ _ (by simp)

lemma œÅ_norm_eq (g : G) (x : A) : A.œÅ g (hom (norm A) x) = hom (norm A) x := by
  simpa using Fintype.sum_bijective (Œ± := A) (g * ¬∑)
    (Group.mulLeft_bijective g) _ _ fun x => by simp

lemma norm_œÅ_eq (g : G) (x : A) : hom (norm A) (A.œÅ g x) = hom (norm A) x := by
  simpa using Fintype.sum_bijective (Œ± := A) (¬∑ * g)
    (Group.mulRight_bijective g) _ _ fun x => by simp

end
section MonoidalClosed
open MonoidalCategory Action

variable [Group G] (A B C : Rep k G)

/-- Given a `k`-linear `G`-representation `(A, œÅ‚ÇÅ)`, this is the 'internal Hom' functor sending
`(B, œÅ‚ÇÇ)` to the representation `Hom‚Çñ(A, B)` that maps `g : G` and `f : A ‚Üí‚Çó[k] B` to
`(œÅ‚ÇÇ g) ‚àò‚Çó f ‚àò‚Çó (œÅ‚ÇÅ g‚Åª¬π)`. -/
@[simps]
protected def ihom (A : Rep k G) : Rep k G ‚•§ Rep k G where
  obj B := Rep.of (Representation.linHom A.œÅ B.œÅ)
  map := fun {X} {Y} f => mkHom' (LinearMap.llcomp k _ _ _ (hom f))
    fun g => LinearMap.ext fun x => LinearMap.ext fun y => by simp [hom_comm_apply'', hom_def]
  map_id := fun _ => by ext; rfl
  map_comp := fun _ _ => by ext; rfl

@[simp] theorem ihom_obj_œÅ_apply {A B : Rep k G} (g : G) (x : A ‚Üí‚Çó[k] B) :
    ((Rep.ihom A).obj B).œÅ g x = B.œÅ g ‚àò‚Çó x ‚àò‚Çó A.œÅ g‚Åª¬π :=
  rfl

/-- Given a `k`-linear `G`-representation `A`, this is the Hom-set bijection in the adjunction
`A ‚äó - ‚ä£ ihom(A, -)`. It sends `f : A ‚äó B ‚ü∂ C` to a `Rep k G` morphism defined by currying the
`k`-linear map underlying `f`, giving a map `A ‚Üí‚Çó[k] B ‚Üí‚Çó[k] C`, then flipping the arguments. -/
@[simps (config := .lemmasOnly)]
def homEquiv (A B C : Rep k G) : (A ‚äó B ‚ü∂ C) ‚âÉ (B ‚ü∂ (Rep.ihom A).obj C) where
  toFun f := mkHom' (TensorProduct.curry (hom f)).flip fun g =>
    LinearMap.ext fun x => LinearMap.ext fun y => by
      have :=  hom_comm_apply'' (A := A ‚äó B) f g (A.œÅ g‚Åª¬π y ‚äó‚Çú[k] x)
      simp only [tensor_œÅ] at this
      simp_all [coe_tensor]
  invFun f := mkHom' (TensorProduct.uncurry k A B C (hom f).flip)
      fun g => TensorProduct.ext' fun x y => by
      simp only [tensor_œÅ]
      simpa [coe_tensor, hom_def, coe_def]
        using LinearMap.ext_iff.1 (hom_comm_apply' f g y) (A.œÅ g x)
  left_inv f := Action.Hom.ext (TensorProduct.ext' fun _ _ => rfl)
  right_inv f := by ext; rfl

variable {A B C}

/-- Porting note: if we generate this with `@[simps]` the linter complains some types in the LHS
simplify. -/
theorem homEquiv_apply_hom (f : A ‚äó B ‚ü∂ C) :
    hom (homEquiv A B C f) = (TensorProduct.curry (hom f)).flip := rfl

/-- Porting note: if we generate this with `@[simps]` the linter complains some types in the LHS
simplify. -/
theorem homEquiv_symm_apply_hom (f : B ‚ü∂ (Rep.ihom A).obj C) :
    hom ((homEquiv A B C).symm f) = TensorProduct.uncurry k A B C (hom f).flip := rfl

instance : MonoidalClosed (Rep k G) where
  closed A :=
    { rightAdj := Rep.ihom A
      adj := Adjunction.mkOfHomEquiv (
      { homEquiv := Rep.homEquiv A
        homEquiv_naturality_left_symm := fun _ _ => Action.Hom.ext
          (TensorProduct.ext' fun _ _ => rfl)
        homEquiv_naturality_right := fun _ _ => Action.Hom.ext (LinearMap.ext
          fun _ => LinearMap.ext fun _ => rfl) })}

@[simp]
theorem ihom_obj_œÅ_def (A B : Rep k G) : ((ihom A).obj B).œÅ = ((Rep.ihom A).obj B).œÅ :=
  rfl

@[simp]
theorem homEquiv_def (A B C : Rep k G) : (ihom.adjunction A).homEquiv B C = Rep.homEquiv A B C :=
  congrFun (congrFun (Adjunction.mkOfHomEquiv_homEquiv _) _) _

@[simp]
theorem ihom_ev_app_hom (A B : Rep k G) :
    ((ihom.ev A).app B).hom
      = TensorProduct.uncurry k A (A ‚Üí‚Çó[k] B) B LinearMap.id.flip := by
  ext; rfl

@[simp]
theorem ihom_ev_app_hom' (A B : Rep k G) :
    hom ((ihom.ev A).app B) = TensorProduct.uncurry k A (A ‚Üí‚Çó[k] B) B LinearMap.id.flip := by
  ext; rfl

@[simp] theorem ihom_coev_app_hom (A B : Rep k G) :
    ((ihom.coev A).app B).hom = (TensorProduct.mk k _ _).flip :=
  LinearMap.ext fun _ => LinearMap.ext fun _ => rfl

@[simp] theorem ihom_coev_app_hom' (A B : Rep k G) :
    hom ((ihom.coev A).app B) = (TensorProduct.mk k _ _).flip := rfl

variable (A B C)

/-- There is a `k`-linear isomorphism between the sets of representation morphisms`Hom(A ‚äó B, C)`
and `Hom(B, Hom‚Çñ(A, C))`. -/
def MonoidalClosed.linearHomEquiv : (A ‚äó B ‚ü∂ C) ‚âÉ‚Çó[k] B ‚ü∂ A ‚ü∂[Rep k G] C :=
  { (ihom.adjunction A).homEquiv _ _ with
    map_add' := fun _ _ => rfl
    map_smul' := fun _ _ => rfl }

/-- There is a `k`-linear isomorphism between the sets of representation morphisms`Hom(A ‚äó B, C)`
and `Hom(A, Hom‚Çñ(B, C))`. -/
def MonoidalClosed.linearHomEquivComm : (A ‚äó B ‚ü∂ C) ‚âÉ‚Çó[k] A ‚ü∂ B ‚ü∂[Rep k G] C :=
  Linear.homCongr k (Œ≤_ A B) (Iso.refl _) ‚â™‚â´‚Çó MonoidalClosed.linearHomEquiv _ _ _

variable {A B C}

-- `simpNF` times out
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquiv_hom (f : A ‚äó B ‚ü∂ C) :
    (MonoidalClosed.linearHomEquiv A B C f).hom = (TensorProduct.curry (hom f)).flip :=
  rfl

@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquiv_hom' (f : A ‚äó B ‚ü∂ C) :
    hom (MonoidalClosed.linearHomEquiv A B C f) = (TensorProduct.curry (hom f)).flip :=
  rfl

-- `simpNF` times out
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquivComm_hom (f : A ‚äó B ‚ü∂ C) :
    (MonoidalClosed.linearHomEquivComm A B C f).hom = TensorProduct.curry (hom f) :=
  rfl

@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquivComm_hom' (f : A ‚äó B ‚ü∂ C) :
    hom (MonoidalClosed.linearHomEquivComm A B C f) = TensorProduct.curry (hom f) :=
  rfl

theorem MonoidalClosed.linearHomEquiv_symm_hom (f : B ‚ü∂ A ‚ü∂[Rep k G] C) :
    ((MonoidalClosed.linearHomEquiv A B C).symm f).hom =
      TensorProduct.uncurry k A B C (hom f).flip := by
  simp [linearHomEquiv]
  rfl

@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquiv_symm_hom' (f : B ‚ü∂ A ‚ü∂[Rep k G] C) :
    hom ((MonoidalClosed.linearHomEquiv A B C).symm f) =
      TensorProduct.uncurry k A B C (hom f).flip := by
  simp [linearHomEquiv]
  rfl

theorem MonoidalClosed.linearHomEquivComm_symm_hom (f : A ‚ü∂ B ‚ü∂[Rep k G] C) :
    ((MonoidalClosed.linearHomEquivComm A B C).symm f).hom
      = TensorProduct.uncurry k A B C (hom f) :=
  TensorProduct.ext' fun _ _ => rfl

@[simp]
theorem MonoidalClosed.linearHomEquivComm_symm_hom' (f : A ‚ü∂ B ‚ü∂[Rep k G] C) :
    hom ((MonoidalClosed.linearHomEquivComm A B C).symm f)
      = TensorProduct.uncurry k A B C (hom f) :=
  TensorProduct.ext' fun _ _ => rfl


end MonoidalClosed

end Rep

namespace Representation
open MonoidalCategory
variable {k G : Type u} [CommRing k] [Monoid G] {V W : Type u} [AddCommGroup V] [AddCommGroup W]
  [Module k V] [Module k W] (œÅ : Representation k G V) (œÑ : Representation k G W)

/-- Tautological isomorphism to help Lean in typechecking. -/
def repOfTprodIso : Rep.of (œÅ.tprod œÑ) ‚âÖ Rep.of œÅ ‚äó Rep.of œÑ :=
  Iso.refl _

theorem repOfTprodIso_apply (x : TensorProduct k V W) : (repOfTprodIso œÅ œÑ).hom.hom x = x :=
  rfl

theorem repOfTprodIso_inv_apply (x : TensorProduct k V W) : (repOfTprodIso œÅ œÑ).inv.hom x = x :=
  rfl

end Representation

/-!
# The categorical equivalence `Rep k G ‚âå Module.{u} (MonoidAlgebra k G)`.
-/


namespace Rep

variable {k G : Type u} [CommRing k] [Monoid G]

-- Verify that the symmetric monoidal structure is available.
example : SymmetricCategory (Rep k G) := by infer_instance

example : MonoidalPreadditive (Rep k G) := by infer_instance

example : MonoidalLinear k (Rep k G) := by infer_instance

noncomputable section

/-- Auxiliary lemma for `toModuleMonoidAlgebra`. -/
theorem to_Module_monoidAlgebra_map_aux {k G : Type*} [CommRing k] [Monoid G] (V W : Type*)
    [AddCommGroup V] [AddCommGroup W] [Module k V] [Module k W] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V)
    (œÉ : G ‚Üí* W ‚Üí‚Çó[k] W) (f : V ‚Üí‚Çó[k] W) (w : ‚àÄ g : G, f.comp (œÅ g) = (œÉ g).comp f)
    (r : MonoidAlgebra k G) (x : V) :
    f ((((MonoidAlgebra.lift k G (V ‚Üí‚Çó[k] V)) œÅ) r) x) =
      (((MonoidAlgebra.lift k G (W ‚Üí‚Çó[k] W)) œÉ) r) (f x) := by
  apply MonoidAlgebra.induction_on r
  ¬∑ intro g
    simp only [one_smul, MonoidAlgebra.lift_single, MonoidAlgebra.of_apply]
    exact LinearMap.congr_fun (w g) x
  ¬∑ intro g h gw hw; simp only [map_add, add_left_inj, LinearMap.add_apply, hw, gw]
  ¬∑ intro r g w
    simp only [map_smul, w, RingHom.id_apply, LinearMap.smul_apply, LinearMap.map_smul‚Çõ‚Çó]

/-- Auxiliary definition for `toModuleMonoidAlgebra`. -/
def toModuleMonoidAlgebraMap {V W : Rep k G} (f : V ‚ü∂ W) :
    ModuleCat.of (MonoidAlgebra k G) (Representation.asModule V.œÅ)
      ‚ü∂ ModuleCat.of (MonoidAlgebra k G) (Representation.asModule W.œÅ) :=
  { f.hom with
    map_smul' := fun r x => to_Module_monoidAlgebra_map_aux V W V.œÅ W.œÅ f.hom f.comm r x }

/-- Functorially convert a representation of `G` into a module over `MonoidAlgebra k G`. -/
def toModuleMonoidAlgebra : Rep k G ‚•§ ModuleCat.{u} (MonoidAlgebra k G) where
  obj V := ModuleCat.of _ (Representation.asModule V.œÅ)
  map f := toModuleMonoidAlgebraMap f

/-- Functorially convert a module over `MonoidAlgebra k G` into a representation of `G`. -/
def ofModuleMonoidAlgebra : ModuleCat.{u} (MonoidAlgebra k G) ‚•§ Rep k G where
  obj M := Rep.of (Representation.ofModule M)
  map f :=
    { hom := { f with map_smul' := fun r x => f.map_smul (algebraMap k _ r) x }
      comm := fun g => by ext; apply f.map_smul }

theorem ofModuleMonoidAlgebra_obj_coe (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra.obj M) = RestrictScalars k (MonoidAlgebra k G) M :=
  rfl

theorem ofModuleMonoidAlgebra_obj_œÅ (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra.obj M).œÅ = Representation.ofModule M :=
  rfl

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def counitIsoAddEquiv {M : ModuleCat.{u} (MonoidAlgebra k G)} :
    (ofModuleMonoidAlgebra ‚ãô toModuleMonoidAlgebra).obj M ‚âÉ+ M := by
  dsimp [ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
  exact (Representation.ofModule M).asModuleEquiv.trans
    (RestrictScalars.addEquiv k (MonoidAlgebra k G) _)

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def unitIsoAddEquiv {V : Rep k G} :
    V ‚âÉ+ ((toModuleMonoidAlgebra ‚ãô ofModuleMonoidAlgebra).obj V) := by
  dsimp [ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
  refine (Representation.asModuleEquiv V.œÅ).symm.trans ?_
  exact (RestrictScalars.addEquiv k (MonoidAlgebra k G) _).symm

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def counitIso (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra ‚ãô toModuleMonoidAlgebra).obj M ‚âÖ M :=
  LinearEquiv.toModuleIso'
    { counitIsoAddEquiv with
      map_smul' := fun r x => by
        set_option tactic.skipAssignedInstances false in
        dsimp [counitIsoAddEquiv]
        /- Porting note: rest of broken proof was `simp`. -/
        rw [AddEquiv.trans_apply]
        rw [AddEquiv.trans_apply]
        erw [@Representation.ofModule_asAlgebraHom_apply_apply k G _ _ _ _ (_)]
        exact AddEquiv.symm_apply_apply _ _}

theorem unit_iso_comm (V : Rep k G) (g : G) (x : V) :
    unitIsoAddEquiv ((V.œÅ g).toFun x) = ((ofModuleMonoidAlgebra.obj
      (toModuleMonoidAlgebra.obj V)).œÅ g).toFun (unitIsoAddEquiv x) := by
  dsimp [unitIsoAddEquiv, ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
/- Porting note: rest of broken proof was
  simp only [AddEquiv.apply_eq_iff_eq, AddEquiv.apply_symm_apply,
    Representation.asModuleEquiv_symm_map_rho, Representation.ofModule_asModule_act] -/
  erw [Representation.asModuleEquiv_symm_map_rho]
  rfl

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def unitIso (V : Rep k G) : V ‚âÖ (toModuleMonoidAlgebra ‚ãô ofModuleMonoidAlgebra).obj V :=
  Action.mkIso
    (LinearEquiv.toModuleIso'
      { unitIsoAddEquiv with
        map_smul' := fun r x => by
          dsimp [unitIsoAddEquiv]
/- Porting note: rest of broken proof was
          simp only [Representation.asModuleEquiv_symm_map_smul,
            RestrictScalars.addEquiv_symm_map_algebraMap_smul] -/
          -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
          erw [AddEquiv.trans_apply,
            Representation.asModuleEquiv_symm_map_smul]
          rfl })
    fun g => by ext; apply unit_iso_comm

/-- The categorical equivalence `Rep k G ‚âå ModuleCat (MonoidAlgebra k G)`. -/
def equivalenceModuleMonoidAlgebra : Rep k G ‚âå ModuleCat.{u} (MonoidAlgebra k G) where
  functor := toModuleMonoidAlgebra
  inverse := ofModuleMonoidAlgebra
  unitIso := NatIso.ofComponents (fun V => unitIso V) (by aesop_cat)
  counitIso := NatIso.ofComponents (fun M => counitIso M) (by aesop_cat)

-- TODO Verify that the equivalence with `ModuleCat (MonoidAlgebra k G)` is a monoidal functor.
end

end Rep
