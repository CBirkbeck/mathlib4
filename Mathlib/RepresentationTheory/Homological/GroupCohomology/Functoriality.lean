import Mathlib.Algebra.Homology.HomologicalComplexAbelian
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.RepresentationTheory.Homological.GroupCohomology.Basic
import Mathlib.RepresentationTheory.Invariants
import Mathlib.RepresentationTheory.Homological.GroupCohomology.LowDegree
universe v u
variable (n : ‚Ñï)

open CategoryTheory

lemma Fin.comp_contractNth {G H : Type*} [MulOneClass G] [MulOneClass H] (f : G ‚Üí* H)
    (j : Fin (n + 1)) (g : Fin (n + 1) ‚Üí G) :
    f ‚àò Fin.contractNth j (¬∑ * ¬∑) g = Fin.contractNth j (¬∑ * ¬∑) (f ‚àò g) := by
  ext x
  rcases lt_trichotomy (x : ‚Ñï) j with (h|h|h)
  ¬∑ simp only [Function.comp_apply, Fin.contractNth_apply_of_lt, h]
  ¬∑ simp only [Function.comp_apply, Fin.contractNth_apply_of_eq, h, f.map_mul]
  ¬∑ simp only [Function.comp_apply, Fin.contractNth_apply_of_gt, h]

namespace LinearMap

lemma ker_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M ‚Üí‚Çó[R] N) (I : Type*) :
    LinearMap.ker (f.compLeft I) = Submodule.pi (Set.univ : Set I) (fun _ => LinearMap.ker f) :=
  Submodule.ext fun _ => ‚ü®fun (hx : _ = _) i _ => congr_fun hx i,
    fun hx => funext fun i => hx i trivial‚ü©

lemma range_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M ‚Üí‚Çó[R] N) (I : Type*) :
    LinearMap.range (f.compLeft I) = Submodule.pi (Set.univ : Set I)
      (fun _ => LinearMap.range f) :=
  Submodule.ext fun _ => ‚ü®fun ‚ü®y, hy‚ü© i _ => ‚ü®y i, congr_fun hy i‚ü©, fun hx => by
    choose y hy using hx
    exact ‚ü®fun i => y i trivial, funext fun i => hy i trivial‚ü©‚ü©

end LinearMap
namespace ModuleCat

variable (R : Type u) [Ring R]

lemma ofHom_comp {M N P : Type v} [AddCommGroup M] [AddCommGroup N] [AddCommGroup P]
    [Module R M] [Module R N] [Module R P] (f : M ‚Üí‚Çó[R] N) (g : N ‚Üí‚Çó[R] P) :
    ofHom (g ‚àò‚Çó f) = ofHom f ‚â´ ofHom g := rfl

end ModuleCat

namespace groupCohomology
open Rep

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  (A : Rep k G) (B : Rep k H) (f : G ‚Üí* H) (œÜ : B ‚Üí‚Çó[k] A) (n : ‚Ñï)

class IsPairMap : Prop where
  compatible : ‚àÄ (g : G), œÜ ‚àò‚Çó B.œÅ (f g) = A.œÅ g ‚àò‚Çó œÜ

namespace IsPairMap
open Representation

variable {A B f œÜ} (S : Subgroup G)

lemma compatible_apply [IsPairMap A B f œÜ] (g : G) (x : B) :
    œÜ (B.œÅ (f g) x) = A.œÅ g (œÜ x) :=
  congr($(compatible g) x)

instance comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k G) (B : Rep k H) (C : Rep k K) (f : G ‚Üí* H) (g : H ‚Üí* K) (œÜ : B ‚Üí‚Çó[k] A)
    (œà : C ‚Üí‚Çó[k] B) [IsPairMap A B f œÜ] [IsPairMap B C g œà] :
    IsPairMap A C (g.comp f) (œÜ.comp œà) where
  compatible x := by
    ext y
    have := congr($(compatible (A := A) (B := B) (f := f) (œÜ := œÜ) x) (œà y))
    have := congr($(compatible (A := B) (B := C) (f := g) (œÜ := œà) (f x)) y)
    simp_all

instance instInf [S.Normal] : IsPairMap A (inf A S) (QuotientGroup.mk' S)
    (invariants (A.œÅ.comp S.subtype)).subtype where
  compatible := by intros; rfl

instance instRes : IsPairMap ((Action.res _ f).obj B) B f LinearMap.id where
  compatible := by intros; rfl

instance instHom {A B : Rep k G} (f : A ‚ü∂ B) : IsPairMap B A (MonoidHom.id G) f.hom where
  compatible := f.comm

variable [IsPairMap A B f œÜ]

variable (A B f œÜ) in
@[simps (config := .lemmasOnly)]
noncomputable def cochainsMap :
    inhomogeneousCochains B ‚ü∂ inhomogeneousCochains A where
  f i := ModuleCat.ofHom (œÜ.compLeft (Fin i ‚Üí G)
    ‚àò‚Çó LinearMap.funLeft k B (fun x : Fin i ‚Üí G => (f ‚àò x)))
  comm' i j (hij : _ = _) := by
    subst hij
    ext x
    funext g
    simp only [CochainComplex.of_x, inhomogeneousCochains.d_def, ModuleCat.coe_comp,
      Function.comp_apply]
    simpa [ModuleCat.ofHom, ModuleCat.coe_of, ModuleCat.hom_def, Fin.comp_contractNth]
      using (compatible_apply _ _).symm

@[simp]
lemma cochainsMap_f_apply (n : ‚Ñï) (x : (inhomogeneousCochains B).X n) (g : Fin n ‚Üí G) :
    (cochainsMap A B f œÜ).f n x g = œÜ (x (f ‚àò g)) :=
  rfl

@[simp]
lemma cochainsMap_id :
    cochainsMap A A (MonoidHom.id _) (Action.Hom.hom <| ùüô A) = ùüô (inhomogeneousCochains A) := by
  rfl

lemma cochainsMap_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k G) (B : Rep k H) (C : Rep k K) (f : G ‚Üí* H) (g : H ‚Üí* K) (œÜ : B ‚Üí‚Çó[k] A)
    (œà : C ‚Üí‚Çó[k] B) [IsPairMap A B f œÜ] [IsPairMap B C g œà] :
    cochainsMap A C (g.comp f) (œÜ.comp œà) = (cochainsMap B C g œà) ‚â´ (cochainsMap A B f œÜ) := by
  rfl

variable (A B f œÜ)
noncomputable abbrev cocyclesMap (n : ‚Ñï) :
    groupCohomology.cocycles B n ‚ü∂ groupCohomology.cocycles A n :=
  HomologicalComplex.cyclesMap (cochainsMap A B f œÜ) n

noncomputable abbrev cohomologyMap (n : ‚Ñï) :
  groupCohomology B n ‚ü∂ groupCohomology A n :=
HomologicalComplex.homologyMap (cochainsMap A B f œÜ) n

@[reassoc (attr := simp)]
lemma cochainsMap_f_0_comp_zeroCochainsLEquiv :
    (cochainsMap A B f œÜ).f 0 ‚â´ (zeroCochainsLEquiv A : (inhomogeneousCochains A).X 0 ‚Üí‚Çó[k] A)
      = (zeroCochainsLEquiv B : (inhomogeneousCochains B).X 0 ‚Üí‚Çó[k] B) ‚â´ ModuleCat.ofHom œÜ := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f ‚àò _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_1_comp_oneCochainsLEquiv :
    (cochainsMap A B f œÜ).f 1 ‚â´ (oneCochainsLEquiv A : (inhomogeneousCochains A).X 1 ‚Üí‚Çó[k] G ‚Üí A)
      = (oneCochainsLEquiv B).toModuleIso.hom
      ‚â´ ModuleCat.ofHom (œÜ.compLeft G ‚àò‚Çó LinearMap.funLeft k B f) := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f ‚àò _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_2_comp_twoCochainsLEquiv :
    (cochainsMap A B f œÜ).f 2
      ‚â´ (twoCochainsLEquiv A : (inhomogeneousCochains A).X 2 ‚Üí‚Çó[k] G √ó G ‚Üí A)
      = (twoCochainsLEquiv B).toModuleIso.hom
      ‚â´ ModuleCat.ofHom (œÜ.compLeft (G √ó G) ‚àò‚Çó LinearMap.funLeft k B (Prod.map f f)) := by
  ext x
  funext g
  show œÜ (x _) = œÜ (x _)
  rcongr x
  fin_cases x <;> rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_3_comp_threeCochainsLEquiv :
    (cochainsMap A B f œÜ).f 3
      ‚â´ (threeCochainsLEquiv A : (inhomogeneousCochains A).X 3 ‚Üí‚Çó[k] G √ó G √ó G ‚Üí A)
      = (threeCochainsLEquiv B).toModuleIso.hom
      ‚â´ ModuleCat.ofHom (œÜ.compLeft (G √ó G √ó G)
        ‚àò‚Çó LinearMap.funLeft k B (Prod.map f (Prod.map f f))) := by
  ext x
  funext g
  show œÜ (x _) = œÜ (x _)
  rcongr x
  fin_cases x <;> rfl

open ShortComplex

def mapH0 : H0 B ‚Üí‚Çó[k] H0 A :=
  LinearMap.codRestrict _ (œÜ ‚àò‚Çó B.œÅ.invariants.subtype) fun ‚ü®c, hc‚ü© g => by
    simp [‚Üê compatible_apply (f := f) g c, hc (f g)]

@[simps]
def mapShortComplexH1 :
    shortComplexH1 B ‚ü∂ shortComplexH1 A where
  œÑ‚ÇÅ := ModuleCat.ofHom œÜ
  œÑ‚ÇÇ := ModuleCat.ofHom (œÜ.compLeft G ‚àò‚Çó LinearMap.funLeft k B f)
  œÑ‚ÇÉ := ModuleCat.ofHom (œÜ.compLeft (G √ó G) ‚àò‚Çó LinearMap.funLeft k B (Prod.map f f))
  comm‚ÇÅ‚ÇÇ := by
    ext x
    funext g
    dsimp [shortComplexH1, dZero]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  comm‚ÇÇ‚ÇÉ := by
    ext x
    funext g
    dsimp [shortComplexH1, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

noncomputable abbrev mapOneCocycles :
    oneCocycles B ‚Üí‚Çó[k] oneCocycles A :=
  ShortComplex.cyclesMap' (mapShortComplexH1 A B f œÜ) (shortComplexH1 B).moduleCatLeftHomologyData
    (shortComplexH1 A).moduleCatLeftHomologyData

noncomputable abbrev mapH1 : H1 B ‚Üí‚Çó[k] H1 A :=
  ShortComplex.leftHomologyMap' (mapShortComplexH1 A B f œÜ)
    (shortComplexH1 B).moduleCatLeftHomologyData
    (shortComplexH1 A).moduleCatLeftHomologyData

@[simp]
lemma subtype_comp_mapOneCocycles :
    (oneCocycles A).subtype ‚àò‚Çó mapOneCocycles A B f œÜ
      = œÜ.compLeft G ‚àò‚Çó LinearMap.funLeft k B f ‚àò‚Çó (oneCocycles B).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH1 A B f œÜ) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H1œÄ_comp_mapH1 :
    mapH1 A B f œÜ ‚àò‚Çó H1œÄ B = H1œÄ A ‚àò‚Çó mapOneCocycles A B f œÜ :=
  leftHomologyœÄ_naturality' (mapShortComplexH1 A B f œÜ) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoOneCocycles_hom :
    cocyclesMap A B f œÜ 1 ‚â´ (isoOneCocycles A).hom
      = (isoOneCocycles B).hom ‚â´ mapOneCocycles A B f œÜ := by
  simp_rw [‚Üê cancel_mono (moduleCatLeftHomologyData (shortComplexH1 A)).i, mapOneCocycles,
      Category.assoc, cyclesMap'_i, isoOneCocycles, ‚Üê Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH1_hom :
    cohomologyMap A B f œÜ 1 ‚â´ (isoH1 A).hom = (isoH1 B).hom ‚â´ mapH1 A B f œÜ := by
  simpa [‚Üê cancel_epi (groupCohomologyœÄ _ _), mapH1, Category.assoc]
    using (leftHomologyœÄ_naturality' (mapShortComplexH1 A B f œÜ)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

@[simps]
def mapShortComplexH2 :
    shortComplexH2 B ‚ü∂ shortComplexH2 A where
  œÑ‚ÇÅ := ModuleCat.ofHom (œÜ.compLeft G ‚àò‚Çó LinearMap.funLeft k B f)
  œÑ‚ÇÇ := ModuleCat.ofHom (œÜ.compLeft (G √ó G) ‚àò‚Çó LinearMap.funLeft k B (Prod.map f f))
  œÑ‚ÇÉ := ModuleCat.ofHom (œÜ.compLeft (G √ó G √ó G)
    ‚àò‚Çó LinearMap.funLeft k B (Prod.map f (Prod.map f f)))
  comm‚ÇÅ‚ÇÇ := by
    ext x
    funext g
    dsimp [shortComplexH2, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  comm‚ÇÇ‚ÇÉ := by
    ext x
    funext g
    dsimp [shortComplexH2, dTwo]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

noncomputable abbrev mapTwoCocycles :
    twoCocycles B ‚Üí‚Çó[k] twoCocycles A :=
  ShortComplex.cyclesMap' (mapShortComplexH2 A B f œÜ) (shortComplexH2 B).moduleCatLeftHomologyData
    (shortComplexH2 A).moduleCatLeftHomologyData

noncomputable abbrev mapH2 : H2 B ‚Üí‚Çó[k] H2 A :=
  ShortComplex.leftHomologyMap' (mapShortComplexH2 A B f œÜ)
    (shortComplexH2 B).moduleCatLeftHomologyData
    (shortComplexH2 A).moduleCatLeftHomologyData

@[simp]
lemma subtype_comp_mapTwoCocycles :
    (twoCocycles A).subtype ‚àò‚Çó mapTwoCocycles A B f œÜ
      = œÜ.compLeft (G √ó G) ‚àò‚Çó LinearMap.funLeft k B (Prod.map f f) ‚àò‚Çó (twoCocycles B).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH2 A B f œÜ) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H2œÄ_comp_mapH2 :
    mapH2 A B f œÜ ‚àò‚Çó H2œÄ B = H2œÄ A ‚àò‚Çó mapTwoCocycles A B f œÜ :=
  leftHomologyœÄ_naturality' (mapShortComplexH2 A B f œÜ) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoTwoCocycles_hom :
    cocyclesMap A B f œÜ 2 ‚â´ (isoTwoCocycles A).hom
      = (isoTwoCocycles B).hom ‚â´ mapTwoCocycles A B f œÜ := by
  simp_rw [‚Üê cancel_mono (moduleCatLeftHomologyData (shortComplexH2 A)).i, mapTwoCocycles,
      Category.assoc, cyclesMap'_i, isoTwoCocycles, ‚Üê Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH2_hom :
    cohomologyMap A B f œÜ 2 ‚â´ (isoH2 A).hom = (isoH2 B).hom ‚â´ mapH2 A B f œÜ := by
  simpa [‚Üê cancel_epi (groupCohomologyœÄ _ _), mapH2, Category.assoc]
    using (leftHomologyœÄ_naturality' (mapShortComplexH2 A B f œÜ)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

end IsPairMap

open IsPairMap

variable (k G) in
@[simps]
noncomputable def cochainsFunctor : Rep k G ‚•§ CochainComplex (ModuleCat k) ‚Ñï where
  obj A := inhomogeneousCochains A
  map f := cochainsMap _ _ (MonoidHom.id _) f.hom
  map_id _ := cochainsMap_id
  map_comp {X Y Z} œÜ œà := cochainsMap_comp Z Y X (MonoidHom.id G) (MonoidHom.id G) œà.hom œÜ.hom

instance : (cochainsFunctor k G).PreservesZeroMorphisms where
instance : (cochainsFunctor k G).Additive where

variable (k G) in
@[simps]
noncomputable def functor (n : ‚Ñï) : Rep k G ‚•§ ModuleCat k where
  obj A := groupCohomology A n
  map {A B} œÜ :=
    letI : IsPairMap B A (MonoidHom.id _) œÜ.hom := instHom œÜ
    cohomologyMap B A (MonoidHom.id _) œÜ.hom n
  map_id A := HomologicalComplex.homologyMap_id _ _
  map_comp f g := by
    simp only [‚Üê HomologicalComplex.homologyMap_comp]
    rfl

open ShortComplex

def mapShortExact (X : ShortComplex (Rep k G)) (H : ShortExact X) :
    ShortExact ((cochainsFunctor k G).mapShortComplex.obj X) :=
  HomologicalComplex.shortExact_of_degreewise_shortExact _ fun i => {
    exact := by
      rw [ShortComplex.moduleCat_exact_iff_range_eq_ker]
      have : LinearMap.range X.f.hom = LinearMap.ker X.g.hom :=
        (H.exact.map (forget‚ÇÇ (Rep k G) (ModuleCat k))).moduleCat_range_eq_ker
      show LinearMap.range (LinearMap.compLeft X.f.hom (Fin i ‚Üí G))
        = LinearMap.ker (LinearMap.compLeft X.g.hom (Fin i ‚Üí G))
      rw [LinearMap.range_compLeft, LinearMap.ker_compLeft, this]
    mono_f := letI := H.2
      (ModuleCat.mono_iff_injective _).2 <| Function.Injective.comp_left <|
        (ModuleCat.mono_iff_injective X.f.hom).1 <| (forget‚ÇÇ (Rep k G) (ModuleCat k)).map_mono X.f
    epi_g := letI := H.3
      (ModuleCat.epi_iff_surjective _).2 <| Function.Surjective.comp_left <|
        (ModuleCat.epi_iff_surjective X.g.hom).1 <| (forget‚ÇÇ (Rep k G) (ModuleCat k)).map_epi X.g }

end groupCohomology
