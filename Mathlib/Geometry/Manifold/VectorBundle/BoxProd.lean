/-
Copyright (c) 2024 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Geometry.Manifold.VectorBundle.Pullback

universe u u'

variable {M M' H H' EM EM' ­ЮЋю : Type*} (F F' : Type*)

variable [NontriviallyNormedField ­ЮЋю]
  [NormedAddCommGroup EM] [NormedSpace ­ЮЋю EM] [TopologicalSpace H] (I : ModelWithCorners ­ЮЋю EM H)
  [NormedAddCommGroup EM'] [NormedSpace ­ЮЋю EM'] [TopologicalSpace H']
    (I' : ModelWithCorners ­ЮЋю EM' H')

variable [TopologicalSpace M] [ChartedSpace H M] --[SmoothManifoldWithCorners I M]
variable [TopologicalSpace M'] [ChartedSpace H' M'] --[SmoothManifoldWithCorners I' M']

variable [NormedAddCommGroup F] [NormedSpace ­ЮЋю F]
  (E : M Рєњ Type u) [(x : M) Рєњ AddCommMonoid (E x)] [(x : M) Рєњ Module ­ЮЋю (E x)]
  [TopologicalSpace (Bundle.TotalSpace F E)] [(x : M) Рєњ TopologicalSpace (E x)]
  [FiberBundle F E] [VectorBundle ­ЮЋю F E] [SmoothVectorBundle F E I]

variable [NormedAddCommGroup F'] [NormedSpace ­ЮЋю F']
  (E' : M' Рєњ Type u') [(x : M') Рєњ AddCommMonoid (E' x)] [(x : M') Рєњ Module ­ЮЋю (E' x)]
  [TopologicalSpace (Bundle.TotalSpace F' E')] [(x : M') Рєњ TopologicalSpace (E' x)]
  [FiberBundle F' E'] [VectorBundle ­ЮЋю F' E']
  [SmoothVectorBundle F' E' I']

noncomputable section

open Bundle
open scoped Manifold

variable (M M') in
/-- The projection from a product of two manifolds onto the first factor, as a bundled smooth map.
-/
@[simps] def SmoothMap.fst : SmoothMap (I.prod I') I (M ├Ќ M') M where
  val := Prod.fst
  property := smooth_fst

variable (M M') in
/-- The projection from a product of two manifolds onto the second factor, as a bundled smooth map.
-/
@[simps] def SmoothMap.snd : SmoothMap (I.prod I') I' (M ├Ќ M') M' where
  val := Prod.snd
  property := smooth_snd

-- FIXME at the moment `Prod.fst *рхќ E` and `SmoothMap.fst M M' I I' *рхќ E` are different types and
-- only the latter carries a vector bundle instance.  Maybe the pullback smooth vector bundle
-- instance should not take a bundled smooth map, just a bare one?

/-- For vector bundles `E` and `E'`, the construction `E Ріъ E'`. -/
abbrev BoxProd := (SmoothMap.fst M M' I I' *рхќ E) ├ЌрхЄ (SmoothMap.snd M M' I I' *рхќ E')

/-- For vector bundles `E` and `E'`, the total space of `E Ріъ E'` is canonically isomorphic to the
product of the total spaces of `E` and `E'` -/
@[simps] def equivProd :
    TotalSpace (F ├Ќ F') (BoxProd I I' E E') РЅЃ TotalSpace F E ├Ќ TotalSpace F' E' where
  toFun p := (РЪеp.1.1, p.2.1РЪЕ, РЪеp.1.2, p.2.2РЪЕ)
  invFun p := РЪе(p.1.1, p.2.1), (p.1.2, p.2.2)РЪЕ
  left_inv _ := rfl
  right_inv _ := rfl

/-- For vector bundles `E` and `E'`, the canonical isomorphism from the total space of `E Ріъ E'` to
the product of the total spaces of `E` and `E'` is smooth. -/
theorem equivProd_smooth :
    Smooth ((I.prod I').prod ­ЮЊў(­ЮЋю, F ├Ќ F')) ((I.prod ­ЮЊў(­ЮЋю, F)).prod (I'.prod ­ЮЊў(­ЮЋю, F')))
      (equivProd F F' I I' E E') := by
  apply Smooth.prod_mk
  ┬и have hРѓЂ := Bundle.Prod.smooth_fst (I.prod I') F (SmoothMap.fst M M' I I' *рхќ E) F'
      (SmoothMap.snd M M' I I' *рхќ E')
    have hРѓѓ := Bundle.Pullback.smooth_lift F E I (I.prod I') (SmoothMap.fst M M' I I')
    exact hРѓѓ.comp hРѓЂ
  ┬и have hРѓЂ := Bundle.Prod.smooth_snd (I.prod I') F (SmoothMap.fst M M' I I' *рхќ E) F'
      (SmoothMap.snd M M' I I' *рхќ E')
    have hРѓѓ := Bundle.Pullback.smooth_lift F' E' I' (I.prod I') (SmoothMap.snd M M' I I')
    exact hРѓѓ.comp hРѓЂ

omit [(x : M) Рєњ Module ­ЮЋю (E x)] [VectorBundle ­ЮЋю F E] [SmoothVectorBundle F E I]
  [(x : M') Рєњ Module ­ЮЋю (E' x)] [VectorBundle ­ЮЋю F' E'] [SmoothVectorBundle F' E' I'] in
/-- For vector bundles `E` and `E'`, the canonical isomorphism from the product of the total spaces
of `E` and `E'` to the total space of `E Ріъ E'` is smooth. -/
theorem equivProd_symm_smooth :
    Smooth ((I.prod ­ЮЊў(­ЮЋю, F)).prod (I'.prod ­ЮЊў(­ЮЋю, F'))) ((I.prod I').prod ­ЮЊў(­ЮЋю, F ├Ќ F'))
      (equivProd F F' I I' E E').symm := by
  apply Bundle.Prod.smooth_of_smooth_fst_comp_of_smooth_snd_comp
  ┬и apply Bundle.Pullback.smooth_of_smooth_proj_comp_of_smooth_lift_comp
    ┬и apply Smooth.prod_map
      ┬и apply smooth_proj
      ┬и apply smooth_proj
    ┬и apply smooth_fst
  ┬и apply Bundle.Pullback.smooth_of_smooth_proj_comp_of_smooth_lift_comp
    ┬и apply Smooth.prod_map
      ┬и apply smooth_proj
      ┬и apply smooth_proj
    ┬и apply smooth_snd
