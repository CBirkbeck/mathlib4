/-
Copyright ¬© 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.CategoryTheory.Sites.Whiskering
import Mathlib.Geometry.Manifold.Sheaf.Basic
import Mathlib.Geometry.Manifold.Algebra.SmoothFunctions

/-! # The sheaf of smooth functions on a manifold

The sheaf of `ùïú`-smooth functions from a manifold `M` to a manifold `N` can be defined as a sheaf of
types using the construction `StructureGroupoid.LocalInvariantProp.sheaf` from the file
`Mathlib.Geometry.Manifold.Sheaf.Basic`.  In this file we write that down (a one-liner), then do the
work of upgrading this to a sheaf of [groups]/[abelian groups]/[rings]/[commutative rings] when `N`
carries more algebraic structure.  For example, if `N` is `ùïú` then the sheaf of smooth functions
from `M` to `ùïú` is a sheaf of commutative rings, the *structure sheaf* of `M`.

## Main definitions

* `smoothSheaf`: The sheaf of smooth functions from `M` to `N`, as a sheaf of types
* `smoothSheaf.eval`: Canonical map onto `N` from the stalk of `smoothSheaf IM I M N` at `x`,
  given by evaluating sections at `x`
* `smoothSheafGroup`, `smoothSheafCommGroup`, `smoothSheafRing`, `smoothSheafCommRing`: The
  sheaf of smooth functions into a [Lie group]/[abelian Lie group]/[smooth ring]/[smooth commutative
  ring], as a sheaf of [groups]/[abelian groups]/[rings]/[commutative rings]
* `smoothSheafCommGroup.compLeft`: For a manifold `M` and a smooth homomorphism `œÜ` between
  abelian Lie groups `A`, `A'`, the 'postcomposition-by-`œÜ`' morphism of sheaves from
  `smoothSheafCommGroup IM I M A` to `smoothSheafCommGroup IM I' M A'`

## TODO

There are variants of `smoothSheafCommGroup.compLeft` for `GroupCat`, `RingCat`, `CommRingCat`;
this is just boilerplate and can be added as needed.

The canonical "evaluation" map `smoothSheaf.eval` from the stalk at `x:M` of the sheaf of smooth
functions `M ‚Üí N` should be upgraded in the presence of algebraic structure on `N`: a group
homomorphism for `smoothSheafGroup` and `smoothSheafCommGroup`, a ring homomorphism for
`smoothSheafRing` and `smoothSheafCommRing`. Also, one wants to identify as types the stalk at
`x` of `smoothSheafGroup` (and similarly for the other algebraic categories) with the stalk at
`x` of `smoothSheaf`.  These tasks require engaging with the colimits API in the category theory
library, but should not be particularly hard.

Currently there is a universe restriction: one can consider the sheaf of smooth functions from `M`
to `N` only if `M` and `N` are in the same universe.  For example, since `‚ÑÇ` is in `Type`, we can
only consider the structure sheaf of complex manifolds in `Type`, which is unsatisfactory. The
obstacle here is in the underlying category theory constructions, and there is WIP (as of June 2023)
to fix this.  See
https://github.com/leanprover-community/mathlib/pull/19153
and cross-references there.
-/


noncomputable section
open scoped Manifold
open TopologicalSpace Opposite

universe u

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {EM : Type*} [NormedAddCommGroup EM] [NormedSpace ùïú EM]
  {HM : Type*} [TopologicalSpace HM] (IM : ModelWithCorners ùïú EM HM)
  variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ùïú E H)
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners ùïú E H')
  (M : Type u) [TopologicalSpace M] [ChartedSpace HM M]
  (N G A A' R : Type u) [TopologicalSpace N] [ChartedSpace H N]
  [TopologicalSpace G] [ChartedSpace H G] [TopologicalSpace A] [ChartedSpace H A]
  [TopologicalSpace A'] [ChartedSpace H' A'] [TopologicalSpace R] [ChartedSpace H R]

section TypeCat

/-- The sheaf of smooth functions from `M` to `N`, as a sheaf of types. -/
def smoothSheaf : TopCat.Sheaf (Type u) (TopCat.of M) :=
  (contDiffWithinAt_localInvariantProp IM I ‚ä§).sheaf M N

variable {M}

instance smoothSheaf.has_coe_to_fun (U : (Opens (TopCat.of M))·µí·µñ) :
    CoeFun ((smoothSheaf IM I M N).val.obj U) (fun _ ‚Ü¶ ‚Üë(unop U) ‚Üí N) :=
  (contDiffWithinAt_localInvariantProp IM I ‚ä§).sheafHasCoeToFun _ _ _

/-- The object of `smoothSheaf IM I M N` for the open set `U` in `M` is
`C^‚àû‚üÆIM, (unop U : Opens M); I, N‚üØ`, the `(IM, I)`-smooth functions from `U` to `N`.  This is not
just a "moral" equality but a literal and definitional equality! -/
lemma smoothSheaf.obj_eq (U : (Opens (TopCat.of M))·µí·µñ) :
    (smoothSheaf IM I M N).val.obj U = C^‚àû‚üÆIM, (unop U : Opens M); I, N‚üØ := rfl

/-- Canonical map from the stalk of `smoothSheaf IM I M N` at `x` to `N`, given by evaluating
sections at `x`. -/
def smoothSheaf.eval (x : M) : (smoothSheaf IM I M N).presheaf.stalk x ‚Üí N :=
  TopCat.stalkToFiber (StructureGroupoid.LocalInvariantProp.localPredicate M N _) x

/-- The `eval` map is surjective at `x`. -/
lemma smoothSheaf.eval_surjective (x : M) : Function.Surjective (smoothSheaf.eval IM I N x) := by
  apply TopCat.stalkToFiber_surjective
  intros n
  exact ‚ü®‚ä§, fun _ ‚Ü¶ n, smooth_const, rfl‚ü©

variable {IM I N}

@[simp] lemma smoothSheaf.eval_germ (U : Opens (TopCat.of M)) (x : U)
    (f : (smoothSheaf IM I M N).val.obj (op U)) :
    smoothSheaf.eval IM I N (x : TopCat.of M) ((smoothSheaf IM I M N).presheaf.germ x f)
    = f x :=
  TopCat.stalkToFiber_germ _ U x f

lemma smoothSheaf.smooth_section {U : (Opens (TopCat.of M))·µí·µñ}
    (f : (smoothSheaf IM I M N).val.obj U) :
    Smooth IM I f :=
(contDiffWithinAt_localInvariantProp IM I ‚ä§).section_spec _ _ _ _

end TypeCat

section LieAddGroup
variable [AddGroup G] [LieAddGroup I G]

instance (U : (Opens (TopCat.of M))·µí·µñ) : AddGroup ((smoothSheaf IM I M G).val.obj U) :=
  (SmoothMap.addGroup : AddGroup C^‚àû‚üÆIM, (unop U : Opens M); I, G‚üØ)

/-- The presheaf of smooth functions from `M` to `G`, for `G` an additive Lie group, as a presheaf
of additive groups. -/
def smoothPresheafAddGroup : TopCat.Presheaf AddGroupCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ AddGroupCat.of ((smoothSheaf IM I M G).val.obj U)
    map := fun h ‚Ü¶ AddGroupCat.ofHom <|
      SmoothMap.restrictAddMonoidHom IM I G <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

end LieAddGroup

section LieGroup
variable [Group G] [LieGroup I G]

@[to_additive existing]
instance (U : (Opens (TopCat.of M))·µí·µñ) : Group ((smoothSheaf IM I M G).val.obj U) :=
  (SmoothMap.group : Group C^‚àû‚üÆIM, (unop U : Opens M); I, G‚üØ)

/-- The presheaf of smooth functions from `M` to `G`, for `G` a Lie group, as a presheaf of groups.
-/
@[to_additive existing]
def smoothPresheafGroup : TopCat.Presheaf GroupCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ GroupCat.of ((smoothSheaf IM I M G).val.obj U)
    map := fun h ‚Ü¶ GroupCat.ofHom <|
      SmoothMap.restrictMonoidHom IM I G <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

/-- The sheaf of smooth functions from `M` to `G`, for `G` a Lie group, as a sheaf of
groups. -/
@[to_additive "The sheaf of smooth functions from `M` to `G`, for `G` an additive Lie group, as a
sheaf of additive groups."]
noncomputable def smoothSheafGroup : TopCat.Sheaf GroupCat.{u} (TopCat.of M) :=
  { val := smoothPresheafGroup IM I M G
    cond := by
      rw [CategoryTheory.Presheaf.isSheaf_iff_isSheaf_forget _ _ (CategoryTheory.forget GroupCat)]
      exact CategoryTheory.Sheaf.cond (smoothSheaf IM I M G) }

end LieGroup

section AddCommLieGroup
variable [AddCommGroup A] [AddCommGroup A'] [LieAddGroup I A] [LieAddGroup I' A']

instance (U : (Opens (TopCat.of M))·µí·µñ) : AddCommGroup ((smoothSheaf IM I M A).val.obj U) :=
  (SmoothMap.addCommGroup : AddCommGroup C^‚àû‚üÆIM, (unop U : Opens M); I, A‚üØ)

/-- The presheaf of smooth functions from `M` to `A`, for `A` an additive abelian Lie group, as a
presheaf of additive abelian groups. -/
def smoothPresheafAddCommGroup : TopCat.Presheaf AddCommGroupCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ AddCommGroupCat.of ((smoothSheaf IM I M A).val.obj U)
    map := fun h ‚Ü¶ AddCommGroupCat.ofHom <|
      SmoothMap.restrictAddMonoidHom IM I A <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

end AddCommLieGroup

section CommLieGroup
variable [CommGroup A] [CommGroup A'] [LieGroup I A] [LieGroup I' A']

@[to_additive existing]
instance (U : (Opens (TopCat.of M))·µí·µñ) : CommGroup ((smoothSheaf IM I M A).val.obj U) :=
  (SmoothMap.commGroup : CommGroup C^‚àû‚üÆIM, (unop U : Opens M); I, A‚üØ)

/-- The presheaf of smooth functions from `M` to `A`, for `A` an abelian Lie group, as a
presheaf of abelian groups. -/
@[to_additive existing]
def smoothPresheafCommGroup : TopCat.Presheaf CommGroupCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ CommGroupCat.of ((smoothSheaf IM I M A).val.obj U)
    map := fun h ‚Ü¶ CommGroupCat.ofHom <|
      SmoothMap.restrictMonoidHom IM I A <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

/-- The sheaf of smooth functions from `M` to `A`, for `A` an abelian Lie group, as a
sheaf of abelian groups. -/
@[to_additive "The sheaf of smooth functions from `M` to
`A`, for `A` an abelian additive Lie group, as a sheaf of abelian additive groups."]
noncomputable def smoothSheafCommGroup : TopCat.Sheaf CommGroupCat.{u} (TopCat.of M) :=
  { val := smoothPresheafCommGroup IM I M A
    cond := by
      rw [CategoryTheory.Presheaf.isSheaf_iff_isSheaf_forget _ _ (CategoryTheory.forget CommGroupCat)]
      exact CategoryTheory.Sheaf.cond (smoothSheaf IM I M A) }

/-- For a manifold `M` and a smooth homomorphism `œÜ` between abelian Lie groups `A`, `A'`, the
'left-composition-by-`œÜ`' morphism of sheaves from `smoothSheafCommGroup IM I M A` to
`smoothSheafCommGroup IM I' M A'`. -/
def smoothSheafCommGroup.compLeft (œÜ : A ‚Üí* A') (hœÜ : Smooth I I' œÜ) :
    smoothSheafCommGroup IM I M A ‚ü∂ smoothSheafCommGroup IM I' M A' :=
  CategoryTheory.Sheaf.Hom.mk <|
  { app := fun _ ‚Ü¶ CommGroupCat.ofHom <| SmoothMap.compLeftMonoidHom _ _ œÜ hœÜ
    naturality := fun _ _ _ ‚Ü¶ rfl }

end CommLieGroup

section AddCommLieGroup
variable [AddCommGroup A] [AddCommGroup A'] [LieAddGroup I A] [LieAddGroup I' A']

/-- For a manifold `M` and a smooth homomorphism `œÜ` between abelian additive Lie groups
`A`, `A'`, the 'left-composition-by-`œÜ`' morphism of sheaves from
`smoothSheafAddCommGroup IM I M A` to `smoothSheafAddCommGroup IM I' M A'`. -/
def smoothSheafAddCommGroup.compLeft (œÜ : A ‚Üí+ A') (hœÜ : Smooth I I' œÜ) :
    smoothSheafAddCommGroup IM I M A ‚ü∂ smoothSheafAddCommGroup IM I' M A' :=
  CategoryTheory.Sheaf.Hom.mk <|
  { app := fun _ ‚Ü¶ AddCommGroupCat.ofHom <| SmoothMap.compLeftAddMonoidHom _ _ œÜ hœÜ
    naturality := fun _ _ _ ‚Ü¶ rfl }

attribute [to_additive existing] smoothSheafCommGroup.compLeft

end AddCommLieGroup

section SmoothRing
variable [Ring R] [SmoothRing I R]

instance (U : (Opens (TopCat.of M))·µí·µñ) : Ring ((smoothSheaf IM I M R).val.obj U) :=
  (SmoothMap.ring : Ring C^‚àû‚üÆIM, (unop U : Opens M); I, R‚üØ)

/-- The presheaf of smooth functions from `M` to `R`, for `R` a smooth ring, as a presheaf
of rings. -/
def smoothPresheafRing : TopCat.Presheaf RingCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ RingCat.of ((smoothSheaf IM I M R).val.obj U)
    map := fun h ‚Ü¶ RingCat.ofHom <|
      SmoothMap.restrictRingHom IM I R <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

/-- The sheaf of smooth functions from `M` to `R`, for `R` a smooth ring, as a sheaf of
rings. -/
def smoothSheafRing : TopCat.Sheaf RingCat.{u} (TopCat.of M) :=
  { val := smoothPresheafRing IM I M R
    cond := by
      rw [CategoryTheory.Presheaf.isSheaf_iff_isSheaf_forget _ _ (CategoryTheory.forget RingCat)]
      exact CategoryTheory.Sheaf.cond (smoothSheaf IM I M R) }

end SmoothRing

section SmoothCommRing
variable [CommRing R] [SmoothRing I R]

instance (U : (Opens (TopCat.of M))·µí·µñ) : CommRing ((smoothSheaf IM I M R).val.obj U) :=
  (SmoothMap.commRing : CommRing C^‚àû‚üÆIM, (unop U : Opens M); I, R‚üØ)

/-- The presheaf of smooth functions from `M` to `R`, for `R` a smooth commutative ring, as a
presheaf of commutative rings. -/
def smoothPresheafCommRing : TopCat.Presheaf CommRingCat.{u} (TopCat.of M) :=
  { obj := fun U ‚Ü¶ CommRingCat.of ((smoothSheaf IM I M R).val.obj U)
    map := fun h ‚Ü¶ CommRingCat.ofHom <|
      SmoothMap.restrictRingHom IM I R <| CategoryTheory.leOfHom h.unop
    map_id := fun _ ‚Ü¶ rfl
    map_comp := fun _ _ ‚Ü¶ rfl }

/-- The sheaf of smooth functions from `M` to `R`, for `R` a smooth commutative ring, as a sheaf of
commutative rings. -/
def smoothSheafCommRing : TopCat.Sheaf CommRingCat.{u} (TopCat.of M) :=
  { val := smoothPresheafCommRing IM I M R
    cond := by
      rw [CategoryTheory.Presheaf.isSheaf_iff_isSheaf_forget _ _ (CategoryTheory.forget CommRingCat)]
      exact CategoryTheory.Sheaf.cond (smoothSheaf IM I M R) }

-- sanity check: applying the `CommRingCat`-to-`TypeCat` forgetful functor to the sheaf-of-rings of
-- smooth functions gives the sheaf-of-types of smooth functions.
example : (CategoryTheory.sheafCompose _ (CategoryTheory.forget CommRingCat)).obj
    (smoothSheafCommRing IM I M R) = (smoothSheaf IM I M R) := rfl

end SmoothCommRing
