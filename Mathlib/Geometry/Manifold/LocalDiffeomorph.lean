/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/

import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.Diffeomorph
import Mathlib.Geometry.Manifold.MFDeriv

/-!
# Local diffeomorphisms between smooth manifolds

In this file, we define `C^n` local diffeomorphisms between manifolds.

A `C^n` map `f : M ‚Üí N` is a **local diffeomorphism at `x`** iff there are neighbourhoods `s`
and `t` of `x` and `f x`, respectively such that `f` restricts to a diffeomorphism from `s` and `t`.
`f` is called a **local diffeomorphism** iff it is a local diffeomorphism at every `x ‚àà M`.

## Main definitions
* `LocalDiffeomorphAt I J M N n f x`: `f` is a `C^n` local diffeomorphism at `x`
* `LocalDiffeomorph I J M N n f`: `f` is a `C^n` local diffeomorphism

## Main results
* Each of `Diffeomorph`, `LocalDiffeomorph`, and `LocalDiffeomorphAt` implies the next condition.
* `LocalDiffeomorph.image`: the image of a local diffeomorphism is open

## TODO
* a local diffeomorphism is a diffeomorphism to its image
* a bijective local diffeomorphism is a diffeomorphism.
* each differential of a `C^n` diffeomorphism (`n ‚â• 1`) is a linear equivalence.
* if `f` is a local diffeomorphism at `x`, the differential `mfderiv I J n f x` is a continuous linear isomorphism.
* conversely, if `f` is `C^n` at `x` and `mfderiv I J n f x` is a linear isomorphism,
`f` is a local diffeomorphism at `x`.
* if `f` is a local diffeomorphism, each differential `mfderiv I J n f x`
is a continuous linear isomorphism.
* Conversely, if `f` is `C^n` and each differential is a linear isomorphism,
`f` is a local diffeomorphism.


## Design decisions
TODO: flesh this out!

## Tags
local diffeomorphism, manifold

-/

open Manifold Set  TopologicalSpace

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {H : Type*} [TopologicalSpace H]
  {G : Type*} [TopologicalSpace G]
  (I : ModelWithCorners ùïú E H) (J : ModelWithCorners ùïú F G)
  (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] (n : ‚Ñï‚àû)

section LocalDiffeomorphAux
/-- A "diffeomorphism on" `s` is a function `f : M ‚Üí N` such that `f` restricts to a diffeomorphism
`s ‚Üí t` between open subsets of `M` and `N`, respectively.
This is an auxiliary definition and should not be used outside of this file. -/
structure LocalDiffeomorphAux extends LocalHomeomorph M N where
  contMDiffOn_toFun : ContMDiffOn I J n toFun source
  contMDiffOn_invFun : ContMDiffOn J I n invFun target

/-- Coercion of a `LocalDiffeomorphAux` to function.
Note that a `LocalDiffeomorphAux` is not `FunLike` (like `LocalHomeomorph`),
as `toFun` doesn't determine `invFun` outside of `target`. -/
instance : CoeFun (LocalDiffeomorphAux I J M N n) fun _ => M ‚Üí N :=
  ‚ü®fun Œ¶ => Œ¶.toFun'‚ü©

/-- A diffeomorphism is a local diffeomorphism. -/
def Diffeomorph.toLocalDiffeomorphAux (h : Diffeomorph I J M N n) : LocalDiffeomorphAux I J M N n :=
  {
    toLocalHomeomorph := h.toHomeomorph.toLocalHomeomorph
    contMDiffOn_toFun := fun x _ ‚Ü¶ h.contMDiff_toFun x
    contMDiffOn_invFun := fun _ _ ‚Ü¶ h.symm.contMDiffWithinAt
  }

-- Add the very basic API we need.
namespace LocalDiffeomorphAux
variable (Œ¶ : LocalDiffeomorphAux I J M N n) (hn : 1 ‚â§ n)
protected theorem contMDiffOn : ContMDiffOn I J n Œ¶ Œ¶.source :=
  Œ¶.contMDiffOn_toFun

protected theorem contMDiffOn_symm : ContMDiffOn J I n Œ¶.invFun Œ¶.target :=
  Œ¶.contMDiffOn_invFun

protected theorem mdifferentiableOn : MDifferentiableOn I J Œ¶ Œ¶.source :=
  (Œ¶.contMDiffOn).mdifferentiableOn hn

protected theorem mdifferentiableOn_symm : MDifferentiableOn J I Œ¶.invFun Œ¶.target :=
  (Œ¶.contMDiffOn_symm).mdifferentiableOn hn

protected theorem mdifferentiableAt {x : M} (hx : x ‚àà Œ¶.source) : MDifferentiableAt I J Œ¶ x :=
  (Œ¶.mdifferentiableOn hn x hx).mdifferentiableAt (Œ¶.open_source.mem_nhds hx)

-- define symm just to make this easier to write?
protected theorem mdifferentiableAt_symm {x : M} (hx : x ‚àà Œ¶.source) :
    MDifferentiableAt J I Œ¶.invFun (Œ¶ x) :=
  (Œ¶.mdifferentiableOn_symm hn (Œ¶ x) (Œ¶.map_source hx)).mdifferentiableAt
  (Œ¶.open_target.mem_nhds (Œ¶.map_source hx))

/- We could add lots of additional API (following `Diffeomorph` and `LocalHomeomorph*), such as
* further continuity and differentiability lemmas
* refl, symm and trans instances; lemmas between them.
As this declaration is meant for internal use only, we keep it simple. -/
end LocalDiffeomorphAux
end LocalDiffeomorphAux

/-- `f : M ‚Üí N` is called a **`C^n` local diffeomorphism at *x*** iff there exist
  open sets `U ‚àã x` and `V ‚àã f x` and a diffeomorphism `Œ¶ : U ‚Üí V` such that `f = Œ¶` on `U`. -/
def IsLocalDiffeomorphAt (f : M ‚Üí N) (x : M) : Prop :=
  ‚àÉ Œ¶ : LocalDiffeomorphAux I J M N n, x ‚àà Œ¶.source ‚àß EqOn f Œ¶ Œ¶.source

/-- `f : M ‚Üí N` is a **`C^n` local diffeomorphism** iff it is a local diffeomorphism
at each `x ‚àà M`. -/
def IsLocalDiffeomorph (f : M ‚Üí N) : Prop :=
  ‚àÄ x : M, IsLocalDiffeomorphAt I J M N n f x

lemma isLocalDiffeomorph_iff {f : M ‚Üí N} :
    IsLocalDiffeomorph I J M N n f ‚Üî ‚àÄ x : M, IsLocalDiffeomorphAt I J M N n f x := by rfl

/-- A `C^n` diffeomorphism is a local diffeomorphism. -/
lemma Diffeomorph.isLocalDiffeomorph (Œ¶ : M ‚âÉ‚Çò^n‚üÆI, J‚üØ N) : IsLocalDiffeomorph I J M N n Œ¶ :=
  fun _ ‚Ü¶ ‚ü®Œ¶.toLocalDiffeomorphAux, by trivial, eqOn_refl Œ¶ _‚ü©

/-- The image of a local diffeomorphism is open. -/
def LocalDiffeomorph.image {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J M N n f) : Opens N := by
  refine ‚ü®range f, ?_‚ü©
  apply isOpen_iff_forall_mem_open.mpr
  intro y hy

  -- Given `y = f x ‚àà range f`, we need to find `V ‚äÜ N` open containing `y`.
  rw [mem_range] at hy
  rcases hy with ‚ü®x, hxy‚ü©

  -- As f is a local diffeo at x, on some open set `U' ‚àã x` it agrees with a diffeo `Œ¶ : U' ‚Üí V'`.
  choose Œ¶ hyp using hf x
  rcases hyp with ‚ü®hxU, heq‚ü©
  -- Then `V:=Œ¶.target` has the desired properties.
  refine ‚ü®Œ¶.target, ?_, Œ¶.open_target, ?_‚ü©
  ¬∑ rw [‚Üê LocalHomeomorph.image_source_eq_target, ‚Üê heq.image_eq]
    exact image_subset_range f Œ¶.source
  ¬∑ rw [‚Üê hxy, heq hxU]
    exact Œ¶.toLocalHomeomorph.map_source hxU

lemma LocalDiffeomorph.image_coe {f : M ‚Üí N} (hf : IsLocalDiffeomorph I J M N n f) :
    (LocalDiffeomorph.image I J M N n hf).1 = range f := rfl
