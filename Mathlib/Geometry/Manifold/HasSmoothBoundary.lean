/-
Copyright (c) 2024 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
import Mathlib.Geometry.Manifold.ContMDiff.Constructions
import Mathlib.Geometry.Manifold.IsManifold.InteriorBoundary
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.MFDeriv.Defs
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions

/-!
# Smooth manifolds with smooth boundary

Many manifolds coming up in differential geometry or applications have "nice" boundary,
i.e. the boundary is again a (smooth) manifold one dimension lower.
The definition `IsManifold` does not enforce this, in order to also include manifolds
with corners. In this file, we define a typeclass `HasNiceBoundary`, for smooth manifolds whose
boundary is again a smooth manifold such that the inclusion $‚àÇM ‚Üí M` is smooth.
We do *not* require that `M` or `‚àÇM` be finite-dimensional
(nor that, in the finite case, `‚àÇM` have co-dimension one).

## Main definitions and results

* `BoundaryManifoldData I E‚ÇÄ H‚ÇÄ I‚ÇÄ` encodes a smooth manifold `M` modelled on `I` having smooth
  boundary: this is encoded by a pair (M‚ÇÄ, f) of a `C^n` manifold `M‚ÇÄ` modelled on `I‚ÇÄ`
  over the pair `(E‚ÇÄ, H‚ÇÄ)` and a smooth embedding `f: M‚ÇÄ ‚Üí M` whose image is precisely `‚àÇM`.

* `BoundaryManifoldData.of_boundaryless`: a boundaryless manifold has smooth boundary
  (namely, any empty type)
* `BoundaryManifoldData.Icc`: a real interval `[x, y]` (for `x < y`) has smooth boundary
* `BoundaryManifoldData.prod_of_boundaryless_left`: if `M` is boundaryless and `N` has smooth
  boundary, so does `M √ó N`
* `BoundaryManifoldData.prod_of_boundaryless_right`: if `M` has smooth boundary and `N` is
  boundaryless, `M √ó N` has smooth boundary
* `BoundaryManifoldData.sum`: if `M` and `N` are modelled on the same model `I` and have smooth
  boundary, so does their disjoint union `M ‚äï N`

## TODO
* `BoundaryManifoldData.euclideanHalfSpace_self`: n-dimensional Euclidean half-space has smooth
  boundary (e.g., `n-1`-dimensional Euclidean space)
* if `M` is `n`-dimensional and modelled on Euclidean half-space
  (such that the model is surjective),
  it has smooth boundary: this might require e.g. invariance of domain

-/

open scoped Manifold

-- Let M, M' and M'' be smooth manifolds *over the same space* `H`, with *the same* `model `I`.
variable {E E' E'' E''' H H' H'' H''' : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup E'] [NormedSpace ‚Ñù E'] [NormedAddCommGroup E'']  [NormedSpace ‚Ñù E'']
  [NormedAddCommGroup E'''] [NormedSpace ‚Ñù E''']
  [TopologicalSpace H] [TopologicalSpace H'] [TopologicalSpace H''] [TopologicalSpace H''']

variable {M : Type*} [TopologicalSpace M] [cm : ChartedSpace H M]
  {I : ModelWithCorners ‚Ñù E H} [IsManifold I ‚ä§ M]
  {M' : Type*} [TopologicalSpace M'] [cm': ChartedSpace H M'] [IsManifold I ‚ä§ M']
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H M'']
  {I'' : ModelWithCorners ‚Ñù E H} [IsManifold I ‚ä§ M'']

/-- Let `M` be a `C^k` real manifold, modelled on the pair `(E, H)`. We say that `M` has nice
boundary if exist a smooth manifold `N` and a smooth embedding `œÜ : N ‚Ü™ M` with image `‚àÇM`.

`BoundaryManifoldData` is a data-carrying structure which captures this: it consists of a smooth
manifold `N` and a smooth embedding `f : M‚ÇÄ ‚Üí M` such that `range f = I.boundary M`.

A priori, we could allow the model spaces `E` and `H` for `N`, as well as the model with corners
on it, to vary freely: for applications in bordism theory, this proves impractical.
To formalise the statement "The manifold `W` has smooth boundary `M \sqcup N`", we could like
to consider the disjoint union of two BoundaryManifoldData: this works best if we fix the model
spaces and model with corners as part of their type.
For this reason, the `ModelWithCorners` (and the underlying pair `(E, H)`)
are part of this structure's parameters.

The first version of this said "I.boundary M is a smooth manifold".
This proved hard to work with, as I.boundary M is a subset, and computing the boundary means
we'd like to rewrite by an equivalence of sets. This runs into DTT, equality of types is bad.

Second version: we prescribe a smooth manifold M‚ÇÄ, and ask for a smooth embedding of M‚ÇÄ into M,
whose image is the boundary of M. This will allow rewriting the boundary.
A smooth embedding is characterised by having injective differential (being an immersion)
and being a topological embedding.
(Perhaps it's not good enough either, we'll see. Let's try!)

Is a pair `(M‚ÇÄ, f)` of a smooth manifold `M‚ÇÄ` modelled over `(E‚ÇÄ, H‚ÇÄ)` and an embedding
`f : M‚ÇÄ ‚Üí M` which is a smooth immersion, such that `range f = I.boundary M`.
-/
structure BoundaryManifoldData.{u} (M : Type*) [TopologicalSpace M] [ChartedSpace H M]
    (I : ModelWithCorners ‚Ñù E H) (k : WithTop ‚Ñï‚àû) [IsManifold I k M]
    {E‚ÇÄ H‚ÇÄ: Type*} [NormedAddCommGroup E‚ÇÄ] [NormedSpace ‚Ñù E‚ÇÄ]
    [TopologicalSpace H‚ÇÄ] (I‚ÇÄ : ModelWithCorners ‚Ñù E‚ÇÄ H‚ÇÄ) where
  /-- A `C^k` manifold `M‚ÇÄ` which describes the boundary of `M` -/
  M‚ÇÄ: Type u
  /-- `M‚ÇÄ` is a topological space-/
  [topologicalSpace: TopologicalSpace M‚ÇÄ]
  /-- A chosen charted space structure on `M‚ÇÄ` on `H‚ÇÄ` -/
  [chartedSpace : ChartedSpace H‚ÇÄ M‚ÇÄ]
  /-- `M‚ÇÄ` is a `C^k` manifold with corners w.r.t. `I‚ÇÄ` -/
  [isManifold : IsManifold I‚ÇÄ k M‚ÇÄ]
  /-- A `C^k` map from the model manifold into `M`, which is required to be a smooth embedding,
  i.e. a `C^k` immersion which is also a topological embedding -/
  f: M‚ÇÄ ‚Üí M
  isEmbedding: Topology.IsEmbedding f
  contMDiff: ContMDiff I‚ÇÄ I k f
  /-- If `f` is `C¬π`, it is an immersion: this condition is vacuous for `C‚Å∞` maps. -/
  isImmersion: (1 : WithTop ‚Ñï‚àû) ‚â§ k ‚Üí ‚àÄ x, Function.Injective (mfderiv I‚ÇÄ I f x)
  /-- `f` maps `M‚ÇÄ` surjectively to the boundary of `M`. -/
  range_eq_boundary: Set.range f = I.boundary M

variable {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {k : WithTop ‚Ñï‚àû}
  {I : ModelWithCorners ‚Ñù E H} [IsManifold I k M]
  {E‚ÇÄ H‚ÇÄ: Type*} [NormedAddCommGroup E‚ÇÄ] [NormedSpace ‚Ñù E‚ÇÄ]
  [TopologicalSpace H‚ÇÄ] (I‚ÇÄ : ModelWithCorners ‚Ñù E‚ÇÄ H‚ÇÄ)
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M'] [IsManifold I k M']
  {N : Type*} [TopologicalSpace N] [ChartedSpace H' N]
  {J : ModelWithCorners ‚Ñù E' H'} [IsManifold J k N]

instance (d : BoundaryManifoldData M I k I‚ÇÄ) : TopologicalSpace d.M‚ÇÄ := d.topologicalSpace

instance (d : BoundaryManifoldData M I k I‚ÇÄ) : ChartedSpace H‚ÇÄ d.M‚ÇÄ := d.chartedSpace

instance (d : BoundaryManifoldData M I k I‚ÇÄ) : IsManifold I‚ÇÄ k d.M‚ÇÄ := d.isManifold

variable (M) in
/-- If `M` is boundaryless, its boundary manifold data is easy to construct. -/
-- We can just take the empty manifold, with a vacuously defined map.
def BoundaryManifoldData.of_boundaryless [BoundarylessManifold I M] :
    BoundaryManifoldData M I k I where
  M‚ÇÄ := Empty
  chartedSpace := ChartedSpace.empty _ _
  f x := (IsEmpty.false x).elim
  isEmbedding := Topology.IsEmbedding.of_subsingleton _
  isManifold := by infer_instance
  isImmersion hk x := (IsEmpty.false x).elim
  range_eq_boundary := by
    have : I.boundary M = ‚àÖ := by
      rw [ModelWithCorners.Boundaryless.iff_boundary_eq_empty]
      infer_instance
    rw [this]
    simp [Empty.instIsEmpty]
  contMDiff x := (IsEmpty.false x).elim

-- TODO: fill in these sorries (low priority)
/-- The `n`-dimensional Euclidean half-space (modelled on itself) has nice boundary
(which is an `n-1`-dimensional manifold). -/
noncomputable def BoundaryManifoldData.euclideanHalfSpace_self (n : ‚Ñï) (k : WithTop ‚Ñï‚àû) :
    BoundaryManifoldData (EuclideanHalfSpace (n+1)) (ùì°‚àÇ (n + 1)) k (ùì° n) where
  M‚ÇÄ := EuclideanSpace ‚Ñù (Fin n)
  isManifold := by infer_instance
  f x := ‚ü®fun i ‚Ü¶ if h: i = 0 then 0 else x (Fin.pred i (by omega)), by simp‚ü©
  isEmbedding := sorry
  contMDiff := sorry
  isImmersion hk x := sorry
  range_eq_boundary := sorry

variable {X Y Z W : Type*} [TopologicalSpace X] [TopologicalSpace Y]
  [TopologicalSpace Z] [TopologicalSpace W]

open Topology

attribute [local instance] ChartedSpace.of_discreteTopology in
attribute [local instance] IsManifold.of_discreteTopology in
noncomputable def BoundaryManifoldData.Icc (k : WithTop ‚Ñï‚àû) :
    BoundaryManifoldData (Set.Icc (0 : ‚Ñù) 1) (ùì°‚àÇ 1) k (ùì° 0) where
  M‚ÇÄ := Fin 2
  f x := if h : x = 0 then ‚ä• else ‚ä§
  isEmbedding := by
    apply IsClosedEmbedding.isEmbedding
    apply IsClosedEmbedding.of_continuous_injective_isClosedMap
      continuous_of_discreteTopology
    ¬∑ intro x y h
      fin_cases x <;> fin_cases y <;> simp_all
    ¬∑ exact fun K _ ‚Ü¶ Set.Finite.isClosed (Finite.Set.finite_image K _)
  contMDiff := contMDiff_of_discreteTopology
  isImmersion hk x := by
    have : Subsingleton (TangentSpace (ùì° 0) x) := by
      change Subsingleton (EuclideanSpace ‚Ñù (Fin 0))
      infer_instance
    exact Function.injective_of_subsingleton _
  range_eq_boundary := by
    rw [boundary_Icc]
    ext x; constructor <;> intro h
    ¬∑ suffices x = ‚ä• ‚à® x = ‚ä§ by simpa
      choose y hy using h
      by_cases y = 0
      exacts [by left; simp_all, by right; simp_all]
    ¬∑ obtain (hx | hx) := h
      exacts [‚ü®0, by simp [hx.symm]‚ü©, ‚ü®1, by simp [hx.symm]‚ü©]

-- missing lemma: mfderiv of Prod.map (know it's smooth)
-- mathlib has versions for Prod.mk, also with left and right constant
section PrereqsDiffGeo

variable  {ùïú : Type u_1} [NontriviallyNormedField ùïú]

section

variable {E E' F F' : Type*}
  [NormedAddCommGroup E] [NormedSpace ùïú E] [NormedAddCommGroup E'] [NormedSpace ùïú E']
  [NormedAddCommGroup F] [NormedSpace ùïú F] [NormedAddCommGroup F'] [NormedSpace ùïú F']
variable {H H' H'' H''' : Type*} [TopologicalSpace H] [TopologicalSpace H']
  [TopologicalSpace H''] [TopologicalSpace H''']
  {I : ModelWithCorners ùïú E H} {I' : ModelWithCorners ùïú E' H'}
  {J : ModelWithCorners ùïú F H''} {J' : ModelWithCorners ùïú F' H'''}
variable {M M' N N' : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [TopologicalSpace M'] [ChartedSpace H' M']
  [TopologicalSpace N] [ChartedSpace H'' N] [TopologicalSpace N'] [ChartedSpace H''' N']
  {f : M ‚Üí N} {g : M' ‚Üí N'} {x : M} {x' : M'}

-- #check MDifferentiable.prod_map

lemma mfderiv_prodMap
    (hf : MDifferentiableAt I J f x) (hg : MDifferentiableAt I' J' g x') :
    mfderiv (I.prod I') (J.prod J') (Prod.map f g) (x, x')
    = (mfderiv I J f x).prodMap (mfderiv I' J' g x') := sorry

-- and variations for within, etc

end

variable {N' : Type*} [TopologicalSpace N'] [ChartedSpace H' N']

@[simp, mfld_simps]
theorem mfderiv_sumMap_at_inl {f : M ‚Üí N} (g : M' ‚Üí N') {p : M} (hf : MDifferentiableAt I J f p) :
    mfderiv I J (Sum.map f g) (Sum.inl p) = mfderiv I J f p := sorry

@[simp, mfld_simps]
theorem mfderiv_sumMap_at_inr (f : M ‚Üí N) {g : M' ‚Üí N'} {p : M'} (hg : MDifferentiableAt I J g p) :
    mfderiv I J (Sum.map f g) (Sum.inr p) = mfderiv I J g p := sorry

-- and variations for within, etc

end PrereqsDiffGeo

open Topology Set

variable {I‚ÇÄ} (M I) in
/-- If `M` is boundaryless and `N` has nice boundary, so does `M √ó N`. -/
def BoundaryManifoldData.prod_of_boundaryless_left [BoundarylessManifold I M]
    (bd : BoundaryManifoldData N J k I‚ÇÄ) :
    BoundaryManifoldData (M √ó N) (I.prod J) k (I.prod I‚ÇÄ) where
  M‚ÇÄ := M √ó bd.M‚ÇÄ
  f := Prod.map id bd.f
  isEmbedding := IsEmbedding.prodMap IsEmbedding.id bd.isEmbedding
  -- XXX: mathlib is currently renaming to prodMap and prodMk; update when that lands
  contMDiff := ContMDiff.prod_map contMDiff_id bd.contMDiff
  isImmersion hk x := by
    rw [mfderiv_prodMap mdifferentiableAt_id ((bd.contMDiff x.2).mdifferentiableAt hk)]
    apply Function.Injective.prodMap
    ¬∑ rw [mfderiv_id]
      exact fun ‚¶Éa‚ÇÅ a‚ÇÇ‚¶Ñ a ‚Ü¶ a
    ¬∑ exact bd.isImmersion hk _
  range_eq_boundary := by
    rw [range_prod_map, ModelWithCorners.boundary_of_boundaryless_left, range_id]
    congr
    exact bd.range_eq_boundary

variable {I‚ÇÄ} (N J) in
/-- If `M` has nice boundary and `N` is boundaryless, `M √ó N` has nice boundary. -/
def BoundaryManifoldData.prod_of_boundaryless_right (bd : BoundaryManifoldData M I k I‚ÇÄ)
    [BoundarylessManifold J N] : BoundaryManifoldData (M √ó N) (I.prod J) k (I‚ÇÄ.prod J) where
  M‚ÇÄ := bd.M‚ÇÄ √ó N
  f := Prod.map bd.f id
  isEmbedding := IsEmbedding.prodMap bd.isEmbedding IsEmbedding.id
  contMDiff := ContMDiff.prod_map bd.contMDiff contMDiff_id
  isImmersion hk x := by
    rw [mfderiv_prodMap ((bd.contMDiff x.1).mdifferentiableAt hk) mdifferentiableAt_id]
    apply Function.Injective.prodMap
    ¬∑ exact bd.isImmersion hk _
    ¬∑ rw [mfderiv_id]
      exact fun ‚¶Éa‚ÇÅ a‚ÇÇ‚¶Ñ a ‚Ü¶ a
  range_eq_boundary := by
    rw [range_prod_map, ModelWithCorners.boundary_of_boundaryless_right, range_id]
    congr
    exact bd.range_eq_boundary

/-- If `M` is an `n`-dimensional `C^k`-manifold modelled on finite-dimensional Euclidean half-space,
its boundary is an `n-1`-manifold.
TODO: this statement as-is is false, as its hypotheses are not strong enough; we also need that
M has boundary captured by the boundary of the half-space
(e.g., modelling a boundaryless manifold on the half-space should be excluded)

Proving this requires knowing homology groups of spheres (or similar). -/
def BoundaryManifoldData.of_Euclidean_halfSpace (n : ‚Ñï) (k : WithTop ‚Ñï‚àû)
    {M : Type} [TopologicalSpace M] [ChartedSpace (EuclideanHalfSpace (n + 1)) M]
    [IsManifold (ùì°‚àÇ (n + 1)) k M] : BoundaryManifoldData M (ùì°‚àÇ (n + 1)) k (ùì° n) := sorry

-- Proven in #22137; we will omit the proof here
lemma Topology.IsEmbedding.sumElim_of_separatedNhds {f : X ‚Üí Z} {g : Y ‚Üí Z}
    (hf : IsEmbedding f) (hg : IsEmbedding g) (hsep : SeparatedNhds (range f) (range g)) :
    IsEmbedding (Sum.elim f g) := sorry

/-- If `M` and `M'` are modelled on the same model `I` and have nice boundary over `I‚ÇÄ`,
their disjoint union also does. -/
noncomputable def BoundaryManifoldData.sum
    (bd : BoundaryManifoldData M I k I‚ÇÄ) (bd' : BoundaryManifoldData M' I k I‚ÇÄ) :
    BoundaryManifoldData (M ‚äï M') I k I‚ÇÄ where
  M‚ÇÄ := bd.M‚ÇÄ ‚äï bd'.M‚ÇÄ
  isManifold := by infer_instance
  f := Sum.map bd.f bd'.f
  isEmbedding := by
    apply IsEmbedding.sumElim_of_separatedNhds
    ¬∑ exact IsEmbedding.inl.comp bd.isEmbedding
    ¬∑ exact IsEmbedding.inr.comp bd'.isEmbedding
    ¬∑ use Set.range (@Sum.inl M M'), Set.range (@Sum.inr M M')
      refine ‚ü®isOpen_range_inl, isOpen_range_inr, ?_, ?_, ?_‚ü©
      ¬∑ rw [range_comp]; exact image_subset_range _ _
      ¬∑ rw [range_comp]; exact image_subset_range _ _
      ¬∑ rw [disjoint_iff]; ext; simp
  contMDiff := bd.contMDiff.sumMap bd'.contMDiff
  isImmersion hk p := by
    cases p with
    | inl x =>
      simp_rw [Sum.map_inl, mfderiv_sumMap_at_inl _ (bd.contMDiff.mdifferentiableAt hk)]
      exact bd.isImmersion hk x
    | inr x =>
      simp_rw [Sum.map_inr, mfderiv_sumMap_at_inr _ (bd'.contMDiff.mdifferentiableAt hk)]
      exact bd'.isImmersion hk x
  range_eq_boundary := by
    rw [Sum.range_eq, ModelWithCorners.boundary_disjointUnion]
    congr
    ¬∑ have : Sum.map bd.f bd'.f ‚àò Sum.inl = (@Sum.inl M M') ‚àò bd.f := by
        ext; simp
      rw [this, range_comp, bd.range_eq_boundary]
    ¬∑ have : Sum.map bd.f bd'.f ‚àò Sum.inr = (@Sum.inr M M') ‚àò bd'.f := by
        ext; simp
      rw [this, range_comp, bd'.range_eq_boundary]
