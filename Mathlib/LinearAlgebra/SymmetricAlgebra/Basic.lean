/-
Copyright (c) 2025 Raphael Douglas Giles. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Raphael Douglas Giles, Zhixuan Dai, Zhenyan Fu, Yiming Fu, Jingting Wang
-/
import Mathlib.LinearAlgebra.TensorAlgebra.Basic

/-!
# Symmetric Algebras

Given a commutative semiring `R`, and an `R`-module `L`, we construct the symmetric algebra of `L`.
This is the free commutative `R`-algebra generated (`R`-linearly) by the module `L`.

## Notation

1. `SymmetricAlgebra R L` is a concrete construction of the symmetric algebra defined as a
   quotient of the tensor algebra. It is endowed with an R-algebra structure and a commutative
   ring structure.
2. `SymmetricAlgebra.Œπ R` is the canonical R-linear map `L ‚Üí TensorAlgebra R L`.
3. Given a morphism `Œπ : L ‚Üí‚Çó[R] A`, `IsSymmetricAlgebra Œπ` is a proposition saying that the algebra
   homomorphism from `SymmetricAlgebra R L` to `A` is bijective.
4. Given a linear map `f : L ‚Üí‚Çó[R] A'` to an commutative R-algebra `A'`, and a morphism
   `Œπ : L ‚Üí‚Çó[R] A` with `p : IsSymmetricAlgebra Œπ`, `IsSymmetricAlgebra.lift p f`
   is the lift of `f` to an `R`-algebra morphism `A ‚Üí‚Çê[R] A'`.

-/

open RingQuot

universe u

variable (R L : Type*) [CommSemiring R] [AddCommMonoid L] [Module R L]

open TensorAlgebra in
/-- Relation on the tensor algebra which will yield the symmetric algebra when
quotiented out by. -/
inductive SymRel : TensorAlgebra R L ‚Üí TensorAlgebra R L ‚Üí Prop where
  | mul_comm (x y : L) : SymRel (Œπ R x * Œπ R y) (Œπ R y * Œπ R x)

/-- Concrete construction of the symmetric algebra of L by quotienting out
the tensor algebra by the commutativity relation. -/
abbrev SymmetricAlgebra := RingQuot (SymRel R L)

namespace SymmetricAlgebra

/-- Algebra homomorphism from the tensor algebra over L to the symmetric algebra over L. -/
abbrev algHom : TensorAlgebra R L ‚Üí‚Çê[R] SymmetricAlgebra R L := RingQuot.mkAlgHom R (SymRel R L)

lemma algHom_surjective : Function.Surjective (algHom R L) :=
  RingQuot.mkAlgHom_surjective _ _

/-- Canonical inclusion of `L` into the symmetric algebra `ùîñ R L`. -/
def Œπ : L ‚Üí‚Çó[R] SymmetricAlgebra R L := (algHom R L).toLinearMap.comp (TensorAlgebra.Œπ R (M := L))

@[elab_as_elim]
theorem induction {motive : SymmetricAlgebra R L ‚Üí Prop}
    (algebraMap : ‚àÄ r, motive (algebraMap R (SymmetricAlgebra R L) r)) (Œπ : ‚àÄ x, motive (Œπ R L x))
    (mul : ‚àÄ a b, motive a ‚Üí motive b ‚Üí motive (a * b))
    (add : ‚àÄ a b, motive a ‚Üí motive b ‚Üí motive (a + b))
    (a : SymmetricAlgebra R L) : motive a := by
  rcases algHom_surjective _ _ a with ‚ü®a, rfl‚ü©
  induction a using TensorAlgebra.induction with
  | algebraMap r => rw [AlgHom.map_algebraMap]; exact algebraMap r
  | Œπ x => exact Œπ x
  | mul x y hx hy => rw [map_mul]; exact mul _ _ hx hy
  | add x y hx hy => rw [map_add]; exact add _ _ hx hy

open TensorAlgebra in
instance : CommSemiring (SymmetricAlgebra R L) where
  mul_comm a b := by
    have Œπ_commute (x y : L) : Commute (Œπ R L x) (Œπ R L y) := by
      simp [commute_iff_eq, Œπ, ‚Üê map_mul, mkAlgHom_rel _ (SymRel.mul_comm x y)]
    change Commute a b
    induction b using SymmetricAlgebra.induction with
    | algebraMap r => exact Algebra.commute_algebraMap_right _ _
    | Œπ x => induction a using SymmetricAlgebra.induction with
      | algebraMap r => exact Algebra.commute_algebraMap_left _ _
      | Œπ y => exact Œπ_commute _ _
      | mul a b ha hb => exact ha.mul_left hb
      | add a b ha hb => exact ha.add_left hb
    | mul b c hb hc => exact hb.mul_right hc
    | add b c hb hc => exact hb.add_right hc

variable {R L} {A : Type*} [CommSemiring A] [Algebra R A] (f : L ‚Üí‚Çó[R] A)

/-- For any linear map `f : L ‚Üí‚Çó[R] A`, `SymmetricAlgebra.lift f` lifts the linear map to an
R-algebra homomorphism from `SymmetricAlgebra R L` to `A`. -/
def lift : SymmetricAlgebra R L ‚Üí‚Çê[R] A :=
  RingQuot.liftAlgHom R (s := SymRel R L) ‚ü®TensorAlgebra.lift R f, fun _ _ r ‚Ü¶ by
    induction r with | mul_comm x y => simp [mul_comm]‚ü©

@[simp]
lemma lift_Œπ_apply (a : L) : lift f (Œπ R L a) = f a := by
  simp [lift, Œπ, algHom]

@[simp]
lemma lift_comp_Œπ : (lift f) ‚àò‚Çó (Œπ R L) = f := LinearMap.ext fun x ‚Ü¶ lift_Œπ_apply f x

theorem algHom_ext {F G : (SymmetricAlgebra R L) ‚Üí‚Çê[R] A}
    (h : F ‚àò‚Çó Œπ R L = (G ‚àò‚Çó Œπ R L : L ‚Üí‚Çó[R] A)) : F = G := by
  ext x
  exact congr($h x)

@[simp]
lemma lift_Œπ : (lift (Œπ R L)) = AlgHom.id R (SymmetricAlgebra R L) := by
  apply algHom_ext
  rw [lift_comp_Œπ]
  rfl

end SymmetricAlgebra

variable {A : Type*} [CommSemiring A] [Algebra R A] (f : L ‚Üí‚Çó[R] A)
variable {R} {L}

/-- Given a morphism `Œπ : L ‚Üí‚Çó[R] A`, `IsSymmetricAlgebra Œπ` is a proposition saying that the
algebra homomorphism from `SymmetricAlgebra R L` to `A` is bijective. -/
def IsSymmetricAlgebra (f : L ‚Üí‚Çó[R] A) : Prop :=
  Function.Bijective (SymmetricAlgebra.lift f)

theorem SymmetricAlgebra.isSymmetricAlgebra_Œπ : IsSymmetricAlgebra (Œπ R L) := by
  rw [IsSymmetricAlgebra, lift_Œπ]
  exact Function.Involutive.bijective (congrFun rfl)

namespace IsSymmetricAlgebra

variable {f : L ‚Üí‚Çó[R] A} (h : IsSymmetricAlgebra f)

section equiv

/-- For `Œπ : L ‚Üí‚Çó[R] A`, construst the algebra isomorphism `(SymmetricAlgebra R L) ‚âÉ‚Çê[R] A`
from `IsSymmetricAlgebra Œπ`. -/
noncomputable def equiv : SymmetricAlgebra R L ‚âÉ‚Çê[R] A :=
  .ofBijective (SymmetricAlgebra.lift f) h

@[simp]
lemma equiv_apply (a : SymmetricAlgebra R L) : h.equiv a = SymmetricAlgebra.lift f a := rfl

@[simp]
lemma equiv_toAlgHom : h.equiv = SymmetricAlgebra.lift f := rfl

@[simp]
lemma equiv_symm_apply (a : L) : h.equiv.symm (f a) = SymmetricAlgebra.Œπ R L a :=
  h.equiv.injective (by simp)

@[simp]
lemma equiv_symm_comp : h.equiv.symm ‚àò‚Çó f = SymmetricAlgebra.Œπ R L :=
  LinearMap.ext fun x ‚Ü¶ equiv_symm_apply h x

end equiv

section UniversalProperty

variable {A' : Type*} [CommSemiring A'] [Algebra R A'] (g : L ‚Üí‚Çó[R] A')

/-- Given a morphism `œÜ : L ‚Üí‚Çó[R] A'`, lift this to a morphism of type `A ‚Üí‚Çê[R] A'` (where `A`
satisfies the universal property of the symmetric algebra of `L`) -/
noncomputable def lift : A ‚Üí‚Çê[R] A' := (SymmetricAlgebra.lift g).comp h.equiv.symm

@[simp]
lemma lift_eq (a : L) : (h.lift g) (f a) = g a := by simp [lift]

@[simp]
lemma lift_comp_linearMap : (h.lift g) ‚àò‚Çó f = g := LinearMap.ext fun x ‚Ü¶ lift_eq h g x

lemma algHom_ext (h : IsSymmetricAlgebra f) {F G : A ‚Üí‚Çê[R] A'}
    (hFG : F ‚àò‚Çó f = (G ‚àò‚Çó f : L ‚Üí‚Çó[R] A')) : F = G := by
  suffices F.comp h.equiv.toAlgHom = G.comp h.equiv.toAlgHom by
    rw [DFunLike.ext'_iff] at this ‚ä¢
    exact h.equiv.surjective.injective_comp_right this
  refine SymmetricAlgebra.algHom_ext (LinearMap.ext fun x ‚Ü¶ ?_)
  simpa using congr($hFG x)

variable {g} in
lemma lift_unique {F : A ‚Üí‚Çê[R] A'} (hF : F ‚àò‚Çó f = g) : F = h.lift g :=
  h.algHom_ext (by simpa)

end UniversalProperty

end IsSymmetricAlgebra
